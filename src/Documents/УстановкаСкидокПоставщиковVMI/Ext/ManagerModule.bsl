Процедура ВыполнитьПроведение(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "СкидкиПоставщиковVMI") Тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "СкидкиПоставщиковVMI", РегистрыСведений_СкидкиПоставщиковVMI(вхСсылкаНаДокумент, вхОтказ));
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "СкидкиVMI") Тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "СкидкиVMI", РегистрыНакопления_СкидкиVMI(вхСсылкаНаДокумент, вхОтказ));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОтменуПроведения(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	
	Если НЕ вхОтказ Тогда
		ПроведениеДокументовКлиентСервер.ОчиститьДвиженияДокумента(вхСсылкаНаДокумент);
	КонецЕсли;
		
КонецПроцедуры

Функция РегистрыСведений_СкидкиПоставщиковVMI(вхСсылкаНаДокумент, вхОтказ) 
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраСведений("СкидкиПоставщиковVMI", ТаблицаДвижений);
	ДатаОкончания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ДатаОкончания");
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	УстановкаСкидокПоставщиковVMI.Дата КАК Период,
	                |	УстановкаСкидокПоставщиковVMI.Ссылка КАК Регистратор,
	                |	УстановкаСкидокПоставщиковVMI.Контрагент,
	                |	УстановкаСкидокПоставщиковVMI.ВидСкидки,
	                |	УстановкаСкидокПоставщиковVMI.ПроцентСкидки,
	                |	&ДатаОкончания КАК ДатаОкончания,
	                |	УстановкаСкидокПоставщиковVMI.Склад
	                |ИЗ
	                |	Документ.УстановкаСкидокПоставщиковVMI КАК УстановкаСкидокПоставщиковVMI
	                |ГДЕ
	                |	УстановкаСкидокПоставщиковVMI.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
	Запрос.УстановитьПараметр("ДатаОкончания", ?(ЗначениеЗаполнено(ДатаОкончания), КонецДня(ДатаОкончания), Дата(3999,12,31,23,59,59)));
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_СкидкиVMI(вхСсылкаНаДокумент, вхОтказ) 
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("СкидкиVMI", ТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УстановкаСкидокПоставщиковVMI.Дата КАК Период,
	               |	УстановкаСкидокПоставщиковVMI.Ссылка КАК Регистратор,
	               |	УстановкаСкидокПоставщиковVMI.Контрагент,
	               |	УстановкаСкидокПоставщиковVMI.МаксимальнаяСуммаСкидки КАК СуммаУпр,
	               |	УстановкаСкидокПоставщиковVMI.Склад,
	               |	УстановкаСкидокПоставщиковVMI.Ссылка КАК УстановкаСкидокПоставщиковVMI
	               |ИЗ
	               |	Документ.УстановкаСкидокПоставщиковVMI КАК УстановкаСкидокПоставщиковVMI
	               |ГДЕ
	               |	УстановкаСкидокПоставщиковVMI.Ссылка = &Ссылка
	               |	И УстановкаСкидокПоставщиковVMI.ВидСкидки = &ВидСкидки";
	Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
	Запрос.УстановитьПараметр("ВидСкидки", Перечисления.ВидыСкидок.СОграничениемПоОбщейСумме);
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	
	Возврат ТаблицаДвижений;
	
КонецФункции