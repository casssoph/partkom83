
//// ОБРАБОТЧИКИ МОДУЛЯ ОБЪЕКТА

Процедура ВыполнитьПроведение(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	
	ПроведениеДокументовКлиентСервер.ОчиститьДвиженияДокумента(вхСсылкаНаДокумент);
	
	ТаблицаСтрокЗаявок = ПолучитьТаблицуСтрокЗаявок(вхСсылкаНаДокумент);
	ПроверкаНаличияЗаказов(вхСсылкаНаДокумент, вхОтказ, ТаблицаСтрокЗаявок);
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "РезервыТоваров") тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "РезервыТоваров", РегистрыНакопления_РезервыТоваров(вхСсылкаНаДокумент, вхОтказ, вхПараметры, ТаблицаСтрокЗаявок));
	КонецЕсли;
	
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "ЗаявкиПокупателей") Или ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "ОтказыПоЗаявкам") Тогда
		ДобавленныеОтказы = Неопределено;
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "ЗаявкиПокупателей", РегистрыНакопления_ЗаявкиПокупателей(вхСсылкаНаДокумент, вхОтказ, вхПараметры, ТаблицаСтрокЗаявок, ДобавленныеОтказы));
		
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "ОтказыПоЗаявкам", РегистрыНакопления_ОтказыПоЗаявкам(вхСсылкаНаДокумент, вхОтказ, вхПараметры, ДобавленныеОтказы));
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "Услуги") тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "Услуги", РегистрыНакопления_Услуги(вхСсылкаНаДокумент, вхОтказ, вхПараметры, ТаблицаСтрокЗаявок));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьОтменуПроведения(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	
	ПроведениеДокументовКлиентСервер.ОчиститьДвиженияДокумента(вхСсылкаНаДокумент);
	
КонецПроцедуры

//// ТАБЛИЦЫ ДВИЖЕНИЙ ДОКУМЕНТОВ

Функция РегистрыНакопления_РезервыТоваров(вхСсылкаНаДокумент, вхОтказ, вхПараметры, Знач ТаблицаСтрокЗаявок)
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("РезервыТоваров", ТаблицаДвижений);
	
	Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |ПОМЕСТИТЬ ТаблицаСтрокЗаявок
	               |ИЗ
	               |	&ТаблицаСтрокЗаявок КАК ТаблицаСтрокЗаявок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РезервыТоваровОстатки.Склад,
	               |	РезервыТоваровОстатки.Номенклатура,
	               |	РезервыТоваровОстатки.Качество,
	               |	РезервыТоваровОстатки.СтрокаЗаявки,
	               |	РезервыТоваровОстатки.СтрокаПрихода,
	               |	-РезервыТоваровОстатки.КоличествоОстаток КАК Количество
	               |ИЗ
	               |	РегистрНакопления.РезервыТоваров.Остатки(
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК РезервыТоваровОстатки";
	Запрос.УстановитьПараметр("ТаблицаСтрокЗаявок", ТаблицаСтрокЗаявок);			   
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход, "ВидДвижения");
	ТаблицаДвижений.ЗаполнитьЗначения(Дата, "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(вхСсылкаНаДокумент, "Регистратор");
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_ЗаявкиПокупателей(вхСсылкаНаДокумент, вхОтказ, вхПараметры, Знач ТаблицаСтрокЗаявок, ДобавленныеОтказы) 
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("ЗаявкиПокупателей", ТаблицаДвижений);
	
	Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |ПОМЕСТИТЬ ТаблицаСтрокЗаявок
	               |ИЗ
	               |	&ТаблицаСтрокЗаявок КАК ТаблицаСтрокЗаявок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаявкиПокупателейОстатки.СтрокаЗаявки КАК СтрокаЗаявки,
	               |	ЗаявкиПокупателейОстатки.КоличествоОстаток КАК Количество
	               |ПОМЕСТИТЬ втЗаявки
	               |ИЗ
	               |	РегистрНакопления.ЗаявкиПокупателей.Остатки(
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ЗаявкиПокупателейОстатки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СтрокаЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОтказыПоЗаявкамОбороты.СтрокаЗаявки КАК СтрокаЗаявки,
	               |	ОтказыПоЗаявкамОбороты.КоличествоОборот КАК Количество
	               |ПОМЕСТИТЬ втОтказы
	               |ИЗ
	               |	РегистрНакопления.ОтказыПоЗаявкам.Обороты(
	               |			,
	               |			,
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ОтказыПоЗаявкамОбороты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СтрокаЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПродажиОбороты.СтрокаЗаявки КАК СтрокаЗаявки,
	               |	ПродажиОбороты.КоличествоОборот КАК Количество
	               |ПОМЕСТИТЬ втВыполнено
	               |ИЗ
	               |	РегистрНакопления.Продажи.Обороты(
	               |			,
	               |			,
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ПродажиОбороты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ПополнениеСкладаОбороты.СтрокаЗаявки,
	               |	ПополнениеСкладаОбороты.КоличествоОборот
	               |ИЗ
	               |	РегистрНакопления.ПополнениеСклада.Обороты(
	               |			,
	               |			,
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ПополнениеСкладаОбороты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СтрокаЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаявкиПокупателейОстатки.Номенклатура,
	               |	ЗаявкиПокупателейОстатки.Качество,
	               |	ЗаявкиПокупателейОстатки.СтрокаЗаявки,
	               |	ЗаявкиПокупателейОстатки.ТорговаяТочка,
	               |	ЗаявкиПокупателейОстатки.ДоговорКонтрагента,
	               |	ЗаявкиПокупателейОстатки.КоличествоОстаток КАК Количество,
	               |	ЗаявкиПокупателейОстатки.СуммаРеглОстаток КАК СуммаРегл,
	               |	ЗаявкиПокупателейОстатки.СуммаУпрОстаток КАК СуммаУпр
	               |ИЗ
	               |	РегистрНакопления.ЗаявкиПокупателей.Остатки(
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ЗаявкиПокупателейОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗаявки.СтрокаЗаявки,
	               |	втЗаявки.Количество - ЕСТЬNULL(втОтказы.Количество, 0) - ЕСТЬNULL(втВыполнено.Количество, 0) КАК Количество
	               |ИЗ
	               |	втЗаявки КАК втЗаявки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втОтказы КАК втОтказы
	               |		ПО втЗаявки.СтрокаЗаявки = втОтказы.СтрокаЗаявки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втВыполнено КАК втВыполнено
	               |		ПО втЗаявки.СтрокаЗаявки = втВыполнено.СтрокаЗаявки
	               |ГДЕ
	               |	втЗаявки.Количество - ЕСТЬNULL(втОтказы.Количество, 0) - ЕСТЬNULL(втВыполнено.Количество, 0) > 0";
	Запрос.УстановитьПараметр("ТаблицаСтрокЗаявок", ТаблицаСтрокЗаявок);			   
	
	Результаты = Запрос.ВыполнитьПакет();
	

	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Результаты[Результаты.Количество() - 2].Выгрузить(), ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	ТаблицаДвижений.ЗаполнитьЗначения(Дата, "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(вхСсылкаНаДокумент, "Регистратор");
	
	Если Не Результаты[Результаты.Количество() - 1].Пустой() Тогда 
		ДобавленныеОтказы = Результаты[Результаты.Количество() - 1].Выгрузить();
	КонецЕсли;
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_ОтказыПоЗаявкам(вхСсылкаНаДокумент, вхОтказ, вхПараметры, ДобавленныеОтказы)
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("ОтказыПоЗаявкам", ТаблицаДвижений);
	
	Если ДобавленныеОтказы = Неопределено Тогда 
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ДобавленныеОтказы, ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(Справочники.СостоянияСтрокДокументов.СнятоПоСрокуПК, "ПричинаОтказа");
	ТаблицаДвижений.ЗаполнитьЗначения(Дата, "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(вхСсылкаНаДокумент, "Регистратор");
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_Услуги(вхСсылкаНаДокумент, вхОтказ, вхПараметры, Знач ТаблицаСтрокЗаявок)
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("Услуги", ТаблицаДвижений);
	
	Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫРАЗИТЬ(ТаблицаСтрокЗаявок.СтрокаЗаявки КАК Справочник.ИдентификаторыСтрокЗаявок) КАК СтрокаЗаявки
	               |ПОМЕСТИТЬ ТаблицаСтрокЗаявок
	               |ИЗ
	               |	&ТаблицаСтрокЗаявок КАК ТаблицаСтрокЗаявок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УслугиОстатки.Номенклатура,
	               |	УслугиОстатки.Заявка,
	               |	-УслугиОстатки.КоличествоОстаток КАК Количество,
	               |	-УслугиОстатки.СуммаОстаток КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.Услуги.Остатки(
	               |			,
	               |			Заявка В
	               |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки.Заявка
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК УслугиОстатки";
	Запрос.УстановитьПараметр("ТаблицаСтрокЗаявок", ТаблицаСтрокЗаявок);			   
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(), ТаблицаДвижений);
	
	ТаблицаДвижений.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход, "ВидДвижения");
	ТаблицаДвижений.ЗаполнитьЗначения(Дата, "Период");
	ТаблицаДвижений.ЗаполнитьЗначения(вхСсылкаНаДокумент, "Регистратор");
	
	Возврат ТаблицаДвижений;
	
КонецФункции

//// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьТаблицуСтрокЗаявок(вхСсылкаНаДокумент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗакрытиеЗаявокПокупателяЗаявки.Документ
	               |ПОМЕСТИТЬ втЗаявки
	               |ИЗ
	               |	Документ.ЗакрытиеЗаявокПокупателя.Заявки КАК ЗакрытиеЗаявокПокупателяЗаявки
	               |ГДЕ
	               |	ЗакрытиеЗаявокПокупателяЗаявки.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ИдентификаторыСтрокЗаявок.Ссылка КАК СтрокаЗаявки
	               |ИЗ
	               |	Справочник.ИдентификаторыСтрокЗаявок КАК ИдентификаторыСтрокЗаявок
	               |ГДЕ
	               |	ИдентификаторыСтрокЗаявок.Заявка В
	               |			(ВЫБРАТЬ
	               |				втЗаявки.Документ
	               |			ИЗ
	               |				втЗаявки)";
				   
	Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ПроверкаНаличияЗаказов(вхСсылкаНаДокумент, вхОтказ, ТаблицаСтрокЗаявок);
	
	ЗапретЗакрытияЗаявокПриНезакрытыхЗаказах = Константы.ЗапретЗакрытияЗаявокПриНезакрытыхЗаказах.Получить();
	Если Не ЗапретЗакрытияЗаявокПриНезакрытыхЗаказах Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |ПОМЕСТИТЬ ТаблицаСтрокЗаявок
	               |ИЗ
	               |	&ТаблицаСтрокЗаявок КАК ТаблицаСтрокЗаявок
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказыПоставщикамОстатки.СтрокаЗаявки КАК СтрокаЗаявки,
	               |	ЗаказыПоставщикамОстатки.КоличествоОстаток КАК Количество
	               |ПОМЕСТИТЬ втЗаказы
	               |ИЗ
	               |	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	               |			,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ЗаказыПоставщикамОстатки
	               |ГДЕ
	               |	ЗаказыПоставщикамОстатки.КоличествоОстаток > 0
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СтрокаЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОтказыПоЗаявкамОбороты.СтрокаЗаявки КАК СтрокаЗаявки,
	               |	СУММА(ОтказыПоЗаявкамОбороты.КоличествоОборот) КАК Количество
	               |ПОМЕСТИТЬ втОтказыПоЗаказам
	               |ИЗ
	               |	РегистрНакопления.ОтказыПоЗаявкам.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			СтрокаЗаявки В
	               |				(ВЫБРАТЬ
	               |					ТаблицаСтрокЗаявок.СтрокаЗаявки
	               |				ИЗ
	               |					ТаблицаСтрокЗаявок)) КАК ОтказыПоЗаявкамОбороты
	               |ГДЕ
	               |	(ОтказыПоЗаявкамОбороты.Регистратор ССЫЛКА Документ.ЗаказПоставщику
	               |			ИЛИ ОтказыПоЗаявкамОбороты.Регистратор ССЫЛКА Документ.КорректировкаЗаказаПоставщику)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОтказыПоЗаявкамОбороты.СтрокаЗаявки
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СтрокаЗаявки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втЗаказы.СтрокаЗаявки,
	               |	втЗаказы.Количество - ЕСТЬNULL(втОтказыПоЗаказам.Количество, 0) КАК Количество
	               |ИЗ
	               |	втЗаказы КАК втЗаказы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ втОтказыПоЗаказам КАК втОтказыПоЗаказам
	               |		ПО втЗаказы.СтрокаЗаявки = втОтказыПоЗаказам.СтрокаЗаявки
	               |ГДЕ
	               |	втЗаказы.Количество - ЕСТЬNULL(втОтказыПоЗаказам.Количество, 0) > 0";
		Запрос.УстановитьПараметр("ТаблицаСтрокЗаявок", ТаблицаСтрокЗаявок);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда 
			вхОтказ = Истина;
			ВызватьИсключение "Настройки В данном документе есть строки заявок с заказами поставщику";
		КонецЕсли;
КонецПроцедуры

//// ВЫГРУЗКА ПРИ ОБМЕНЕ

Функция ВыгрузитьЭлементы(вхТаблицаСсылокНаОбъекты, вхПланОбмена, ВыгружаемыеОбъекты = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	лМетаданныеПланаОбмена = Неопределено;
	лТип = ТипЗнч(вхПланОбмена);
	Если (лТип = Тип("Строка")) тогда
		лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.Найти(вхПланОбмена);
	ИначеЕсли (лТип = Тип("ОбъектМетаданных")) И Метаданные.ПланыОбмена.Содержит(вхПланОбмена) тогда
		лМетаданныеПланаОбмена = вхПланОбмена;
	КонецЕсли;
	
	Если (лМетаданныеПланаОбмена = Неопределено) тогда
		ВызватьИсключение "[ВыгрузитьЭлементы]: неправильный параметр номер 2.";
	КонецЕсли;
	
	Если ВыгружаемыеОбъекты = Неопределено Тогда 
		ВыгружаемыеОбъекты = Новый Массив;
	КонецЕсли;
	
	Если лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_TopLog Тогда 
		
		лМенеджерПланаОбмена = ПланыОбмена[лМетаданныеПланаОбмена.Имя];
		
		лЗапрос = Новый Запрос;
		лЗапрос.УстановитьПараметр("ТаблицаСсылок", вхТаблицаСсылокНаОбъекты);
		лЗапрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
		лЗапрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Ссылка
		|ПОМЕСТИТЬ Объекты
		|ИЗ
		|	&ТаблицаСсылок КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезервыТоваров.Номенклатура,
		|	РезервыТоваров.Номенклатура.Наименование,
		|	РезервыТоваров.Номенклатура.Артикул,
		|	РезервыТоваров.СтрокаЗаявки.IDSite КАК SSID
		|ИЗ
		|	РегистрНакопления.РезервыТоваров КАК РезервыТоваров
		|ГДЕ
		|	РезервыТоваров.Регистратор В
		|			(ВЫБРАТЬ
		|				Объекты.Ссылка
		|			ИЗ
		|				Объекты)";
		
		лРезультатЗапроса = лЗапрос.Выполнить();
		
		Если НЕ лРезультатЗапроса.Пустой() Тогда
			
			лТипОбъектаXDTO = ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), "СнятиеРезерва");
			лОбъект = ФабрикаXDTO.Создать(лТипОбъектаXDTO);
			лСписок = лОбъект.ПолучитьСписок("СтрокаТовары");
			
			лВыборка = лРезультатЗапроса.Выбрать();
			
			Пока лВыборка.Следующий() Цикл
				
				НоваяСтрока = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), лСписок.ВладеющееСвойство.Тип.Имя)); 
				ЗаполнитьЗначенияСвойств(НоваяСтрока, лВыборка, "НоменклатураНаименование,НоменклатураАртикул,SSID");
				НоваяСтрока.НоменклатураСсылка = XMLСтрока(лВыборка.Номенклатура);
				лСписок.Добавить(НоваяСтрока);
				
				ВыгружаемыеОбъекты.Добавить(лВыборка.Ссылка);
				
			КонецЦикла;
			//лОбъект.СтрокаТовары = лСписок;
			Результат.Добавить(лОбъект);
		КонецЕсли; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

