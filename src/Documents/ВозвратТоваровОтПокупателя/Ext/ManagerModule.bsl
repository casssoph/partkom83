
//// ОБРАБОТЧИКИ МОДУЛЯ ОБЪЕКТА

Процедура ВыполнитьПроведение(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ВыполнитьПроведение";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	лКонтроль = Неопределено;
	лФильтр = Неопределено;
	ПроведениеДокументовКлиентСервер.ПрочитатьЗначение(вхПараметры, "ДанныеОбъекта.Контроль", лКонтроль);
	ПроведениеДокументовКлиентСервер.ПрочитатьЗначение(вхПараметры, "Фильтр", лФильтр);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата, ВидОперации");
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "ТоварыНаСкладах") тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "ТоварыНаСкладах", РегистрыНакопления_ТоварыНаСкладах(вхСсылкаНаДокумент, вхОтказ, вхПараметры));
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "ТоварыКОтгрузке") тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "ТоварыКОтгрузке", РегистрыНакопления_ТоварыКОтгрузке(вхСсылкаНаДокумент, вхОтказ, вхПараметры));
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "ПартииТоваров") тогда
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ДокументОснование");
		Если ЗначениеЗаполнено(ДокументОснование) Тогда 
			БлокировкаДанных = Новый БлокировкаДанных;
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад КАК Склад,
			|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
			|	ВозвратТоваровОтПокупателяТовары.Качество
			|ИЗ
			|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
			|ГДЕ
			|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВозвратТоваровОтПокупателяТовары.Номенклатура
			|ИЗ
			|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
			|ГДЕ
			|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка";
			
			Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
			Результаты = Запрос.ВыполнитьПакет();
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.ПартииТоваров");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = Результаты.Получить(0);
			
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Качество", "Качество");
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("Последовательность.ПартионныйУчет");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = Результаты.Получить(1);
			
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
			
			БлокировкаДанных.Заблокировать();
		КонецЕсли;
		
		НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПартионныйУчет, вхПараметры);
		
		лБазовая = ПроведениеДокументовКлиентСервер.ЗначенияДвиженийДокумента(вхСсылкаНаДокумент, Метаданные.РегистрыНакопления.ПартииТоваров);	
		лРазделенныеБазовая = РаботаСПоследовательностямиКлиентСервер.РазделенныеДанные(лБазовая, лФильтр);
		лИсходная = лРазделенныеБазовая.Включенные;
		
		СтруктураТаблиц = РегистрыНакопления_ПартииТоваров(вхСсылкаНаДокумент, вхОтказ, вхПараметры, лФильтр); 
		
		лТребуемая = СтруктураТаблиц.ПартииТоваров;
		
		//Удалим служебные колонки 
		ОбщегоНазначения.УдалитьКолонки(лИсходная, лТребуемая);
		
		лРазностныеДанные = РаботаСПоследовательностямиКлиентСервер.РазностныеДанные(лИсходная, лТребуемая); 
		ПроведениеДокументовКлиентСервер.ЗаписатьДвижения(вхСсылкаНаДокумент, Метаданные.РегистрыНакопления.ПартииТоваров,
		лРазностныеДанные, лРазделенныеБазовая.Исключенные);
		
		КонтрольОстатковПартий(вхСсылкаНаДокумент, вхОтказ, вхПараметры);
		
		ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПартионныйУчет, вхПараметры);
		Если лФильтр = Неопределено Тогда 
			РаботаСПоследовательностямиКлиентСервер.ЗарегистрироватьОбъектПоСсылке(вхСсылкаНаДокумент, "ПартионныйУчет", Истина);
		КонецЕсли;
		
		Если СтруктураТаблиц.ВозвратыМФП <> Неопределено Тогда 
			ЗаписатьВозвратыМФП(вхСсылкаНаДокумент, СтруктураТаблиц.ВозвратыМФП);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "Продажи") тогда
		ОбщегоНазначения.ЗаписатьДвиженияДокумента(вхСсылкаНаДокумент, "Продажи", РегистрыНакопления_Продажи(вхСсылкаНаДокумент, вхОтказ, вхПараметры));
	КонецЕсли;
	
	Если ПроведениеДокументовКлиентСервер.ПроводитсяПо(вхПараметры, "Взаиморасчеты") тогда
		// регистр накопления "Взаиморасчеты"
		
		//ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ДоговорКонтрагента");
		//
		//БлокировкаДанных = Новый БлокировкаДанных;
		//
		//ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.Взаиморасчеты");
		//ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		//ЭлементБлокировки.УстановитьЗначение("ДоговорКонтрагента", ДоговорКонтрагента);
		//
		//ЭлементБлокировки = БлокировкаДанных.Добавить("Последовательность.ПоРасчетамСКонтрагентами");
		//ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		//ЭлементБлокировки.УстановитьЗначение("ДоговорКонтрагента", ДоговорКонтрагента);
		//БлокировкаДанных.Заблокировать();
		
		НовоеПроведениеПоВзаиморасчетам = глЗначениеПеременной("НовоеПроведениеПоВзаиморасчетам");
		
		лОчищать = Ложь;
		Если НовоеПроведениеПоВзаиморасчетам Тогда 
			лОчищать = ПроведениеДокументовКлиентСервер.НеобходимоОчиститьДвиженияВзаиморасчеты(вхСсылкаНаДокумент);
		Иначе	
			Если (лКонтроль <> Неопределено) тогда
				Если лКонтроль.Свойство("СтарыеЗначения") Тогда
					лСтарыеЗначения = лКонтроль.СтарыеЗначения.Получить(Метаданные.Последовательности.ПоРасчетамСКонтрагентами);
					лНовыеЗначения = лКонтроль.НовыеЗначения.Получить(Метаданные.Последовательности.ПоРасчетамСКонтрагентами);
					Если (лСтарыеЗначения <> Неопределено) И (лНовыеЗначения <> Неопределено) тогда
						лОчищать = (лСтарыеЗначения.Шапка.Дата < лНовыеЗначения.Шапка.Дата)
						И лСтарыеЗначения.Шапка.Проведен;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПоРасчетамСКонтрагентами, вхПараметры);
		
		Если лОчищать тогда
			ПроведениеДокументовКлиентСервер.ОчиститьДвиженияДокумента(вхСсылкаНаДокумент, Метаданные.РегистрыНакопления.Взаиморасчеты);
			лБазовая = Неопределено;
			ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("Взаиморасчеты", лБазовая);
			лТребуемая = РегистрыНакопления_Взаиморасчеты(вхСсылкаНаДокумент, вхОтказ, вхПараметры);
		Иначе
			лТребуемая = РегистрыНакопления_Взаиморасчеты(вхСсылкаНаДокумент, вхОтказ, вхПараметры);
			лБазовая = ПроведениеДокументовКлиентСервер.ЗначенияДвиженийДокумента(вхСсылкаНаДокумент, Метаданные.РегистрыНакопления.Взаиморасчеты);	
		КонецЕсли;
		
		лРазделенныеБазовая = РаботаСПоследовательностямиКлиентСервер.РазделенныеДанные(лБазовая, лФильтр);
		лИсходная = лРазделенныеБазовая.Включенные;
		//лТребуемая = РегистрыНакопления_Взаиморасчеты(вхСсылкаНаДокумент, вхОтказ, вхПараметры);
		лРазностныеДанные = РаботаСПоследовательностямиКлиентСервер.РазностныеДанные(лИсходная, лТребуемая); 
		ПроведениеДокументовКлиентСервер.ЗаписатьДвижения(вхСсылкаНаДокумент, Метаданные.РегистрыНакопления.Взаиморасчеты,
		лРазностныеДанные, лРазделенныеБазовая.Исключенные);
		
		ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПоРасчетамСКонтрагентами, вхПараметры);
		Если НовоеПроведениеПоВзаиморасчетам И лФильтр = Неопределено Тогда 
			РаботаСПоследовательностямиКлиентСервер.ЗарегистрироватьОбъектПоСсылке(вхСсылкаНаДокумент, "ПоРасчетамСКонтрагентами", Истина);
		КонецЕсли;
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "СтатусДокумента") = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят Тогда
			РегистрыСведений.ОтложенноеСнятиеБлокировкиКонтрагентов.ДобавитьДляОбработки(вхСсылкаНаДокумент);
		КонецЕсли;
		
	КонецЕсли;
	
	Если вхПараметры <> Неопределено И вхПараметры.Свойство("ДанныеОбъекта") 
		И вхПараметры.ДанныеОбъекта.Свойство("ДобавитьВОчередьОтправкиДокументовОтгрузки") Тогда
		РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.ДобавитьВОчередьОтправкиДокументовОтгрузки(вхСсылкаНаДокумент);
	КонецЕсли;

	
КонецПроцедуры

Процедура ВыполнитьОтменуПроведения(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ВыполнитьОтменуПроведения";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПоРасчетамСКонтрагентами, вхПараметры);
	НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПартионныйУчет, вхПараметры);
	
	ПроведениеДокументовКлиентСервер.ОчиститьДвиженияДокумента(вхСсылкаНаДокумент);
	
	ОчиститьВозвратыМФП(вхСсылкаНаДокумент);
	
	ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПоРасчетамСКонтрагентами, вхПараметры);
	ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, Метаданные.Последовательности.ПартионныйУчет, вхПараметры);
	
	РаботаСПоследовательностямиКлиентСервер.ЗарегистрироватьОбъектПоСсылке(вхСсылкаНаДокумент, "ПартионныйУчет", Ложь);
	
	Если глЗначениеПеременной("НовоеПроведениеПоВзаиморасчетам") Тогда 
		РаботаСПоследовательностямиКлиентСервер.ЗарегистрироватьОбъектПоСсылке(вхСсылкаНаДокумент, "ПоРасчетамСКонтрагентами", Ложь);
	КонецЕсли;
КонецПроцедуры

Процедура ОчиститьВозвратыМФП(вхСсылкаНаДокумент);
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ОчиститьВозвратыМФП";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата"); 
	
	Если Не ПроведениеДокументовКлиентСервер.ИспользуетсяНоваяСхемаМФПДляВозвратов(Реквизиты.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ВозвратыМФП.СоздатьНаборЗаписей();
	НЗ.Отбор.ВозвратПокупателя.Установить(вхСсылкаНаДокумент);
	НЗ.Записать();	
	
КонецПроцедуры

//После записи движений
Процедура КонтрольОстатковПартий(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_КонтрольОстатковПартий";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////

	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СтатусДокумента,Склад,АктРассмотренияВозврата,ДокументОснование"); 
	
	Если Не ЗначениеЗаполнено(Реквизиты.АктРассмотренияВозврата) Тогда
		//Здесь контроль только для документов по процессу возврата
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваров.Номенклатура,
		|	ВЫБОР
		|		КОГДА &БезПартий
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ИдентификаторыСтрокПриходов.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваров.СтрокаПрихода
		|	КОНЕЦ КАК СтрокаПрихода,
		|	СУММА(ВЫБОР
		|			КОГДА ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ПартииТоваров.Количество
		|			ИНАЧЕ -ПартииТоваров.Количество
		|		КОНЕЦ) КАК Количество
		|ПОМЕСТИТЬ ПартииВозврата
		|ИЗ
		|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|ГДЕ
		|	ПартииТоваров.Регистратор = &Возврат
		|	И НЕ ПартииТоваров.ВнутреннееПеремещение
		|	И ПартииТоваров.Активность
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваров.Номенклатура,
		|	ВЫБОР
		|		КОГДА &БезПартий
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ИдентификаторыСтрокПриходов.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваров.СтрокаПрихода
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииВозврата.Номенклатура,
		|	СУММА(ПартииВозврата.Количество) КАК Количество
		|ПОМЕСТИТЬ НоменклатураВозврата
		|ИЗ
		|	ПартииВозврата КАК ПартииВозврата
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииВозврата.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваров.Номенклатура,
		|	ВЫБОР
		|		КОГДА &БезПартий
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ИдентификаторыСтрокПриходов.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваров.СтрокаПрихода
		|	КОНЕЦ КАК СтрокаПрихода,
		|	СУММА(ПартииТоваров.Количество) КАК Количество
		|ПОМЕСТИТЬ СписанныеПартии
		|ИЗ
		|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НоменклатураВозврата КАК НоменклатураВозврата
		|		ПО (НоменклатураВозврата.Номенклатура = ПартииТоваров.Номенклатура)
		|ГДЕ
		|	ПартииТоваров.Регистратор = &ДокументПродажи
		|	И ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход)
		|	И НЕ ПартииТоваров.ВнутреннееПеремещение
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииТоваров.Номенклатура,
		|	ВЫБОР
		|		КОГДА &БезПартий
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ИдентификаторыСтрокПриходов.ПустаяСсылка)
		|		ИНАЧЕ ПартииТоваров.СтрокаПрихода
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписанныеПартии.Номенклатура,
		|	СписанныеПартии.СтрокаПрихода,
		|	СписанныеПартии.Количество
		|ПОМЕСТИТЬ ОстаткиПартий
		|ИЗ
		|	СписанныеПартии КАК СписанныеПартии
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПартииВозврата.Номенклатура,
		|	ПартииВозврата.СтрокаПрихода,
		|	-ПартииВозврата.Количество
		|ИЗ
		|	ПартииВозврата КАК ПартииВозврата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиПартий.Номенклатура,
		|	ОстаткиПартий.СтрокаПрихода,
		|	СУММА(ОстаткиПартий.Количество) КАК Количество
		|ИЗ
		|	ОстаткиПартий КАК ОстаткиПартий
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиПартий.Номенклатура,
		|	ОстаткиПартий.СтрокаПрихода
		|
		|ИМЕЮЩИЕ
		|	СУММА(ОстаткиПартий.Количество) < 0";
		
	//КонтрольБезУчетаПартий = ВозвратыОтПокупателяСервер.ПодбиратьПартииПоРазмещениям(Реквизиты.Дата);
	КонтрольБезУчетаПартий = Истина; //ХудинВВ XX-1976
	
	Запрос.УстановитьПараметр("БезПартий", КонтрольБезУчетаПартий);
	Запрос.УстановитьПараметр("Возврат", вхСсылкаНаДокумент);
	Запрос.УстановитьПараметр("ДокументПродажи", Реквизиты.ДокументОснование);
	
	ОтрицательныеОстатки = Запрос.Выполнить().Выгрузить();
	
	ТекстОшибок = "";
	Для каждого СтрокаОстатков Из ОтрицательныеОстатки Цикл
		ТекстОшибок = ТекстОшибок + Символы.ПС +"Номенклатура: "+СтрокаОстатков.Номенклатура+
						?(КонтрольБезУчетаПартий, "", ", Строка прихода: "+СтрокаОстатков.СтрокаПрихода)+
						", Превышение: "+(-СтрокаОстатков.Количество);
	КонецЦикла;
	
	Если ОтрицательныеОстатки.Количество() > 0 Тогда
		ТекстОшибки = "Превышено количество к возврату:"+ Символы.ПС + ТекстОшибок;
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

КонецПроцедуры

//// ТАБЛИЦЫ ДВИЖЕНИЙ ДОКУМЕНТОВ

Функция РегистрыНакопления_ТоварыКОтгрузке(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) 
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РегистрыНакопления_ТоварыКОтгрузке";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("ТоварыКОтгрузке", ТаблицаДвижений);
	
	//Если ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза() Тогда
	//	Возврат ТаблицаДвижений;
	//КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СтатусДокумента,Склад,ВалютаДокумента,КурсВзаиморасчетов,КратностьВзаиморасчетов,ДокументОснование,Организация,ВидОперации"); 
	
	Если Реквизиты.Дата < глЗначениеПеременной("ДатаЗапускаПервогоЭтапа") Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.Дата < ПараметрыСеанса.ДатаНачалаРаботыТовары Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.МФП Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки,
		|	ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателя) КАК Статус,
		|	ВозвратТоваровОтПокупателяТовары.КоличествоПлан КАК Количество,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Период
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
		ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_ТоварыНаСкладах(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт 
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РегистрыНакопления_ТоварыНаСкладах";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("ТоварыНаСкладах", ТаблицаДвижений);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СтатусДокумента,Склад,ВалютаДокумента,КурсВзаиморасчетов,КратностьВзаиморасчетов,ДокументОснование,Организация,ВидОперации,АктРассмотренияВозврата,Склад.СкладНедостач"); 
	
	Если Реквизиты.Дата < глЗначениеПеременной("ДатаЗапускаПервогоЭтапа") Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.Дата < ПараметрыСеанса.ДатаНачалаРаботыТовары Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.МФП Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(Реквизиты.АктРассмотренияВозврата) Тогда
		
		//Размещенное количество приходуем на склад
		//Неразмещенное количество приходуем на склад недостач
		СкладНедостач = ОбщегоНазначения.СкладНедостач(Реквизиты.Склад);
		Если Не ЗначениеЗаполнено(СкладНедостач) Тогда
			ВызватьИсключение "Не задан склад недостач для склада "+Реквизиты.Склад;
		КонецЕсли;
		
		Если НЕ (Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен
			//ХудинВВ 26032019 XX-1983
			ИЛИ  Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы) Тогда
			Возврат ТаблицаДвижений
		КонецЕсли;
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка,
		               |	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
		               |	ВозвратТоваровОтПокупателяТовары.Качество КАК Качество,
		               |	СУММА(ВозвратТоваровОтПокупателяТовары.КоличествоРазмещено) КАК КоличествоРазмещено,
		               |	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК КоличествоПринято
		               |ПОМЕСТИТЬ ВТТовары
		               |ИЗ
		               |	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		               |ГДЕ
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка,
		               |	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		               |	ВозвратТоваровОтПокупателяТовары.Качество
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВТТовары.Ссылка.Склад КАК Склад,
		               |	ВТТовары.Номенклатура,
		               |	ВТТовары.Качество,
		               |	ВЫБОР
		               |		КОГДА ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят)
		               |				ИЛИ ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринятДляЭкспертизы)
		               |			ТОГДА ВТТовары.КоличествоПринято
		               |		ИНАЧЕ ВТТовары.КоличествоРазмещено
		               |	КОНЕЦ КАК Количество,
		               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		               |	ВТТовары.Ссылка КАК Регистратор,
		               |	ВТТовары.Ссылка.Дата КАК Период
		               |ИЗ
		               |	ВТТовары КАК ВТТовары
		               |ГДЕ
		               |	ВЫБОР
		               |			КОГДА ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят)
		               |					ИЛИ ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринятДляЭкспертизы)
		               |				ТОГДА ВТТовары.КоличествоПринято
		               |			ИНАЧЕ ВТТовары.КоличествоРазмещено
		               |		КОНЕЦ > 0
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	&СкладНедостач,
		               |	ВТТовары.Номенклатура,
		               |	ВТТовары.Качество,
		               |	ВТТовары.КоличествоПринято - ВТТовары.КоличествоРазмещено,
		               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		               |	ВТТовары.Ссылка,
		               |	ВТТовары.Ссылка.Дата
		               |ИЗ
		               |	ВТТовары КАК ВТТовары
		               |ГДЕ
		               |	ВТТовары.КоличествоПринято > ВТТовары.КоличествоРазмещено
		               |	И (ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен)
		               |			ИЛИ ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы))";
		
	Иначе
		//Это документ из 77
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Качество,
		|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Период
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Качество,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата";
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
	Запрос.УстановитьПараметр("СкладНедостач", СкладНедостач);
	
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция РегистрыНакопления_Взаиморасчеты(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) 
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РегистрыНакопления_Взаиморасчеты";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("Взаиморасчеты", ТаблицаДвижений);
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата") < ПараметрыСеанса.ДатаНачалаРаботыВзаиморасчеты Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	СтатусДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "СтатусДокумента");
	Если СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый
		ИЛИ СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ
		//ХудинВВ 26032019 XX-1983
		ИЛИ СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен
		ИЛИ СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринятДляЭкспертизы
		ИЛИ СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ВидОперации") = Перечисления.ВидыОперацийВозвратаОтПокупателя.Комиссия Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ВидОперации") = Перечисления.ВидыОперацийВозвратаОтПокупателя.VMI Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ДоговорКонтрагента");
	ДоговорВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ДоговорКонтрагентаВзаиморасчетов");
	
	ДоговорДвижений = ?(ЗначениеЗаполнено(ДоговорВзаиморасчетов),ДоговорВзаиморасчетов,ДоговорКонтрагента);
	
	СформироватьДвижения = Истина;
	
	Если ТипЗнч(вхПараметры) = Тип("Структура") И вхПараметры.Свойство("Фильтр") И ТипЗнч(вхПараметры.Фильтр) = Тип("Структура") 
		И вхПараметры.Фильтр.Свойство("ДоговорКонтрагента") 
		И Не вхПараметры.Фильтр.ДоговорКонтрагента = ДоговорДвижений Тогда
		СформироватьДвижения = Ложь;
	КонецЕсли;
	
	Если СформироватьДвижения Тогда
		
		ВалютаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "ВалютаДокумента");
		КурсВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "КурсВзаиморасчетов");
		КратностьВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "КратностьВзаиморасчетов");
		СуммаДокумента = -ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "СуммаДокумента");
		ЗнакДвижения = -1;
		ТаблицаДвижений = УправлениеВзаиморасчетами.ПростоеПроведениеПоВзаиморасчетам(вхСсылкаНаДокумент, ДоговорДвижений
		, Неопределено, ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, СуммаДокумента, ЗнакДвижения);
		
	КонецЕсли;
	
	Возврат ТаблицаДвижений
	
КонецФункции

Функция РегистрыНакопления_ПартииТоваров(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено, вхФильтр = Неопределено)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РегистрыНакопления_ПартииТоваров";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("ПартииТоваров", ТаблицаДвижений);
	
	ВозвратыМФП = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраСведений("ВозвратыМФП", ВозвратыМФП);
	
	ВозвращаемоеЗначение =  Новый Структура("ПартииТоваров, ВозвратыМФП", ТаблицаДвижений, ВозвратыМФП);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СтатусДокумента,Склад,ВалютаДокумента,КурсВзаиморасчетов,КратностьВзаиморасчетов,ДокументОснование,Организация,ВидОперации,Контрагент, АктРассмотренияВозврата"); 
	
	Если Реквизиты.Дата < ПараметрыСеанса.ДатаНачалаРаботыТовары Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Реквизиты.Дата < глЗначениеПеременной("ДатаЗапускаПроведенияПоПартиямРезервам") Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Реквизиты.АктРассмотренияВозврата) Тогда
		//Заполнено основание и Акт возврата
		Если Не ЗначениеЗаполнено(Реквизиты.ДокументОснование) Тогда
			ВызватьИсключение "Не заполнен документ-основание!";			
		КонецЕсли;
		
		Если НЕ (Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен
			//ХудинВВ 26032019 XX-1983
			ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы)Тогда
			Возврат ВозвращаемоеЗначение
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Ссылка,
		               |	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад КАК Склад,
		               |	ВозвратТоваровОтПокупателяТовары.Качество,
		               |	ВозвратТоваровОтПокупателяТовары.СтрокаПрихода,
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка.Организация КАК Организация,
		               |	ВозвратТоваровОтПокупателяТовары.Количество КАК КоличествоПринято,
		               |	ВозвратТоваровОтПокупателяТовары.КоличествоРазмещено КАК КоличествоРазмещено,
		               |	ВозвратТоваровОтПокупателяТовары.Себестоимость,
		               |	ВозвратТоваровОтПокупателяТовары.СебестоимостьЦена,
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка.УчитыватьНДС КАК УчитыватьНДС,
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
		               |	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
		               |	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтрокиВДокументе
		               |ПОМЕСТИТЬ ВТТовары
		               |ИЗ
		               |	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		               |ГДЕ
		               |	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		               |	ВТТовары.Ссылка КАК Регистратор,
		               |	ВТТовары.Ссылка.Дата КАК Период,
		               |	ВТТовары.Номенклатура,
		               |	ВТТовары.Склад,
		               |	ВТТовары.Качество,
		               |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартии.Собственный) КАК СтатусПартии,
		               |	ВТТовары.СтрокаПрихода,
		               |	ВТТовары.Организация КАК Организация,
		               |	ВТТовары.КоличествоРазмещено КАК Количество,
		               |	ВТТовары.СебестоимостьЦена * ВТТовары.КоличествоРазмещено КАК Себестоимость,
		               |	ВТТовары.СебестоимостьЦена,
		               |	ВТТовары.УчитыватьНДС,
		               |	ВТТовары.СуммаВключаетНДС,
		               |	ВТТовары.СтавкаНДС,
		               |	ВТТовары.НомерСтрокиВДокументе,
		               |	ВТТовары.СебестоимостьЦена * ВТТовары.КоличествоРазмещено КАК СуммаРубли,
		               |	0 КАК СуммаДоллары,
		               |	0 КАК СуммаЕвро,
		               |	ВТТовары.СебестоимостьЦена * ВТТовары.КоличествоРазмещено КАК СуммаБезНДС,
		               |	ЛОЖЬ КАК ВнутреннееПеремещение,
		               |	НЕОПРЕДЕЛЕНО КАК ДокументОснование
		               |ИЗ
		               |	ВТТовары КАК ВТТовары
		               |ГДЕ
		               |	ВТТовары.КоличествоРазмещено > 0
		               |	И (ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен)
		               |			ИЛИ ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы))
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		               |	ВТТовары.Ссылка,
		               |	ВТТовары.Ссылка.Дата,
		               |	ВТТовары.Номенклатура,
		               |	ВТТовары.Склад.СкладНедостач,
		               |	&КачествоНедостача,
		               |	ЗНАЧЕНИЕ(Перечисление.СтатусыПартии.Собственный),
		               |	ВТТовары.СтрокаПрихода,
		               |	ВТТовары.Организация,
		               |	ВТТовары.КоличествоПринято - ВТТовары.КоличествоРазмещено,
		               |	ВТТовары.СебестоимостьЦена,
		               |	ВТТовары.СебестоимостьЦена * (ВТТовары.КоличествоПринято - ВТТовары.КоличествоРазмещено),
		               |	ВТТовары.УчитыватьНДС,
		               |	ВТТовары.СуммаВключаетНДС,
		               |	ВТТовары.СтавкаНДС,
		               |	ВТТовары.НомерСтрокиВДокументе,
		               |	ВТТовары.СебестоимостьЦена * (ВТТовары.КоличествоПринято - ВТТовары.КоличествоРазмещено),
		               |	0,
		               |	0,
		               |	ВТТовары.СебестоимостьЦена * (ВТТовары.КоличествоПринято - ВТТовары.КоличествоРазмещено),
		               |	ЛОЖЬ,
		               |	НЕОПРЕДЕЛЕНО
		               |ИЗ
		               |	ВТТовары КАК ВТТовары
		               |ГДЕ
		               |	ВТТовары.КоличествоПринято > ВТТовары.КоличествоРазмещено
		               |	И (ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен)
		               |			ИЛИ ВТТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы))";
		
		Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
		Запрос.УстановитьПараметр("КачествоНедостача", Справочники.Качество.Недостача);
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		
		Если ТЗ.Количество() > 0 Тогда 
			
			ВалютаДокумента = Реквизиты.ВалютаДокумента;
			КурсДокумента = Реквизиты.КурсВзаиморасчетов;
			КратностьДокумента = Реквизиты.КратностьВзаиморасчетов;
			
			КурсДоллара = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаДоллар, Реквизиты.Дата);
			КурсЕвро = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаЕвро, Реквизиты.Дата);
			Для Каждого Товар Из ТЗ Цикл
				Если Товар.УчитыватьНДС И Товар.СуммаВключаетНДС Тогда 
					СтНДС =  УчетНДС.ПолучитьСтавкуНДС(Товар.СтавкаНДС);
					Товар.СуммаБезНДС = 100 * Товар.СуммаБезНДС / (100 + СтНДС);
				КонецЕсли;
				Если ВалютаДокумента = ПараметрыСеанса.ВалютаРубль Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаДоллар, 1, КурсДоллара.Курс, 1, КурсДоллара.Кратность);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаЕвро, 1, КурсЕвро.Курс, 1, КурсЕвро.Кратность);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаДоллар Тогда
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаЕвро, КурсДоллара.Курс, КурсЕвро.Курс, КурсДоллара.Кратность, КурсЕвро.Кратность);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаЕвро Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаДоллар, КурсЕвро.Курс, КурсДоллара.Курс, КурсЕвро.Кратность, КурсДоллара.Кратность);
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ТаблицаДвижений.Добавить(), Товар);
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.МФП Тогда
		
		//В новой схеме все движения делают возвраты от клиента
		Если ПроведениеДокументовКлиентСервер.ИспользуетсяНоваяСхемаМФПДляВозвратов(Реквизиты.Дата) Тогда
			Возврат ВозвращаемоеЗначение;
		КонецЕсли;
		
		ОрганизацияКонтрагента = Справочники.ПолитикиМФП.ОрганизацияКонтрагента(Реквизиты.Контрагент, Реквизиты.Дата);
		
		Если Не ЗначениеЗаполнено(ОрганизацияКонтрагента) Тогда
			ВызватьИсключение "Не удалось определить организацию собственного контрагента "+Реквизиты.Контрагент+"!";
		КонецЕсли;
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
		|	ВозвратТоваровОтПокупателяТовары.ВозвратПокупателя.Дата КАК Период,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
		|	ВозвратТоваровОтПокупателяТовары.Качество,
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартии.Собственный) КАК СтатусПартии,
		|	ВозвратТоваровОтПокупателяТовары.СтрокаПрихода,
		|	&ОрганизацияКонтрагента КАК Организация,
		|	ВозвратТоваровОтПокупателяТовары.Количество,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаРубли,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаДоллары,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаЕвро,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаБезНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.УчитыватьНДС КАК УчитыватьНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
		|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтрокиВДокументе,
		|	ЛОЖЬ КАК ВнутреннееПеремещение,
		|	Неопределено КАК ДокументОснование
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
		|	ВозвратТоваровОтПокупателяТовары.ВозвратПокупателя.Дата КАК Период,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад КАК Склад,
		|	ВозвратТоваровОтПокупателяТовары.Качество,
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартии.Собственный) КАК СтатусПартии,
		|	ВозвратТоваровОтПокупателяТовары.СтрокаПрихода,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Организация КАК Организация,
		|	ВозвратТоваровОтПокупателяТовары.Количество,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаРубли,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаДоллары,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаЕвро,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаБезНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.УчитыватьНДС КАК УчитыватьНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
		|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтрокиВДокументе,
		|	ЛОЖЬ КАК ВнутреннееПеремещение,
		|	Неопределено КАК ДокументОснование
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка";
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
		Запрос.УстановитьПараметр("ОрганизацияКонтрагента", ОрганизацияКонтрагента);
		
		Если ТипЗнч(вхФильтр) = Тип("Структура") И вхФильтр.Свойство("Номенклатура") Тогда 
			Запрос.Текст = Запрос.Текст + " И ВозвратТоваровОтПокупателяТовары.Номенклатура = &Номенклатура";
			Запрос.УстановитьПараметр("Номенклатура", вхФильтр.Номенклатура);
		КонецЕсли;
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		
		Если ТЗ.Количество() > 0 Тогда 
			
			//ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
			ВалютаДокумента = Реквизиты.ВалютаДокумента;
			КурсДокумента = Реквизиты.КурсВзаиморасчетов;
			КратностьДокумента = Реквизиты.КратностьВзаиморасчетов;
			
			КурсДоллара = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаДоллар, Реквизиты.Дата);
			КурсЕвро = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаЕвро, Реквизиты.Дата);
			Для Каждого Товар Из ТЗ Цикл
				Если Товар.УчитыватьНДС И Товар.СуммаВключаетНДС Тогда 
					СтНДС =  УчетНДС.ПолучитьСтавкуНДС(Товар.СтавкаНДС);
					Товар.СуммаБезНДС = 100 * Товар.СуммаБезНДС / (100 + СтНДС);
				КонецЕсли;
				Если ВалютаДокумента = ПараметрыСеанса.ВалютаРубль Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаДоллар, 1, КурсДоллара.Курс, 1, КурсДоллара.Кратность);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаЕвро, 1, КурсЕвро.Курс, 1, КурсЕвро.Кратность);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаДоллар Тогда
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаЕвро, КурсДоллара.Курс, КурсЕвро.Курс, КурсДоллара.Кратность, КурсЕвро.Кратность);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаЕвро Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаДоллар, КурсЕвро.Курс, КурсДоллара.Курс, КурсЕвро.Кратность, КурсДоллара.Кратность);
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ТаблицаДвижений.Добавить(), Товар);
				
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Не ЗначениеЗаполнено(Реквизиты.ДокументОснование) Тогда 
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Период,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
		|	ВозвратТоваровОтПокупателяТовары.Качество,
		|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартии.Собственный) КАК СтатусПартии,
		|	ВозвратТоваровОтПокупателяТовары.СтрокаПрихода,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Организация,
		|	ВозвратТоваровОтПокупателяТовары.Количество,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаРубли,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаДоллары,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаЕвро,
		|	ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СуммаБезНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.УчитыватьНДС КАК УчитыватьНДС,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ВозвратТоваровОтПокупателяТовары.СтавкаНДС,
		|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтрокиВДокументе,
		|	ЛОЖЬ КАК ВнутреннееПеремещение,
		|	Неопределено КАК ДокументОснование
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка");
		
		Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
		Если ТипЗнч(вхФильтр) = Тип("Структура") И вхФильтр.Свойство("Номенклатура") Тогда 
			Запрос.Текст = Запрос.Текст + " И ВозвратТоваровОтПокупателяТовары.Номенклатура = &Номенклатура";
			Запрос.УстановитьПараметр("Номенклатура", вхФильтр.Номенклатура);
		КонецЕсли;
		
		ТЗ = Запрос.Выполнить().Выгрузить();
		
		Если ТЗ.Количество() > 0 Тогда 
			
			//ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(вхСсылкаНаДокумент, "Дата");
			ВалютаДокумента = Реквизиты.ВалютаДокумента;
			КурсДокумента = Реквизиты.КурсВзаиморасчетов;
			КратностьДокумента = Реквизиты.КратностьВзаиморасчетов;
			
			КурсДоллара = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаДоллар, Реквизиты.Дата);
			КурсЕвро = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаЕвро, Реквизиты.Дата);
			Для Каждого Товар Из ТЗ Цикл
				Если Товар.УчитыватьНДС И Товар.СуммаВключаетНДС Тогда 
					СтНДС =  УчетНДС.ПолучитьСтавкуНДС(Товар.СтавкаНДС);
					Товар.СуммаБезНДС = 100 * Товар.СуммаБезНДС / (100 + СтНДС);
				КонецЕсли;
				Если ВалютаДокумента = ПараметрыСеанса.ВалютаРубль Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаДоллар, 1, КурсДоллара.Курс, 1, КурсДоллара.Кратность);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаРубль,
					ПараметрыСеанса.ВалютаЕвро, 1, КурсЕвро.Курс, 1, КурсЕвро.Кратность);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаДоллар Тогда
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаЕвро, КурсДоллара.Курс, КурсЕвро.Курс, КурсДоллара.Кратность, КурсЕвро.Кратность);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаЕвро Тогда
					Товар.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаЕвро,
					ПараметрыСеанса.ВалютаДоллар, КурсЕвро.Курс, КурсДоллара.Курс, КурсЕвро.Кратность, КурсДоллара.Кратность);
					Товар.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.Себестоимость, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					Товар.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Товар.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
					ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ТаблицаДвижений.Добавить(), Товар);
				
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		ТоварыРеализации = Неопределено;
		РеквизитыРеализации = Неопределено;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ПартииТоваров.Номенклатура,
		|	ПартииТоваров.Склад,
		|	ПартииТоваров.Качество,
		|	ПартииТоваров.СтатусПартии,
		|	ПартииТоваров.СтрокаПрихода,
		|	ПартииТоваров.Организация,
		|	ПартииТоваров.Количество,
		|	ПартииТоваров.СуммаРубли,
		|	ПартииТоваров.СуммаДоллары,
		|	ПартииТоваров.СуммаЕвро,
		|	ПартииТоваров.СуммаБезНДС,
		|	ПартииТоваров.Организация = &Организация КАК ЭтаОрганизация
		|ИЗ
		|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
		|ГДЕ
		|	ПартииТоваров.Регистратор = &Регистратор  %УсловиеПоНоменклатуре%
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтаОрганизация УБЫВ";
		Запрос.УстановитьПараметр("Регистратор", Реквизиты.ДокументОснование);
		Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
		Если ТипЗнч(вхФильтр) = Тип("Структура") И вхФильтр.Свойство("Номенклатура") Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", " И ПартииТоваров.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", вхФильтр.Номенклатура); 
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", "");
		КонецЕсли;
		ОстаткиПартий = Запрос.Выполнить().Выгрузить();
		
		ДобавленныеСтрокиПрихода = Новый ТаблицаЗначений;
		ДобавленныеСтрокиПрихода.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
		ДобавленныеСтрокиПрихода.Колонки.Добавить("СтрокаПрихода", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыСтрокПриходов"));
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтроки,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад КАК Склад,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Качество КАК Качество,
		|	ВозвратТоваровОтПокупателяТовары.Количество КАК Количество,
		|	ВозвратТоваровОтПокупателяТовары.СтрокаПрихода КАК СтрокаПрихода,
		|	ВозвратТоваровОтПокупателяТовары.НомерСтроки КАК НомерСтрокиВДокументе
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
		|	И ВозвратТоваровОтПокупателяТовары.Количество > 0 %УсловиеПоНоменклатуре%";
		Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
		Если ТипЗнч(вхФильтр) = Тип("Структура") И вхФильтр.Свойство("Номенклатура") Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", " И ВозвратТоваровОтПокупателяТовары.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Номенклатура", вхФильтр.Номенклатура);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", "");
		КонецЕсли;
		Товары = Запрос.Выполнить().Выбрать();
		Пока Товары.Следующий() Цикл
			Отбор = Новый Структура("Номенклатура,Качество");
			ЗаполнитьЗначенияСвойств(Отбор, Товары);
			КоличествоРаспределить = Товары.Количество;
			
			Строки = ОстаткиПартий.НайтиСтроки(Отбор);			
			ИндексСтроки = 0;
			
			Пока КоличествоРаспределить > 0 И ИндексСтроки < Строки.Количество() Цикл 
				СтрокаОст = Строки.Получить(ИндексСтроки);
				Если СтрокаОст.Количество > 0 Тогда 
					СписываемоеКоличество = Мин(КоличествоРаспределить, СтрокаОст.Количество);
					
					СтрокаДвижений = ТаблицаДвижений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаОст,,"Организация");
					СтрокаДвижений.Организация =  Реквизиты.Организация;
					СтрокаДвижений.Количество = СписываемоеКоличество;
					СтрокаДвижений.СтатусПартии = Перечисления.СтатусыПартии.Собственный;
					СтрокаДвижений.Период = Реквизиты.Дата;
					СтрокаДвижений.Регистратор = вхСсылкаНаДокумент;
					СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Приход;
					СтрокаДвижений.Склад = Реквизиты.Склад;
					СтрокаДвижений.НомерСтрокиВДокументе = Товары.НомерСтрокиВДокументе;
					
					Если СтрокаДвижений.Количество <> СтрокаОст.Количество Тогда 
						СтрокаДвижений.СуммаРубли   = СтрокаДвижений.Количество * СтрокаДвижений.СуммаРубли/СтрокаОст.Количество;
						СтрокаДвижений.СуммаДоллары = СтрокаДвижений.Количество * СтрокаДвижений.СуммаДоллары/СтрокаОст.Количество;
						СтрокаДвижений.СуммаЕвро 	= СтрокаДвижений.Количество * СтрокаДвижений.СуммаЕвро/СтрокаОст.Количество;
						СтрокаДвижений.СуммаБезНДС 	= СтрокаДвижений.Количество * СтрокаДвижений.СуммаБезНДС/СтрокаОст.Количество;
					КонецЕсли;
					СтрокаОст.Количество   = СтрокаОст.Количество - СписываемоеКоличество;	
					СтрокаОст.СуммаРубли   = СтрокаОст.СуммаРубли - СтрокаДвижений.СуммаРубли;	
					СтрокаОст.СуммаДоллары = СтрокаОст.СуммаДоллары - СтрокаДвижений.СуммаДоллары;
					СтрокаОст.СуммаЕвро    = СтрокаОст.СуммаЕвро - СтрокаДвижений.СуммаЕвро;
					СтрокаОст.СуммаБезНДС    = СтрокаОст.СуммаБезНДС - СтрокаДвижений.СуммаБезНДС;
					
					КоличествоРаспределить = КоличествоРаспределить - СписываемоеКоличество;
				КонецЕсли;
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;
			Если КоличествоРаспределить > 0 Тогда 
				Если ТоварыРеализации = Неопределено Тогда 
					ТоварыРеализации = ПолучитьТоварыРеализации(Реквизиты.ДокументОснование, вхФильтр);
				КонецЕсли;
				Если РеквизитыРеализации = Неопределено Тогда 
					РеквизитыРеализации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.ДокументОснование, "Дата,ВалютаДокумента,КурсВзаиморасчетов,КратностьВзаиморасчетов,УчитыватьНДС,СуммаВключаетНДС"); 
					КурсДокумента = РеквизитыРеализации.КурсВзаиморасчетов;
					КратностьДокумента = РеквизитыРеализации.КратностьВзаиморасчетов;
					КурсДоллара = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаДоллар, РеквизитыРеализации.Дата);
					КурсЕвро = МодульВалютногоУчета.ПолучитьКурсВалюты(ПараметрыСеанса.ВалютаЕвро, РеквизитыРеализации.Дата);
				КонецЕсли;	
				
				СтрокаДвижений = ТаблицаДвижений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДвижений, Товары);
				СтрокаДвижений.Период = Реквизиты.Дата;
				СтрокаДвижений.Регистратор = вхСсылкаНаДокумент;
				СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Приход;
				СтрокаДвижений.Количество = КоличествоРаспределить;
				СтрокаДвижений.СтатусПартии = Перечисления.СтатусыПартии.Собственный;
				СтрокаДвижений.Организация = Реквизиты.Организация;
				СтрокаДвижений.Склад = Реквизиты.Склад;
				Отбор = Новый Структура("Номенклатура,Качество");
				ЗаполнитьЗначенияСвойств(Отбор, Товары);
				Строки = ТоварыРеализации.НайтиСтроки(Отбор);
				Если Строки.Количество() > 0 Тогда 
					СтрокаТовары = Строки[0];
					СтрокаДвижений.СтрокаПрихода = СтрокаТовары.СтрокаПрихода;
					СтрокаДвижений.СуммаРубли = СтрокаДвижений.Количество * СтрокаТовары.Цена - СтрокаДвижений.Количество * СтрокаТовары.Цена * СтрокаТовары.ПроцентСкидкиНаценки / 100;
					СтрокаДвижений.СуммаДоллары = СтрокаДвижений.СуммаРубли;
					СтрокаДвижений.СуммаЕвро 	= СтрокаДвижений.СуммаРубли;
					СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаДвижений.СуммаРубли,
					РеквизитыРеализации.УчитыватьНДС, РеквизитыРеализации.СуммаВключаетНДС,
					УчетНДС.ПолучитьСтавкуНДС(СтрокаТовары.СтавкаНДС));
					Если РеквизитыРеализации.УчитыватьНДС И РеквизитыРеализации.СуммаВключаетНДС Тогда 
						СтрокаДвижений.СуммаБезНДС 	= СтрокаДвижений.СуммаРубли - СуммаНДС;
					Иначе
						СтрокаДвижений.СуммаБезНДС 	= СтрокаДвижений.СуммаРубли;
					КонецЕсли;
					Если РеквизитыРеализации.ВалютаДокумента = ПараметрыСеанса.ВалютаРубль Тогда
						СтрокаДвижений.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаРубли, ПараметрыСеанса.ВалютаРубль,
						ПараметрыСеанса.ВалютаДоллар, 1, КурсДоллара.Курс, 1, КурсДоллара.Кратность);
						СтрокаДвижений.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаРубли, ПараметрыСеанса.ВалютаРубль,
						ПараметрыСеанса.ВалютаЕвро, 1, КурсЕвро.Курс, 1, КурсЕвро.Кратность);
					ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаДоллар Тогда
						СтрокаДвижений.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаДоллары, ПараметрыСеанса.ВалютаДоллар,
						ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
						СтрокаДвижений.СуммаЕвро = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаДоллары, ПараметрыСеанса.ВалютаЕвро,
						ПараметрыСеанса.ВалютаЕвро, КурсДоллара.Курс, КурсЕвро.Курс, КурсДоллара.Кратность, КурсЕвро.Кратность);
						СтрокаДвижений.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
						ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					ИначеЕсли ВалютаДокумента = ПараметрыСеанса.ВалютаЕвро Тогда
						СтрокаДвижений.СуммаДоллары = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаЕвро, ПараметрыСеанса.ВалютаЕвро,
						ПараметрыСеанса.ВалютаДоллар, КурсЕвро.Курс, КурсДоллара.Курс, КурсЕвро.Кратность, КурсДоллара.Кратность);
						СтрокаДвижений.СуммаРубли = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаЕвро, ПараметрыСеанса.ВалютаДоллар,
						ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
						СтрокаДвижений.СуммаБезНДС = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаДвижений.СуммаБезНДС, ПараметрыСеанса.ВалютаДоллар,
						ПараметрыСеанса.ВалютаРубль, КурсДокумента, 1, КратностьДокумента, 1);
					КонецЕсли;
				Иначе
					ВызватьИсключение "В документе-основании не найден такой товар";
				КонецЕсли;
				Если Не ЗначениеЗаполнено(СтрокаДвижений.СтрокаПрихода) Тогда 
					Если ЗначениеЗаполнено(Товары.СтрокаПрихода) Тогда 
						СтрокаДвижений.СтрокаПрихода = Товары.СтрокаПрихода;
					Иначе 
						СсылкаНаОбъект = Справочники.ИдентификаторыСтрокПриходов.ПолучитьСсылку();
						НоваяСтрокаПрихода = Справочники.ИдентификаторыСтрокПриходов.СоздатьЭлемент();
						НоваяСтрокаПрихода.Дата = Реквизиты.Дата;
						НоваяСтрокаПрихода.Приход = вхСсылкаНаДокумент;
						НоваяСтрокаПрихода.УстановитьСсылкуНового(СсылкаНаОбъект);
						НоваяСтрокаПрихода.Наименование = СсылкаНаОбъект.УникальныйИдентификатор();
						НоваяСтрокаПрихода.Записать();
						
						СтрокаДвижений.СтрокаПрихода =  НоваяСтрокаПрихода.Ссылка;
						
						НовСтр = ДобавленныеСтрокиПрихода.Добавить();
						НовСтр.НомерСтроки = Товары.НомерСтроки;
						НовСтр.СтрокаПрихода = НоваяСтрокаПрихода.Ссылка;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ДобавленныеСтрокиПрихода.Количество() > 0 Тогда 
			Док = вхСсылкаНаДокумент.ПолучитьОбъект();
			Для Каждого Стр Из ДобавленныеСтрокиПрихода Цикл 
				Строка = Док.Товары.Найти(Стр.НомерСтроки, "НомерСтроки");
				Строка.СтрокаПрихода = Стр.СтрокаПрихода;
			КонецЦикла;
			Док.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
	КонецЕсли;
	
	ДополнитьТаблицуДвижениямиМФП(вхСсылкаНаДокумент, ТаблицаДвижений, ВозвращаемоеЗначение.ВозвратыМФП);
	
	ВозвращаемоеЗначение.ПартииТоваров 	= ТаблицаДвижений;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция  ДобавитьНаценкуНаСписаннуюСебестоимость(вхСсылкаНаДокумент, ТаблицаДляОбработки)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ДобавитьНаценкуНаСписаннуюСебестоимость";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	// не будем высчитыать точную наценку, возьмем примерную, по первой попавшеся связи между организациями
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,Организация,ВидОперации,Контрагент,АктРассмотренияВозврата,ДокументОснование"); 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", 				Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("ТаблицаДляОбработки", 			ТаблицаДляОбработки);
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаДляОбработки.СтрокаПрихода
	|ПОМЕСТИТЬ ТаблицаДляОбработки
	|ИЗ
	|	&ТаблицаДляОбработки КАК ТаблицаДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	ПартииТоваров.Организация КАК ОрганизацияПродавецСписаниеПартий,
	|	ВЫРАЗИТЬ(ПартииТоваров.Регистратор КАК Документ.РеализацияТоваровУслуг).Организация КАК ОрганизацияПокупательСписаниеПартий
	|ИЗ
	|	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДляОбработки КАК ТаблицаДляОбработки
	|		ПО ПартииТоваров.СтрокаПрихода = ТаблицаДляОбработки.СтрокаПрихода
	|			И (ПартииТоваров.Регистратор = &ДокументОснование)
	|ГДЕ
	|	ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НЕ ПартииТоваров.ВнутреннееПеремещение
	|	И ПартииТоваров.Организация <> ВЫРАЗИТЬ(ПартииТоваров.Регистратор КАК Документ.РеализацияТоваровУслуг).Организация";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ПравилаСозданияВозвратовМФП = Справочники.ПолитикиМФП.ПравилаСозданияВозвратовМФП(Реквизиты.Дата);
		ПравилаСозданияВозвратовМФП = ПравилаСозданияВозвратовМФП.Скопировать(Новый Структура("ОрганизацияПокупательСписаниеПартий", Реквизиты.Организация));
		
		вхПараметры = Новый Структура("ОрганизацияПродавецСписаниеПартий, ОрганизацияПокупательСписаниеПартий, Порядок",
		Выборка.ОрганизацияПродавецСписаниеПартий,Выборка.ОрганизацияПокупательСписаниеПартий,0);
		
		ПроцентыНаценки = Справочники.ПолитикиМФП.ПроцентыНаценкиДляЭтапаЦепочки(ПравилаСозданияВозвратовМФП, вхПараметры);
		
		Для каждого СтрокаТаблицы ИЗ ТаблицаДляОбработки Цикл
			Для каждого ПроцентНаценки Из ПроцентыНаценки Цикл
				СтрокаТаблицы.СуммаРубли 	= СтрокаТаблицы.СуммаРубли + СтрокаТаблицы.СуммаРубли*ПроцентНаценки/100;
				СтрокаТаблицы.СуммаДоллары 	= СтрокаТаблицы.СуммаДоллары + СтрокаТаблицы.СуммаДоллары*ПроцентНаценки/100;
				СтрокаТаблицы.СуммаЕвро 	= СтрокаТаблицы.СуммаЕвро + СтрокаТаблицы.СуммаЕвро*ПроцентНаценки/100;
				СтрокаТаблицы.СуммаБезНДС 	= СтрокаТаблицы.СуммаБезНДС + СтрокаТаблицы.СуммаБезНДС*ПроцентНаценки/100;
			КонецЦикла;			
		КонецЦикла;    		
	КонецЕсли;
	
КонецФункции

Процедура ДополнитьТаблицуДвижениямиМФП(вхСсылкаНаДокумент, вхТаблицаДвижений, ТаблицаДвиженийВозвратыМФП = Неопределено)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ДополнитьТаблицуДвижениямиМФП";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "КурсВзаиморасчетов,КратностьВзаиморасчетов,УчитыватьНДС,СуммаВключаетНДС,Дата,Организация,ВидОперации,Контрагент,Контрагент.ПокупательИзДрБазы,АктРассмотренияВозврата,Склад,ВалютаДокумента,ДокументОснование"); 
	
	Если Не ПроведениеДокументовКлиентСервер.ИспользуетсяНоваяСхемаМФПДляВозвратов(Реквизиты.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	ВнутренниеКонтрагенты = Справочники.ПолитикиМФП.СобственыеКонтрагенты(Реквизиты.Дата);
	Если ВнутренниеКонтрагенты.Найти(Реквизиты.Контрагент) <> Неопределено
		или Реквизиты.КонтрагентПокупательИзДрБазы Тогда
		//Это мфп возврат из 77
		Возврат;
	КонецЕсли;	
	
	выхТаблицаДвижений 	= вхТаблицаДвижений.СкопироватьКолонки();
	ТаблицаДляОбработки = вхТаблицаДвижений.СкопироватьКолонки();
	
	Для каждого СтрокаДвижений Из вхТаблицаДвижений Цикл
		Если  СтрокаДвижений.Склад =   Реквизиты.Склад И  
			СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаДляОбработки.Добавить(), СтрокаДвижений);
		Иначе
			//приходы на склад недостач никак обрабатывать не будем
			ЗаполнитьЗначенияСвойств(выхТаблицаДвижений.Добавить(), СтрокаДвижений);
		КонецЕсли;
	КонецЦикла;
	
	//Наценим на движения суммарную наценку по цепочке МФП, чтобы получить примерную возвращаемую себестоимость
	ДобавитьНаценкуНаСписаннуюСебестоимость(вхСсылкаНаДокумент, ТаблицаДляОбработки);	
	
	//
	////Получим таблицу, на основании данных которой будем делать движения по МФП
	////Не будем учитывать приход на склады недостач 
	//ТаблицаДляОбработки = вхТаблицаДвижений.Скопировать(Новый Структура("Склад, ВидДвижения", Реквизиты.Склад, ВидДвиженияНакопления.Приход));	
	
	
	
	//Получим таблицу, какое количество на какую конечную организацию мы можем передать
  	ТаблицаКПередаче = ПолучитьТаблицуКПередачеМеждуОрганизациями(вхСсылкаНаДокумент, ТаблицаДляОбработки);
	
	//Получим таблицу со всеми промежуточными организациями цепочки МФП
	ТаблицаКПередачеСМФП = ДобавитьДвиженияПоМФПВТаблицуПередачи(вхСсылкаНаДокумент, ТаблицаКПередаче);
	
	
	//Добавляем приход с наценкой
	Для каждого СтрокаТаблицыКПередаче Из ТаблицаДляОбработки Цикл
		ЗаполнитьЗначенияСвойств(выхТаблицаДвижений.Добавить(), СтрокаТаблицыКПередаче);
	КонецЦикла;
	
	//Добавляем движения МФП
	Для каждого СтрокаТаблицыКПередаче Из ТаблицаКПередачеСМФП Цикл
		
		СтрокаРасход 						= выхТаблицаДвижений.Добавить();
		СтрокаРасход.ВидДвижения 			= ВидДвиженияНакопления.Расход;
		СтрокаРасход.Организация 			= СтрокаТаблицыКПередаче.ОрганизацияПокупатель;
		СтрокаРасход.Период 				= Реквизиты.Дата;
		СтрокаРасход.Регистратор 			= вхСсылкаНаДокумент;
		ЗаполнитьЗначенияСвойств(СтрокаРасход, СтрокаТаблицыКПередаче, 
		"Номенклатура, Склад, Качество, СтрокаПрихода, Количество, СуммаРубли, СуммаДоллары, СуммаЕвро, СуммаБезНДС");
		СтрокаРасход.СтатусПартии 			= Перечисления.СтатусыПартии.Собственный;
		СтрокаРасход.НомерСтрокиВДокументе 	= 0;
		
		СтрокаПриход 						= выхТаблицаДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПриход, СтрокаРасход);
		СтрокаПриход.ВидДвижения 			= ВидДвиженияНакопления.Приход;
		СтрокаПриход.Период 				= Реквизиты.Дата;
		СтрокаПриход.СуммаРубли 			= СтрокаТаблицыКПередаче.СуммаРублиПриход;
		СтрокаПриход.СуммаДоллары 			= СтрокаТаблицыКПередаче.СуммаДолларыПриход;
		СтрокаПриход.СуммаЕвро 				= СтрокаТаблицыКПередаче.СуммаЕвроПриход;
		СтрокаПриход.СуммаБезНДС 			= СтрокаТаблицыКПередаче.СуммаБезНДСПриход;
		СтрокаПриход.Организация 			= СтрокаТаблицыКПередаче.ОрганизацияПродавец;
		
	КонецЦикла;
	
	//Для регистра  ВозвратыМФП
	ОбщегоНазначения.СоздатьСтруктуруРегистраСведений("ВозвратыМФП", ТаблицаДвиженийВозвратыМФП);
	Для каждого СтрокаКПередаче Из ТаблицаКПередаче Цикл
		Если СтрокаКПередаче.ОрганизацияПродавецСписаниеПартий <>  СтрокаКПередаче.ОрганизацияПокупательСписаниеПартий Тогда
			НоваяСтрока =  ТаблицаДвиженийВозвратыМФП.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКПередаче);
			НоваяСтрока.Период 						= Реквизиты.Дата;
			НоваяСтрока.ВозвратПокупателя 			= вхСсылкаНаДокумент;
			НоваяСтрока.Цена 						= 0;
			НоваяСтрока.ЦенаВходящая 				= СтрокаКПередаче.СуммаРубли/СтрокаКПередаче.Количество;
		КонецЕсли;
	КонецЦикла;
	
	вхТаблицаДвижений = выхТаблицаДвижений;
	
	
КонецПроцедуры

Функция ДобавитьДвиженияПоМФПВТаблицуПередачи(вхСсылкаНаДокумент, ТаблицаКПередаче)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ДобавитьДвиженияПоМФПВТаблицуПередачи";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,Организация,ВидОперации,Контрагент,АктРассмотренияВозврата"); 
	
	ПравилаСозданияВозвратовМФП = Справочники.ПолитикиМФП.ПравилаСозданияВозвратовМФП(Реквизиты.Дата);
	ПравилаСозданияВозвратовМФП = ПравилаСозданияВозвратовМФП.Скопировать(Новый Структура("ОрганизацияПокупательСписаниеПартий", Реквизиты.Организация));
	
	Справочники.ПолитикиМФП.ДобавитьПроцентыНаценки(ПравилаСозданияВозвратовМФП, "ВОЗР", "ПравилаСозданияМФП.Порядок <= &Порядок");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПравилаСозданияВозвратовМФП", 	ПравилаСозданияВозвратовМФП);
	Запрос.УстановитьПараметр("ВнутренниеКонтрагенты", 			Справочники.ПолитикиМФП.СобственыеКонтрагенты(Реквизиты.Дата));
	Запрос.УстановитьПараметр("ТаблицаКПередаче", 				ТаблицаКПередаче);
	 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаСозданияВозвратовМФП.Политика,
	|	ПравилаСозданияВозвратовМФП.ОрганизацияПродавецСписаниеПартий,
	|	ПравилаСозданияВозвратовМФП.ОрганизацияПокупательСписаниеПартий,
	|	ПравилаСозданияВозвратовМФП.ОрганизацияПродавец,
	|	ПравилаСозданияВозвратовМФП.ОрганизацияПокупатель,
	|	ПравилаСозданияВозвратовМФП.ДоговорКонтрагента,
	|	ПравилаСозданияВозвратовМФП.ПроцентНаценки,
	|	ПравилаСозданияВозвратовМФП.Порядок
	|ПОМЕСТИТЬ ТаблицаПолитик
	|ИЗ
	|	&ПравилаСозданияВозвратовМФП КАК ПравилаСозданияВозвратовМФП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКПередаче.Склад,
	|	ТаблицаКПередаче.Регистратор,
	|	ТаблицаКПередаче.Номенклатура,
	|	ТаблицаКПередаче.Качество,
	|	ТаблицаКПередаче.СтрокаПрихода,
	|	ТаблицаКПередаче.ОрганизацияПродавецСписаниеПартий,
	|	ТаблицаКПередаче.ОрганизацияПокупательСписаниеПартий,
	|	ТаблицаКПередаче.Количество,
	|	ТаблицаКПередаче.СуммаРубли,
	|	ТаблицаКПередаче.СуммаДоллары,
	|	ТаблицаКПередаче.СуммаЕвро,
	|	ТаблицаКПередаче.СуммаБезНДС
	|ПОМЕСТИТЬ ВозвратыМФП
	|ИЗ
	|	&ТаблицаКПередаче КАК ТаблицаКПередаче
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратыМФП.Склад,
	|	ВозвратыМФП.Регистратор,
	|	ВозвратыМФП.Номенклатура,
	|	ВозвратыМФП.Качество,
	|	ВозвратыМФП.СтрокаПрихода,
	|	ВозвратыМФП.ОрганизацияПродавецСписаниеПартий КАК Возвраты_ОрганизацияПродавецСписаниеПартий,
	|	ВозвратыМФП.ОрганизацияПокупательСписаниеПартий КАК Возвраты_ОрганизацияПокупательСписаниеПартий,
	|	ТаблицаПолитик.ОрганизацияПродавец КАК ОрганизацияПродавецПолитика,
	|	ТаблицаПолитик.ОрганизацияПокупатель КАК ОрганизацияПокупательПолитика,
	|	ТаблицаПолитик.ОрганизацияПродавецСписаниеПартий КАК ОрганизацияПродавецСписаниеПартий,
	|	ТаблицаПолитик.ОрганизацияПокупательСписаниеПартий КАК ОрганизацияПокупательСписаниеПартий,
	|	ТаблицаПолитик.ДоговорКонтрагента,
	|	ВозвратыМФП.Количество,	
	|	ТаблицаПолитик.Политика,
	|	ТаблицаПолитик.ПроцентНаценки,
	|	ТаблицаПолитик.Порядок,
	|	ВозвратыМФП.СуммаРубли,
	|	ВозвратыМФП.СуммаДоллары,
	|	ВозвратыМФП.СуммаЕвро,
	|	ВозвратыМФП.СуммаБезНДС
	|ПОМЕСТИТЬ ТаблицаПродажСПолитиками
	|ИЗ
	|	ВозвратыМФП КАК ВозвратыМФП
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПолитик КАК ТаблицаПолитик
	|		ПО (ТаблицаПолитик.ОрганизацияПродавецСписаниеПартий = ВозвратыМФП.ОрганизацияПродавецСписаниеПартий)
	|			И (ТаблицаПолитик.ОрганизацияПокупательСписаниеПартий = ВозвратыМФП.ОрганизацияПокупательСписаниеПартий)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПродажСПолитиками.Возвраты_ОрганизацияПродавецСписаниеПартий КАК ОрганизацияПродавец,
	|	ТаблицаПродажСПолитиками.Возвраты_ОрганизацияПокупательСписаниеПартий КАК ОрганизацияПокупатель
	|ИЗ
	|	ТаблицаПродажСПолитиками КАК ТаблицаПродажСПолитиками
	|ГДЕ
	|	ТаблицаПродажСПолитиками.Политика ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродажСПолитиками.Склад КАК Склад,
	|	ТаблицаПродажСПолитиками.Регистратор,
	|	ТаблицаПродажСПолитиками.Номенклатура,
	|	ТаблицаПродажСПолитиками.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПродажСПолитиками.Качество,
	|	ТаблицаПродажСПолитиками.СтрокаПрихода,
	|	ТаблицаПродажСПолитиками.ОрганизацияПродавецПолитика КАК ОрганизацияПродавец,
	|	ТаблицаПродажСПолитиками.ОрганизацияПокупательПолитика КАК ОрганизацияПокупатель,
	|	ТаблицаПродажСПолитиками.ОрганизацияПродавецСписаниеПартий КАК ОрганизацияПродавецСписаниеПартий,
	|	ТаблицаПродажСПолитиками.ОрганизацияПокупательСписаниеПартий КАК ОрганизацияПокупательСписаниеПартий,
	|	ТаблицаПродажСПолитиками.Количество,
	|	ТаблицаПродажСПолитиками.СуммаРубли,
	|	ТаблицаПродажСПолитиками.СуммаДоллары,
	|	ТаблицаПродажСПолитиками.СуммаЕвро,
	|	ТаблицаПродажСПолитиками.СуммаБезНДС,
	|	ТаблицаПродажСПолитиками.СуммаРубли КАК СуммаРублиПриход,
	|	ТаблицаПродажСПолитиками.СуммаДоллары КАК СуммаДолларыПриход,
	|	ТаблицаПродажСПолитиками.СуммаЕвро КАК СуммаЕвроПриход,
	|	ТаблицаПродажСПолитиками.СуммаБезНДС КАК СуммаБезНДСПриход,
	|	ТаблицаПродажСПолитиками.Политика,
	|	ТаблицаПродажСПолитиками.ПроцентНаценки,
	|	ТаблицаПродажСПолитиками.Порядок
	|ИЗ
	|	ТаблицаПродажСПолитиками КАК ТаблицаПродажСПолитиками
	|ГДЕ
	|	НЕ ТаблицаПродажСПолитиками.Политика ЕСТЬ NULL"; 
	 
	Результаты = Запрос.ВыполнитьПакет();
	
	ОрганизацииБезПолитик = Результаты[Результаты.Количество() - 2].Выгрузить();
	
	Если ОрганизацииБезПолитик.Количество() > 0 Тогда
		
		//Сообщать о том что не можем вернуть какой-то товар не будем
		
		//лТекстОшибки = "Для данных организаций не заданы политики МФП!";
		//Для каждого Стр ИЗ ОрганизацииБезПолитик Цикл
		//	лТекстОшибки = лТекстОшибки + Символы.ПС + "Продавец: "+Стр.ОрганизацияПродавец+". Покупатель: "+Стр.ОрганизацияПокупатель;
		//КонецЦикла;
		//Сообщить(лТекстОшибки);
		//ВызватьИсключение лТекстОшибки;
	КонецЕсли;
	
	ТЗ = Результаты[Результаты.Количество() - 1].Выгрузить();
	
	Для каждого СтрокаТЗ ИЗ  ТЗ Цикл
		
		ПроцентыНаценки = Справочники.ПолитикиМФП.ПроцентыНаценкиПоСтроке(ПравилаСозданияВозвратовМФП, СтрокаТЗ);
		КоличествоПроцентов = ПроцентыНаценки.Количество();

		сч = 1;
		Для каждого ПроцентНаценки Из ПроцентыНаценки Цикл
			
			СтрокаТЗ.СуммаРублиПриход 	= СтрокаТЗ.СуммаРублиПриход/((100+ПроцентНаценки)/100);
			СтрокаТЗ.СуммаДолларыПриход = СтрокаТЗ.СуммаДолларыПриход/((100+ПроцентНаценки)/100);
			СтрокаТЗ.СуммаЕвроПриход 	= СтрокаТЗ.СуммаЕвроПриход/((100+ПроцентНаценки)/100);
			СтрокаТЗ.СуммаБезНДСПриход 	= СтрокаТЗ.СуммаБезНДСПриход/((100+ПроцентНаценки)/100);
			
			Если сч < КоличествоПроцентов Тогда
				СтрокаТЗ.СуммаРубли 	= СтрокаТЗ.СуммаРубли/((100+ПроцентНаценки)/100);
				СтрокаТЗ.СуммаДоллары 	= СтрокаТЗ.СуммаДоллары/((100+ПроцентНаценки)/100);
				СтрокаТЗ.СуммаЕвро 		= СтрокаТЗ.СуммаЕвро/((100+ПроцентНаценки)/100);
				СтрокаТЗ.СуммаБезНДС 	= СтрокаТЗ.СуммаБезНДС/((100+ПроцентНаценки)/100);
			КонецЕсли;
			сч =сч+1;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТЗ;
	
КонецФункции

Функция ПолучитьТаблицуКПередачеМеждуОрганизациями(вхСсылкаНаДокумент, ТаблицаДляОбработки)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьТаблицуКПередачеМеждуОрганизациями";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,Организация,ВидОперации,Контрагент,АктРассмотренияВозврата,ДокументОснование"); 
	
	//Сначала нужно определить сколько и какой организации мы можем вернуть
	//Для этого нужно учитывать сколько мы покупали и сколько мы уже возвращали
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВнутренниеКонтрагенты", 			Справочники.ПолитикиМФП.СобственыеКонтрагенты(Реквизиты.Дата));
	Запрос.УстановитьПараметр("ВидОперацииМФП", 				Перечисления.ВидыОперацийВозвратаОтПокупателя.МФП);
	Запрос.УстановитьПараметр("Дата", 							Реквизиты.Дата);
	Запрос.УстановитьПараметр("ДокументОснование", 				Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("Ссылка", 						вхСсылкаНаДокумент);
	Запрос.УстановитьПараметр("ТаблицаДляОбработки", 			ТаблицаДляОбработки);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаДляОбработки.Номенклатура,
	               |	ТаблицаДляОбработки.Склад,
	               |	ТаблицаДляОбработки.Качество,
	               |	ТаблицаДляОбработки.СтрокаПрихода,
	               |	ТаблицаДляОбработки.НомерСтрокиВДокументе,
	               |	ТаблицаДляОбработки.Организация КАК Организация,
	               |	ТаблицаДляОбработки.Регистратор КАК Регистратор,
	               |	ТаблицаДляОбработки.Количество,
	               |	ТаблицаДляОбработки.СуммаРубли,
	               |	ТаблицаДляОбработки.СуммаДоллары,
	               |	ТаблицаДляОбработки.СуммаЕвро,
	               |	ТаблицаДляОбработки.СуммаБезНДС
	               |ПОМЕСТИТЬ ТаблицаДляОбработки
	               |ИЗ
	               |	&ТаблицаДляОбработки КАК ТаблицаДляОбработки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаДляОбработки.Номенклатура,
	               |	ТаблицаДляОбработки.Склад,
	               |	ТаблицаДляОбработки.Качество,
	               |	ТаблицаДляОбработки.СтрокаПрихода,
	               |	ТаблицаДляОбработки.НомерСтрокиВДокументе,
	               |	ТаблицаДляОбработки.Организация КАК ОрганизацияПокупатель,
	               |	ТаблицаДляОбработки.Регистратор КАК ВозвратПокупателя,
	               |	&ДокументОснование КАК Реализация,
	               |	СУММА(ТаблицаДляОбработки.Количество) КАК Количество,
	               |	СУММА(ТаблицаДляОбработки.СуммаРубли) КАК СуммаРубли,
	               |	СУММА(ТаблицаДляОбработки.СуммаДоллары) КАК СуммаДоллары,
	               |	СУММА(ТаблицаДляОбработки.СуммаЕвро) КАК СуммаЕвро,
	               |	СУММА(ТаблицаДляОбработки.СуммаБезНДС) КАК СуммаБезНДС,
	               |	СУММА(ТаблицаДляОбработки.СуммаРубли) / СУММА(ТаблицаДляОбработки.Количество) КАК Цена
	               |ПОМЕСТИТЬ ТаблицаВозвратов
	               |ИЗ
	               |	ТаблицаДляОбработки КАК ТаблицаДляОбработки
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаДляОбработки.Номенклатура,
	               |	ТаблицаДляОбработки.НомерСтрокиВДокументе,
	               |	ТаблицаДляОбработки.Качество,
	               |	ТаблицаДляОбработки.Склад,
	               |	ТаблицаДляОбработки.СтрокаПрихода,
	               |	ТаблицаДляОбработки.Организация,
	               |	ТаблицаДляОбработки.Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаВозвратов.СтрокаПрихода,
	               |	ТаблицаВозвратов.Номенклатура,
	               |	ТаблицаВозвратов.Реализация
	               |ПОМЕСТИТЬ СтрокиПриходаВозвратов
	               |ИЗ
	               |	ТаблицаВозвратов КАК ТаблицаВозвратов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПартииТоваров.Номенклатура,
	               |	ПартииТоваров.Качество,
	               |	ПартииТоваров.СтрокаПрихода,
	               |	ПартииТоваров.Организация КАК ОрганизацияПродавецСписаниеПартий,
	               |	ВЫРАЗИТЬ(ПартииТоваров.Регистратор КАК Документ.РеализацияТоваровУслуг).Организация КАК ОрганизацияПокупательСписаниеПартий,
	               |	СУММА(ПартииТоваров.Количество) КАК Количество,
	               |	СУММА(ПартииТоваров.СуммаРубли) КАК СуммаРубли,
	               |	СУММА(ПартииТоваров.СуммаДоллары) КАК СуммаДоллары,
	               |	СУММА(ПартииТоваров.СуммаЕвро) КАК СуммаЕвро,
	               |	СУММА(ПартииТоваров.СуммаБезНДС) КАК СуммаБезНДС
	               |ПОМЕСТИТЬ ТаблицаПродаж
	               |ИЗ
	               |	РегистрНакопления.ПартииТоваров КАК ПартииТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтрокиПриходаВозвратов КАК СтрокиПриходаВозвратов
	               |		ПО ПартииТоваров.СтрокаПрихода = СтрокиПриходаВозвратов.СтрокаПрихода
	               |			И ПартииТоваров.Номенклатура = СтрокиПриходаВозвратов.Номенклатура
	               |			И ПартииТоваров.Регистратор = СтрокиПриходаВозвратов.Реализация
	               |ГДЕ
	               |	ПартииТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |	И НЕ ПартииТоваров.ВнутреннееПеремещение
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПартииТоваров.Номенклатура,
	               |	ПартииТоваров.Качество,
	               |	ПартииТоваров.СтрокаПрихода,
	               |	ПартииТоваров.Организация,
	               |	ПартииТоваров.Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВозвратыМФП.Номенклатура,
	               |	ВозвратыМФП.Качество,
	               |	ВозвратыМФП.СтрокаПрихода,
	               |	ВозвратыМФП.ОрганизацияПродавецСписаниеПартий КАК ОрганизацияПродавецСписаниеПартий,
	               |	ВозвратыМФП.ОрганизацияПокупательСписаниеПартий КАК ОрганизацияПокупательСписаниеПартий,
	               |	СУММА(ВозвратыМФП.Количество) КАК Количество
	               |ПОМЕСТИТЬ УжеВернулиРаньше
	               |ИЗ
	               |	РегистрСведений.ВозвратыМФП КАК ВозвратыМФП
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтрокиПриходаВозвратов КАК СтрокиПриходаВозвратов
	               |		ПО ВозвратыМФП.СтрокаПрихода = СтрокиПриходаВозвратов.СтрокаПрихода
	               |			И ВозвратыМФП.Номенклатура = СтрокиПриходаВозвратов.Номенклатура
	               |ГДЕ
	               |	ВозвратыМФП.Период < &Дата
				   |	И ВозвратыМФП.ВозвратПокупателя <> &Ссылка
	               |	И ВозвратыМФП.ВозвратПокупателя.ДокументОснование = &ДокументОснование
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВозвратыМФП.Номенклатура,
	               |	ВозвратыМФП.Качество,
	               |	ВозвратыМФП.СтрокаПрихода,
	               |	ВозвратыМФП.ОрганизацияПродавецСписаниеПартий,
	               |	ВозвратыМФП.ОрганизацияПокупательСписаниеПартий
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПродаж.Номенклатура,
	               |	ТаблицаПродаж.Качество,
	               |	ТаблицаПродаж.СтрокаПрихода,
	               |	ТаблицаПродаж.ОрганизацияПродавецСписаниеПартий,
	               |	ТаблицаПродаж.ОрганизацияПокупательСписаниеПартий,
	               |	ТаблицаПродаж.Количество - ЕСТЬNULL(УжеВернулиРаньше.Количество, 0) КАК Количество,
	               |	ТаблицаПродаж.Количество КАК КоличествоРТУ,
	               |	ТаблицаПродаж.СуммаРубли КАК СуммаРубли,
	               |	ТаблицаПродаж.СуммаДоллары КАК СуммаДоллары,
	               |	ТаблицаПродаж.СуммаЕвро КАК СуммаЕвро,
	               |	ТаблицаПродаж.СуммаБезНДС КАК СуммаБезНДС
	               |ИЗ
	               |	ТаблицаПродаж КАК ТаблицаПродаж
	               |		ЛЕВОЕ СОЕДИНЕНИЕ УжеВернулиРаньше КАК УжеВернулиРаньше
	               |		ПО ТаблицаПродаж.Номенклатура = УжеВернулиРаньше.Номенклатура
	               |			И ТаблицаПродаж.Качество = УжеВернулиРаньше.Качество
	               |			И ТаблицаПродаж.СтрокаПрихода = УжеВернулиРаньше.СтрокаПрихода
	               |			И ТаблицаПродаж.ОрганизацияПродавецСписаниеПартий = УжеВернулиРаньше.ОрганизацияПродавецСписаниеПартий
	               |			И ТаблицаПродаж.ОрганизацияПокупательСписаниеПартий = УжеВернулиРаньше.ОрганизацияПокупательСписаниеПартий
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВЫБОР
	               |		КОГДА ТаблицаПродаж.ОрганизацияПродавецСписаниеПартий = ТаблицаПродаж.ОрганизацияПокупательСписаниеПартий
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ,
	               |	ТаблицаПродаж.Количество - ЕСТЬNULL(УжеВернулиРаньше.Количество, 0),
	               |	ТаблицаПродаж.СтрокаПрихода.Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВозвратов.ВозвратПокупателя,
	               |	ТаблицаВозвратов.Номенклатура,
	               |	ТаблицаВозвратов.Склад,
	               |	ТаблицаВозвратов.Качество,
	               |	ТаблицаВозвратов.СтрокаПрихода,
	               |	ТаблицаВозвратов.НомерСтрокиВДокументе,
	               |	isnull(ВозвратТоваровотПокупателяТовары.СтрокаЗаявки, Значение(Справочник.ИдентификаторыСтрокЗаявок.ПустаяСсылка)) КАК СтрокаЗаявки,
	               |	ТаблицаВозвратов.ОрганизацияПокупатель,
	               |	ТаблицаВозвратов.Количество,
	               |	ТаблицаВозвратов.СуммаРубли,
	               |	ТаблицаВозвратов.СуммаДоллары,
	               |	ТаблицаВозвратов.СуммаЕвро,
	               |	ТаблицаВозвратов.СуммаБезНДС,
	               |	ТаблицаВозвратов.СуммаРубли / ТаблицаВозвратов.Количество КАК ЦенаРубли,
	               |	ТаблицаВозвратов.СуммаДоллары / ТаблицаВозвратов.Количество КАК ЦенаДоллары,
	               |	ТаблицаВозвратов.СуммаЕвро / ТаблицаВозвратов.Количество КАК ЦенаЕвро,
	               |	ТаблицаВозвратов.СуммаБезНДС / ТаблицаВозвратов.Количество КАК ЦенаБезНДС,
	               |	ТаблицаВозвратов.Цена КАК Цена,
	               |	ТаблицаВозвратов.Цена КАК ЦенаВходящая
	               |ИЗ
	               |	ТаблицаВозвратов КАК ТаблицаВозвратов
				   |	Левое соединение Документ.ВозвратТоваровотПокупателя.Товары КАК ВозвратТоваровотПокупателяТовары
				   |		По ТаблицаВозвратов.НомерСтрокиВДокументе = ВозвратТоваровотПокупателяТовары.НомерСтроки
				   |			И ТаблицаВозвратов.ВозвратПокупателя = ВозвратТоваровотПокупателяТовары.Ссылка";
	
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ТаблицаОстатков = Результаты[Результаты.Количество() - 2].Выгрузить();
	ТаблицаВозвратов = Результаты[Результаты.Количество() - 1].Выгрузить();
	
	//ХудинВВ XX-2502 29052019 Добавил строки заявок в таблицу возвратов. В таблицу остатков не добавлял, остатки по строкам заявок не контролируем.
	
	ТаблицаДвижений = ТаблицаДляОбработки.СкопироватьКолонки();
	ТаблицаДвижений.Колонки.Добавить("ОрганизацияПродавецСписаниеПартий", Новый ОписаниеТипов(("СправочникСсылка.Организации")));
	ТаблицаДвижений.Колонки.Добавить("ОрганизацияПокупательСписаниеПартий", Новый ОписаниеТипов(("СправочникСсылка.Организации")));
	ТаблицаДвижений.Колонки.Добавить("СтрокаЗаявки", Новый ОписаниеТипов(("СправочникСсылка.ИдентификаторыСтрокЗаявок")));
	
	 // Нужно определить организации, по которым еще есть остаток к возврату
	 Для каждого СтрокаВозвратов Из ТаблицаВозвратов Цикл
		 
		 СтруктураОтбора = Новый структура("Номенклатура, Качество, СтрокаПрихода, ОрганизацияПокупательСписаниеПартий");
		 ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаВозвратов);
		 СтруктураОтбора.ОрганизацияПокупательСписаниеПартий = СтрокаВозвратов.ОрганизацияПокупатель;
		 
		 СтрокиОстатков =  ТаблицаОстатков.НайтиСтроки(СтруктураОтбора);
		 
		 Списать = СтрокаВозвратов.Количество;
		 
		 Для каждого СтрокаОстатков Из СтрокиОстатков Цикл
			 
			 Списано = Мин(Списать, СтрокаОстатков.Количество);
			 
			 Если Списано <= 0 Тогда
				 Продолжить;
			 КонецЕсли;
			 
			 НоваяСтрока = ТаблицаДвижений.Добавить();
			 ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатков, , "Количество");
			 ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВозвратов, , "Количество");
			 НоваяСтрока.Количество 		= Списано;
			 НоваяСтрока.Период				= Реквизиты.Дата;
			 
			 //Суммы
			 Если НоваяСтрока.Количество <> СтрокаОстатков.Количество Тогда 
				 НоваяСтрока.СуммаРубли   	= НоваяСтрока.Количество * СтрокаВозвратов.ЦенаРубли;
				 НоваяСтрока.СуммаДоллары 	= НоваяСтрока.Количество * СтрокаВозвратов.ЦенаДоллары;
				 НоваяСтрока.СуммаЕвро 		= НоваяСтрока.Количество * СтрокаВозвратов.ЦенаЕвро;
				 НоваяСтрока.СуммаБезНДС 	= НоваяСтрока.Количество * СтрокаВозвратов.ЦенаБезНДС;
			 КонецЕсли;
			 
			 СтрокаОстатков.Количество = СтрокаОстатков.Количество - Списано;			 
			 
			 Списать = Списать -  Списано;
			 
		 КонецЦикла;
		 
		 Если Списать > 0 Тогда
		 КонецЕсли;
		 
	 КонецЦикла;
	 
	 Возврат ТаблицаДвижений;

КонецФункции

Процедура ЗаписатьВозвратыМФП(вхСсылкаНаДокумент, вхТаблицаДвижений)
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ЗаписатьВозвратыМФП";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	 НЗ = РегистрыСведений.ВозвратыМФП.СоздатьНаборЗаписей();
	 НЗ.Отбор.ВозвратПокупателя.Установить(вхСсылкаНаДокумент);
	 НЗ.Загрузить(вхТаблицаДвижений);
	 НЗ.Записать();
	
КонецПроцедуры


Функция РегистрыНакопления_Продажи(вхСсылкаНаДокумент, вхОтказ, вхПараметры = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РегистрыНакопления_Продажи";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ТаблицаДвижений = Неопределено;
	ОбщегоНазначения.СоздатьСтруктуруРегистраНакопления("Продажи", ТаблицаДвижений);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СтатусДокумента,ВалютаДокумента,КратностьВзаиморасчетов,КурсВзаиморасчетов,ДокументОснование, ВидОперации");
	
	
	Если Реквизиты.Дата < глЗначениеПеременной("ДатаЗапускаПервогоЭтапа") Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Если Реквизиты.Дата < ПараметрыСеанса.ДатаНачалаРаботыТовары Тогда
		Возврат ТаблицаДвижений
	КонецЕсли;
	
	Если Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринятДляЭкспертизы 
		ИЛИ Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	//#Убрал не нужные движения по мфп  Kalinin V.A. ( 2018-08-31 )
	// 
	Если ЗначениеЗаполнено(Реквизиты.ДокументОснование) 
		и ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.ДокументОснование,"ЭтоМФП") тогда 
		Возврат ТаблицаДвижений;		
	КонецЕсли; 	
	//Если ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза() Тогда
	//	Возврат ТаблицаДвижений;
	//КонецЕсли;
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.МФП Тогда
		Возврат ТаблицаДвижений;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.Качество,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад,
	|	ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.ТорговаяТочка,
	|	ВозвратТоваровОтПокупателяТовары.Количество КАК КоличествоВозврат,
	|	ВозвратТоваровОтПокупателяТовары.Сумма КАК СуммаРеглВозврат,
	|	ВозвратТоваровОтПокупателяТовары.Сумма КАК СуммаУпрВозврат,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Период,
	|	-ВозвратТоваровОтПокупателяТовары.Себестоимость КАК СебестоимостьРубли,
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.Филиал
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	ВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	ВалютаУправленческогоУчета = глЗначениеПеременной("ВалютаУправленческогоУчета");
	
	Для Каждого СтрокаТЧ Из ТаблицаДвижений Цикл 
		Если Не Реквизиты.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
			СтрокаТЧ.СуммаРеглВозврат = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТЧ.СуммаРеглВозврат, Реквизиты.ВалютаДокумента,
			ВалютаРегламентированногоУчета, Реквизиты.КурсВзаиморасчетов, 1, Реквизиты.КратностьВзаиморасчетов, 1);
		КонецЕсли;
		Если Не Реквизиты.ВалютаДокумента = ВалютаУправленческогоУчета Тогда
			КурсВалютыУправленческогоУчета = МодульВалютногоУчета.ПолучитьКурсВалюты(ВалютаУправленческогоУчета, Реквизиты.Дата);
			
			СтрокаТЧ.СуммаУпрВозврат = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СтрокаТЧ.СуммаУпрВозврат, Реквизиты.ВалютаДокумента,
			ВалютаУправленческогоУчета, Реквизиты.КурсВзаиморасчетов, КурсВалютыУправленческогоУчета.Курс, Реквизиты.КратностьВзаиморасчетов, КурсВалютыУправленческогоУчета.Кратность);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДвижений;
	
КонецФункции

//// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьМетаданные()
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьМетаданные";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	Возврат Метаданные.Документы.ВозвратТоваровОтПокупателя;	
КонецФункции

Функция ПолучитьРеквизитыКонтроля(вхПараметр = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьРеквизитыКонтроля";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Результат = Новый Структура;
	Если (вхПараметр = Метаданные.ПланыОбмена.ОбменПартКом83_77) тогда
		Результат = ОбменДаннымиКлиентСервер.РеквизитыКонтроляПоДокументу(ПолучитьМетаданные(), ИсключаемыеРеквизитыКонтроляРегистрации());
	Иначе
		Результат.Вставить("Шапка", "Дата,Проведен");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИсключаемыеРеквизитыКонтроляРегистрации() Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ИсключаемыеРеквизитыКонтроляРегистрации";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ИсключаемыеРеквизиты = ОбменДаннымиКлиентСервер.ИнициализироватьТаблицуИсключаемыхРеквизитовКонтроля();
	ОбменДаннымиКлиентСервер.ДобавитьВИсключаемыеРевизиты(ИсключаемыеРеквизиты, "Ссылка");
	
	Возврат ИсключаемыеРеквизиты;
	
КонецФункции

Функция ПолучитьЗначенияРеквизитовКонтроля(вхСсылкаНаОбъект, вхПараметр = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьЗначенияРеквизитовКонтроля";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	Возврат	РаботаСПоследовательностямиКлиентСервер.ПолучитьЗначенияРеквизитовКонтроля(вхСсылкаНаОбъект, вхПараметр);
КонецФункции

Функция ПолучитьДанныеГраницПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, вхФильтр = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьДанныеГраницПоследовательности";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Результат = Неопределено;
	лМетаданныеПоследовательности = Неопределено;
	Если (ТипЗнч(вхПоследовательность) = Тип("Строка")) тогда
		лМетаданныеПоследовательности = Метаданные.Последовательности.Найти(вхПоследовательность);
	ИначеЕсли (ТипЗнч(вхПоследовательность) = Тип("ОбъектМетаданных")) И Метаданные.Последовательности.Содержит(вхПоследовательность) тогда
		лМетаданныеПоследовательности = вхПоследовательность;
	КонецЕсли;
	
	Если (лМетаданныеПоследовательности = Неопределено) тогда
		ВызватьИсключение "[ПолучитьДанныеГраницПоследовательности]: неправильный параметр номер 2.";	
	КонецЕсли;
	
	Если (лМетаданныеПоследовательности = Метаданные.Последовательности.ПоРасчетамСКонтрагентами) тогда
		Результат = ПроведениеДокументовКлиентСервер.ЗначенияДвиженийДокумента(вхСсылкаНаДокумент,
		Метаданные.РегистрыНакопления.Взаиморасчеты, вхФильтр);
	ИначеЕсли лМетаданныеПоследовательности = Метаданные.Последовательности.ПартионныйУчет Тогда 	
		Результат = ПроведениеДокументовКлиентСервер.ЗначенияДвиженийДокумента(вхСсылкаНаДокумент,
		Метаданные.РегистрыНакопления.ПартииТоваров, вхФильтр);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, вхПараметры = Неопределено)
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_НачатьКорректировкуГраницПоследовательности";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	РаботаСПоследовательностямиКлиентСервер.НачатьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, вхПараметры);	
КонецПроцедуры

Процедура ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, вхПараметры = Неопределено)
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ЗакончитьКорректировкуГраницПоследовательности";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	РаботаСПоследовательностямиКлиентСервер.ЗакончитьКорректировкуГраницПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, вхПараметры);	
КонецПроцедуры

Функция ПолучитьМассивСтатусовДокумента(Назначение = Неопределено, КодВозврата = Неопределено) Экспорт
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьМассивСтатусовДокумента";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ИспользуетсяКод06 = Справочники.ИменованныеКонстантыПроцессов.ЗначениеКонстанты("ИспользуетсяКод06") = Истина;
	
	Если Назначение = Неопределено Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СтатусыДокументов.Ссылка
		|ИЗ
		|	Справочник.СтатусыДокументов КАК СтатусыДокументов
		|ГДЕ
		|	СтатусыДокументов.Родитель = &Родитель
		|	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ)
		|	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен)
		|	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы)
		|	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен)
		|УПОРЯДОЧИТЬ ПО
		|	СтатусыДокументов.Код");
		Запрос.УстановитьПараметр("Родитель", Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателя);
		Запрос.УстановитьПараметр("КодВозврата", КодВозврата);
		Запрос.УстановитьПараметр("ИспользуетсяКод06", ИспользуетсяКод06);
		
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	ИначеЕсли Назначение = "ПоАктуВозврата" Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	СтатусыДокументов.Ссылка
		                      |ИЗ
		                      |	Справочник.СтатусыДокументов КАК СтатусыДокументов
		                      |ГДЕ
		                      |	СтатусыДокументов.Родитель = &Родитель
		                      |	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ)
		                      |	И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен)
		                      |	И ВЫБОР
		                      |			КОГДА &КодВозврата = ЗНАЧЕНИЕ(Справочник.КодыВозврата.НаЭкспертизу)
		                      |					И &ИспользуетсяКод06
		                      |				ТОГДА НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят)
		                      |						И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен)
		                      |			ИНАЧЕ НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринятДляЭкспертизы)
		                      |					И НЕ СтатусыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещенДляЭкспертизы)
		                      |		КОНЕЦ
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	СтатусыДокументов.Код");
		Запрос.УстановитьПараметр("Родитель", Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателя);
		Запрос.УстановитьПараметр("КодВозврата", КодВозврата);
		Запрос.УстановитьПараметр("ИспользуетсяКод06", ИспользуетсяКод06);
		
		Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	ИначеЕсли Назначение = "Движение по регистрам" Тогда
		Массив = Новый Массив;
		Массив.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателя);
		Возврат Массив;
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗаписиПоследовательности(вхСсылкаНаДокумент, вхПоследовательность, Проведение) Экспорт 
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьЗаписиПоследовательности";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	лМетаданныеПоследовательности = Неопределено;	
	Если (ТипЗнч(вхПоследовательность) = Тип("Строка")) тогда
		лМетаданныеПоследовательности = Метаданные.Последовательности.Найти(вхПоследовательность);
	ИначеЕсли (ТипЗнч(вхПоследовательность) = Тип("ОбъектМетаданных")) И Метаданные.Последовательности.Содержит(вхПоследовательность) тогда
		лМетаданныеПоследовательности = вхПоследовательность;
	КонецЕсли;
	
	Если (лМетаданныеПоследовательности = Неопределено) тогда
		ВызватьИсключение "[ПолучитьДанныеДляПоследовательности]: неправильный параметр номер 1.";
	КонецЕсли;
	
	лМетаданныеДокумента = вхСсылкаНаДокумент.Метаданные();
	Если НЕ лМетаданныеПоследовательности.Документы.Содержит(лМетаданныеДокумента) тогда
		ВызватьИсключение "[ПолучитьДанныеДляПоследовательности]: неправильный параметр номер 1.";
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(вхСсылкаНаДокумент, "Дата,СуммаДокумента,ДоговорКонтрагента,ДоговорКонтрагентаВзаиморасчетов");
	лРезультат = ОбщегоНазначения.СоздатьСтруктуруПоследовательности(лМетаданныеПоследовательности);
	Если (лМетаданныеПоследовательности = Метаданные.Последовательности.ПартионныйУчет) Тогда
		Если Проведение 
			И Реквизиты.Дата >= ПараметрыСеанса.ДатаНачалаРаботыТовары 
			И Реквизиты.Дата >= глЗначениеПеременной("ДатаЗапускаПервогоЭтапа") Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Период,
			|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Регистратор,
			|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура
			|ИЗ
			|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
			|ГДЕ
			|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", вхСсылкаНаДокумент);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл 
				ЗаполнитьЗначенияСвойств(лРезультат.Добавить(), Выборка); 
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли (лМетаданныеПоследовательности = Метаданные.Последовательности.ПоРасчетамСКонтрагентами) Тогда
		Если Проведение И (Реквизиты.СуммаДокумента <> 0) И Реквизиты.Дата >= ПараметрыСеанса.ДатаНачалаРаботыВзаиморасчеты Тогда
			лСтрокаРезультат = лРезультат.Добавить();
			лСтрокаРезультат.ДоговорКонтрагента = ?(ЗначениеЗаполнено(Реквизиты.ДоговорКонтрагентаВзаиморасчетов),Реквизиты.ДоговорКонтрагентаВзаиморасчетов, Реквизиты.ДоговорКонтрагента);
			лСтрокаРезультат.Период = Реквизиты.Дата;
			лСтрокаРезультат.Регистратор = вхСсылкаНаДокумент;
		КонецЕсли;
	Иначе 		
		
		ВызватьИсключение "[ПолучитьЗаписиПоследовательности]: неправильный параметр номер 1.";
		
	КонецЕсли;
	
	Результат = ПроведениеДокументовКлиентСервер.ПолучитьМоментыВремени(лМетаданныеПоследовательности, лРезультат);
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТоварыРеализации(ДокументОснование, вхФильтр)
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПолучитьТоварыРеализации";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка.Склад КАК Склад,
	|	РеализацияТоваровУслугТовары.Качество,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслугТовары.СтрокаПрихода,
	|	РеализацияТоваровУслугТовары.Сумма,
	|	РеализацияТоваровУслугТовары.СуммаНДС,
	|	РеализацияТоваровУслугТовары.Цена,
	|	РеализацияТоваровУслугТовары.ПроцентСкидкиНаценки,
	|	РеализацияТоваровУслугТовары.СтавкаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Если ТипЗнч(вхФильтр) = Тип("Структура") И вхФильтр.Свойство("Номенклатура") Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", " И РеализацияТоваровУслугТовары.Номенклатура = &Номенклатура");
		Запрос.УстановитьПараметр("Номенклатура", вхФильтр.Номенклатура); 
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеПоНоменклатуре%", "");
	КонецЕсли;
	ТоварыРеализации = Запрос.Выполнить().Выгрузить();
	
	Возврат ТоварыРеализации;
	
КонецФункции

Функция ОпределитьВидОперации(Дата, Контрагент) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ОпределитьВидОперации";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Если Дата < Дата(2019,1,1) Тогда
		ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.Продажа;
	ИначеЕсли ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ГоловнойКонтрагент.ОбратнаяРеализацияПриВозвратеОтКлиента") = Истина Тогда
		ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.ОбратнаяРеализация;
	Иначе
		ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.Корректировка;
	КонецЕсли;
	
	Возврат ВидОперации;
	
КонецФункции

Процедура СоздатьУдалитьСчетФактуру(ВозвратСсылка, УдалитьДокумент = Ложь) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_СоздатьУдалитьСчетФактуру";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда		
		Выполнить(лЗамена);
		Возврат;		
	КонецЕсли;
	
	///////////////////////////////////////////////////////////////////////////
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВозвратСсылка, "ВидОперации, СтатусДокумента, УчитыватьНДС, Проведен");
	
	Если Не РазрешенВводСчетФактуры(ВозвратСсылка) Тогда
		УдалитьДокумент = Истина;
	КонецЕсли;
	
	Если УдалитьДокумент 
		ИЛИ  Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ
		ИЛИ  Реквизиты.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен
		ИЛИ НЕ Реквизиты.УчитыватьНДС Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокументСсылка", ВозвратСсылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СФ.Ссылка
		|ИЗ
		|	Документ.СчетФактураВыданный.ДокументыОснования КАК СФ
		|ГДЕ
		|	СФ.ДокументОснование = &ДокументСсылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СФ.Ссылка
		|ИЗ
		|	Документ.СчетФактураПолученный.ДокументыОснования КАК СФ
		|ГДЕ
		|	СФ.ДокументОснование = &ДокументСсылка";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СФ = Выборка.Ссылка.ПолучитьОбъект();
			СФ.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
	ИначеЕсли Реквизиты.УчитыватьНДС Тогда 
		
		ТипСФ = Документы.ВозвратТоваровОтПокупателя.ОпределитьВидСчетаФактуры(ВозвратСсылка);
		
		НайденныйДокумент = УчетНДС.НайтиПодчиненныйСчетФактуру(ВозвратСсылка, ТипСФ);	
		
		Если ЗначениеЗаполнено(НайденныйДокумент) тогда
			СФ = НайденныйДокумент.ПолучитьОбъект();
		Иначе
			СФ = Документы[ТипСФ].СоздатьДокумент();	
		КонецЕсли;
		
		СФ.ДокументыОснования.Очистить();
		//СФ.ДатаНомерДокументовОплаты.Очистить();
		СФ.Заполнить(ВозвратСсылка);
		СФ.ПометкаУдаления = Ложь;
		
		//ХудинВВ XX-2582 29052019 ((
		СФ.ДополнительныеСвойства.Вставить("СнятьОграничениеПоДатеЗапрета", Истина);
		СФ.ДополнительныеСвойства.Вставить("РежимБога", Истина);
		//))
		СФ.Записать(?(Реквизиты.Проведен,РежимЗаписиДокумента.Проведение, ?(СФ.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись) ));

		//Удалим документ другого вида, если он есть
		ДругойТипСФ = ?(ТипСФ = "СчетФактураПолученный", "СчетФактураВыданный", "СчетФактураПолученный");
		НайденныйДокументУдалить = УчетНДС.НайтиПодчиненныйСчетФактуру(ВозвратСсылка, ДругойТипСФ);
		Если ЗначениеЗаполнено(НайденныйДокументУдалить) Тогда
			СФУдалить = НайденныйДокументУдалить.ПолучитьОбъект();
			СФУдалить.УстановитьПометкуУдаления(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтатусыДляВводаСчетФактуры() Экспорт
	
	 лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_СтатусыДляВводаСчетФактуры";
	 лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	 Если Не лЗамена = Неопределено Тогда
	 	АлгоритмыЗначениеВозврата = Неопределено;		
	 	Выполнить(лЗамена);		
	 	Возврат АлгоритмыЗначениеВозврата;		
	 КонецЕсли;	
	 ///////////////////////////////////////////////////////////////////////////
	 
	 Статусы = Новый Массив;
	 Статусы.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят);
	 Статусы.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен);
	 
	 Возврат Статусы;
	
 КонецФункции
 
 Функция ОпределитьВидСчетаФактуры(Документ) Экспорт
	 
	 лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ОпределитьВидСчетаФактуры";
	 лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	 Если Не лЗамена = Неопределено Тогда
	 	АлгоритмыЗначениеВозврата = Неопределено;		
	 	Выполнить(лЗамена);		
	 	Возврат АлгоритмыЗначениеВозврата;		
	 КонецЕсли;	
	 ///////////////////////////////////////////////////////////////////////////
	 
	 Если ТипЗнч(Документ) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		ВидОперации = Документ.ВидОперации;
	Иначе
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидОперации");
	КонецЕсли;
	
	ВидСчетаФактуры = "";
	Если ВидОперации =  Перечисления.ВидыОперацийВозвратаОтПокупателя.Корректировка Тогда
		ВидСчетаФактуры = "СчетФактураВыданный";
	ИначеЕсли ВидОперации =  Перечисления.ВидыОперацийВозвратаОтПокупателя.ОбратнаяРеализация Тогда
		ВидСчетаФактуры = "СчетФактураПолученный";
	КонецЕсли;

	Возврат ВидСчетаФактуры;
	
КонецФункции

Функция РазрешенВводСчетФактуры(ДокументСсылка, ТекстОшибки = "") Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_РазрешенВводСчетФактуры";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////

	РазрешенВводСчетФактуры = Истина;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "СтатусДокумента, УчитыватьНДС, НомерВходящегоДокументаСФ, ДатаВходящегоДокументаСФ");
	
	ТипСФ = Документы.ВозвратТоваровОтПокупателя.ОпределитьВидСчетаФактуры(ДокументСсылка);
	
	Если НЕ ЗначениеЗаполнено(ТипСФ) Тогда
		ТекстОшибки = "Не определен вид счета-фактуры для данного вида операции!";
		РазрешенВводСчетФактуры = Ложь;
	КонецЕсли;
	
	Если ТипСФ = "СчетФактураПолученный" Тогда
		Если (НЕ ЗначениеЗаполнено(Реквизиты.НомерВходящегоДокументаСФ)
			ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.ДатаВходящегоДокументаСФ)) Тогда
			ТекстОшибки = "Не заполнены номер и дата входящего документа!";
			РазрешенВводСчетФактуры = Ложь;
		КонецЕсли;
	КонецЕсли;
	Если ТипСФ = "СчетФактураВыданный" Тогда
		Если Реквизиты.УчитыватьНДС = Ложь Тогда
			ТекстОшибки = "Нельзя создавать счет-фактуру для документа без признака учета НДС!";
			РазрешенВводСчетФактуры = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СтатусыДляВводаСчетФактуры = Документы.ВозвратТоваровОтПокупателя.СтатусыДляВводаСчетФактуры();
	Если СтатусыДляВводаСчетФактуры.Найти(Реквизиты.СтатусДокумента) = Неопределено Тогда
		ТекстОшибки = "Нельзя создавать счет-фактуру для документа в текущем статусе!";
		РазрешенВводСчетФактуры = Ложь;
	КонецЕсли;
	
	Возврат РазрешенВводСчетФактуры;	
	
КонецФункции


//// ОБМЕНЫ

//Выгрузка
Функция ВыгрузитьЭлементы(вхТаблицаСсылокНаОбъекты, вхПланОбмена, ВыгружаемыеОбъекты = Неопределено) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ВыгрузитьЭлементы";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Результат = Новый Массив;
	
	лМетаданныеПланаОбмена = Неопределено;
	лТип = ТипЗнч(вхПланОбмена);
	Если (лТип = Тип("Строка")) тогда
		лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.Найти(вхПланОбмена);
	ИначеЕсли (лТип = Тип("ОбъектМетаданных")) И Метаданные.ПланыОбмена.Содержит(вхПланОбмена) тогда
		лМетаданныеПланаОбмена = вхПланОбмена;
	КонецЕсли;
	
	Если (лМетаданныеПланаОбмена = Неопределено) тогда
		ВызватьИсключение "[ВыгрузитьЭлементы]: неправильный параметр номер 2.";
	КонецЕсли;
	
	Если ВыгружаемыеОбъекты = Неопределено Тогда 
		ВыгружаемыеОбъекты = Новый Массив;
	КонецЕсли;
	
	Если лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_TopLog Тогда 
		
		лМенеджерПланаОбмена = ПланыОбмена[лМетаданныеПланаОбмена.Имя];
		
		лЗапрос = Новый Запрос;
		лЗапрос.УстановитьПараметр("ТаблицаСсылок", вхТаблицаСсылокНаОбъекты);
		лЗапрос.УстановитьПараметр("ПустаяЗаявка", Документы.ЗаявкаПокупателя.ПустаяСсылка());
		лЗапрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Ссылка
		|ПОМЕСТИТЬ Объекты
		|ИЗ
		|	&ТаблицаСсылок КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка,
		|	ВозвратТоваровОтПокупателя.Ссылка.Номер КАК Номер
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Объекты КАК Объекты
		|		ПО ВозвратТоваровОтПокупателя.Ссылка = Объекты.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.АктРассмотренияВозврата <> ЗНАЧЕНИЕ(Документ.АктРассмотренияВозврата.ПустаяСсылка)
		|	И ВозвратТоваровОтПокупателя.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен)
		|	И ВозвратТоваровОтПокупателя.СостояниеОтменыВТоплог = ЗНАЧЕНИЕ(Перечисление.СостоянияОтменыЗаказаНаПриемкуВТоплог.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка КАК Ссылка,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата.Штрихкод КАК Штрихкод,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Ссылка.КодВозврата.Код, 0) КАК КодВозврата,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Склад.ФизическийСклад КАК Склад,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Номер КАК Номер,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Дата КАК Дата,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата.Номер КАК НомерВходящегоДокумента,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата.Дата КАК ДатаВходящегоДокумента,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата.Номер КАК НомерВходящегоСФ,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата.Дата КАК ДатаВходящегоСФ,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.ТорговаяТочка.Код ЕСТЬ NULL
		|			ТОГДА ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Ссылка.Контрагент.ОсновнаяТорговаяТочка, ЗНАЧЕНИЕ(Справочник.ТорговыеТочки.ПустаяСсылка))
		|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Ссылка.ТорговаяТочка
		|	КОНЕЦ КАК Контрагент,
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.Организация КАК Организация,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Ссылка.Склад.СкладVMI, ЛОЖЬ) КАК ОтветХранение,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	ВозвратТоваровОтПокупателяТовары.Номенклатура.Артикул КАК НоменклатураАртикул,
		|	ВозвратТоваровОтПокупателяТовары.Цена,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки.ПрайсПоставщика.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладПолучатель,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки.IDSite, """") КАК SSID,
		|	ВЫБОР
		|		КОГДА ВозвратТоваровОтПокупателяТовары.Ссылка.ТорговаяТочка.Код ЕСТЬ NULL
		|			ТОГДА ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.Ссылка.Контрагент.ОсновнаяТорговаяТочка, ЗНАЧЕНИЕ(Справочник.ТорговыеТочки.ПустаяСсылка))
		|		ИНАЧЕ ВозвратТоваровОтПокупателяТовары.Ссылка.ТорговаяТочка
		|	КОНЕЦ КАК Клиент,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки.ПоследняяКорректировка.ТорговаяТочка.Город, ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки.Заявка.ТорговаяТочка.Город, ЗНАЧЕНИЕ(Справочник.Города.ПустаяСсылка))) КАК Город,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаПрихода.НомерГТД.Представление, """") КАК ГТД,
		|	ВозвратТоваровОтПокупателяТовары.КоличествоПлан КАК Количество,
		|	ЕСТЬNULL(ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки.Заявка, &ПустаяЗаявка) = &ПустаяЗаявка КАК Виртуальная
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Объекты КАК Объекты
		|		ПО ВозвратТоваровОтПокупателяТовары.Ссылка = Объекты.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтПокупателяТовары.Ссылка.АктРассмотренияВозврата <> ЗНАЧЕНИЕ(Документ.АктРассмотренияВозврата.ПустаяСсылка)
		|	И ВозвратТоваровОтПокупателяТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый)
		|ИТОГИ ПО
		|	Ссылка";
		
		РезультатыЗапроса = лЗапрос.ВыполнитьПакет();
		
		РезультатЗапросаПриемка = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1];
		РезультатЗапросаОтмена  = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2];
		
		Если НЕ РезультатЗапросаПриемка.Пустой() Тогда
			лТипОбъектаXDTO = ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), "Документы.ЗаказНаПриемку");
			лВыборка = РезультатЗапросаПриемка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Ссылка");
			
			Пока лВыборка.Следующий() Цикл
				
				лОбъект = ФабрикаXDTO.Создать(лТипОбъектаXDTO);
				ЗаполнитьЗначенияСвойств(лОбъект, лВыборка, "Номер,Дата,НомерВходящегоДокумента,ДатаВходящегоДокумента,НомерВходящегоСФ,ДатаВходящегоСФ,СуммаДокумента,ОтветХранение");
				лОбъект.ВидДокумента 		= "ВозвратТоваровОтПокупателя";
				лОбъект.Ссылка 				= XMLСтрока(лВыборка.Ссылка);
				лОбъект.СкладСсылка 		= XMLСтрока(лВыборка.Склад);
				лОбъект.КонтрагентСсылка 	= XMLСтрока(лВыборка.Контрагент);
				лОбъект.ОрганизацияСсылка 	= XMLСтрока(лВыборка.Организация);
				лОбъект.ШтрихКод 			= XMLСтрока(лВыборка.ШтрихКод);
				лОбъект.КодВозврата 		= XMLСтрока(лВыборка.КодВозврата);
				
				лТовары = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), "Документы.ЗаказНаПриемку.Товары"));
				лТоварыСписок = лТовары.ПолучитьСписок("СтрокаТовары");
				
				ВыборкаПоТоварам = лВыборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				ТТПополнениеСклада = Неопределено;
				
				Пока ВыборкаПоТоварам.Следующий() Цикл
					НоваяСтрока = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), лТоварыСписок.ВладеющееСвойство.Тип.Имя)); 
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоТоварам, "НоменклатураНаименование,НоменклатураАртикул,SSID,Количество,Цена,ГТД");
					НоваяСтрока.НоменклатураСсылка = XMLСтрока(ВыборкаПоТоварам.Номенклатура);
					НоваяСтрока.КлиентСсылка = XMLСтрока(ВыборкаПоТоварам.Клиент);
					НоваяСтрока.ГородСсылка = XMLСтрока(ВыборкаПоТоварам.Город);
					НоваяСтрока.СкладПолучательСсылка = XMLСтрока(ВыборкаПоТоварам.Склад);
					
					лТоварыСписок.Добавить(НоваяСтрока);
					
				КонецЦикла;	
				
				лОбъект.Товары = лТовары;
				Результат.Добавить(лОбъект);
				//Семенов И.П. 07.02.2019 XX-1768(
				ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(лВыборка.Ссылка, лОбъект, УправлениеСвойствамиПовтИсп.ЗаказНаПриемку());
				//)Семенов И.П
				
				ВыгружаемыеОбъекты.Добавить(лВыборка.Ссылка);
				
			КонецЦикла;

		КонецЕсли;
		
		Если НЕ РезультатЗапросаОтмена.Пустой() Тогда
			
			лТипОбъектаXDTO = ФабрикаXDTO.Тип(лМенеджерПланаОбмена.URIПространстваИмен(), "ОтменаПТУ");
			лВыборка = РезультатЗапросаОтмена.Выбрать();
			
			Пока лВыборка.Следующий() Цикл
				
				лОбъект = ФабрикаXDTO.Создать(лТипОбъектаXDTO);
				лОбъект.Ссылка 				= XMLСтрока(лВыборка.Ссылка);
				лОбъект.Номер 				= лВыборка.Номер;
				лОбъект.Отменен 			= Истина;
				лОбъект.Комментарий 		= "";
				
				Результат.Добавить(лОбъект);
				//Семенов И.П. 07.02.2019 XX-1768(
				ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(лВыборка.Ссылка, лОбъект, "ОтменаПТУ");
				//)Семенов И.П
			КонецЦикла;
			
		КонецЕсли;

		
	КонецЕсли;
	
	//ХудинВВ XX-2442 28052019 Временно
	Если ЭтоАктивнаяЗадачаJirа(Справочники.ЗадачиJira.XX2442) Тогда
		
		Если Результат.Количество() <> вхТаблицаСсылокНаОбъекты.Количество() Тогда
			
			ТекстОшибки = "Выбраны для выгрузки не все зарегистрированные объекты!"+Символы.ПС;
			ТекстОшибки = ТекстОшибки+Символы.ПС+"Таблица ссылок (количество):"+вхТаблицаСсылокНаОбъекты.Количество()+Символы.ПС;
			ТекстОшибки = ТекстОшибки+Символы.ПС+"Результат (количество):"+Результат.Количество()+Символы.ПС;
			
			ТекстОшибки = ТекстОшибки+Символы.ПС+"Таблица ссылок: "+Символы.ПС;
			Для каждого Стр Из вхТаблицаСсылокНаОбъекты Цикл
				ТекстОшибки = ТекстОшибки+Стр.Ссылка+Символы.ПС;
			КонецЦикла;
			
			ТекстОшибки = ТекстОшибки+Символы.ПС+"Таблица выбранных объектов: "+Символы.ПС;
			Для каждого Стр Из Результат Цикл
				ТекстОшибки = ТекстОшибки+Стр.Номер+Символы.ПС;
			КонецЦикла;
			
			ТекстОшибки = ТекстОшибки+Символы.ПС+"Не выгружены:"+Символы.ПС;
			Для каждого Стр Из вхТаблицаСсылокНаОбъекты Цикл
				
				Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Стр.Ссылка, "Номер, СтатусДокумента, Проведен, АктРассмотренияВозврата");
				
				Найдено = Ложь;
				Для каждого ТекОбъект Из Результат Цикл
					Если ТекОбъект.Номер = Реквизиты.Номер Тогда
						Найдено = Истина;
					КонецЕсли;				
				КонецЦикла;
				Если Не Найдено Тогда
					ТекстОшибки = ТекстОшибки+""+Стр.Ссылка+", Статус "+Реквизиты.СтатусДокумента+", Проведен "+Реквизиты.Проведен+", Акт "+Реквизиты.АктРассмотренияВозврата+Символы.ПС;
					
				КонецЕсли;
				
			КонецЦикла;
			
			КритическиеСобытияСервер.ЗарегистрироватьКритическоеСобытие(
			"", 
			Справочники.СобытияДляОтправкиЭлектронныхПисем.ОшибкаОбменаСТопЛогВозвраты,
			"Выгружены не все выбранные объекты",
			,
			Истина,
			ТекстОшибки,
			"Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ВыгрузитьЭлементы");
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//Загрузка
Процедура ЗагрузитьЭлемент(ОбъектXDTO, вхОтправитель, Отказ, вхПараметры = Неопределено, СтруктураОтчета = Неопределено) Экспорт // 26.03.19 Строганов Роман > Убираю лишний параметр (5) - НомерСообщения = 0. Он инициализирован в структуре «вхПараметры» 
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ЗагрузитьЭлемент";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	лМетаданныеПланаОбмена = Метаданные.НайтиПоТипу(ТипЗнч(вхОтправитель));
	
	Если лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_TopLog Тогда
		НомерПотока = ?(лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_TopLog, 0, 1);
		Попытка
			
			ИмяТипаОбъекта =  ОбъектXDTO.Тип().Имя;
			
			Если ИмяТипаОбъекта = "РезультатПриемки" Тогда
				ЗагрузитьРезультатПриемки(ОбъектXDTO, вхПараметры, СтруктураОтчета);
			ИначеЕсли ИмяТипаОбъекта = "ОтменаПТУ" Тогда
				ЗагрузитьРезультатОтмены(ОбъектXDTO, вхПараметры, СтруктураОтчета);
			Иначе 
				ВызватьИсключение "Загрузка возврата товаров от покупателя для вида объекта "+ИмяТипаОбъекта+" не поддерживается!";
			КонецЕсли;
			
		Исключение
			СтруктураОшибки = Новый Структура;
			СтруктураОшибки.Вставить("ОбъектXDTO", ИмяТипаОбъекта);
			СтруктураОшибки.Вставить("GUID", ОбъектXDTO.ЗаказСсылка);
			СтруктураОшибки.Вставить("ИмяОбъектаМетаданных", "ВозвратТоваровОтПокупателя");
			СтруктураОшибки.Вставить("СообщениеОбОшибке", ОписаниеОшибки());
			СтруктураОшибки.Вставить("НомерСообщения", вхПараметры.НомерСообщения);
			СтруктураОшибки.Вставить("ДатаЗагрузкиСообщения", ТекущаяДата());
			СтруктураОшибки.Вставить("НомерПотока", ?(лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_TopLog, 0, 1));
			ОбменДаннымиКлиентСервер.ЗаписатьОшибкиПриОбменеСТопЛог(СтруктураОшибки);
			
			ДокСсылка = Документы.ВозвратТоваровОтПокупателя.ПолучитьСсылку(Новый УникальныйИдентификатор(ОбъектXDTO.ЗаказСсылка));
			Если НЕ ОбменДаннымиКлиентСервер.ЭтоБитаяСсылка(ДокСсылка) Тогда 
				РегистрыСведений.ИсторияОбменаСТопЛогПоОбъектам.Добавить(ДокСсылка, вхПараметры.НомерСообщения, Истина, "Ошибка загрузки: "+ОписаниеОшибки(), , Ложь, НомерПотока); 
			КонецЕсли;
			//Семенов И.П. 06.02.2019 XX-1768(
			ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(ДокСсылка, ОбъектXDTO, , Истина, СтруктураОшибки.СообщениеОбОшибке);
			//)Семенов И.П.
			
			// 25.03.19 Строганов Роман > 
			ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокСсылка, Истина, Истина, СтруктураОшибки.СообщениеОбОшибке);
			// 25.03.19 Строганов Роман <
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьРезультатОтмены(ОбъектXDTO, ДопПараметры = Неопределено, СтруктураОтчета = Неопределено)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ЗагрузитьРезультатОтмены";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	Если ЗначениеЗаполнено(ОбъектXDTO.Ссылка) Тогда 
		ДокСсылка = Документы.ВозвратТоваровОтПокупателя.ПолучитьСсылку(Новый УникальныйИдентификатор(ОбъектXDTO.Ссылка));
		
		Если ОбменДаннымиКлиентСервер.ЭтоБитаяСсылка(ДокСсылка) Тогда 
			ВызватьИсключение "Не найден возврат товаров от покупателя с guid = " + ОбъектXDTO.Ссылка;
		КонецЕсли;
	Иначе
		ДокСсылка = Документы.ВозвратТоваровОтПокупателя.НайтиПоНомеру(ОбщегоНазначения.ПреобразоватьНомер(ОбъектXDTO.Номер), ТекущаяДата());
		Если Не ЗначениеЗаполнено(ДокСсылка) Тогда 
			ВызватьИсключение "Не найден возврат товаров от покупателя с номером = " + ОбъектXDTO.Номер;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыВозврата =   ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокСсылка, "СтатусДокумента, ОбновленИзТопЛог, СостояниеОтменыВТоплог");
	
	Если ЗначениеЗаполнено(РеквизитыВозврата.СостояниеОтменыВТоплог) Тогда
		//Уже приходил результат отмены
		
		// 25.03.19 Строганов Роман > 
		ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокСсылка, Истина, Ложь, "Уже приходил результат отмены");
		// 25.03.19 Строганов Роман <
		
		Возврат;
	КонецЕсли;
	
	Если РеквизитыВозврата.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят
		ИЛИ РеквизитыВозврата.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен Тогда
		//Уже приняли
		
		// 25.03.19 Строганов Роман > 
		ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокСсылка, Истина, Ложь, "Уже приняли");
		// 25.03.19 Строганов Роман <
		
		Возврат;
	КонецЕсли;   
	
	ДокументОбъект = ДокСсылка.ПолучитьОбъект();
	
	ДокументОбъект.СостояниеОтменыВТоплог = ?(ОбъектXDTO.Отменен = Истина, Перечисления.СостоянияОтменыЗаказаНаПриемкуВТоплог.Отменен, Перечисления.СостоянияОтменыЗаказаНаПриемкуВТоплог.НеОтменен);
	ДокументОбъект.ОбновленИзТопЛог = Истина;
	
	Если ДокументОбъект.СостояниеОтменыВТоплог = РеквизитыВозврата.СостояниеОтменыВТоплог
		И ДокументОбъект.ОбновленИзТопЛог =  РеквизитыВозврата.ОбновленИзТопЛог
		И  ДокументОбъект.СтатусДокумента =  РеквизитыВозврата.СтатусДокумента Тогда
		//Ничего не изменилось	
		
		// 25.03.19 Строганов Роман > 
		ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокСсылка, Истина, Ложь, "Ничего не изменилось");
		// 25.03.19 Строганов Роман <

		Возврат;
	КонецЕсли;
	
	Попытка
		
		//ХудинВВ 06052019 XX-2386 т.к. документ не принят или размещен, то можем перезаписать в закрытом периоде {{
		ДокументОбъект.ДополнительныеСвойства.Вставить("СнятьОграничениеПоДатеЗапрета", Истина);
		ДокументОбъект.ДополнительныеСвойства.Вставить("РежимБога", Истина);
		//}}
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		НомерСообщения = ?(ДопПараметры = Неопределено, 0, ДопПараметры.НомерСообщения);
		НомерПотока = ?(ДопПараметры = Неопределено, 0, ДопПараметры.НомерПотока);
		РегистрыСведений.ИсторияОбменаСТопЛогПоОбъектам.Добавить(ДокументОбъект.Ссылка, НомерСообщения, , , , Ложь, НомерПотока); 
		//Семенов И.П. 06.02.2019 XX-1768(
		ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(ДокументОбъект.Ссылка, ОбъектXDTO);
		//)Семенов И.П.
		
		// 25.03.19 Строганов Роман > 
		ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокументОбъект.Ссылка, Истина, Ложь);
		// 25.03.19 Строганов Роман <
	Исключение
		
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗагрузитьРезультатПриемки(ОбъектXDTO, ДопПараметры = Неопределено, СтруктураОтчета = Неопределено)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ЗагрузитьРезультатПриемки";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	Если ЗначениеЗаполнено(ОбъектXDTO.ЗаказСсылка) Тогда 
		ДокСсылка = Документы.ВозвратТоваровОтПокупателя.ПолучитьСсылку(Новый УникальныйИдентификатор(ОбъектXDTO.ЗаказСсылка));
		
		Если ОбменДаннымиКлиентСервер.ЭтоБитаяСсылка(ДокСсылка) Тогда 
			ВызватьИсключение "Не найден возврат товаров от покупателя с guid = " + ОбъектXDTO.ЗаказСсылка;
		КонецЕсли;
	Иначе
		ДокСсылка = Документы.ВозвратТоваровОтПокупателя.НайтиПоНомеру(ОбщегоНазначения.ПреобразоватьНомер(ОбъектXDTO.ЗаказНомер), ТекущаяДата());
		Если Не ЗначениеЗаполнено(ДокСсылка) Тогда 
			ВызватьИсключение "Не найден возврат товаров от покупателя с номером = " + ОбъектXDTO.ЗаказНомер;
		КонецЕсли;
	КонецЕсли;
	
	СтатусДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокСсылка, "СтатусДокумента");
	
	//Загружаем приемку только для документа в статусе Новый или отменен
	Если НЕ (СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый 
		ИЛИ СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен) Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьВходящее = РегистрыСведений.ИсторияОбменаСТопЛогПоОбъектам.ЕстьСообщениеПоОбъекту(ДокСсылка, Ложь);
	
	Если Не ЕстьВходящее Тогда 
		//Первая приемка
		Дата = ТекущаяДата();
	Иначе
		Дата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокСсылка, "Дата");
	КонецЕсли;
	
	ТоварыXDTO = ОбъектXDTO.Товары.ПолучитьСписок("СтрокаТовары");
	СоотвSSID = ОбменДаннымиКлиентСервер.СоответствиеСтрокЗаявокИSSID(ТоварыXDTO, ДокСсылка);
	
	ТоварыВМС = Новый ТаблицаЗначений;
	ТоварыВМС.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура")); 
	ТоварыВМС.Колонки.Добавить("СтрокаЗаявки", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыСтрокЗаявок"));
	ТоварыВМС.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(15,3)));
	
	ПричинаОтказаНаименованиеТопЛог = "";
	
	Для Каждого СтрокаТовары Из ТоварыXDTO Цикл
		
		Если ЗначениеЗаполнено(СтрокаТовары.Качество) Тогда
			ПричинаОтказаНаименованиеТопЛог = СтрокаТовары.Качество;			
		КонецЕсли;
		
		Если СтрокаТовары.ЭтоБрак Тогда 
			Продолжить;
		КонецЕсли;

		СтрокаЗаявки = ОбменДаннымиКлиентСервер.НайтиСтрокуЗаявкиВСоответствии(СоотвSSID, СтрокаТовары.SSID);
		
		Если Не ЗначениеЗаполнено(СтрокаЗаявки) Тогда 
			ВызватьИсключение "Не найдена строка заявки с IDSite = " + СтрокаТовары.SSID;
		Иначе
			НоваяСтрока = ТоварыВМС.Добавить();
			НоваяСтрока.СтрокаЗаявки = СтрокаЗаявки;
		КонецЕсли;
		
		НоваяСтрока.Количество = СтрокаТовары.КоличествоПринято;
		НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаТовары.НоменклатураСсылка));
		
	КонецЦикла;
	
	ТоварыВМС.Свернуть("Номенклатура,СтрокаЗаявки", "Количество");
	
	ДокументОбъект = ДокСсылка.ПолучитьОбъект();
	
	ТоварыСтарые 	= ДокументОбъект.Товары.Выгрузить();
	ТоварыНовые 	= ТоварыСтарые.СкопироватьКолонки();
	
	Для каждого стрТовары Из ТоварыВМС Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура,СтрокаЗаявки");
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, стрТовары);
		
		СтрокиОстатка = ТоварыСтарые.НайтиСтроки(СтруктураОтбора);
		
		Распределить = стрТовары.Количество;
		
		Если СтрокиОстатка.Количество() = 0 Тогда
			ВызватьИсключение "Не найдена строка с номенклатурой "+СтруктураОтбора.Номенклатура+", строкой заявки " + СтруктураОтбора.СтрокаЗаявки;
		КонецЕсли;
		
		Для каждого СтрокаОстатка Из СтрокиОстатка Цикл
			
			Распределено = Мин(Распределить, СтрокаОстатка.КоличествоПлан);
			Если Распределено < 0 Тогда
				Продолжить;
			КонецЕсли;
			
			
			НоваяСтрока = ТоварыНовые.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатка);
			НоваяСтрока.Количество = Распределено;
			
			Распределить = Распределить - Распределено;
			СтрокаОстатка.Количество = СтрокаОстатка.Количество - Распределено;
			
		КонецЦикла;
		
		Если Распределить > 0 Тогда
			//Остаток кидаем на последнюю строку
			НоваяСтрока.Количество = НоваяСтрока.Количество + Распределить;
		КонецЕсли;
		
		
	КонецЦикла;
	
	Если ТоварыНовые.Количество() = 0 Тогда
		//очистим принятое
		Для каждого Стр Из ДокументОбъект.Товары Цикл
			Стр.Количество = 0;
		КонецЦикла;
	Иначе
		ДокументОбъект.Товары.Загрузить(ТоварыНовые);
	КонецЕсли;
	ДокументОбъект.ПересчитатьСуммуТабличнойЧасти();
	ДокументОбъект.Дата = Дата;
	ДокументОбъект.ПометкаУдаления = Ложь;
	
	КодВозвратаЧисло = Число(ОбъектXDTO.КодВозврата);
	Если НЕ КодВозвратаЧисло = 0 Тогда
		//ВызватьИсключение "Не указан код возврата!";
		ДокументОбъект.КодВозврата = Справочники.КодыВозврата.НайтиПоКоду(КодВозвратаЧисло);
	КонецЕсли;
	
	СкладВСообщении = Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор(ОбъектXDTO.СкладСсылка));
	Если Не ЗначениеЗаполнено(СкладВСообщении.СкладВозвратов) Тогда
		ВызватьИсключение "Для склада "+СкладВСообщении+" не указан склад возврата!";
	Иначе
		ДокументОбъект.Склад = СкладВСообщении.СкладВозвратов;
	КонецЕсли;
	
	СтарыйСтатус = СтатусДокумента;
	
	Если ДокументОбъект.Товары.Итог("Количество") = 0 Тогда
		ДокументОбъект.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ;
	Иначе
		ДокументОбъект.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят;
	КонецЕсли;
	
	ДокументОбъект.ОбновленИзТопЛог = Истина;
	
	Если ЗначениеЗаполнено(ПричинаОтказаНаименованиеТопЛог) Тогда
		ПричинаОтказа = Справочники.ПричиныОтказаВВозвратеКлиенту.НайтиПоРеквизиту("НаименованиеТоплог", ПричинаОтказаНаименованиеТопЛог);
		Если Не ЗначениеЗаполнено(ПричинаОтказа) Тогда
			НоваяПричинаОтказа = Справочники.ПричиныОтказаВВозвратеКлиенту.СоздатьЭлемент();
			НоваяПричинаОтказа.НаименованиеТоплог = ПричинаОтказаНаименованиеТопЛог;
			НоваяПричинаОтказа.Наименование 	  = ПричинаОтказаНаименованиеТопЛог;
			НоваяПричинаОтказа.УстановитьНовыйКод();  
			НоваяПричинаОтказа.ОбменДанными.Загрузка = Истина;
			НоваяПричинаОтказа.Записать();
			ПричинаОтказа = НоваяПричинаОтказа.Ссылка;
			
			КритическиеСобытияСервер.ЗарегистрироватьКритическоеСобытие(
			ПричинаОтказа,
			Справочники.СобытияДляОтправкиЭлектронныхПисем.НоваяПричинаОтказаИзТоплог,
			"Нужно заполнить текст сообщения на сайт для новой причины отказа в возврате клиенту: "+НоваяПричинаОтказа.Наименование,
			,
			Истина,
			,
			"Документ.ВозвратТоваровОтПокупателя.МодульМенеджера.ЗагрузитьРезультатПриемки");
		КонецЕсли;
		ДокументОбъект.ПричинаОтказа = ПричинаОтказа;
	КонецЕсли;
	
	Попытка
		
		#Если Сервер Тогда
			ОбменСТопЛогСервер.ИницилизироватьДействияНадОбъектом(ДопПараметры);
			ОбменСТопЛогСервер.УстановитьПризнакНаличияПроведения(ДопПараметры);
		#КонецЕсли
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		#Если Сервер Тогда
			ОбменСТопЛогСервер.УстановитьПризнакУспешногоПроведения(ДопПараметры);
		#КонецЕсли
		
		НомерСообщения = ?(ДопПараметры = Неопределено, 0, ДопПараметры.НомерСообщения);
		НомерПотока = ?(ДопПараметры = Неопределено, 0, ДопПараметры.НомерПотока);
		РегистрыСведений.ИсторияОбменаСТопЛогПоОбъектам.Добавить(ДокументОбъект.Ссылка, НомерСообщения, , , , Ложь, НомерПотока); 
		//Семенов И.П. 06.02.2019 XX-1768(
		ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(ДокументОбъект.Ссылка, ОбъектXDTO);
		//)Семенов И.П.
		
		// 25.03.19 Строганов Роман > 
		ОбменДаннымиКлиентСервер.ЗафиксироватьРезультатВСтруктуреОтчета(СтруктураОтчета, ДокументОбъект.Ссылка, Истина, Ложь);
		// 25.03.19 Строганов Роман <
	Исключение
		//ДокументОбъект.СтатусДокумента = СтарыйСтатус;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		#Если Сервер Тогда
			ОбменСТопЛогСервер.ОбработатьДействияНадОбъектом(ДопПараметры, ДокументОбъект.Ссылка, ОписаниеОшибки(), "ЗагрузитьРезультатПриемки");
		#КонецЕсли
		
		Набор = РегистрыСведений.ОтложенноеПроведениеДокументовИзТопЛог.СоздатьНаборЗаписей();
		Набор.Отбор.СсылкаНаДокумент.Установить(ДокументОбъект.Ссылка);
		Стр = Набор.Добавить();
		Стр.СсылкаНаДокумент = ДокументОбъект.Ссылка;
		Набор.Записать();
		
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
	
КонецПроцедуры

//// ПЕЧАТЬ

Функция ПодготовитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента(Ссылка) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПодготовитьДанныеДляПечатиУниверсальногоКорректировочногоДокумента";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ДанныеДокумента = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Ссылка, 
	"Ссылка,Дата,ВидОперации,СтатусДокумента, Организация,Контрагент,ДоговорКонтрагента,ДокументОснование,ДокументОснование.Номер,ДокументОснование.Дата,ДокументОснование.УчитыватьНДС,АктРассмотренияВозврата,АктРассмотренияВозврата.СтатусДокумента");
	
	Если Не ЗначениеЗаполнено(ДанныеДокумента.ДокументОснование) Тогда
		ТекстСообщения = "Не указан документ-основание!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДанныеДокумента.ВидОперации = Перечисления.ВидыОперацийВозвратаОтПокупателя.ОбратнаяРеализация Тогда
		ТекстСообщения = "Для документа с видом операции ""Обратная продажа"" печать УКД запрещена!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	//ХудинВВ XX-1907
	Если НЕ (ДанныеДокумента.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят
		ИЛИ ДанныеДокумента.СтатусДокумента = Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен) Тогда
		ТекстСообщения = "Печать УКД разрешена для документа в статусе ""Принят"" или ""Размещен""!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	//ХудинВВ XX-2346 24052019
	Если Справочники.СтатусыДокументов.ЭтоСтатусКРО(ДанныеДокумента.АктРассмотренияВозвратаСтатусДокумента)
		И НЕ РольДоступна("ПолныеПрава") Тогда
		ТекстСообщения = "АРВ находится на расследовании у КРО, поэтому по нему запрещено печатать документы для покупателя!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДокументОснование", 	ДанныеДокумента.ДокументОснование);
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Дата", 				ДанныеДокумента.Дата);
	Запрос.УстановитьПараметр("ПустойКонтрагент", 	Справочники.Контрагенты.ПустаяСсылка());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Сумма) КАК Сумма,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВозвратТоваровОтПокупателяТовары.СтрокаЗаявки
	|ПОМЕСТИТЬ ВТСтрокиЗаявокВозвратов
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ВТТовары
	|		ПО (ВТТовары.Номенклатура = ВозвратТоваровОтПокупателяТовары.Номенклатура)
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.ДокументОснование = &ДокументОснование
	|	И ВозвратТоваровОтПокупателяТовары.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеализацияТоваровУслуг.Грузоотправитель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА ""он же""
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
//#XX-2394 Kalinin V.A. ( 2019-05-15 )  /*
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.КонтрагентВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.Контрагенты.Пустаяссылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.КонтрагентВзаиморасчетов
	|	КОНЕЦ КАК Покупатель,
//	|	РеализацияТоваровУслуг.Контрагент КАК Покупатель,
// */
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеализацияТоваровУслуг.Грузополучатель, &ПустойКонтрагент) = &ПустойКонтрагент
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК Сумма,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК Валюта,
	|	РеализацияТоваровУслуг.УчитыватьНДС КАК УчитыватьНДС,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	РеализацияТоваровУслуг.Склад.Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ВидАгентскогоДоговора
	|ПОМЕСТИТЬ ВТИсходныйДокумент
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИсходныйДокумент.Организация,
	|	ВТИсходныйДокумент.Покупатель,
	|	ВТИсходныйДокумент.Грузополучатель,
	|	ВТИсходныйДокумент.Поставщик,
	|	ВТИсходныйДокумент.Грузоотправитель,
	|	ВТИсходныйДокумент.Сумма,
	|	ВТИсходныйДокумент.Валюта,
	|	ВТИсходныйДокумент.УчитыватьНДС,
	|	ВТИсходныйДокумент.СуммаВключаетНДС,
	|	ВТИсходныйДокумент.Подразделение,
	|	ВТИсходныйДокумент.ВидАгентскогоДоговора
	|ИЗ
	|	ВТИсходныйДокумент КАК ВТИсходныйДокумент";
	
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Поставщик = Шапка.Поставщик;
	
	
	ДанныеДокумента.Вставить("Номер",                            ОбщегоНазначения.ПолучитьНомерНаПечать(Ссылка));
	ДанныеДокумента.Вставить("ВидСчетаФактуры",                  Перечисления.ВидСчетаФактурыВыставленного.Корректировочный);
	ДанныеДокумента.Вставить("Исправление",                      Ложь);
	ДанныеДокумента.Вставить("НомерИсправления",                 "");
	ДанныеДокумента.Вставить("НомерИсправляемогоКорректировочногоДокумента", "");
	ДанныеДокумента.Вставить("ДатаИсправляемогоКорректировочногоДокумента",  '00010101');
	ДанныеДокумента.Вставить("Продавец",                         ДанныеДокумента.Контрагент);
	ДанныеДокумента.Вставить("КППКонтрагента",                   "");
	ДанныеДокумента.Вставить("СчетФактураБезНДС",                Ложь);
		
	ДанныеДляПечати = Новый Структура();
	ДанныеДляПечати.Вставить("Организация",       	Шапка.Организация);
	ДанныеДляПечати.Вставить("Поставщик",         	Поставщик);
	ДанныеДляПечати.Вставить("Валюта",			  	Шапка.Валюта);
	ДанныеДляПечати.Вставить("Покупатель",        	Шапка.Покупатель);
	ДанныеДляПечати.Вставить("СуммаВключаетНДС",  	Шапка.СуммаВключаетНДС);
	ДанныеДляПечати.Вставить("Подразделение",     	Шапка.Подразделение);
	ДанныеДляПечати.Вставить("ВидАгентскогоДоговора",Шапка.ВидАгентскогоДоговора);	
	ДанныеДляПечати.Вставить("ДатаСчетФактуры",		ДанныеДокумента.Дата);
	ДанныеДляПечати.Вставить("ВидСчетаФактуры",		ДанныеДокумента.ВидСчетаФактуры);
	ДанныеДляПечати.Вставить("Ссылка",         		Ссылка);
	ДанныеДляПечати.Вставить("Номер",               ?(ДанныеДокумента.Исправление, ДанныеДокумента.НомерИсправляемогоКорректировочногоДокумента, ДанныеДокумента.Номер));
	ДанныеДляПечати.Вставить("Дата",                ?(ДанныеДокумента.Исправление, ДанныеДокумента.ДатаИсправляемогоКорректировочногоДокумента, ДанныеДокумента.Дата));
	ДанныеДляПечати.Вставить("НомерИсправления",    ?(ДанныеДокумента.Исправление, ДанныеДокумента.НомерИсправления, "--"));
	ДанныеДляПечати.Вставить("ДатаИсправления",     ?(ДанныеДокумента.Исправление, ДанныеДокумента.Дата, '00010101'));
	ДанныеДляПечати.Вставить("СчетФактураБезНДС",   ДанныеДокумента.СчетФактураБезНДС);
	ДанныеДляПечати.Вставить("КППКонтрагента",      ДанныеДокумента.КППКонтрагента);
	ДанныеДляПечати.Вставить("ВидСчетаФактуры",     ДанныеДокумента.ВидСчетаФактуры);
	
	
	Сф = УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка);
	Если ЗначениеЗаполнено(Сф) Тогда
		РеквизитыСФ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сф, "Номер, Дата, ИдентификаторГосКонтракта");
		ДанныеДляПечати.Вставить("ДатаСчетФактуры",     РеквизитыСФ.Дата);
		ДанныеДляПечати.Вставить("НомерСчетФактуры",    ОбщегоНазначения.ПолучитьНомерНаПечать(Сф));
		ДанныеДляПечати.Вставить("ИдентификаторГосКонтракта", УчетНДС.ТекстЗаголовкаИДГК(РеквизитыСФ.Дата) + РеквизитыСФ.ИдентификаторГосКонтракта);
	Иначе
		ДанныеДляПечати.Вставить("ДатаСчетФактуры",    Дата(1,1,1));
		ДанныеДляПечати.Вставить("НомерСчетФактуры",   "");
		ДанныеДляПечати.Вставить("ИдентификаторГосКонтракта", УчетНДС.ТекстЗаголовкаИДГК(ДанныеДокумента.Дата));
	КонецЕсли;   	
	
	
	НомерОснования 	= Неопределено;
	ДатаОснования 	= Неопределено;
	Если ДанныеДокумента.ДокументОснованиеУчитыватьНДС Тогда
		ДанныеОснования = ДанныеОснованияДляПечатиУКД(ДанныеДокумента);
		НомерОснования  = ДанныеОснования.НомерОснования;
		ДатаОснования   = ДанныеОснования.ДатаОснования;
	КонецЕсли;
	ДанныеДляПечати.Вставить("НомерОснования", 		НомерОснования);
	ДанныеДляПечати.Вставить("ДатаОснования", 		ДатаОснования);
	
	// 28.06.19 Строганов Роман > XX-2720: Добавился параметр ВидДокументаДоверенности. Если он заполнен - подбираются данные о доверенностях ответственных лиц. 
	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизации(Шапка.Организация, ДанныеДокумента.Дата,, Перечисления.ВидыПечатныхДокументов.УКД);
	// 28.06.19 Строганов Роман < XX-2720
	
	ДанныеДляПечати.Вставить("ФИОРуководителя", 		Руководители.Руководитель);
	ДанныеДляПечати.Вставить("ФИОГлавногоБухгалтера", 	Руководители.ГлавныйБухгалтер);
	ДанныеДляПечати.Вставить("ДолжностьРуководителя", 	Руководители.РуководительДолжность);
	
	УчитыватьСуммуВозвратов = Истина;
	
	//Товары первичной продажи
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(250)) КАК НаименованиеТовара,
	|	РеализацияТоваровУслугТовары.Номенклатура.Изготовитель.Наименование КАК ПроизводительНаименование,
	|	РеализацияТоваровУслугТовары.Номенклатура.Наименование КАК ТоварНаименование,
	|	РеализацияТоваровУслугТовары.ЕдиницаИзмерения КАК НаименованиеЕдиницыИзмерения,
	|	РеализацияТоваровУслугТовары.СтавкаНДС,
	|	РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслугТовары.Ссылка.УчитыватьНДС КАК УчитыватьНДС,
	|	РеализацияТоваровУслугТовары.Количество КАК КоличествоДоИзменения,
	|	РеализацияТоваровУслугТовары.Количество КАК КоличествоДляРаспределения,
	|	РеализацияТоваровУслугТовары.Количество КАК КоличествоПослеИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.Количество = 0
	|				ТОГДА 0
	|			ИНАЧЕ РеализацияТоваровУслугТовары.Сумма / РеализацияТоваровУслугТовары.Количество
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаСНДСДоИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.Количество = 0
	|				ТОГДА 0
	|			ИНАЧЕ РеализацияТоваровУслугТовары.Сумма / РеализацияТоваровУслугТовары.Количество
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаСНДСПослеИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.Количество = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА (РеализацияТоваровУслугТовары.Сумма - РеализацияТоваровУслугТовары.СуммаНДС) / РеализацияТоваровУслугТовары.Количество
	|					ИНАЧЕ РеализацияТоваровУслугТовары.Сумма / РеализацияТоваровУслугТовары.Количество
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаДоИзменения,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РеализацияТоваровУслугТовары.Количество = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА (РеализацияТоваровУслугТовары.Сумма - РеализацияТоваровУслугТовары.СуммаНДС) / РеализацияТоваровУслугТовары.Количество
	|					ИНАЧЕ РеализацияТоваровУслугТовары.Сумма / РеализацияТоваровУслугТовары.Количество
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ЦенаПослеИзменения,
	|	РеализацияТоваровУслугТовары.Сумма КАК СуммаДоИзменения,
	|	РеализацияТоваровУслугТовары.Сумма КАК СуммаПослеИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаНДСДоИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СуммаНДСПослеИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СтоимостьБезНДСПослеИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СтоимостьБезНДСДоИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СтоимостьСНДСПослеИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК СтоимостьСНДСДоИзменения,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаНДСУменьшение,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаНДСУвеличение,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаБезНДСУменьшение,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаБезНДСУвеличение,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаСНДСУменьшение,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК РазницаСНДСУвеличение,
	|	РеализацияТоваровУслугТовары.ЕдиницаИзмерения.ЕдиницаПоКлассификатору.Код КАК ЕдиницаИзмеренияКод,
	|	РеализацияТоваровУслугТовары.Номенклатура.Код КАК ТоварКод,
	|	РеализацияТоваровУслугТовары.Номенклатура.Артикул КАК ТоварАртикул
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ВТТовары
	|		ПО (ВТТовары.Номенклатура = РеализацияТоваровУслугТовары.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиЗаявокВозвратов КАК ВТСтрокиЗаявокВозвратов
	|		ПО (ВТСтрокиЗаявокВозвратов.СтрокаЗаявки = РеализацияТоваровУслугТовары.СтрокаЗаявки)
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ДокументОснование
	|	И РеализацияТоваровУслугТовары.Количество > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТСтрокиЗаявокВозвратов.СтрокаЗаявки ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	НомерСтроки";
	
	ТоварыРеализации = Запрос.Выполнить().Выгрузить();
	
	//Товары предыдущих возвратов
	//ХудинВВ XX-2560 24052019 Добавил условие на статус
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ВТТовары
	|		ПО (ВТТовары.Номенклатура = ВозвратТоваровОтПокупателяТовары.Номенклатура)
	|ГДЕ
	|	ВозвратТоваровОтПокупателяТовары.Ссылка.ДокументОснование = &ДокументОснование
	|	И ВозвратТоваровОтПокупателяТовары.Ссылка.Дата < &Дата
	|	И ВозвратТоваровОтПокупателяТовары.Ссылка.Проведен
	|	И (ВозвратТоваровОтПокупателяТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяПринят)
	|			ИЛИ ВозвратТоваровОтПокупателяТовары.Ссылка.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.ВозвратТоваровОтПокупателяРазмещен))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) > 0";
	ПредыдущиеВозвраты = Запрос.Выполнить().Выгрузить();
	
	//Вычтем из первичной отгрузки предыдущие возвраты, получим Таблицу ДО
	Для каждого СтрокаВозврата Из ПредыдущиеВозвраты Цикл
		
		КВозврату = СтрокаВозврата.Количество;
		
		СтрокиПродажи = ТоварыРеализации.НайтиСтроки(Новый Структура("Номенклатура", СтрокаВозврата.Номенклатура));
		
		Для каждого СтрокаПродажи Из СтрокиПродажи Цикл
			
			Списать = Мин(КВозврату, СтрокаПродажи.КоличествоДоИзменения);
			Если Списать = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаПродажи.КоличествоДоИзменения 		= СтрокаПродажи.КоличествоДоИзменения - Списать;
			СтрокаПродажи.КоличествоДляРаспределения 	= СтрокаПродажи.КоличествоДляРаспределения - Списать;
			
			СтрокаПродажи.КоличествоПослеИзменения 		= СтрокаПродажи.КоличествоДоИзменения;
			
			//Суммы
			СуммаВозврата = ?(СтрокаВозврата.Количество = Списать, 
													 СтрокаВозврата.Сумма, 
													(СтрокаВозврата.Сумма/СтрокаВозврата.Количество)*Списать);
													
			СуммаНДСВозврата = ?(СтрокаВозврата.Количество = Списать, 
										СтрокаВозврата.СуммаНДС, 
										(СтрокаВозврата.СуммаНДС/СтрокаВозврата.Количество)*Списать);
										
			СтрокаПродажи.СуммаДоИзменения = СтрокаПродажи.СуммаДоИзменения - СуммаВозврата;
													
			СтрокаПродажи.ЦенаСНДСДоИзменения = ?(СтрокаПродажи.КоличествоДоИзменения = 0,
													 0, 
													СтрокаПродажи.СуммаДоИзменения/СтрокаПродажи.КоличествоДоИзменения);
													
			//Правильнее будет вычесть сумму ндс возврата, но реализация могла быть без ндс, поэтому рассчитаем										
			СтрокаПродажи.СуммаНДСДоИзменения = УчетНДС.РассчитатьСуммуНДС(СтрокаПродажи.СуммаДоИзменения, 
																		СтрокаПродажи.УчитыватьНДС, 
																		СтрокаПродажи.СуммаВключаетНДС, 
																		СтрокаПродажи.СтавкаНДС);
													
			СтрокаПродажи.ЦенаДоИзменения = ?(СтрокаПродажи.КоличествоДоИзменения = 0,
													 0, 
													?(СтрокаПродажи.СуммаВключаетНДС,
													(СтрокаПродажи.СуммаДоИзменения-СтрокаПродажи.СуммаНДСДоИзменения)/СтрокаПродажи.КоличествоДоИзменения,
													СтрокаПродажи.СуммаДоИзменения/СтрокаПродажи.КоличествоДоИзменения));

													
			СтрокаПродажи.СуммаПослеИзменения = СтрокаПродажи.СуммаДоИзменения;
			СтрокаПродажи.ЦенаСНДСПослеИзменения = СтрокаПродажи.ЦенаСНДСДоИзменения;
			СтрокаПродажи.СуммаНДСПослеИзменения = СтрокаПродажи.СуммаНДСДоИзменения;
			СтрокаПродажи.ЦенаПослеИзменения = СтрокаПродажи.ЦенаДоИзменения;

			КВозврату = КВозврату - Списать;
		КонецЦикла;
		
	КонецЦикла;
	
	//Товары текущего возврата
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.Количество КАК Количество,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДС КАК СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.Сумма КАК Сумма
	|ИЗ
	|	ВТТовары КАК ВозвратТоваровОтПокупателяТовары";
	ТекущиеВозвраты = Запрос.Выполнить().Выгрузить();
	
	//Заполним количество после
	Для каждого СтрокаВозврата Из ТекущиеВозвраты Цикл
		
		КВозврату = СтрокаВозврата.Количество;
		
		СтрокиПродажи = ТоварыРеализации.НайтиСтроки(Новый Структура("Номенклатура", СтрокаВозврата.Номенклатура));
		
		Для каждого СтрокаПродажи Из СтрокиПродажи Цикл
			
			Списать = Мин(КВозврату, СтрокаПродажи.КоличествоДляРаспределения);
			Если Списать = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаПродажи.КоличествоПослеИзменения = СтрокаПродажи.КоличествоПослеИзменения - Списать;
			
			СтрокаПродажи.КоличествоДляРаспределения = СтрокаПродажи.КоличествоДляРаспределения - Списать;
			
			СуммаВозврата = ?(СтрокаВозврата.Количество = Списать, 
															СтрокаВозврата.Сумма, 
															(СтрокаВозврата.Сумма/СтрокаВозврата.Количество)*Списать);
															
			СуммаНДСВозврата = ?(СтрокаВозврата.Количество = Списать, 
										СтрокаВозврата.СуммаНДС, 
										(СтрокаВозврата.СуммаНДС/СтрокаВозврата.Количество)*Списать);
										
			//РазницаНДСУменьшение
			СтрокаПродажи.РазницаНДСУменьшение = СуммаНДСВозврата;
				
			//РазницаСНДСУменьшение
			СтрокаПродажи.РазницаСНДСУменьшение = ?(СтрокаПродажи.СуммаВключаетНДС, СуммаВозврата, СуммаВозврата + СуммаНДСВозврата);
				
			//РазницаБезНДСУменьшение
			СтрокаПродажи.РазницаБезНДСУменьшение = ?(СтрокаПродажи.СуммаВключаетНДС, СуммаВозврата - СуммаНДСВозврата, СуммаВозврата);
				
			СтрокаПродажи.СуммаПослеИзменения = СтрокаПродажи.СуммаПослеИзменения - СуммаВозврата;
				
			СтрокаПродажи.ЦенаСНДСПослеИзменения = ?(СтрокаПродажи.КоличествоПослеИзменения = 0,
															0, 
															СтрокаПродажи.СуммаПослеИзменения/СтрокаПродажи.КоличествоПослеИзменения);
			
			КВозврату = КВозврату - Списать;
			
		КонецЦикла;
		
	КонецЦикла;
	
	//Рассчитаем суммы
	СтрокиУдалить = Новый Массив;
	Для каждого СтрокаТаблицы Из ТоварыРеализации Цикл
		
		Если СтрокаТаблицы.КоличествоПослеИзменения = СтрокаТаблицы.КоличествоДоИзменения Тогда
			СтрокиУдалить.Добавить(СтрокаТаблицы);
			Продолжить;			
		КонецЕсли;
		
		СуммаДоИзменения 		= СтрокаТаблицы.СуммаДоИзменения; 
		СуммаПослеИзменения 	= СтрокаТаблицы.СуммаПослеИзменения;		
		
		СтрокаТаблицы.СуммаНДСДоИзменения = УчетНДС.РассчитатьСуммуНДС(СуммаДоИзменения, 
																		СтрокаТаблицы.УчитыватьНДС, 
																		СтрокаТаблицы.СуммаВключаетНДС, 
																		СтрокаТаблицы.СтавкаНДС);
																		
		СтрокаТаблицы.СуммаНДСПослеИзменения = 	СтрокаТаблицы.СуммаНДСДоИзменения - СтрокаТаблицы.РазницаНДСУменьшение;															
		СтрокаТаблицы.ЦенаПослеИзменения 	 = ?(СтрокаТаблицы.КоличествоПослеИзменения = 0,
													0, 
													?(СтрокаТаблицы.СуммаВключаетНДС,
													(СтрокаТаблицы.СуммаПослеИзменения-СтрокаТаблицы.СуммаНДСПослеИзменения)/СтрокаТаблицы.КоличествоПослеИзменения,
													СтрокаТаблицы.СуммаПослеИзменения/СтрокаТаблицы.КоличествоПослеИзменения));

													
		СтрокаТаблицы.СтоимостьБезНДСДоИзменения 	= ?(СтрокаТаблицы.СуммаВключаетНДС, СуммаДоИзменения - СтрокаТаблицы.СуммаНДСДоИзменения, СуммаДоИзменения);
		СтрокаТаблицы.СтоимостьБезНДСПослеИзменения = ?(СтрокаТаблицы.СуммаВключаетНДС, СуммаПослеИзменения - СтрокаТаблицы.СуммаНДСПослеИзменения, СуммаПослеИзменения);
		
		СтрокаТаблицы.СтоимостьСНДСДоИзменения =  ?(СтрокаТаблицы.СуммаВключаетНДС, СуммаДоИзменения, СуммаДоИзменения + СтрокаТаблицы.СуммаНДСДоИзменения);
		СтрокаТаблицы.СтоимостьСНДСПослеИзменения =  ?(СтрокаТаблицы.СуммаВключаетНДС, СуммаПослеИзменения, СуммаПослеИзменения + СтрокаТаблицы.СуммаНДСПослеИзменения);

		
		//РазницаНДСУвеличение
		СтрокаТаблицы.РазницаНДСУвеличение = ?(СтрокаТаблицы.СуммаНДСДоИзменения - СтрокаТаблицы.СуммаНДСПослеИзменения < 0, 
		(СтрокаТаблицы.СуммаНДСДоИзменения - СтрокаТаблицы.СуммаНДСПослеИзменения)*(-1), 0);
		

		//РазницаБезНДСУвеличение
		Если СтрокаТаблицы.СуммаВключаетНДС	Тогда
			СтрокаТаблицы.РазницаБезНДСУвеличение = ?(СуммаДоИзменения - СтрокаТаблицы.СуммаНДСДоИзменения - (СуммаПослеИзменения - СтрокаТаблицы.СуммаНДСПослеИзменения) < 0,
			(СуммаДоИзменения - СтрокаТаблицы.СуммаНДСДоИзменения - (СуммаПослеИзменения - СтрокаТаблицы.СуммаНДСПослеИзменения))*(-1),
			0);
		Иначе
			СтрокаТаблицы.РазницаБезНДСУвеличение = ?(СуммаДоИзменения - СуммаПослеИзменения < 0,
			(СуммаДоИзменения - СуммаПослеИзменения)*(-1),
			0);
		КонецЕсли;
		
		//РазницаСНДСУвеличение
		Если СтрокаТаблицы.СуммаВключаетНДС	Тогда
			СтрокаТаблицы.РазницаСНДСУвеличение = ?(СуммаДоИзменения - СуммаПослеИзменения < 0,
			(СуммаДоИзменения - СуммаПослеИзменения)*(-1),
			0);
		Иначе
			СтрокаТаблицы.РазницаСНДСУвеличение = ?(СуммаДоИзменения + СтрокаТаблицы.СуммаНДСДоИзменения - (СуммаПослеИзменения + СтрокаТаблицы.СуммаНДСПослеИзменения) < 0,
			(СуммаДоИзменения + СтрокаТаблицы.СуммаНДСДоИзменения - (СуммаПослеИзменения + СтрокаТаблицы.СуммаНДСПослеИзменения))*(-1),
			0);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого СтрокаУдалить Из СтрокиУдалить Цикл
		 ТоварыРеализации.Удалить(СтрокаУдалить);		
	КонецЦикла;
	
	Для Каждого Строчка Из ТоварыРеализации Цикл
		Строчка.НаименованиеТовара = ?(ПустаяСтрока(Строчка.ТоварАртикул), "", Строчка.ТоварАртикул + " || ") + Строчка.ТоварНаименование + ?(ПустаяСтрока(Строчка.ПроизводительНаименование), "", " || " + Строчка.ПроизводительНаименование);
		Строчка.НомерСтроки = ТоварыРеализации.Индекс(Строчка)+1;
	КонецЦикла;
	
	ДанныеДляПечати.Вставить("ТабличнаяЧасть", ТоварыРеализации); 
	
	СФ = УчетНДС.НайтиПодчиненныйДокумент(ДанныеДокумента.ДокументОснование, "СчетФактураВыданный");
	Если ЗначениеЗаполнено(СФ) Тогда
		НомерУПД = СФ.Номер;
		ДатаУПД = СФ.Дата;
	Иначе
		НомерУПД = ДанныеДокумента.ДокументОснованиеНомер;
		ДатаУПД = ДанныеДокумента.ДокументОснованиеДата;
	КонецЕсли;
	НомерДатаПервичногоДокумента = "№ "+ НомерУПД + " от " + Формат(ДатаУПД, "ДФ=дд.ММ.гггг");
	РеквизитыПередаточныхДокументов = "Универсальный передаточный документ " + НомерДатаПервичногоДокумента;
	ДанныеДляПечати.Вставить("РеквизитыПередаточныхДокументов", РеквизитыПередаточныхДокументов);
	
	ДанныеДляПечати.Вставить("Основание", Строка(ДанныеДокумента.ДоговорКонтрагента));
	
	Если Не ДанныеДокумента.ДокументОснованиеУчитыватьНДС Тогда
		ДанныеДляПечати.Вставить("СтатусУКД", 2);
	Иначе
		ДанныеДляПечати.Вставить("СтатусУКД", 1);
	КонецЕсли;

	Возврат ДанныеДляПечати;

КонецФункции // СобратьДанныеДляПечатиКорректировочногоСчетаФактуры

// Функция формирует табличный документ с печатной формой универсального корректировочного документа
//
// Возвращаемое значение:
//  Табличный документ - печатная форма универсального корректировочного документа
//
Функция ПечатьУниверсальногоКорректировочногоДокумента(ДанныеДляПечати, ТабДокумент = Неопределено) Экспорт
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ПечатьУниверсальногоКорректировочногоДокумента";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////	
	
	Если ДанныеДляПечати = Неопределено Тогда
		Возврат ДанныеДляПечати;
	КонецЕсли;
	
	//Для документа с признаком учета ндс, обязательно быть создана счет-фактура
	
	
	Таблица = ДанныеДляПечати.ТабличнаяЧасть;
	
	СуммаУменьшения = Таблица.Итог("РазницаБезНДСУменьшение") + Таблица.Итог("РазницаСНДСУменьшение") + Таблица.Итог("РазницаНДСУменьшение");
	СуммаУвеличения = Таблица.Итог("РазницаБезНДСУвеличение") + Таблица.Итог("РазницаСНДСУвеличение") + Таблица.Итог("РазницаНДСУвеличение");
	
	//ХудинВВ XX-1907
	//Если СуммаУменьшения = 0 И СуммаУвеличения = 0 Тогда
	//	ТекстСообщения = "Печать универсального корректировочного документа без изменения суммовых показателей не возможна!";
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	//	Возврат Неопределено;
	//КонецЕсли;
	
	Если ТабДокумент = Неопределено Тогда		
		ТабДокумент = Новый ТабличныйДокумент;
		
		// Устанавливаем параметры печати и колонтитулы
		ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ТабДокумент.ПолеСверху = 12;
		ТабДокумент.ПолеСлева  = 0;
		ТабДокумент.ПолеСнизу  = 0;
		ТабДокумент.ПолеСправа = 0;
		ТабДокумент.АвтоМасштаб = Истина;
		ТабДокумент.Защита	   = НЕ РольДоступна("ПолныеПрава");
		
	КонецЕсли;
	
	Если ДанныеДляПечати.Дата < УчетНДС.НачалоДействияПостановления981() Тогда
		Макет = ПолучитьОбщийМакет("УниверсальныйКорректировочныйДокумент");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УниверсальныйКорректировочныйДокумент";
	Иначе
		Макет = ПолучитьОбщийМакет("УниверсальныйКорректировочныйДокумент981");
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_УниверсальныйКорректировочныйДокумент981";
	КонецЕсли;
	
	УчетНДС.ВывестиКорректировочныйСчетФактуруВТабличныйДокумент(ТабДокумент, Макет, ДанныеДляПечати);
	
	УчетНДС.ВывестиПодвалУниверсальногоКорректировочногоДокументаВТабличныйДокумент(ТабДокумент, Макет, ДанныеДляПечати);
	
	Возврат ТабДокумент;

КонецФункции

Функция ДанныеОснованияДляПечатиУКД(ДанныеДокумента)
	
	лКлючАлгоритма = "Документ_ВозвратТоваровОтПокупателя_МодульМенеджера_ДанныеОснованияДляПечатиУКД";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////	
	
	ДанныеОснования = Новый Структура("Ссылка, НомерОснования, ДатаОснования");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Ссылка,
		|	СчетФактураВыданный.Номер КАК НомерОснования,
		|	СчетФактураВыданный.Дата КАК ДатаОснования
		|ПОМЕСТИТЬ втДокументы
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.ДокументОснование = &ДокументОснование
		|	И (СчетФактураВыданный.Проведен
		|			ИЛИ СчетФактураВыданный.ДокументОснование.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиРеализаций.База77))
		|	И НЕ СчетФактураВыданный.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка,
		|	ВозвратТоваровОтПокупателя.Номер,
		|	ВозвратТоваровОтПокупателя.Дата
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.Ссылка <> &Ссылка
		|	И ВозвратТоваровОтПокупателя.ДокументОснование = &ДокументОснование
		|	И ВозвратТоваровОтПокупателя.МоментВремени < &МоментВремени
		|	И ВозвратТоваровОтПокупателя.Ссылка.Проведен
		|	И НЕ ВозвратТоваровОтПокупателя.Ссылка.СтатусДокумента В (&ИсключаемыеСтатусы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	втДокументы.Ссылка,
		|	втДокументы.НомерОснования,
		|	втДокументы.ДатаОснования
		|ИЗ
		|	втДокументы КАК втДокументы
		|
		|УПОРЯДОЧИТЬ ПО
		|	втДокументы.ДатаОснования УБЫВ";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеДокумента.ДокументОснование);
	Запрос.УстановитьПараметр("Ссылка", ДанныеДокумента.Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", ДанныеДокумента.Ссылка.МоментВремени());
	
	ИсключаемыеСтатусы = Новый Массив;
	ИсключаемыеСтатусы.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтказ);
	ИсключаемыеСтатусы.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяОтменен);
	ИсключаемыеСтатусы.Добавить(Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый);
	Запрос.УстановитьПараметр("ИсключаемыеСтатусы", ИсключаемыеСтатусы);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			СФ = УчетНДС.НайтиПодчиненныйСчетФактуру(Выборка.Ссылка);
			Если ЗначениеЗаполнено(СФ) И СФ.Проведен Тогда
				Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СФ, "Номер, Дата");
				ДанныеОснования.НомерОснования 	= Реквизиты.Номер;
				ДанныеОснования.ДатаОснования 	= Реквизиты.Дата;
			Иначе
				ДанныеОснования.НомерОснования 	= Выборка.НомерОснования;
				ДанныеОснования.ДатаОснования 	= Выборка.ДатаОснования;
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(ДанныеОснования, Выборка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОснования;
	
КонецФункции
