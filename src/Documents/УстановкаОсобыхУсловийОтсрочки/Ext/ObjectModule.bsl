Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Документы.УстановкаОсобыхУсловийОтсрочки.ВыполнитьПроведение(Ссылка, Отказ);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Документы.УстановкаОсобыхУсловийОтсрочки.ВыполнитьОтменуПроведения(Ссылка, Отказ);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Дата <> НачалоДня(Дата) Тогда
		Дата = НачалоДня(Дата);
	КонецЕсли;
	
	ЗаполнитьРеквизитыСтроковыхПредставлений();
	
КонецПроцедуры

Процедура  ЗаполнитьРеквизитыСтроковыхПредставлений()
	
	ЗаполнитьИзготовителиСтрокой();	
	ЗаполнитьТипПоставкиСтрокой();
	
КонецПроцедуры

Процедура  ЗаполнитьИзготовителиСтрокой()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Изготовители.Изготовитель
	|ПОМЕСТИТЬ Изготовители
	|ИЗ
	|	&Изготовители КАК Изготовители
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(Изготовители.Изготовитель) КАК Изготовитель
	|ИЗ
	|	Изготовители КАК Изготовители";
	
	Запрос.УстановитьПараметр("Изготовители", ОсобыеУсловия.Выгрузить(,"Изготовитель"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИзготовителиСтрокой_Нов = "";
	Пока Выборка.Следующий() Цикл
		ИзготовителиСтрокой_Нов = ИзготовителиСтрокой_Нов + ?(ИзготовителиСтрокой_Нов = "", "", ", ") + Выборка.Изготовитель;
	КонецЦикла;
	
	Если ИзготовителиСтрокой <> ИзготовителиСтрокой_Нов Тогда
		ИзготовителиСтрокой = ИзготовителиСтрокой_Нов;
	КонецЕсли;
	
КонецПроцедуры

Процедура  ЗаполнитьТипПоставкиСтрокой()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТипыПоставки.ТипПоставки
	|ПОМЕСТИТЬ ТипыПоставки
	|ИЗ
	|	&ТипыПоставки КАК ТипыПоставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(ТипыПоставки.ТипПоставки) КАК ТипПоставки
	|ИЗ
	|	ТипыПоставки КАК ТипыПоставки";
	
	Запрос.УстановитьПараметр("ТипыПоставки", ОсобыеУсловия.Выгрузить(,"ТипПоставки"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТипПоставкиСтрокой_Нов = "";
	Пока Выборка.Следующий() Цикл
		ТипПоставкиСтрокой_Нов = ТипПоставкиСтрокой_Нов + ?(ТипПоставкиСтрокой_Нов = "", "", ", ") + Выборка.ТипПоставки;
	КонецЦикла;
	
	Если ТипПоставкиСтрокой <> ТипПоставкиСтрокой_Нов Тогда
		ТипПоставкиСтрокой = ТипПоставкиСтрокой_Нов;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

