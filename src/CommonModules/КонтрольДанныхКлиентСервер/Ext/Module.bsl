Функция ПолучитьОшибкиВыгрузкиРТУ(Период,Событие) Экспорт
	
	лКлючАлгоритма = "ОбщийМодуль_КонтрольДанныхКлиентСервер_ПолучитьОшибкиВыгрузкиРТУ";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;
		Выполнить(лЗамена);
		Возврат АлгоритмыЗначениеВозврата;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	ВЫБОР
	|		КОГДА ИсторияОбменовПоОбъектам.Ошибка
	|			ТОГДА ""При выгрузке произошли Ошибки"" + ИсторияОбменовПоОбъектам.ТекстОшибки
	|		ИНАЧЕ ""Документ не выгружен""
	|	КОНЕЦ КАК ОписаниеОшибки,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.ТипДоставки) КАК ТипДоставки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияОбменовПоОбъектам КАК ИсторияОбменовПоОбъектам
	|		ПО РеализацияТоваровУслуг.Ссылка = ИсторияОбменовПоОбъектам.Объект
	|			И (ИсторияОбменовПоОбъектам.Узел = &УзелТоплог)
	|			И (ИсторияОбменовПоОбъектам.Исходящее = ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	(ИсторияОбменовПоОбъектам.Объект ЕСТЬ NULL
	|			ИЛИ ИсторияОбменовПоОбъектам.Ошибка)
	|	И РеализацияТоваровУслуг.Проведен = ИСТИНА
	|	И РеализацияТоваровУслуг.ЭтоМФП = ЛОЖЬ
	|	И РеализацияТоваровУслуг.флНеВыгружатьВТопЛог = ЛОЖЬ
	|	И РеализацияТоваровУслуг.Дата >= &НачалоПериода
	|	И НЕ РеализацияТоваровУслуг.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.РеализацияТоваровУслугНовый)
	|	И РеализацияТоваровУслуг.Дата <= &КонецПериода
	|	И РеализацияТоваровУслуг.Склад.ОбменСTopLog = ИСТИНА";
	
	Запрос.УстановитьПараметр("КонецПериода", ТекущаяДата() - 600);  //10 минут временой лаг на выгрузку в топлог
	Запрос.УстановитьПараметр("НачалоПериода", ТекущаяДата() - Период * 24*60*60);
	Запрос.УстановитьПараметр("УзелТоплог",    ОбменССайтомСервер.УзелПланаОбмена("ОбменПартКом83_TopLog_РТУ",Ложь));
	Запрос.УстановитьПараметр("Событие", Событие);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

Функция ПолучитьОшибкиСтатусовРТУ(Период, Событие) Экспорт
	
	лКлючАлгоритма = "ОбщийМодуль_КонтрольДанныхКлиентСервер_ПолучитьОшибкиСтатусовРТУ";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;
		Выполнить(лЗамена);
		Возврат АлгоритмыЗначениеВозврата;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.СтатусДокумента) КАК СтатусДокумента,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Контрагент) КАК Контрагент
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиСайта КАК УчетныеЗаписиСайта
	|		ПО РеализацияТоваровУслуг.ТорговаяТочка = УчетныеЗаписиСайта.Владелец
	// 07.06.19 Строганов Роман > Вырезаю в рамках XX-2484
	//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КритическиеСобытияСистемы КАК КритическиеСобытияСистемы
	//|		ПО РеализацияТоваровУслуг.Ссылка = КритическиеСобытияСистемы.ИсточникКритическогоСобытия
	//|			И (КритическиеСобытияСистемы.Событие = &Событие)
	// 07.06.19 Строганов Роман < Вырезаю в рамках XX-2484 
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен = ИСТИНА
	|	И НЕ РеализацияТоваровУслуг.СтатусДокумента = ЗНАЧЕНИЕ(Справочник.СтатусыДокументов.РеализацияТоваровУслугОтгружен)
	|	И РеализацияТоваровУслуг.ЭтоМФП = ЛОЖЬ
	|	И РеализацияТоваровУслуг.Дата > &Период
	|	И ВЫБОР
	|			КОГДА УчетныеЗаписиСайта.Ссылка ЕСТЬ NULL
	|				ТОГДА РеализацияТоваровУслуг.Дата <= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(&ТекущаяДата КАК ДАТА), ДЕНЬ), ДЕНЬ, -7)
	// 30.05.19 Строганов Роман > XX-2484: 3 дня для транзитных РТУ
	|			КОГДА РеализацияТоваровУслуг.Склад.Филиал <> РеализацияТоваровУслуг.Контрагент.Регион.Филиал
	|				ТОГДА РеализацияТоваровУслуг.Дата <= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(&ТекущаяДата КАК ДАТА), ДЕНЬ), ДЕНЬ, -3)
	// 30.05.19 Строганов Роман < XX-2484: 3 дня для транзитных РТУ
	|			ИНАЧЕ РеализацияТоваровУслуг.Дата <= ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(&ТекущаяДата КАК ДАТА), ДЕНЬ), ДЕНЬ, -1)
	|		КОНЕЦ
	|	И РеализацияТоваровУслуг.флНеВыгружатьВТопЛог = ЛОЖЬ";
	// 07.06.19 Строганов Роман > Вырезаю в рамках XX-2484
	//	|	И КритическиеСобытияСистемы.ИсточникКритическогоСобытия ЕСТЬ NULL";
	// 07.06.19 Строганов Роман < Вырезаю в рамках XX-2484

	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("Событие", Событие);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции	

Функция ПолучитьОшибкиВыпискиVMIбезРасписания(ДатаНачала,ДатаОкончания,Событие) Экспорт
	
	лКлючАлгоритма = "ОбщийМодуль_КонтрольДанныхКлиентСервер_ПолучитьОшибкиВыпискиVMIбезРасписания";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;
		Выполнить(лЗамена);
		Возврат АлгоритмыЗначениеВозврата;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПартииТоваровVMI.Регистратор КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровVMI.Склад) КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровVMI.ДоговорКонтрагента.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ПартииТоваровVMI.ДоговорКонтрагента.Владелец) КАК Контрагент
	|ИЗ
	|	РегистрНакопления.ПартииТоваровVMI КАК ПартииТоваровVMI
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасписанияОтчетовПоставщикамVMI КАК РасписанияОтчетовПоставщикамVMI
	|		ПО ПартииТоваровVMI.ДоговорКонтрагента.Организация = РасписанияОтчетовПоставщикамVMI.Организация
	|			И ПартииТоваровVMI.Склад = РасписанияОтчетовПоставщикамVMI.Склад
	|			И ПартииТоваровVMI.ДоговорКонтрагента.Владелец = РасписанияОтчетовПоставщикамVMI.ПрайсПоставщика.Владелец
	|			И (РасписанияОтчетовПоставщикамVMI.Используется = ИСТИНА)
	|			И (РасписанияОтчетовПоставщикамVMI.НачальнаяДата < &НачалоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КритическиеСобытияСистемы КАК КритическиеСобытияСистемы
	|		ПО ПартииТоваровVMI.Регистратор = КритическиеСобытияСистемы.ИсточникКритическогоСобытия
	|			И (КритическиеСобытияСистемы.Событие = &Событие)
	|ГДЕ
	|	ПартииТоваровVMI.Период >= &НачалоПериода
	|	И ПартииТоваровVMI.Период <= &КонецПериода
	|	И ПартииТоваровVMI.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ПартииТоваровVMI.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И РасписанияОтчетовПоставщикамVMI.ПрайсПоставщика ЕСТЬ NULL
	|	И ПартииТоваровVMI.ДоговорКонтрагента.СлужебныйДоговор = ЛОЖЬ
	|	И ПартииТоваровVMI.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ОтветХранение)
	|	И ПартииТоваровVMI.ДоговорКонтрагента.ПометкаУдаления = ЛОЖЬ
	|	И КритическиеСобытияСистемы.ИсточникКритическогоСобытия ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("Событие", Событие);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции	 
