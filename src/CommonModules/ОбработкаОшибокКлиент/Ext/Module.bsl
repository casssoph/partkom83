
Функция ПолучитьОписаниеОшибки(вхСсылкаИлиОбъект)
	
	Результат = Неопределено;
	лЭтоОбъект = (вхСсылкаИлиОбъект <> вхСсылкаИлиОбъект.Ссылка);
	Если лЭтоОбъект И вхСсылкаИлиОбъект.ЭтоНовый() тогда
		Возврат Результат;
	КонецЕсли;
	
	лЗапрос = Новый Запрос;
	лЗапрос.УстановитьПараметр("Объект", вхСсылкаИлиОбъект.Ссылка);
	лЗапрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Р.ДатаОшибки,
	|	Р.ОписаниеОшибки
	|ИЗ
	|	РегистрСведений.ОписанияОшибокОбъектов КАК Р
	|ГДЕ
	|	Р.Объект = &Объект";
	лВыборка = лЗапрос.Выполнить().Выбрать();
	Если лВыборка.Следующий() тогда
		Результат = Новый Структура("ДатаОшибки,ОписаниеОшибки");
		ЗаполнитьЗначенияСвойств(Результат, лВыборка);
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьХТМЛ_Пустой()
	Возврат 
	"<html><body></body></html>";	
КонецФункции

Функция ПолучитьХТМЛ_ОписаниеОшибки(вхОписаниеОшибки)
	Возврат 
	"<html>
	|	<head>
	|		<style>
	|			html {
	|				overflow: hidden;
	|				margin: 0;
	|				padding: 0;
	|				width: 100%;
	|				height: 100%;
	|			}
	|			body {
	|				cursor: arrow;
	|				margin: 3px;
	|				padding: 0 0 0 60px;
	|				overflow: hidden;
	|				text-overflow: ellipsis;
	|				background-color: #FFDDDD;
	|				background-image: url(file://" + мВременныеФайлыКартинок.Кирпич48 + ");
	|				background-repeat: no-repeat;
	|				background-position: 5px 5px;
	|				border: 1px solid #FF0000;
	|				width: 100%;
	|				height: 100%;
	|			}
	|			td {
	|				font-family: Tahoma, sans-serif;
	|				font-size: 8pt;
	|			}
	|		</style>
	|	</head>
	|	<body>
	|		<table width=100% height=100% border=0>
	|			<tr>
	|				<td valign=middle>
	|					" + вхОписаниеОшибки.ОписаниеОшибки + "
	|				</td>
	|			</tr>
	|		</table>
	|	</body>
	|</html>";
КонецФункции

Процедура ОбработатьПанельОписанияОшибок(вхФорма) Экспорт
	
	лСсылкаИлиОбъект = вхФорма.ЭтотОбъект;
	лПанельОписаниеОшибки = вхФорма.ЭлементыФормы.Найти("ПанельОписаниеОшибки");
	Если (лПанельОписаниеОшибки = Неопределено) тогда
		Возврат;
	КонецЕсли;
	лПолеДокументаОписаниеОшибки = вхФорма.ЭлементыФормы.Найти("ПолеДокументаОписаниеОшибки");
	Если (лПолеДокументаОписаниеОшибки = Неопределено) тогда
		Возврат;
	КонецЕсли;
	
	лОписаниеОшибки = ПолучитьОписаниеОшибки(вхФорма.ЭтотОбъект);
	Если (лОписаниеОшибки = Неопределено) тогда
		лПолеДокументаОписаниеОшибки.УстановитьТекст(ПолучитьХТМЛ_Пустой());
		лПанельОписаниеОшибки.Свертка = РежимСверткиЭлементаУправления.Верх;
	Иначе
		лПолеДокументаОписаниеОшибки.УстановитьТекст(ПолучитьХТМЛ_ОписаниеОшибки(лОписаниеОшибки));
		лПанельОписаниеОшибки.Свертка = РежимСверткиЭлементаУправления.Нет;				
	КонецЕсли;
	
КонецПроцедуры
