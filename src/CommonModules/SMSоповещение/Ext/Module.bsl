
// отправка сообщений
Процедура ВыполнитьРассылку() экспорт
	
	ЖурналSMS = РегистрыСведений.ЖурналSMSоповещения;
	ОтборSMS = Новый Структура("Отправлено",Ложь);
	ПакетSMS = ЖурналSMS.Выбрать(ДобавитьМесяц(ТекущаяДата(),-1),ТекущаяДата(),ОтборSMS); 
	
	СчОшибок = 0;
	СчВсего = 0;
	ОшибкаИтого = "";
	КаталогВыгрузки = КаталогВыгрузкиSMS();
	
	Пока ПакетSMS.Следующий() Цикл
		
		Попытка
			sms = ПакетSMS.ПолучитьМенеджерЗаписи();
			sms.Прочитать();
		Исключение
		    Продолжить;
		КонецПопытки;
		
		Если НЕ ЗначениеЗаполнено(sms.ВидSMSоповещения) тогда
			Продолжить;
		КонецЕсли;
		
		sms.Дата_Отправки = ТекущаяДата();
		
		Ошибка = "";
		ИсточникОшибки = "Данные не корректны";
		СчВсего = СчВсего + 1;

		Если НЕ ЗначениеЗаполнено(sms.Получатель_НомерТелефона) тогда
			Ошибка = Ошибка + Символы.ПС + "err: не задан номер телефона получателя";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(sms.ТекстSMSоповещения) тогда
			Ошибка = Ошибка + Символы.ПС + "err: не задан текст сообщения";
		КонецЕсли;
		
		ПараметрыНастройки = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(sms.ВидSMSоповещения,"Отправитель,ВидАвтооповещенияПоEmail,ИспользоватьWebСервис");
		Если НЕ ПараметрыНастройки.ИспользоватьWebСервис тогда
			Если НЕ ФайлСуществует(КаталогВыгрузки) тогда                                                                              
				Ошибка = Ошибка + Символы.ПС + "err: каталог выгрузки sms не доступен";
			КонецЕсли;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ПараметрыНастройки.Отправитель) тогда
			Ошибка = Ошибка + Символы.ПС + "err: не задан Отправитель";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(sms.id) тогда
			Ошибка = Ошибка + Символы.ПС + "err: не задан id";
		КонецЕсли;
		
		л_НомерТелефона = ПроверитьНомерТелефона(sms.Получатель_НомерТелефона);
		Если НЕ ЗначениеЗаполнено(л_НомерТелефона) тогда
			Ошибка = Ошибка + Символы.ПС + "err: номер телефона не соответствует спецификации";
		КонецЕсли;
		
		л_ТекстSMSоповещения = ПроверитьТекстSMS(sms.ТекстSMSоповещения);
		Если НЕ ЗначениеЗаполнено(л_ТекстSMSоповещения) тогда
			Ошибка = Ошибка + Символы.ПС + "err: текст SMS не соответствует спецификации";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Ошибка) тогда
			
			ИсточникОшибки = "HTTP-запрос не выполнен";
			
			ПараметрыСообщения = Новый Структура();
			ПараметрыСообщения.Вставить("ID",sms.id);
			ПараметрыСообщения.Вставить("ИспользоватьWebСервис",ПараметрыНастройки.ИспользоватьWebСервис);
			ПараметрыСообщения.Вставить("НомерТелефона",л_НомерТелефона);
			ПараметрыСообщения.Вставить("ТекстSMSоповещения",л_ТекстSMSоповещения);
			ПараметрыСообщения.Вставить("id_web",sms.id_web);
			
			Ошибка = ОтправитьSMS(ПараметрыСообщения);
			
			sms.id_web = ПараметрыСообщения.id_web;
		КонецЕсли;
		
	Если ЗначениеЗаполнено(Ошибка) тогда
			
			sms.Ошибка = Истина;
			sms.ОписаниеОшибки = ИсточникОшибки + " " + Ошибка;
			sms.Статус = Перечисления.СтатусыSMSоповещения.Ожидание;
			
			СчОшибок = СчОшибок + 1;
			Ошибка = СокрлП(СчОшибок) + ". Получатель: [" + СокрлП(sms.Получатель_КА) + "]; ID SMS = [" + СокрЛП(sms.id) + "]" + Символы.ПС + Ошибка;
			ОшибкаИтого = ОшибкаИтого + Ошибка;
			
		Иначе
			
			sms.Получатель_НомерТелефона = л_НомерТелефона;
			sms.ТекстSMSоповещения = л_ТекстSMSоповещения;
			sms.Отправлено = Истина;
			sms.Ошибка = Ложь;
			sms.Статус = ?(ПараметрыНастройки.ИспользоватьWebСервис,Перечисления.СтатусыSMSоповещения.Доставлено,Перечисления.СтатусыSMSоповещения.Отправлено);
			sms.ОписаниеОшибки = "";
			
		КонецЕсли;
		
		Попытка
			sms.Записать(Истина);
		Исключение
			ОшибкаИтого = ОшибкаИтого + "err: ошибка записи ID SMS = [" + СокрЛП(sms.id) + "]" + Символы.ПС + ОписаниеОшибки();
		КонецПопытки;
			
	КонецЦикла;
	
	Если СчОшибок > 0 И ЗначениеЗаполнено(ПараметрыНастройки.ВидАвтооповещенияПоEmail) тогда
		ОшибкаИтого_Заголовок = "Из [" + СокрЛП(СчВсего) + "] sms, [" + СокрЛП(СчОшибок) + "] отправить не удалось";
		РассылкаСообщенийОбОшибках.ОтправитьЭлектронноеСообщениеБезСохранения(ПараметрыНастройки.ВидАвтооповещенияПоEmail,ОшибкаИтого,ОшибкаИтого_Заголовок);
	КонецЕсли;
	
КонецПроцедуры
Функция ОтправитьSMS(пар_SMS)
	
	Ошибка = "";
	
	Если пар_SMS.ИспользоватьWebСервис тогда
		
		Если пар_SMS.id_web = 0 тогда
		
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Authorization", СтрокаАвторизации());
			Заголовки.Вставить("Content-Type", "multipart/form-data;boundary=boundary");
			
			ТелоЗапроса = "";
			Boundary = "boundary";
			
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("--"+boundary);
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("Content-Disposition: form-data; name=""phone""");
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("");
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП(пар_SMS.НомерТелефона);
			
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("--"+boundary);
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("Content-Disposition: form-data; name=""msg""");
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("");
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП(пар_SMS.ТекстSMSоповещения);
			
			ТелоЗапроса = ТелоЗапроса + Символы.ПС + СокрЛП("--"+boundary+"--");
			
			ПараметрыЗапроса = Новый Структура;
			ПараметрыЗапроса.Вставить("Сервер",			АдресХостаSMS());
			ПараметрыЗапроса.Вставить("ТелоЗапроса",	ТелоЗапроса);
			ПараметрыЗапроса.Вставить("Заголовки",		Заголовки);
			ПараметрыЗапроса.Вставить("HTTPМетод",		"post");
			ПараметрыЗапроса.Вставить("АдресРесурса",	"/sms/");
			
			ТекстОшибки = "";
			Результат = ВыполнитьЗапросWebProxy(ПараметрыЗапроса, ТекстОшибки);
			
			Если Результат = Неопределено Тогда
				Ошибка = "Ошибка POST-запроса:" + Символы.ПС + ТекстОшибки;
				Возврат Ошибка;
			КонецЕсли;
			
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(Результат);
			ЧтениеJSON.Прочитать();
			СтруктураОтвета = ОбменДаннымиСервер.РазобратьСтруктуруJSON(ЧтениеJSON);
			
			Если ТипЗнч(СтруктураОтвета) = Тип("Структура") тогда
				Если СтруктураОтвета.success = Ложь Тогда
					Ошибка = "Ошибка POST-запроса: success:false";
					Возврат Ошибка;	
				КонецЕсли;
			Иначе
				Ошибка = "Ошибка чтения JSON: " + СокрЛП(Результат);
				Возврат Ошибка;	
			КонецЕсли;
			
			пар_SMS.id_web = СтруктураОтвета.msg;
			
		КонецЕсли;
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Authorization", СтрокаАвторизации());
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("Сервер",		АдресХостаSMS());
		ПараметрыЗапроса.Вставить("ТелоЗапроса",	"");
		ПараметрыЗапроса.Вставить("Заголовки",		Заголовки);
		ПараметрыЗапроса.Вставить("HTTPМетод",		"get");
		ПараметрыЗапроса.Вставить("АдресРесурса",	"/sms/"+Формат(Число(пар_SMS.id_web),"ЧЦ=15; ЧДЦ=; ЧГ="));
		
		ТекстОшибки = "";
		Результат = ВыполнитьЗапросWebProxy(ПараметрыЗапроса, ТекстОшибки); 
		
		Если Результат = Неопределено Тогда
			Ошибка = "Ошибка GET-запроса:" + Символы.ПС + ТекстОшибки;
			Возврат Ошибка;
		КонецЕсли;
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Результат);
		ЧтениеJSON.Прочитать();
		СтруктураОтвета = ОбменДаннымиСервер.РазобратьСтруктуруJSON(ЧтениеJSON);
		
		Если ТипЗнч(СтруктураОтвета) = Тип("Структура") тогда
			Если СтруктураОтвета.success = Ложь Тогда
				Ошибка = "Ошибка GET-запроса: success:false";
				Возврат Ошибка;	
			КонецЕсли;
		Иначе
			Ошибка = "Ошибка чтения JSON: " + СокрЛП(Результат);
			Возврат Ошибка;	
		КонецЕсли;
		
	Иначе
		
		Попытка
			ИмяВременногоФайлика = ПолучитьИмяВременногоФайла("txt");
			
			ТекстФайла = пар_SMS.НомерТелефона + Символы.ПС + пар_SMS.ТекстSMSоповещения;
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			ТекстовыйДокумент.УстановитьТекст(ТекстФайла);
			ТекстовыйДокумент.Записать(ИмяВременногоФайлика, "windows-1251");
			
			Если НЕ ФайлСуществует(ИмяВременногоФайлика) тогда
				Ошибка = "err: ошибка записи файла sms";
			Иначе
				
				т_ИФ = пар_SMS.НомерТелефона + ТекущаяДата();
			    т_ИФ = СтрЗаменить(т_ИФ, ".", "");
			    т_ИФ = СтрЗаменить(т_ИФ, ":", "");
				т_ИФ = СтрЗаменить(т_ИФ, " ", "_");
				ИмяФайла = т_ИФ+".txt";	
				ПолноеИмяФайла = КаталогВыгрузкиSMS() + ИмяФайла;
				
				УдалитьФайлы(ПолноеИмяФайла);
				ПереместитьФайл(ИмяВременногоФайлика, ПолноеИмяФайла);
			
			КонецЕсли;
			
		Исключение
			Ошибка = "err: ошибка формирования файла sms" + Символы.ПС + ОписаниеОшибки();
			Возврат Ошибка;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Ошибка;
КонецФункции

Функция ВыполнитьЗапросWebProxy(ПараметрыЗапроса, ТекстОшибки) экспорт      
	// честно стрырено: Худин Обработка VMI_Client.epf
	
	ВозвращаемоеЗначение = Неопределено;
	
	Попытка
		HTTPСоединение = Новый HTTPСоединение(ПараметрыЗапроса.Сервер,,,,,,,);
		
		HTTPЗапрос = Новый HTTPЗапрос(ПараметрыЗапроса.АдресРесурса, ПараметрыЗапроса.Заголовки); 
		HTTPЗапрос.УстановитьТелоИзСтроки(ПараметрыЗапроса.ТелоЗапроса , КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
		
		Если НРег(ПараметрыЗапроса.HTTPМетод) = "get" Тогда
			Ответ = HTTPСоединение.Получить(HTTPЗапрос);
		Иначе
			Ответ = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КонецЕсли;
		
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		
		Если Ответ.КодСостояния = 200 Тогда
			ВозвращаемоеЗначение = ТекстОтвета;
		Иначе
			ТекстОшибки = ТекстОтвета;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Сообщить("Ошибка выполнение запроса: "+Ответ.КодСостояния+", "+ТекстОшибки);
		КонецЕсли;
		
		HTTPСоединение = Неопределено;
		HTTPЗапрос = Неопределено;
		
	Исключение
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;

КонецФункции

Функция СтрокаАвторизации()
	
	//Логин = "";
	Пароль = "T25KZVd6VTVBR3J0eVhCQkJkdG50TUJOY2tVb2JP";
	//Если Не ЗначениеЗаполнено(Логин) Тогда
	//	Возврат "";
	//КонецЕсли;
	Если Не ЗначениеЗаполнено(Пароль) Тогда
		Возврат "";
	КонецЕсли;
	
	//Возврат СоздатьСтрокуBase64(СокрЛП(Логин)+":"+СокрЛП(Пароль));
	Возврат Пароль;//СоздатьСтрокуBase64(СокрЛП(Пароль));
	
КонецФункции
Функция КаталогВыгрузкиSMS()
	Возврат "\\1csql01-g9\1c_exch\SendSMS\";
КонецФункции
Функция АдресХостаSMS()
	Возврат "sa.part-kom.ru";
КонецФункции

Функция ФайлСуществует(Знач ПутьКФайлу) Экспорт 
      
   	Файл = Новый Файл(ПутьКФайлу); 
   	Возврат Файл.Существует(); 

КонецФункции 

// формирование сообщений
Процедура ПримерЗапускаПроцессаФормированияSMSоповещения() экспорт
	
	пар_Триггер = Справочники.НастройкаSMSоповещения.Триггер_01;
	пар_Источник = Новый Массив;
	пар_Источник.Добавить(Документы.ЗаявкаПокупателя.НайтиПоНомеру("УДИ00000001"));
	пар_Источник.Добавить(Документы.ЗаявкаПокупателя.НайтиПоНомеру("УДИ00000002"));
	
	ЛаунчерSMS(Новый Структура("Триггер,Источник",пар_Триггер,пар_Источник));
	
КонецПроцедуры
Процедура ЛаунчерSMS(пар_Основание) экспорт
	
	л_Триггер = пар_Основание.Триггер;
	массив_Источник = пар_Основание.Источник;
	
	Если НЕ ЗначениеЗаполнено(л_Триггер) ИЛИ массив_Источник.Количество() = 0 тогда
		Возврат;
	КонецЕсли;
	
	л_ТекущийМоментВремени = ТекущаяДата();
	
	ПараметрыТриггера = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(л_Триггер,"ШаблонSMS,Пауза,ВидАвтооповещенияПоEmail,ИспользоватьWebСервис,Префикс");
	
	л_ШаблонТекстаSMS = ПараметрыТриггера.ШаблонSMS;
	л_ШаблоныSMS = ПолучитьСписокШаблонов(л_ШаблонТекстаSMS).НайтиСтроки(новый Структура("Выбран",Истина));
	
	Для каждого выб_ссылка из массив_Источник цикл
		
		л_КА = Справочники.Контрагенты.ПустаяСсылка();
		Если ТипЗнч(выб_ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
			л_КА = выб_ссылка;
		ИначеЕсли ТипЗнч(выб_ссылка) = Тип("ДокументСсылка.ЗаявкаПокупателя") Тогда
			л_КА = ОбщегоНазначения.ПолучитьЗначениеРеквизита(выб_ссылка,"Контрагент");
		ИначеЕсли ТипЗнч(выб_ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			л_КА = ОбщегоНазначения.ПолучитьЗначениеРеквизита(выб_ссылка,"Контрагент");
		ИначеЕсли ТипЗнч(выб_ссылка) = Тип("ДокументСсылка.МегаЛогист_МаршрутноеЗадание") Тогда
			л_КА = ОбщегоНазначения.ПолучитьЗначениеРеквизита(выб_ссылка,"Контрагент");
		КонецЕсли;
		
		л_SMS = л_ШаблонТекстаSMS;
		Для каждого шаблончика из л_ШаблоныSMS цикл
			ПараметрыШаблончика = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(шаблончика.Ссылка,"Наименование,Алгоритм");
			л_наименование = ПараметрыШаблончика.Наименование;
			л_алгоритм = ПараметрыШаблончика.Алгоритм; 
			л_Значение = ПолучитьЗначениеШаблона(выб_ссылка,л_алгоритм);
			
			л_SMS = СтрЗаменить(л_SMS,"[" + л_наименование + "]",СокрЛП(л_Значение));
		КонецЦикла;
		
		л_id = ПараметрыТриггера.Префикс + Формат(ПолучитьIdSMS(ПараметрыТриггера.Префикс) + 1,"ЧЦ=" + (12-СтрДлина(ПараметрыТриггера.Префикс)) + "; ЧВН=; ЧГ=");
		
		Попытка
			НоваяЗапись = РегистрыСведений.ЖурналSMSоповещения.СоздатьМенеджерЗаписи();
			НоваяЗапись.Период = л_ТекущийМоментВремени + ПараметрыТриггера.Пауза;
			НоваяЗапись.id = л_id;
			НоваяЗапись.Источник = выб_ссылка;
			НоваяЗапись.ВидSMSоповещения = л_Триггер;
			НоваяЗапись.Получатель_КА = л_КА;
			НоваяЗапись.Получатель_НомерТелефона = ПолучитьНомерТелефонаКА_ЭД(л_КА);
			НоваяЗапись.ТекстSMSоповещения = л_SMS;
			НоваяЗапись.Дата_Формирования = л_ТекущийМоментВремени;
			НоваяЗапись.Статус = Перечисления.СтатусыSMSоповещения.Ожидание;
			НоваяЗапись.Записать();
		Исключение	
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьIdSMS(пар_Префикс)
	
	счетчик = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ПОДСТРОКА(ЖурналSMSоповещения.id, 0, 3)) КАК счетчик
	|ИЗ
	|	РегистрСведений.ЖурналSMSоповещения КАК ЖурналSMSоповещения
	|ГДЕ 
	|	ПОДСТРОКА(ЖурналSMSоповещения.id, 0, 3) = &Преф";

	Запрос.УстановитьПараметр("Преф", пар_Префикс);
	Результат = Запрос.Выполнить().Выгрузить();

	Если Результат.Количество() > 0 тогда
		 счетчик = Результат[0].счетчик
	КонецЕсли;
	
	Возврат счетчик;
КонецФункции
Функция ПолучитьНомерТелефонаКА_ЭД(пар_КА)
	
	искомый_номер_телефона = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		
	"ВЫБРАТЬ 
	|	КонтактнаяИнформация.НомерТелефона
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|где
	|	КонтактнаяИнформация.Объект = &КА
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонЭкспрессДоставки)
	|	И НЕ КонтактнаяИнформация.НомерТелефона = """"
	|";
	Запрос.УстановитьПараметр("КА", пар_КА);
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() > 0 тогда
		искомый_номер_телефона = Результат[0].НомерТелефона;
	КонецЕсли;
	
	Возврат искомый_номер_телефона;

КонецФункции
Функция ПолучитьЗначениеШаблона(пар_ссылка,пар_алгоритм)
	
	Искомое_Значение = неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = пар_алгоритм;
	Запрос.УстановитьПараметр("Ссылка",пар_ссылка);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() > 0 тогда
		Искомое_Значение = Результат[0].Искомое_Значение;
	КонецЕсли;
	
	Возврат Искомое_Значение;
КонецФункции

Функция ПроверитьНомерТелефона(пар_НомерТелефона)
	
	л_НомерТелефона = "";
	ПравильныеСимволы = "0123456789";
	Для Сч = 1 по СтрДлина(пар_НомерТелефона) Цикл
		ТекСимв = Сред(пар_НомерТелефона, Сч, 1);
		Если Найти(ПравильныеСимволы, ТекСимв) > 0 Тогда
			л_НомерТелефона = л_НомерТелефона + ТекСимв;
		Иначе
			л_НомерТелефона = л_НомерТелефона + "";
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СтрДлина(л_НомерТелефона) = 11 Тогда
		л_НомерТелефона = "";
	КонецЕсли;
	
	Если Лев(л_НомерТелефона, 1) = "7" ИЛИ Лев(л_НомерТелефона, 1) = "8" Тогда
		л_НомерТелефона = "7" + Прав(л_НомерТелефона,10);
	Иначе
		л_НомерТелефона = "";
	КонецЕсли;
	
	Возврат л_НомерТелефона;
	
КонецФункции
Функция ПроверитьТекстSMS(пар_ТекстSMS)
	л_ТекстSMS = "";
	
	ПравильныеСимволы = "0123456789[]QWERTYUIOPASDFGHJKLZXCVBNMqwertyuiopasdfghjklzxcvbnmЙЦУКЕНГШЩЗХЪФЫВАПРОЛДЖЭЯЧСМИТЬБЮйцукенгшщзхъфывапролджэячсмитьбю(){}+-.,:;!?№%?@'";
	Для Сч = 1 по СтрДлина(пар_ТекстSMS) Цикл
		ТекСимв = Сред(пар_ТекстSMS, Сч, 1);
		Если Найти(ПравильныеСимволы, ТекСимв) > 0 Тогда
			л_ТекстSMS = л_ТекстSMS + ТекСимв;
		Иначе
			л_ТекстSMS = л_ТекстSMS + " ";
		КонецЕсли;
	КонецЦикла;
	
	Возврат л_ТекстSMS;
КонецФункции

// настройка SMS
Функция ЗапроситьСписокШаблонов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	 "ВЫБРАТЬ
	 |	ШаблоныSMSоповещения.Ссылка
	 |ИЗ
	 |	Справочник.ШаблоныSMSоповещения КАК ШаблоныSMSоповещения
	 |ГДЕ
	 |	ШаблоныSMSоповещения.ПометкаУдаления = ЛОЖЬ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции
Функция ВыбратьШаблон() экспорт
	
	желаемый_шаблон = "";
	
	Результат = ЗапроситьСписокШаблонов();
	
	Адрес = Результат.ВыбратьСтроку("Выбери Шаблон");
	Если НЕ Адрес = Неопределено тогда
		желаемый_шаблон = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Адрес.Ссылка,"Наименование");
	КонецЕсли;
	
	Возврат желаемый_шаблон;
КонецФункции
Функция ПолучитьСписокШаблонов(пар_SMS) экспорт
	
	Результат = ЗапроситьСписокШаблонов();
	Результат.Колонки.Добавить("Выбран");
	
	Для каждого стр из Результат цикл
		Если СтрНайти(пар_SMS, "[" + ОбщегоНазначения.ПолучитьЗначениеРеквизита(стр.Ссылка,"Наименование") + "]") > 0 тогда
			стр.Выбран = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

