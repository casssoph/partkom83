//Обмен 1С-Сайт: ОбменПартКом83_Сайт_состояние_заявок//
Процедура ДобавитьДанныеСостояниеСтрокЗаявок(Список, НомерСообщения, СтруктураХранения) Экспорт
	
	Если НЕ РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Сайт","Выгружать состояния строк заявок", Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ОтборЗаявок = Новый Массив;
	
	ТипОбъектаXDTO = ФабрикаXDTO.Тип("http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema", "СостояниеСтрокиЗаявки");
	Выборка = ВыборкаСостоянийСтрокЗаявок();
	Пока Выборка.Следующий() Цикл
		
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
		ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка, "IDSite,Дата,Артикул,КоличествоВЗаявке,КоличествоЗаказано,КоличествоОтказано,КоличествоНаСкладе,КоличествоВыдано,КоличествоВРезерве,КоличествоПоступило,Цена,ЗакупочнаяЦена");
		ОбъектXDTO.Состояние = Выборка.Состояние.УникальныйИдентификатор();
		ОбъектXDTO.Склад = Выборка.Склад.УникальныйИдентификатор();
		ОбъектXDTO.НомерЗаявки = Выборка.НомерДокументаЗаявки;
		Список.Добавить(ОбъектXDTO);
		
		ОтборЗаявок.Добавить(Выборка.СтрокаЗаявки);
		
	КонецЦикла;
	
	УстанавливаемыеПоля = Новый Структура;
	УстанавливаемыеПоля.Вставить("ДатаОтправки", ТекущаяДата());
	УстанавливаемыеПоля.Вставить("НомерСообщенияОтправленного", НомерСообщения);

	ОтборПоСтрокамЗаявки = Новый Структура;
	ОтборПоСтрокамЗаявки.Вставить("ВидСравнения", ВидСравнения.ВСписке);
	ОтборПоСтрокамЗаявки.Вставить("Значение", ОтборЗаявок); 
	
	ПоляОтбора = Новый Структура;
	ПоляОтбора.Вставить("СтрокаЗаявки", ОтборПоСтрокамЗаявки);
	
	Структура = Новый Структура("ИмяТаблицы", "РегистрСведений.СостояниеЗаявокПокупателя");
	Структура.Вставить("УстанавливаемыеПоля", УстанавливаемыеПоля);
	Структура.Вставить("ПоляОтбора", ПоляОтбора);
	Структура.Вставить("СтруктураХранения", СтруктураХранения);
	
	ОписаниеОшибки = РаботаСSQL.UpdateAtWhere(Структура);
	
КонецПроцедуры
Функция ВыборкаСостоянийСтрокЗаявок()
	
	КоличествоОбъектов = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен (Общее)","Количество объектов в обмене", 1000);
	ОбратныйПорядокВыборки = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен (Общее)","Обратный порядок выборки", Истина);
	УсловиеОтбора = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен (Общее)","Условие запроса состояний строк заявок", "");
	
	Запрос = Новый Запрос(ТекстЗапросаВыборкиСостояний());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1234", Формат(КоличествоОбъектов, "ЧГ="));
	Если НЕ ОбратныйПорядокВыборки Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " УБЫВ", "");
	КонецЕсли;
	Если ЗначениеЗаполнено(УсловиеОтбора) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ИСТИНА", "И " + УсловиеОтбора);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции
Функция ТекстЗапросаВыборкиСостояний()
	
	//При изменении текста запроса, требуется учесть замены текста запроса в процедуре ВыборкаСостоянийСтрокЗаявок()//
	Возврат "ВЫБРАТЬ ПЕРВЫЕ 1234
	        |	СостояниеЗаявокПокупателя.ДатаИзменения КАК Дата,
	        |	ВЫБОР
	        |		КОГДА СостояниеЗаявокПокупателя.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	        |			ТОГДА ЕСТЬNULL(СостояниеЗаявокПокупателя.СтрокаЗаявки.Заявка.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	        |		ИНАЧЕ СостояниеЗаявокПокупателя.Склад
	        |	КОНЕЦ КАК Склад,
	        |	СостояниеЗаявокПокупателя.Номенклатура,
	        |	СостояниеЗаявокПокупателя.IDSite,
	        |	СостояниеЗаявокПокупателя.СтрокаЗаявки,
	        |	СостояниеЗаявокПокупателя.Состояние,
	        |	СостояниеЗаявокПокупателя.КоличествоВЗаявке,
	        |	СостояниеЗаявокПокупателя.КоличествоЗаказано - СостояниеЗаявокПокупателя.КоличествоПоступило КАК КоличествоЗаказано,
	        |	СостояниеЗаявокПокупателя.КоличествоОтказ КАК КоличествоОтказано,
	        |	СостояниеЗаявокПокупателя.КоличествоПоступило,
	        |	СостояниеЗаявокПокупателя.КоличествоВРезерве,
	        |	ВЫБОР
	        |		КОГДА СостояниеЗаявокПокупателя.КоличествоВРезерве <> 0
	        |			ТОГДА СостояниеЗаявокПокупателя.КоличествоВРезерве
	        |		ИНАЧЕ СостояниеЗаявокПокупателя.КоличествоПоступило
	        |	КОНЕЦ - СостояниеЗаявокПокупателя.КоличествоОтгружено КАК КоличествоНаСкладе,
	        |	СостояниеЗаявокПокупателя.КоличествоОтгружено КАК КоличествоВыдано,
	        |	ВЫБОР
	        |		КОГДА СостояниеЗаявокПокупателя.СтрокаЗаявки.ПоследняяКорректировка <> ЗНАЧЕНИЕ(Документ.КорректировкаЗаявкиПокупателя.ПустаяСсылка)
	        |			ТОГДА СостояниеЗаявокПокупателя.СтрокаЗаявки.ПоследняяКорректировка
	        |		ИНАЧЕ СостояниеЗаявокПокупателя.СтрокаЗаявки.Заявка
	        |	КОНЕЦ КАК ДокументСвязи
	        |ПОМЕСТИТЬ ДанныеСтрокиЗаявки
	        |ИЗ
	        |	РегистрСведений.СостояниеЗаявокПокупателя КАК СостояниеЗаявокПокупателя
	        |ГДЕ
	        |	НЕ СостояниеЗаявокПокупателя.СтрокаЗаявки.Виртуальная
	        |	И НЕ СостояниеЗаявокПокупателя.ПолученоСайтом
	        |	И ИСТИНА
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	СостояниеЗаявокПокупателя.ДатаИзменения УБЫВ
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ЗаявкаПокупателяТовары.Ссылка КАК Документ,
	        |	ЗаявкаПокупателяТовары.СтрокаЗаявки,
	        |	ЗаявкаПокупателяТовары.Номенклатура
	        |ПОМЕСТИТЬ ОпределениеНоменклатуры
	        |ИЗ
	        |	Документ.ЗаявкаПокупателя.Товары КАК ЗаявкаПокупателяТовары
	        |ГДЕ
	        |	ЗаявкаПокупателяТовары.Ссылка В
	        |			(ВЫБРАТЬ
	        |				ДанныеСтрокиЗаявки.ДокументСвязи
	        |			ИЗ
	        |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	        |	И ЗаявкаПокупателяТовары.СтрокаЗаявки В
	        |			(ВЫБРАТЬ
	        |				ДанныеСтрокиЗаявки.СтрокаЗаявки
	        |			ИЗ
	        |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	        |
	        |ОБЪЕДИНИТЬ ВСЕ
	        |
	        |ВЫБРАТЬ
	        |	КорректировкаЗаявкиПокупателяТовары.Ссылка,
	        |	КорректировкаЗаявкиПокупателяТовары.СтрокаЗаявки,
	        |	КорректировкаЗаявкиПокупателяТовары.Номенклатура
	        |ИЗ
	        |	Документ.КорректировкаЗаявкиПокупателя.Товары КАК КорректировкаЗаявкиПокупателяТовары
	        |ГДЕ
	        |	КорректировкаЗаявкиПокупателяТовары.Ссылка В
	        |			(ВЫБРАТЬ
	        |				ДанныеСтрокиЗаявки.ДокументСвязи
	        |			ИЗ
	        |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	        |	И КорректировкаЗаявкиПокупателяТовары.СтрокаЗаявки В
	        |			(ВЫБРАТЬ
	        |				ДанныеСтрокиЗаявки.СтрокаЗаявки
	        |			ИЗ
	        |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДанныеСтрокиЗаявки.Дата,
	        |	ЕСТЬNULL(ДанныеСтрокиЗаявки.СтрокаЗаявки.Заявка, ЗНАЧЕНИЕ(Документ.ЗаявкаПокупателя.ПустаяСсылка)) КАК ДокументЗаявки,
	        |	ЕСТЬNULL(ДанныеСтрокиЗаявки.СтрокаЗаявки.Заявка.Номер, """") КАК НомерДокументаЗаявки,
	        |	ВЫБОР
	        |		КОГДА ДанныеСтрокиЗаявки.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	        |			ТОГДА ЕСТЬNULL(ОпределениеНоменклатуры.Номенклатура.Артикул, """")
	        |		ИНАЧЕ ЕСТЬNULL(ДанныеСтрокиЗаявки.Номенклатура.Артикул, """")
	        |	КОНЕЦ КАК Артикул,
	        |	ДанныеСтрокиЗаявки.Склад,
	        |	ДанныеСтрокиЗаявки.IDSite,
	        |	ДанныеСтрокиЗаявки.СтрокаЗаявки,
	        |	ДанныеСтрокиЗаявки.Состояние,
	        |	ДанныеСтрокиЗаявки.КоличествоВЗаявке,
	        |	ДанныеСтрокиЗаявки.КоличествоОтказано,
	        |	ДанныеСтрокиЗаявки.КоличествоНаСкладе,
	        |	ДанныеСтрокиЗаявки.КоличествоВыдано,
	        |	ДанныеСтрокиЗаявки.ДокументСвязи,
	        |	ДанныеСтрокиЗаявки.КоличествоВРезерве,
	        |	ДанныеСтрокиЗаявки.КоличествоПоступило,
	        |	ДанныеСтрокиЗаявки.КоличествоЗаказано,
	        |	ЕСТЬNULL(ДанныеСтрокиЗаявки.СтрокаЗаявки.Цена, 0) КАК Цена,
	        |	ЕСТЬNULL(ДанныеСтрокиЗаявки.СтрокаЗаявки.ЦенаЗакупки, 0) КАК ЗакупочнаяЦена
	        |ИЗ
	        |	ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ОпределениеНоменклатуры КАК ОпределениеНоменклатуры
	        |		ПО ДанныеСтрокиЗаявки.СтрокаЗаявки = ОпределениеНоменклатуры.СтрокаЗаявки
	        |			И ДанныеСтрокиЗаявки.ДокументСвязи = ОпределениеНоменклатуры.Документ";
	
КонецФункции

Процедура ФиксацияПринятогоСообщения(НомерСообщения, СтруктураХранения) Экспорт
	
	УстанавливаемыеПоля = Новый Структура;
	УстанавливаемыеПоля.Вставить("ПолученоСайтом", Истина);
	УстанавливаемыеПоля.Вставить("ДатаОбработкиСайтом", ТекущаяДата());

	ОтборПоНомеруСообщения = Новый Структура;
	ОтборПоНомеруСообщения.Вставить("ВидСравнения", ВидСравнения.Интервал);
	ОтборПоНомеруСообщения.Вставить("Минимум", 1);
	ОтборПоНомеруСообщения.Вставить("Максимум", НомерСообщения);
	
	ПоляОтбора = Новый Структура;
	ПоляОтбора.Вставить("НомерСообщенияОтправленного", ОтборПоНомеруСообщения);
	ПоляОтбора.Вставить("ПолученоСайтом", Ложь);
	
	Структура = Новый Структура("ИмяТаблицы", "РегистрСведений.СостояниеЗаявокПокупателя");
	Структура.Вставить("УстанавливаемыеПоля", УстанавливаемыеПоля);
	Структура.Вставить("ПоляОтбора", ПоляОтбора);
	Структура.Вставить("СтруктураХранения", СтруктураХранения);
	
	ОписаниеОшибки = РаботаСSQL.UpdateAtWhere(Структура);
	
КонецПроцедуры
Процедура СнятьРегистрациюСЗаписей(Выборка) Экспорт
	
	УстанавливаемыеПоля = Новый Структура;
	УстанавливаемыеПоля.Вставить("НомерСообщенияОтправленного", 0);
	УстанавливаемыеПоля.Вставить("ДатаОтправки", '00010101');
	УстанавливаемыеПоля.Вставить("ДатаОбработкиСайтом", '00010101');
	УстанавливаемыеПоля.Вставить("ПолученоСайтом", Ложь);
	
	КлючевыеПоля = Новый Структура;
	КлючевыеПоля.Вставить("СтрокаЗаявки");	
	
	Структура = Новый Структура("ИмяТаблицы", "РегистрСведений.СостояниеЗаявокПокупателя");
	Структура.Вставить("КлючевыеПоля", КлючевыеПоля);
	Структура.Вставить("УстанавливаемыеПоля", УстанавливаемыеПоля);
	Структура.Вставить("ВыборкаДанных", Выборка);
	
	ОписаниеОшибки = РаботаСSQL.UpdateAtJOIN(Структура);
	
КонецПроцедуры
Процедура ОтметитьЗаписиКакВыгруженные(Выборка) Экспорт
	
	УстанавливаемыеПоля = Новый Структура;
	УстанавливаемыеПоля.Вставить("НомерСообщенияОтправленного", 0);
	УстанавливаемыеПоля.Вставить("ДатаОтправки", ТекущаяДата());
	УстанавливаемыеПоля.Вставить("ДатаОбработкиСайтом", ТекущаяДата());
	УстанавливаемыеПоля.Вставить("ПолученоСайтом", Истина);
	
	КлючевыеПоля = Новый Структура;
	КлючевыеПоля.Вставить("СтрокаЗаявки");	
	
	Структура = Новый Структура("ИмяТаблицы", "РегистрСведений.СостояниеЗаявокПокупателя");
	Структура.Вставить("КлючевыеПоля", КлючевыеПоля);
	Структура.Вставить("УстанавливаемыеПоля", УстанавливаемыеПоля);
	Структура.Вставить("ВыборкаДанных", Выборка);
	
	ОписаниеОшибки = РаботаСSQL.UpdateAtJOIN(Структура);
	
КонецПроцедуры
