//Обмен 1С-Сайт: ОбменПартКом83_Сайт_состояние_заявок//
Процедура ДобавитьДанныеСостояниеСтрокЗаявок(Список, НомерСообщения) Экспорт
	
	Если НЕ РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Сайт","Выгружать состояния строк заявок", Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДата();
	ТипОбъектаXDTO = ФабрикаXDTO.Тип("http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema", "СостояниеСтрокиЗаявки");
	Выборка = ВыборкаСостоянийСтрокЗаявок();
	Пока Выборка.Следующий() Цикл
		
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
		ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка, "IDSite,Дата,Артикул,КоличествоВЗаявке,КоличествоЗаказано,КоличествоОтказано,КоличествоНаСкладе,КоличествоВыдано");
		ОбъектXDTO.Состояние = Выборка.Состояние.УникальныйИдентификатор();
		ОбъектXDTO.Склад = Выборка.Склад.УникальныйИдентификатор();
		Список.Добавить(ОбъектXDTO);
		
		Запись = РегистрыСведений.СостояниеЗаявокПокупателя.СоздатьМенеджерЗаписи();
		Запись.СтрокаЗаявки = Выборка.СтрокаЗаявки;
		Запись.Прочитать();
		Запись.НомерСообщенияОтправленного = НомерСообщения;
		Запись.ДатаОтправки = ТекущаяДата;
		Запись.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
Процедура ФиксацияПринятогоСообщения(НомерСообщения) Экспорт
	
	ТекущаяДата = ТекущаяДата();
	Выборка = ВыборкаПодтвержденныхСтрокЗаявок(НомерСообщения);
	Пока Выборка.Следующий() Цикл
		Запись = РегистрыСведений.СостояниеЗаявокПокупателя.СоздатьМенеджерЗаписи();
		Запись.СтрокаЗаявки = Выборка.СтрокаЗаявки;
		Запись.Прочитать();
		Запись.ДатаОбработкиСайтом = ТекущаяДата;
		Запись.ПолученоСайтом = Истина;
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция ВыборкаСостоянийСтрокЗаявок()
	
	КоличествоОбъектов = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен (Общее)","Количество объектов в обмене", 1000);
	ОбратныйПорядокВыборки = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен (Общее)","Обратный порядок выборки", Истина);
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1234
	                      |	СостояниеЗаявокПокупателя.ДатаИзменения КАК Дата,
	                      |	СостояниеЗаявокПокупателя.СтрокаЗаявки.Заявка.Склад КАК Склад,
	                      |	СостояниеЗаявокПокупателя.IDSite,
	                      |	СостояниеЗаявокПокупателя.СтрокаЗаявки,
	                      |	СостояниеЗаявокПокупателя.Состояние,
	                      |	СостояниеЗаявокПокупателя.КоличествоВЗаявке,
	                      |	СостояниеЗаявокПокупателя.КоличествоЗаказано,
	                      |	СостояниеЗаявокПокупателя.КоличествоОтказ КАК КоличествоОтказано,
	                      |	СостояниеЗаявокПокупателя.КоличествоПоступило КАК КоличествоНаСкладе,
	                      |	СостояниеЗаявокПокупателя.КоличествоОтгружено КАК КоличествоВыдано,
	                      |	ВЫБОР
	                      |		КОГДА СостояниеЗаявокПокупателя.СтрокаЗаявки.ПоследняяКорректировка <> ЗНАЧЕНИЕ(Документ.КорректировкаЗаявкиПокупателя.ПустаяСсылка)
	                      |			ТОГДА СостояниеЗаявокПокупателя.СтрокаЗаявки.ПоследняяКорректировка
	                      |		ИНАЧЕ СостояниеЗаявокПокупателя.СтрокаЗаявки.Заявка
	                      |	КОНЕЦ КАК ДокументСвязи
	                      |ПОМЕСТИТЬ ДанныеСтрокиЗаявки
	                      |ИЗ
	                      |	РегистрСведений.СостояниеЗаявокПокупателя КАК СостояниеЗаявокПокупателя
	                      |ГДЕ
	                      |	НЕ СостояниеЗаявокПокупателя.СтрокаЗаявки.Виртуальная
	                      |	И НЕ СостояниеЗаявокПокупателя.ПолученоСайтом
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	СостояниеЗаявокПокупателя.ДатаИзменения УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗаявкаПокупателяТовары.Ссылка КАК Документ,
	                      |	ЗаявкаПокупателяТовары.СтрокаЗаявки,
	                      |	ЗаявкаПокупателяТовары.Номенклатура
	                      |ПОМЕСТИТЬ ОпределениеНоменклатуры
	                      |ИЗ
	                      |	Документ.ЗаявкаПокупателя.Товары КАК ЗаявкаПокупателяТовары
	                      |ГДЕ
	                      |	ЗаявкаПокупателяТовары.Ссылка В
	                      |			(ВЫБРАТЬ
	                      |				ДанныеСтрокиЗаявки.ДокументСвязи
	                      |			ИЗ
	                      |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	                      |	И ЗаявкаПокупателяТовары.СтрокаЗаявки В
	                      |			(ВЫБРАТЬ
	                      |				ДанныеСтрокиЗаявки.СтрокаЗаявки
	                      |			ИЗ
	                      |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	КорректировкаЗаявкиПокупателяТовары.Ссылка,
	                      |	КорректировкаЗаявкиПокупателяТовары.СтрокаЗаявки,
	                      |	КорректировкаЗаявкиПокупателяТовары.Номенклатура
	                      |ИЗ
	                      |	Документ.КорректировкаЗаявкиПокупателя.Товары КАК КорректировкаЗаявкиПокупателяТовары
	                      |ГДЕ
	                      |	КорректировкаЗаявкиПокупателяТовары.Ссылка В
	                      |			(ВЫБРАТЬ
	                      |				ДанныеСтрокиЗаявки.ДокументСвязи
	                      |			ИЗ
	                      |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	                      |	И КорректировкаЗаявкиПокупателяТовары.СтрокаЗаявки В
	                      |			(ВЫБРАТЬ
	                      |				ДанныеСтрокиЗаявки.СтрокаЗаявки
	                      |			ИЗ
	                      |				ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДанныеСтрокиЗаявки.Дата,
	                      |	ЕСТЬNULL(ОпределениеНоменклатуры.Номенклатура.Артикул, """") КАК Артикул,
	                      |	ДанныеСтрокиЗаявки.Склад,
	                      |	ДанныеСтрокиЗаявки.IDSite,
	                      |	ДанныеСтрокиЗаявки.СтрокаЗаявки,
	                      |	ДанныеСтрокиЗаявки.Состояние,
	                      |	ДанныеСтрокиЗаявки.КоличествоВЗаявке,
	                      |	ДанныеСтрокиЗаявки.КоличествоЗаказано,
	                      |	ДанныеСтрокиЗаявки.КоличествоОтказано,
	                      |	ДанныеСтрокиЗаявки.КоличествоНаСкладе,
	                      |	ДанныеСтрокиЗаявки.КоличествоВыдано,
	                      |	ДанныеСтрокиЗаявки.ДокументСвязи
	                      |ИЗ
	                      |	ДанныеСтрокиЗаявки КАК ДанныеСтрокиЗаявки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ОпределениеНоменклатуры КАК ОпределениеНоменклатуры
	                      |		ПО ДанныеСтрокиЗаявки.СтрокаЗаявки = ОпределениеНоменклатуры.СтрокаЗаявки
	                      |			И ДанныеСтрокиЗаявки.ДокументСвязи = ОпределениеНоменклатуры.Документ");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1234", Формат(КоличествоОбъектов, "ЧГ="));
	Если НЕ ОбратныйПорядокВыборки Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " УБЫВ", "");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции
Функция ВыборкаПодтвержденныхСтрокЗаявок(НомерПринятого)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СостояниеЗаявокПокупателя.СтрокаЗаявки
	                      |ИЗ
	                      |	РегистрСведений.СостояниеЗаявокПокупателя КАК СостояниеЗаявокПокупателя
	                      |ГДЕ
	                      |	СостояниеЗаявокПокупателя.НомерСообщенияОтправленного МЕЖДУ 1 И &НомерПринятого
	                      |	И НЕ СостояниеЗаявокПокупателя.ПолученоСайтом");
	Запрос.УстановитьПараметр("НомерПринятого", НомерПринятого);
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции