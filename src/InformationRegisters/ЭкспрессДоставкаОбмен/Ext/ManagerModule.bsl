Процедура ОтобразитьСтруктуру(Приемник, ДанныеПакета) Экспорт
	
	Приемник = "";
	Если ЗначениеЗаполнено(ДанныеПакета) Тогда
		Попытка
			Структура = ЗначениеИзСтрокиВнутр(ДанныеПакета);
			Если ТипЗнч(Структура) = Тип("Структура") Тогда
				РазобратьСтруктуру(Приемник, Структура);
			КонецЕсли;
		Исключение
		КонецПопытки
	КонецЕсли;
	
КонецПроцедуры

Процедура РазобратьСтруктуру(Приемник, Структура, Табуляция = "")
	
	Для Каждого ЭлементСтруктуры Из Структура Цикл
		Ключ = ЭлементСтруктуры.Ключ;
		Значение = ЭлементСтруктуры.Значение;
		ТипЗначения = ТипЗнч(Значение);
		Если ТипЗначения = Тип("Структура") Тогда
			Приемник = Приемник + Табуляция + "<" + Ключ + ">" + Символы.ПС;
			РазобратьСтруктуру(Приемник, Значение, Табуляция + Символы.Таб);
			Приемник = Приемник + Табуляция + "</" + Ключ + ">" + Символы.ПС;
		ИначеЕсли ТипЗначения = Тип("Массив") Тогда
			Для Каждого ЭлементМассива Из Значение Цикл
				Приемник = Приемник + Табуляция + "<" + Ключ + ">" + Символы.ПС;
				РазобратьСтруктуру(Приемник, ЭлементМассива, Табуляция + Символы.Таб);
				Приемник = Приемник + Табуляция + "</" + Ключ + ">" + Символы.ПС;
			КонецЦикла;
		ИначеЕсли ТипЗначения = Тип("Число") Тогда
			ВставитьТег(Приемник, Ключ, Формат(Значение, "ЧГ="), Табуляция);
		ИначеЕсли ТипЗначения = Тип("Строка") Тогда
			ВставитьТег(Приемник, Ключ, Значение, Табуляция);
		ИначеЕсли ТипЗначения = Тип("Булево") Тогда
			ВставитьТег(Приемник, Ключ, ?(Значение, 1, 0), Табуляция);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВставитьТег(Приемник, Ключ, Значение, Табуляция)
	
	Приемник = Приемник + Табуляция + "<" + Ключ + ">" + Значение + "</" + Ключ + ">" + Символы.ПС;
	
КонецПроцедуры