
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидФормыДерево = Истина;
	ИзменитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтображение()
	
	Элементы.Список.Видимость = НЕ ВидФормыДерево;
	Элементы.НастройкиДерево.Видимость = ВидФормыДерево;
	Элементы.ФормаДобавить.Видимость = НЕ ВидФормыДерево;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидФормы(Команда)
	
	ВидФормыДерево = Не ВидФормыДерево;
	ИзменитьОтображение();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройки(Команда)
	
	ЗаполнитьНастройкиНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьНастройкиНаСервере()
	
	Макет = РегистрыСведений.НастройкиПодсистем.ПолучитьМакет("ПервоначальныеНастройки");
	ОбластьНастройки = Макет.ПолучитьОбласть("Настройки");
	Для Индекс = 1 По ОбластьНастройки.ВысотаТаблицы Цикл
		Подсистема = ОбластьНастройки.Область(Индекс,1).Текст;
		Параметр = ОбластьНастройки.Область(Индекс,2).Текст;
		Значение = Вычислить(ОбластьНастройки.Область(Индекс,3).Текст);
		
		НаборЗаписей = РегистрыСведений.НастройкиПодсистем.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подсистема.Установить(Подсистема);
		НаборЗаписей.Отбор.Параметр.Установить(Параметр);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Подсистема = Подсистема;
			Запись.Параметр = Параметр;
			Запись.Значение = Значение;
			НаборЗаписей.Записать();
			Сообщить(Подсистема + "[" + Параметр + "] = " + Значение);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

