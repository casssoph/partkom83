Процедура Добавить(Документ, ВидПечатногоДокумента) Экспорт
	
	лКлючАлгоритма = "РегистрСведений_ДатыПечатиДокументов_МодульМенеджера_Добавить";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДатыПечатиДокументов.Документ
		|ИЗ
		|	РегистрСведений.ДатыПечатиДокументов КАК ДатыПечатиДокументов
		|ГДЕ
		|	ДатыПечатиДокументов.Документ = &Документ
		|	И ДатыПечатиДокументов.ВидПечатногоДокумента = &ВидПечатногоДокумента
		|	И Напечатан";
	
	Запрос.УстановитьПараметр("ВидПечатногоДокумента", ВидПечатногоДокумента);
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ДатыПечатиДокументов.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Отбор.ВидПечатногоДокумента.Установить(ВидПечатногоДокумента);
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 Тогда
		текЗапись = НЗ[0]; 
		текЗапись.ДатаПечати 			= ТекущаяДата();
		текЗапись.Пользователь 			= ПараметрыСеанса.ТекущийПользователь;
		текЗапись.Напечатан				= Истина;
		текЗапись.ДатаОтмены 			= Дата(1,1,1);
		текЗапись.ПользовательОтмены	= Справочники.Пользователи.ПустаяСсылка();
	Иначе
		текЗапись = НЗ.Добавить();
		текЗапись.Документ 				= Документ;
		текЗапись.ВидПечатногоДокумента = ВидПечатногоДокумента;
		текЗапись.ДатаПечати 			= ТекущаяДата();
		текЗапись.Пользователь 			= ПараметрыСеанса.ТекущийПользователь;
		текЗапись.Напечатан				= Истина;
		
	КонецЕсли;
	НЗ.Записать();
	
КонецПроцедуры

Процедура Отменить(Документ, ВидПечатногоДокумента) Экспорт
	
	лКлючАлгоритма = "РегистрСведений_ДатыПечатиДокументов_МодульМенеджера_Отменить";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	
	НЗ = РегистрыСведений.ДатыПечатиДокументов.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Отбор.ВидПечатногоДокумента.Установить(ВидПечатногоДокумента);
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 И НЗ[0].Напечатан = Истина Тогда
		текЗапись = НЗ[0]; 
		текЗапись.Напечатан				= Ложь;
		текЗапись.ДатаОтмены 			= ТекущаяДата();
		текЗапись.ПользовательОтмены	= ПараметрыСеанса.ТекущийПользователь;
		НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры


Функция ДанныеДокумента(Документ, ВидПечатногоДокумента) Экспорт
	
	лКлючАлгоритма = "РегистрСведений_ДатыПечатиДокументов_МодульМенеджера_ДанныеДокумента";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	СтрокаПолей = ПоляРегистраСтрокой();
	ДанныеДокумента = Новый Структура(СтрокаПолей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	"+СтрокаПолей+"
	|ИЗ
	|	РегистрСведений.ДатыПечатиДокументов КАК ДатыПечатиДокументов
	|ГДЕ
	|	ДатыПечатиДокументов.Документ = &Документ
	|	И ДатыПечатиДокументов.ВидПечатногоДокумента = &ВидПечатногоДокумента";
	
	Запрос.УстановитьПараметр("ВидПечатногоДокумента", 	ВидПечатногоДокумента);
	Запрос.УстановитьПараметр("Документ", 				Документ);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
	КонецЕсли;
	
	//Состояние строкой
	СостояниеСтрокой = "";
	Если Не ДанныеДокумента.Напечатан = Истина Тогда 
		СостояниеСтрокой = "Не распечатан. Установить отметку печати.";
	Иначе
		СостояниеСтрокой = "Распечатан "+ДанныеДокумента.ДатаПечати+", "+ДанныеДокумента.Пользователь;
	КонецЕсли;	
	ДанныеДокумента.Вставить("СостояниеСтрокой", СостояниеСтрокой);
	
	Возврат ДанныеДокумента;
	
КонецФункции

Функция ПоляРегистраСтрокой()
	
	Возврат "Документ, ВидПечатногоДокумента, ДатаПечати, Пользователь, ДатаОтмены, ПользовательОтмены, Напечатан";
	
КонецФункции