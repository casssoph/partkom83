//Сдвигает границу назад, если переданная дата меньше границы
Процедура Добавить(СсылкаНаДокумент, ДатаДокумента = Неопределено ) Экспорт
	
	Если ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаСоздания = НачалоДня(ДатаДокумента);
	Иначе
		ДатаСоздания = НачалоДня(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаДокумент, "Дата"));
	КонецЕсли;
	
	Если Не ПроведениеДокументовКлиентСервер.ИспользуютсяМФП(ДатаСоздания) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГраницаМФП.ДатаСоздания
		|ИЗ
		|	РегистрСведений.ГраницаМФП КАК ГраницаМФП
		|ГДЕ
		|	ГраницаМФП.ДатаСоздания <= &ДатаСоздания";
	
	Запрос.УстановитьПараметр("ДатаСоздания", ДатаСоздания);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		 НЗ = РегистрыСведений.ГраницаМФП.СоздатьНаборЗаписей();
		 НоваяЗапись =  НЗ.Добавить();
		 НоваяЗапись.ДатаСоздания 	= ДатаСоздания;
		 НоваяЗапись.Документ	 	= СсылкаНаДокумент;
		 НоваяЗапись.Пользователь	= ПараметрыСеанса.ТекущийПользователь;
		 НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

//Сдвигает границу вперед, если переданная дата совпадает с датой границы
Процедура СдвинутьГраницу(ОбработаннаяДата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГраницаМФП.ДатаСоздания
		|ИЗ
		|	РегистрСведений.ГраницаМФП КАК ГраницаМФП
		|ГДЕ
		|	ГраницаМФП.ДатаСоздания = &ОбработаннаяДата";
	
	Запрос.УстановитьПараметр("ОбработаннаяДата", ОбработаннаяДата);
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		 НЗ = РегистрыСведений.ГраницаМФП.СоздатьНаборЗаписей();
		 НоваяЗапись 				=  НЗ.Добавить();
		 НоваяЗапись.ДатаСоздания 	= ОбработаннаяДата+86400;
		 НоваяЗапись.Пользователь	= ПараметрыСеанса.ТекущийПользователь;
		 НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьГраницу() Экспорт
	
	СтруктураГраницы = Новый Структура("Дата, Документ, Пользователь", Дата(1,1,1), Неопределено, Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГраницаМФП.ДатаСоздания КАК Дата,
		|	ГраницаМФП.Пользователь КАК Пользователь,
		|	ГраницаМФП.Документ КАК Документ
		|ИЗ
		|	РегистрСведений.ГраницаМФП КАК ГраницаМФП";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураГраницы, Выборка);
	КонецЕсли;
	
	Возврат СтруктураГраницы;	
	
КонецФункции