
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	
	лОбъект = ЭтотОбъект.Отбор.Объект.Значение;
	Если ЗначениеЗаполнено(лОбъект) тогда
		лТип = ТипЗнч(лОбъект);
		Если (лТип <> Неопределено) тогда
			лМДОбъект = Метаданные.НайтиПоТипу(лТип);
			Если (лМДОбъект <> Неопределено)
				И Метаданные.ПланыОбмена.ОбменПартКом77_83.Состав.Содержит(лМДОбъект) тогда
				
				лЗапрос = Новый Запрос;
				лЗапрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменПартКом77_83.ЭтотУзел());
				лЗапрос.Текст = 
				"ВЫБРАТЬ
				|	Т.Ссылка
				|ИЗ
				|	ПланОбмена.ОбменПартКом77_83 КАК Т
				|ГДЕ
				|	Т.Ссылка <> &ЭтотУзел
				|	И НЕ Т.ПометкаУдаления";
				
				лУзлы = лЗапрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
				Если (лУзлы.Количество() > 0) тогда
					//Семенов И.П. 31.01.2019 XX-1768(
					//ПланыОбмена.ЗарегистрироватьИзменения(лУзлы, лОбъект);
					ОбменДаннымиКлиентСервер.ЗарегистрироватьИзмененияВПланеОбмена(лУзлы, лОбъект);
					//)Семенов И.П.
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
