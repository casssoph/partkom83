
Процедура СоздатьЗапись(НомерСообщения, Узел, ТелоСообщения, Исходящее=Ложь, ТаблицаИсторииОбъектов=Неопределено, Ошибка=Ложь, ТекстОшибки="") Экспорт
	
	НаборЗаписей = РегистрыСведений.ИсторияОбменовПоСообщениям.СоздатьНаборЗаписей();
	
	Период = ТекущаяДата();
	
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.НомерСообщения.Установить(НомерСообщения);
	НаборЗаписей.Отбор.Исходящее.Установить(Исходящее);
	НаборЗаписей.Отбор.Узел.Установить(Узел);
	
	ЗаписьНабора = НаборЗаписей.Добавить();
	
	ЗаписьНабора.Период             = Период;
	ЗаписьНабора.НомерСообщения     = НомерСообщения;
	ЗаписьНабора.Узел               = Узел;
	ЗаписьНабора.Исходящее          = Исходящее;
	
	ЗаписьНабора.ХранилищеСообщения = Новый ХранилищеЗначения(ТелоСообщения, Новый СжатиеДанных(9));
	
	КоличествоОбъектов = 0;
	Если ТаблицаИсторииОбъектов <> Неопределено Тогда 
		КоличествоОбъектов = ТаблицаИсторииОбъектов.Количество();
	КонецЕсли;
	ЗаписьНабора.КоличествоОбъектов = КоличествоОбъектов;
	ЗаписьНабора.Ошибка            	= Ошибка;
	ЗаписьНабора.ТекстОшибки        = ТекстОшибки;	
	ЗаписьНабора.Пользователь       = глЗначениеПеременной("глТекущийПользователь");
	Если КоличествоОбъектов<>0 Тогда 
		ЗаписьНабора.ЕстьОшибкиПоОбъектам = ТаблицаИсторииОбъектов.Найти(Истина,"Ошибка")<>Неопределено;
	КонецЕсли;
	ЗаписьНабора.ПланОбменаПредставление = Узел.Метаданные().Синоним;
	
	НаборЗаписей.Записать(Истина);
	
	Если НЕ Ошибка И КоличествоОбъектов <> 0 Тогда 
		
		РегистрыСведений.ИсторияОбменовПоОбъектам.СоздатьЗаписиПоСообщению(ЗаписьНабора, ТаблицаИсторииОбъектов);
		
	КонецЕсли;
	
	
КонецПроцедуры