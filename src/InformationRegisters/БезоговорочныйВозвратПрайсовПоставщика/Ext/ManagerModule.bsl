
Функция ПараметрыПрайса(ПрайсПоставщика, Знач ДатаЗапроса = Неопределено) Экспорт
	
	лКлючАлгоритма = "РегистрСведений_БезоговорочныйВозвратПрайсовПоставщика_МодульМенеджера_ПараметрыПрайса";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	Если Не ЗначениеЗаполнено(ДатаЗапроса) Тогда
		ДатаЗапроса = ТекущаяДата();
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура("БезоговорочныйВозвратПоставщика, НачалоДействия, ОкончаниеДействия");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БезоговорочныйВозвратПрайсовПоставщикаСрезПоследних.БезоговорочныйВозвратПоставщика,
		|	БезоговорочныйВозвратПрайсовПоставщикаСрезПоследних.НачалоДействия,
		|	БезоговорочныйВозвратПрайсовПоставщикаСрезПоследних.ОкончаниеДействия КАК ОкончаниеДействия
		|ИЗ
		|	РегистрСведений.БезоговорочныйВозвратПрайсовПоставщика.СрезПоследних(&ДатаЗапроса, ПрайсПоставщика = &ПрайсПоставщика) КАК БезоговорочныйВозвратПрайсовПоставщикаСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаЗапроса", ДатаЗапроса);
	Запрос.УстановитьПараметр("ПрайсПоставщика", ПрайсПоставщика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ДействуетБезоговорочныйВозвратПоставщика(ПрайсПоставщика, Знач ДатаПроверки = Неопределено) Экспорт
	
	ДействуетБезоговорочныйВозвратПоставщика = Ложь;
	
	Если Не ЗначениеЗаполнено(ДатаПроверки) Тогда
		ДатаПроверки = ТекущаяДата();
	КонецЕсли;
	
	ПараметрыПрайса = ПараметрыПрайса(ПрайсПоставщика, ДатаПроверки);
	
	Если  ПараметрыПрайса.БезоговорочныйВозвратПоставщика = Истина
		И ДатаПроверки >= ПараметрыПрайса.НачалоДействия		
		И (Не ЗначениеЗаполнено(ПараметрыПрайса.ОкончаниеДействия)
		ИЛИ ДатаПроверки <= КонецДня(ПараметрыПрайса.ОкончаниеДействия)) Тогда
		
		ДействуетБезоговорочныйВозвратПоставщика = Истина;
	КонецЕсли;
	
	Возврат ДействуетБезоговорочныйВозвратПоставщика;	
	
КонецФункции

Функция ДобавитьПараметрыПрайса(СтруктураПараметров) Экспорт
	
	лКлючАлгоритма = "РегистрСведений_БезоговорочныйВозвратПрайсовПоставщика_МодульМенеджера_ДобавитьПараметрыПрайса";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	ПараметрыПрайса = ПараметрыПрайса(СтруктураПараметров.ПрайсПоставщика);	
	
	Если ПараметрыПрайса.БезоговорочныйВозвратПоставщика <> СтруктураПараметров.БезоговорочныйВозвратПоставщика
		ИЛИ ПараметрыПрайса.НачалоДействия <> СтруктураПараметров.НачалоДействия
		ИЛИ ПараметрыПрайса.ОкончаниеДействия <> СтруктураПараметров.ОкончаниеДействия Тогда
		
		
		МЗ = РегистрыСведений.БезоговорочныйВозвратПрайсовПоставщика.СоздатьМенеджерЗаписи();
		МЗ.Период = ТекущаяДата();
		ЗаполнитьЗначенияСвойств(МЗ, СтруктураПараметров, "ПрайсПоставщика, БезоговорочныйВозвратПоставщика, НачалоДействия, ОкончаниеДействия");
		МЗ.Записать();		
		
	КонецЕсли;
	
КонецФункции