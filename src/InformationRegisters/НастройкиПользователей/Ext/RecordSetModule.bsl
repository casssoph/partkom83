Процедура ПередЗаписью(Отказ, Замещение)
	
	лКлючАлгоритма = "РегистрСведений_НастройкиПользователей_МодульНабораЗаписей_ПередЗаписью";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////////////////////////////////////
	
	// 04.07.19 Строганов Роман > 
	ЗафиксироватьИсториюИзмененийНастроек();
	// 04.07.19 Строганов Роман <
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Перем ОбновлятьЗначениеГлПеременной;
	
	КэшНастроекПользователей = глЗначениеПеременной("ЗначенияНастроекПользователей");
	УправлениеПользователями.ИнформироватьОИзмененииНастроекПравАктивныхПользователей(ЭтотОбъект, 
																					  КэшНастроекПользователей, 
																					  "Настройка",
																					  глЗначениеПеременной("глТекущийПользователь"), 
																					  "значения настроек",
																					  ОбновлятьЗначениеГлПеременной);
																					  
	Если ОбновлятьЗначениеГлПеременной Тогда
		глЗначениеПеременнойУстановить("ЗначенияНастроекПользователей", КэшНастроекПользователей, Истина);
	КонецЕсли;																						  
	
КонецПроцедуры

Процедура ЗафиксироватьИсториюИзмененийНастроек()
	
	СоответствиеЗначений = Новый Соответствие;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПользователей.Значение,
	|	НастройкиПользователей.Настройка
	|ИЗ
	|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
	|ГДЕ
	|	НастройкиПользователей.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ЭтотОбъект.Отбор.Пользователь.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеЗначений.Вставить(Выборка.Настройка, Выборка.Значение);
	КонецЦикла;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		СтароеЗначение = СоответствиеЗначений[Запись.Настройка];
		
		Если СтароеЗначение = Неопределено И (Не ЗначениеЗаполнено(Запись.Значение) Или Запись.Значение = Ложь) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтароеЗначение <> Запись.Значение Тогда
			МенеджерЗаписи = РегистрыСведений.ИсторияИзмененийНастроекПользователей.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = ТекущаяДата();
			МенеджерЗаписи.Пользователь = Запись.Пользователь;
			МенеджерЗаписи.Показатель = Запись.Настройка;
			МенеджерЗаписи.ЗначениеДо = СтароеЗначение;
			МенеджерЗаписи.ЗначениеПосле = Запись.Значение;
			МенеджерЗаписи.Ответственный = ПараметрыСеанса.ТекущийПользователь;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

