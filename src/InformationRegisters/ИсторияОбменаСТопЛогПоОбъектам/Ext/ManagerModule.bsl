
Процедура Добавить(Объект, НомерСообщения, ЕстьОшибки = Ложь, ОписаниеОшибки = "", Период = Неопределено, Исходящее = Истина, НомерПотока = 0) Экспорт
	
	МЗ = РегистрыСведений.ИсторияОбменаСТопЛогПоОбъектам.СоздатьМенеджерЗаписи();
	МЗ.Объект  			= Объект;
	МЗ.НомерСообщения 	= НомерСообщения;
	МЗ.ЕстьОшибки 		= ЕстьОшибки;
	МЗ.ОписаниеОшибки 	= ОписаниеОшибки;
	МЗ.Исходящее 		= Исходящее;
	МЗ.Период    		= ?(Не ЗначениеЗаполнено(Период), ТекущаяДата(), Период);
	МЗ.НомерПотока = НомерПотока;
	Если Не ЗначениеЗаполнено(МЗ.Объект) Тогда
		Возврат;
	КонецЕсли;
	
	МЗ.Записать();	
	
КонецПроцедуры

Функция ТекстСообщения(НомерСообщения, Период, Исходящее, НомерПотока = 0) Экспорт
	
	Сообщение = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсторияОбменаСТопЛог.Сообщение
		|ИЗ
		|	РегистрСведений.ИсторияОбменаСТопЛог КАК ИсторияОбменаСТопЛог
		|ГДЕ
		|	ИсторияОбменаСТопЛог.НомерСообщения = &НомерСообщения
		|	И ИсторияОбменаСТопЛог.Исходящее = &Исходящее
		|	И НАЧАЛОПЕРИОДА(ИсторияОбменаСТопЛог.ДатаЗагрузкиСообщения, ДЕНЬ) = &ДатаЗагрузкиСообщения
		|	И ИсторияОбменаСТопЛог.НомерПотока = &НомерПотока";
	
	Запрос.УстановитьПараметр("ДатаЗагрузкиСообщения", 	НачалоДня(Период));
	Запрос.УстановитьПараметр("НомерСообщения", 		НомерСообщения);
	Запрос.УстановитьПараметр("Исходящее", 				Исходящее);
	Запрос.УстановитьПараметр("НомерПотока", 			НомерПотока);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Сообщение = Выборка.Сообщение;
	КонецЕсли;

	Возврат Сообщение;
	
КонецФункции

Функция ЕстьСообщениеПоОбъекту(Объект, Исходящее = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", 	Объект);
	Запрос.УстановитьПараметр("Исходящее", 	Исходящее);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИсторияОбменаСТопЛогПоОбъектамСрезПоследних.Объект
	               |ИЗ
	               |	РегистрСведений.ИсторияОбменаСТопЛогПоОбъектам.СрезПоследних(
	               |			,
	               |			Объект = &Ссылка
	               |				И НЕ ЕстьОшибки
	               |				И Исходящее = &Исходящее) КАК ИсторияОбменаСТопЛогПоОбъектамСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции