Функция ИсторияОбменовПоОбъекту(Ссылка, ВидОбмена = Неопределено) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Макет");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ИсторияОбменов.Период КАК Период,
	                      |	ИсторияОбменов.ВидОбмена КАК ВидОбмена,
	                      |	ИсторияОбменов.НомерПакета,
	                      |	ИсторияОбменов.Ссылка,
	                      |	ИсторияОбменов.Комментарий
	                      |ИЗ
	                      |	РегистрСведений.ИсторияОбменов КАК ИсторияОбменов
	                      |ГДЕ
	                      |	ИсторияОбменов.Ссылка = &Ссылка
	                      |	И (ИсторияОбменов.ВидОбмена = &ВидОбмена
	                      |			ИЛИ &ВидОбмена = НЕОПРЕДЕЛЕНО)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период
	                      |ИТОГИ ПО
	                      |	ВидОбмена");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидОбмена", ВидОбмена);
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Пока Выборка.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("ВидОбмена");
		Область.Параметры.ВидОбмена = Выборка.ВидОбмена;
		ТабДок.Вывести(Область);
		ВыборкаДетальная = Выборка.Выбрать();
		ТабДок.НачатьГруппуСтрок();
		Пока ВыборкаДетальная.Следующий() Цикл
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаДетальная);
			ТабДок.Вывести(Область);
		КонецЦикла;
		ТабДок.ЗакончитьГруппуСтрок();
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции