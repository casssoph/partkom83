
Процедура СоздатьЗаписиПоСообщению(ДанныеСообщения, ТаблицаИсторииОбъектов) Экспорт
	
	НаборЗаписей = РегистрыСведений.ИсторияОбменовПоОбъектам.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.НомерСообщения.Установить(ДанныеСообщения.НомерСообщения);
	НаборЗаписей.Отбор.Исходящее.Установить(ДанныеСообщения.Исходящее);
	НаборЗаписей.Отбор.Узел.Установить(ДанныеСообщения.Узел);
	НаборЗаписей.Отбор.ДатаСообщения.Установить(ДанныеСообщения.Период);
	
	Для Каждого СтрокаТаблицы Из ТаблицаИсторииОбъектов Цикл  
	
		ЗаписьНабора = НаборЗаписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, ДанныеСообщения,,"Период, ХранилищеСообщения");
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, СтрокаТаблицы);
		
		ЗаписьНабора.ДатаСообщения      = ДанныеСообщения.Период;		
		ЗаписьНабора.ХранилищеСообщения = Новый ХранилищеЗначения(СтрокаТаблицы.ТелоСообщения, Новый СжатиеДанных(9));
				
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
		
	
КонецПроцедуры

Функция СоздатьТаблицуДляИсторииОбъектов() Экспорт
	
	ТаблицаИстории = Новый ТаблицаЗначений;
	
	ТаблицаИстории.Колонки.Добавить("Период");
	ТаблицаИстории.Колонки.Добавить("Объект");
	ТаблицаИстории.Колонки.Добавить("ТелоСообщения");
	ТаблицаИстории.Колонки.Добавить("Ошибка");
	ТаблицаИстории.Колонки.Добавить("ТекстОшибки");
	ТаблицаИстории.Колонки.Добавить("ИмяМетаданных");
	
	Возврат ТаблицаИстории;
	
КонецФункции

Процедура ДобавитьСтрокуВТаблицуИсторииОбъектов(ТаблицаИстории, Объект, ТелоСообщения, Ошибка=Ложь, ТекстОшибки="", ИмяМетаданных="") Экспорт
	
	Если ТаблицаИстории = Неопределено Тогда 
		Возврат;	
	КонецЕсли;
	
	СтрокаИстории = ТаблицаИстории.Добавить();
	
	СтрокаИстории.Период = ТекущаяДата();
	СтрокаИстории.Объект = Объект;
	ОбъектЭтоСсылка = Ложь;
	ТипОбъекта = ТипЗнч(Объект);
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипОбъекта)
		ИЛИ Документы.ТипВсеСсылки().СодержитТип(ТипОбъекта) Тогда 
		
		ОбъектЭтоСсылка = Истина;
		Если НЕ ЗначениеЗаполнено(Объект.ВерсияДанных) Тогда  
			СтрокаИстории.Объект = Строка(Объект.УникальныйИдентификатор());
		КонецЕсли;		
	КонецЕсли;
	
	ТекстСообщения = ТелоСообщения;
	Если ТипЗнч(ТелоСообщения) = Тип("ДокументDOM") Тогда 
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку("UTF-8");
		ЗаписьDOM = Новый ЗаписьDOM;
		ЗаписьDOM.Записать(ТелоСообщения,ЗаписьXML);
		ТекстСообщения = ЗаписьXML.Закрыть();
	ИначеЕсли ТипЗнч(ТелоСообщения) = Тип("ОбъектXDTO") Тогда 
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку("UTF-8");
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ТелоСообщения);
		ТекстСообщения = ЗаписьXML.Закрыть();
	КонецЕсли;
	СтрокаИстории.ТелоСообщения = ТекстСообщения;
	СтрокаИстории.Ошибка        = Ошибка;
	СтрокаИстории.ТекстОшибки   = ТекстОшибки;
		
	Если ИмяМетаданных = "" И ОбъектЭтоСсылка Тогда 
		СтрокаИстории.ИмяМетаданных = Объект.Метаданные().ПолноеИмя();
	Иначе
		СтрокаИстории.ИмяМетаданных = ИмяМетаданных;
	КонецЕсли;
	
КонецПроцедуры