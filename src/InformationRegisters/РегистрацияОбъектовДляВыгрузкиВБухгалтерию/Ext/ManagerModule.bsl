
Процедура Зарегистрировать(Объект) Экспорт 
	
	лКлючАлгоритма = "РегистрСведений_РегистрацияОбъектовДляВыгрузкиВБухгалтерию_МодульМенеджера_Зарегистрировать";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////
	
	Если Объект.ДополнительныеСвойства.Свойство("ОтключитьМеханизмРегистрацииОбъектов") Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	Регистрировать = Ложь;
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеТоваровУслуг") Тогда 
		
		Если (Объект.СтатусДокумента = Справочники.СтатусыДокументов.ПоступлениеТоваровПринят
			ИЛИ Объект.СтатусДокумента = Справочники.СтатусыДокументов.ПоступлениеТоваровРазмещен
			ИЛИ Объект.ЭтоМФП)
			И Объект.ВидОперацииПоступления <> Перечисления.ВидыПоступленияТоваров.ОтветХранение 
			И НЕ Объект.Склад.Бонусный
			И НЕ Объект.Склад.Сувенирный
			И Объект.СуммаДокумента <> 0 Тогда 
			
			Регистрировать = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		
		Если Объект.СтатусДокумента = Справочники.СтатусыДокументов.РеализацияТоваровУслугОтгружен
			И Объект.СуммаДокумента <> 0
			И (Объект.Организация.Франчайзи = ЛОЖЬ
				ИЛИ Объект.Организация.Франчайзи = ИСТИНА
				И Объект.ДоговорКонтрагента.ВидОплаты = Перечисления.ВидыДенежныхСредств.Безналичные)
			Тогда 
			
			Регистрировать = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		
		Если Объект.СтатусДокумента <> Справочники.СтатусыДокументов.ВозвратТоваровОтПокупателяНовый
			И Объект.СуммаДокумента <> 0
			И ЗначениеЗаполнено(Объект.АктРассмотренияВозврата)
			Тогда 
			
			Регистрировать = Истина;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		
		Если Объект.СуммаДокумента <> 0
			И Объект.флСпособПроведения = Перечисления.СпособыПроведенияДокументов.ТолькоТовары
			Тогда 
			
			Регистрировать = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Регистрировать Тогда 
		
		МенеджерЗаписи = РегистрыСведений.РегистрацияОбъектовДляВыгрузкиВБухгалтерию.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект        = Объект.Ссылка;
		МенеджерЗаписи.ИмяМетаданных = Объект.Метаданные().Имя;
		МенеджерЗаписи.ДатаОбъекта   = Объект.Дата;
		Попытка
			МенеджерЗаписи.Записать();
		Исключение
			ОбщегоНазначения.ЗаписатьОшибкуВЖурналРегистрации("Регистрация для выгрузки в БП","Не удалось зарегистрировать по причине:
																								|"+ОписаниеОшибки());
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьРегистрацию(ОбъектСсылка = Неопределено, ИмяМетаданных = "") Экспорт 
	
	лКлючАлгоритма = "РегистрСведений_РегистрацияОбъектовДляВыгрузкиВБухгалтерию_МодульМенеджера_УдалитьРегистрацию";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;
	///////////////////////////////////////////
	
	Если ОбъектСсылка = Неопределено
		И ИмяМетаданных = "" Тогда 
		
		НаборЗаписей = РегистрыСведений.РегистрацияОбъектовДляВыгрузкиВБухгалтерию.СоздатьНаборЗаписей();
		НаборЗаписей.Записать();

	Иначе
			
		МассивМетаданных = Новый Массив;
		ТипИмяМетаданных = ТипЗнч(ИмяМетаданных);
		Если ТипИмяМетаданных = Тип("Строка") Тогда 
			Если ИмяМетаданных <> "" Тогда 
				МассивМетаданных.Добавить(ИмяМетаданных);
			КонецЕсли;
		ИначеЕсли ТипИмяМетаданных = Тип("Массив") Тогда 
			МассивМетаданных = ИмяМетаданных;	
		КонецЕсли;
		
		МассивОбъектов = Новый Массив;
		ТипыОбъектов = Метаданные.РегистрыСведений.РегистрацияОбъектовДляВыгрузкиВБухгалтерию.Измерения.Объект.Тип.Типы();
		ТипОбъектСсылка = ТипЗнч(ОбъектСсылка);
		Если ТипыОбъектов.Найти(ТипОбъектСсылка) <> Неопределено Тогда 
			Если ЗначениеЗаполнено(ОбъектСсылка) Тогда 
				МассивОбъектов.Добавить(ОбъектСсылка);
			КонецЕсли;
		ИначеЕсли ТипОбъектСсылка = Тип("Массив") Тогда 
			МассивОбъектов = ОбъектСсылка;	
		КонецЕсли;
		
		Для Каждого ЭлементИмяМетаданных Из МассивМетаданных Цикл 
			
			НаборЗаписей = РегистрыСведений.РегистрацияОбъектовДляВыгрузкиВБухгалтерию.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИмяМетаданных.Установить(ЭлементИмяМетаданных);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		Для Каждого ЭлементОбъект Из МассивОбъектов Цикл 
			
			НаборЗаписей = РегистрыСведений.РегистрацияОбъектовДляВыгрузкиВБухгалтерию.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ЭлементОбъект);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры