
Процедура Добавить(Документ, Период = Неопределено, ДатаОтправки = Неопределено, ДатаОтменыОтправки = Неопределено, Комментарий = "", Ошибка = Ложь) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	МЗ 						= РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.СоздатьМенеджерЗаписи();
	МЗ.Документ 			= Документ;
	МЗ.Период 				= ?(ЗначениеЗаполнено(Период), Период, ТекущаяДата());
	МЗ.ДатаОтправки 		= ДатаОтправки;
	МЗ.ДатаОтменыОтправки 	= ДатаОтменыОтправки;
	МЗ.Комментарий 			= Комментарий;
	МЗ.Ошибка 				= Ошибка;
	МЗ.Записать();
	
КонецПроцедуры

Процедура ДобавитьКОтправке(Документ, Период = Неопределено, Комментарий = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеДокумента = СостояниеДокумента(Документ);
	Если СостояниеДокумента.ДатаОтправки = Дата(1,1,1)
		И СостояниеДокумента.ДатаОтменыОтправки = Дата(1,1,1) Тогда 
		//Документ уже стоит в очереди на отправку
		Возврат;
	КонецЕсли;
	
	Добавить(Документ, Период, Комментарий );
	
КонецПроцедуры


Процедура Удалить(Документ) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Записать();
	
КонецПроцедуры

Процедура УстановитьДатуОтправки(Документ, ДатаОтправки = Неопределено, Комментарий = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Прочитать();
	Для каждого ЗаписьНабора Из НЗ Цикл
		Если Не ЗначениеЗаполнено(ЗаписьНабора.ДатаОтправки) И Не ЗначениеЗаполнено(ЗаписьНабора.ДатаОтменыОтправки) Тогда
			ЗаписьНабора.ДатаОтправки 	= ?(ЗначениеЗаполнено(ДатаОтправки), ДатаОтправки, ТекущаяДата());
			ЗаписьНабора.Комментарий 	= Комментарий;
			ЗаписьНабора.Ошибка 		= Ложь;
		КонецЕсли;
	КонецЦикла;
	Если НЗ.Количество() > 0 Тогда
		НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДатуОтменыОтправки(Документ, ДатаОтменыОтправки = Неопределено, Комментарий = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Прочитать();
	Для каждого ЗаписьНабора Из НЗ Цикл
		Если Не ЗначениеЗаполнено(ЗаписьНабора.ДатаОтправки) И Не ЗначениеЗаполнено(ЗаписьНабора.ДатаОтменыОтправки) Тогда
			ЗаписьНабора.ДатаОтменыОтправки = ?(ЗначениеЗаполнено(ДатаОтменыОтправки), ДатаОтменыОтправки, ТекущаяДата());
			ЗаписьНабора.Комментарий 		= Комментарий;
			ЗаписьНабора.Ошибка 			= Ложь;
		КонецЕсли;
	КонецЦикла;
	Если НЗ.Количество() > 0 Тогда
		НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОшибкуОтправки(Документ, Комментарий = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.ОчередьОтправкиДокументовОтгрузки.СоздатьНаборЗаписей();
	НЗ.Отбор.Документ.Установить(Документ);
	НЗ.Прочитать();
	Для каждого ЗаписьНабора Из НЗ Цикл
		Если Не ЗначениеЗаполнено(ЗаписьНабора.ДатаОтправки) Тогда
			ЗаписьНабора.Комментарий = Комментарий;
			ЗаписьНабора.Ошибка 	 = Истина;
		КонецЕсли;
	КонецЦикла;
	Если НЗ.Количество() > 0 Тогда
		НЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция СостояниеДокумента(Документ, СостояниеСтрокой = Ложь) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	СостояниеДокумента = Новый Структура("Документ, ДатаОтправки, ДатаОтменыОтправки, Комментарий, Ошибка");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОчередьОтправкиДокументовОтгрузкиСрезПоследних.Документ,
	|	ОчередьОтправкиДокументовОтгрузкиСрезПоследних.ДатаОтправки,
	|	ОчередьОтправкиДокументовОтгрузкиСрезПоследних.ДатаОтменыОтправки,
	|	ОчередьОтправкиДокументовОтгрузкиСрезПоследних.Комментарий,
	|	ОчередьОтправкиДокументовОтгрузкиСрезПоследних.Ошибка
	|ИЗ
	|	РегистрСведений.ОчередьОтправкиДокументовОтгрузки.СрезПоследних(, Документ = &Документ) КАК ОчередьОтправкиДокументовОтгрузкиСрезПоследних";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СостояниеДокумента, Выборка); 
	КонецЕсли;
	
	Если СостояниеСтрокой Тогда
		
		Если СостояниеДокумента.Документ = Неопределено Тогда
			//Нет ни одной записи
			ВозвращаемоеЗначение = "Документ не отмечен к отправке";
		ИначеЕсли СостояниеДокумента.Ошибка Тогда
			ВозвращаемоеЗначение = "Не удалось отправить документ по причине: "+СостояниеДокумента.Комментарий+". Документ ожидает повторной отправки.";
		ИначеЕсли ЗначениеЗаполнено(СостояниеДокумента.ДатаОтменыОтправки) Тогда
			ВозвращаемоеЗначение = "Отправка документа отменена "+СостояниеДокумента.ДатаОтменыОтправки+", "+СостояниеДокумента.Комментарий;
		ИначеЕсли Не ЗначениеЗаполнено(СостояниеДокумента.ДатаОтправки) Тогда
			ВозвращаемоеЗначение = "Документ ожидает отправки";
		Иначе
			ВозвращаемоеЗначение = "Документ отправлен "+СостояниеДокумента.ДатаОтправки;
		КонецЕсли;
		
	Иначе
		
		ВозвращаемоеЗначение = СостояниеДокумента;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Процедура УстановитьБлокировку(Документ) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьОтправкиДокументовОтгрузки");
	ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();

КонецПроцедуры