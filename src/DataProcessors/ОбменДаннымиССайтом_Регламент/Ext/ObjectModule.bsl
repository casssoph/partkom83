Процедура ВыполнитьРегламентноеЗадание() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторУзлаОбмена) тогда
		ВызватьИсключение "Не заполнен реквизит ""Узел обмена"".";		
	КонецЕсли;
	
	Настройка = Справочники.НастройкиРеквизитовДляОбменов.Обмен_1С_Сайт;
	мАдресВебСервиса = ?(ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза(), Настройка.СтрокаДляРабочейБазы, Настройка.СтрокаДляТестовойБазы);
			
	лМетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт;
	лМенеджерПланаОбмена = ОбщегоНазначения.МенеджерОбъектаПоМетаданным(лМетаданныеПланаОбмена);
	
	лПолучатель = лМенеджерПланаОбмена.ЭтотУзел();
	лКодПолучателя = Число(лПолучатель.ИдентификаторУзла);
	
	лОтправитель = ОбменДаннымиКлиентСервер.ПолучитьВходящийУзелОбмена(лМетаданныеПланаОбмена, ИдентификаторУзлаОбмена);
	лКодОтправителя = лОтправитель.ИдентификаторУзла;
	лНомерПринятого = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(лОтправитель, "НомерПринятого");
	
	лОпределения = Новый WSОпределения(мАдресВебСервиса);
	лПрокси = Новый WSПрокси(лОпределения, лОпределения.Сервисы[0].URIПространстваИмен,	лОпределения.Сервисы[0].Имя, лОпределения.Сервисы[0].ТочкиПодключения[0].Имя);
	
	лОтветСервера = лПрокси.GetExchangeMessage(лМетаданныеПланаОбмена.Имя, лКодПолучателя, лНомерПринятого);
	ОбменДаннымиВызовСервера.ЗарегистрироватьСообщениеВИсторииОбменаССайтом(лКодОтправителя, лКодПолучателя, лОтветСервера, лНомерПринятого);
	лУпаковщик = DataExchangeПовтИсп.ПолучитьУпаковщик();
	лСообщениеОбмена = лУпаковщик.UnpackWideString(лОтветСервера);
	
	// без этого на сервере не работает почему то
	//-потому что в конце текста - Символ(0)
	лТекстовыйДокумент = Новый ТекстовыйДокумент;
	лТекстовыйДокумент.УстановитьТекст(лСообщениеОбмена);
	лСообщениеОбмена = лТекстовыйДокумент.ПолучитьТекст();
	
	лМенеджерПланаОбмена.ЗагрузитьСообщениеОбмена(лСообщениеОбмена);
	лУпаковщик = Неопределено;
		
КонецПроцедуры
