Процедура ВыполнитьРегламентноеЗадание() Экспорт
		
	Если НЕ ЗначениеЗаполнено(ИдентификаторУзлаОбмена) тогда
		ВызватьИсключение "Не заполнен реквизит ""Узел обмена"" Отправителя.";		
	КонецЕсли;
	
	URI = "http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema";
	Настройка = Справочники.НастройкиРеквизитовДляОбменов.Обмен_1С_Сайт_Заявки;
	АдресВебСервиса = ?(ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза(), Настройка.СтрокаДляРабочейБазы, Настройка.СтрокаДляТестовойБазы);
	МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок;
	МенеджерПланаОбмена = ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок;
	
	Получатель = МенеджерПланаОбмена.ЭтотУзел();
	Отправитель = ОбменДаннымиКлиентСервер.ПолучитьВходящийУзелОбмена(МетаданныеПланаОбмена, ИдентификаторУзлаОбмена);
	Если НЕ ЗначениеЗаполнено(Отправитель) тогда
		//Семенов И.П. 31.01.2019 XX-1768(
		ОбменДаннымиКлиентСервер.ЗафиксироватьСообщениеВИсторииОбмена(0,МенеджерПланаОбмена.ПустаяСсылка(),"",,Истина,"Не найден отправитель сообщения в списке узлов.");
		//)Семенов И.П.
		ВызватьИсключение "[ЗагрузитьСообщениеОбмена]: не найден отправитель сообщения в списке узлов.";	
	КонецЕсли;                                                                      		
	
	КодПолучателя = Получатель.ИдентификаторУзла;
	КодОтправителя = ИдентификаторУзлаОбмена;
	НомерПринятого = Отправитель.НомерОтправленного;
	
	//Семенов И.П. 31.01.2019 XX-1768(
	Попытка
	//)Семенов И.П.
		Определения = Новый WSОпределения(АдресВебСервиса);
		Прокси = Новый WSПрокси(Определения, Определения.Сервисы[0].URIПространстваИмен, Определения.Сервисы[0].Имя, Определения.Сервисы[0].ТочкиПодключения[0].Имя);
		
		ОтветСервера = Прокси.GetExchangeMessage("orders_nn", КодПолучателя, НомерПринятого);
		РаспакованноеСообщение = ОбменДаннымиСервер.РаспаковатьСообщениеОбмена(ОтветСервера);
		
		ТипСообщениеОбмена = ФабрикаXDTO.Тип(URI, "СообщениеОбмена");
		ЧтениеХМЛ = Новый ЧтениеXML;
		ЧтениеХМЛ.УстановитьСтроку(РаспакованноеСообщение);
		ДанныеХДТО = ФабрикаXDTO.ПрочитатьXML(ЧтениеХМЛ, ТипСообщениеОбмена);
		ОбъектыXDTO = ДанныеХДТО.Объекты.ПолучитьСписок("Объект");
	//Семенов И.П. 31.01.2019 XX-1768(
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		ОбменДаннымиКлиентСервер.ЗафиксироватьСообщениеВИсторииОбмена(0,Отправитель,"",,Истина,ТекстОшибки);
		ВызватьИсключение "[ЗагрузитьСообщениеОбмена]: "+ТекстОшибки;
	КонецПопытки;
	//)Семенов И.П.
	
	НомерСообщения = ДанныеХДТО.НомерСообщения;
	Если НомерСообщения <= НомерПринятого Тогда
		//Семенов И.П. 31.01.2019 XX-1768(
		ОбменДаннымиКлиентСервер.ЗафиксироватьСообщениеВИсторииОбмена(НомерСообщения,Отправитель,РаспакованноеСообщение,,Истина,"Неправильное значение элемента ""НомерСообщения"": сообщение с таким номером уже загружалось.");
		//)Семенов И.П.
		ВызватьИсключение "[ЗагрузитьСообщениеОбмена]: неправильное значение элемента ""НомерСообщения"": сообщение с таким номером уже загружалось.";
	КонецЕсли;
	
	Если ОбъектыXDTO.Количество() > 0 Тогда
		//Семенов И.П. 31.01.2019 XX-1768(
		ОбменДаннымиКлиентСервер.НачатьЗаписьВИсториюОбменовПоОбъектам();
		//)Семенов И.П.
		РегистрыСведений.ИсторияОбменаССайтом_ЗаявкиПокупателей.ЗарегистрироватьСообщение(КодОтправителя, КодПолучателя, ОтветСервера, НомерПринятого);
		ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок.ОбработатьПакетЗаявокССайта(ОбъектыXDTO, URI, НомерСообщения, Ложь);
	КонецЕсли;
	
	//Семенов И.П. 31.01.2019 XX-1768(
	ОбменДаннымиКлиентСервер.ЗафиксироватьСообщениеВИсторииОбмена(НомерСообщения,Отправитель,РаспакованноеСообщение);
	//)Семенов И.П.
	
	ОбъектУзла = Отправитель.ПолучитьОбъект();
	ОбъектУзла.НомерОтправленного = НомерСообщения;
	ОбъектУзла.Записать();
	
КонецПроцедуры

