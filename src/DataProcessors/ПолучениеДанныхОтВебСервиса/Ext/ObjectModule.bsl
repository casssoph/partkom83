// Получает данные изготовителя с сайта
//
// Параметры:
//  вхНаименование  - <Строка> - наименование детали
//                 
//
// Возвращаемое значение:
//   <Структура>   - структура вида "УИД, Наименование, Тип, ЗаменаИмя, ЗаменаУИД"
//
Функция ПолучитьИзготовителя(вхНаименование) Экспорт
	
	Если Не ЗначениеЗаполнено(СокрЛП(вхНаименование)) Тогда
		Возврат Неопределено
	КонецЕсли;
	
	СтруктураВозврат = Новый Структура("УИД, Наименование, Тип, ЗаменаИмя, ЗаменаУИД");
	
	лСоединение = Новый COMОбъект("WinHttp.WinHttpRequest.5.1");
	лСоединение.Open("POST", "http://testexchange.part-kom.ru/uuid/brand", Ложь);
	лСоединение.SetRequestHeader("Accept-Encoding", "gzip, deflate");
	лСоединение.SetRequestHeader("Content-Type", "application/x-www-form-urlencoded");
	лСтрокаЗапроса = "brand=" + СокрЛП(вхНаименование);
	лСоединение.Send(лСтрокаЗапроса);
	Если (лСоединение.Status = 200) тогда
		ЧтениеХМЛ = Новый ЧтениеXML;
		ЧтениеХМЛ.УстановитьСтроку(лСоединение.ResponseText);
		
		ПостроительDOM = Новый ПостроительDOM;
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеХМЛ);
		
		Ответ = ДокументDOM.ПервыйДочерний;
		
		Для Каждого ТекЭлемент Из Ответ.ДочерниеУзлы Цикл
			
			Если ТекЭлемент.ИмяУзла = "success" Тогда
				Если ТекЭлемент.ТекстовоеСодержимое = "0" Тогда
					Возврат Ответ.ДочерниеУзлы.Элемент(1).ТекстовоеСодержимое
				КонецЕсли;
			КонецЕсли;
			
			Если ТекЭлемент.ИмяУзла = "data" Тогда
				
				Для Каждого ТекУзел Из ТекЭлемент.ДочерниеУзлы Цикл
					
					Если ТекУзел.ИмяУзла = "uuid" Тогда
						СтруктураВозврат.УИД = ТекУзел.ТекстовоеСодержимое;
					КонецЕсли;
					
					Если ТекУзел.ИмяУзла = "name" Тогда
						СтруктураВозврат.Наименование = ТекУзел.ТекстовоеСодержимое;
					КонецЕсли;
					
					Если ТекУзел.ИмяУзла = "type" Тогда
						СтруктураВозврат.Тип = ТекУзел.ТекстовоеСодержимое;
					КонецЕсли;
					
					Если ТекУзел.ИмяУзла = "replacement" Тогда
						
						Для Каждого ТекУзелЗамены Из ТекУзел.ДочерниеУзлы Цикл
							
							Если ТекУзелЗамены.ИмяУзла = "uuid" Тогда
								СтруктураВозврат.ЗаменаУИД = ТекУзелЗамены.ТекстовоеСодержимое;
							КонецЕсли;
							
							Если ТекУзелЗамены.ИмяУзла = "name" Тогда
								СтруктураВозврат.ЗаменаИмя = ТекУзелЗамены.ТекстовоеСодержимое;
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЧтениеХМЛ.Закрыть();
	КонецЕсли;
	
	Возврат СтруктураВозврат
	
КонецФункции // ПолучитьИзготовителя()

// Получает данные номенклатуры с сайта
//
// Параметры:
//  вхСтруктура  - <Структура> - вида "Бренд, Артикул, Имя"
//                 Бренд - GUID изготовителя
//                 Артикул - артикул
//                 Имя - наименование
//
// Возвращаемое значение:
//   <Структура>   - структура вида "УИД, Наименование, Артикул, ЗаменаИмя, ЗаменаУИД"
//
Функция ПолучитьНоменклатуру(вхСтруктура) Экспорт
	
	СтруктураВозврат = Новый Структура("УИД, Наименование, Артикул, ЗаменаИмя, ЗаменаУИД");
	
	Настройка = Справочники.НастройкиРеквизитовДляОбменов.API_Сайт;
	АдресWS = ?(ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза(), Настройка.СтрокаДляРабочейБазы, Настройка.СтрокаДляТестовойБазы);
	
	лСоединение = Новый COMОбъект("WinHttp.WinHttpRequest.5.1");
	лСоединение.Open("POST", "http://" + АдресWS + "/uuid/part", Ложь);
	лСоединение.SetRequestHeader("Accept-Encoding", "gzip, deflate");
	лСоединение.SetRequestHeader("Content-Type", "application/x-www-form-urlencoded");
	лСтрокаЗапроса = "brand_uuid=" + СокрЛП(вхСтруктура.Бренд);
	лСтрокаЗапроса = лСтрокаЗапроса + "&number=" + СокрЛП(вхСтруктура.Артикул);
	лСтрокаЗапроса = лСтрокаЗапроса + "&name=" + СокрЛП(вхСтруктура.Имя);
	лСоединение.Send(лСтрокаЗапроса);
	Если лСоединение.Status = 200 тогда
		ЧтениеХМЛ = Новый ЧтениеXML;
		ЧтениеХМЛ.УстановитьСтроку(лСоединение.ResponseText);
		ПостроительDOM = Новый ПостроительDOM;
		ДокументDOM = ПостроительDOM.Прочитать(ЧтениеХМЛ);
		Разыменователь = Новый РазыменовательПространствИменDOM(ДокументDOM);
			
		НоменклатураНайдена = (ЗначениеЭлементаDOM(ДокументDOM, "/response/success", Разыменователь, 0) = "1");
		Если НоменклатураНайдена Тогда
			
			uuid = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/uuid", Разыменователь, "");
			brand_uuid = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/brand_uuid", Разыменователь, "");
			number = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/number", Разыменователь, "");
			name = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/name", Разыменователь, "");
			replacement_brand_uuid = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/replacement/brand_uuid", Разыменователь, "");
			replacement_name = ЗначениеЭлементаDOM(ДокументDOM, "/response/data/replacement/name", Разыменователь, "");
			
			СтруктураВозврат.УИД = uuid;
			СтруктураВозврат.Наименование = name;
			СтруктураВозврат.Артикул = number;
			СтруктураВозврат.ЗаменаИмя = replacement_name;
			СтруктураВозврат.ЗаменаУИД = replacement_brand_uuid;
		КонецЕсли;
		ЧтениеХМЛ.Закрыть();
	КонецЕсли;
	
	Возврат СтруктураВозврат;
	
КонецФункции
Функция ЗначениеЭлементаDOM(ДокументDOM, Путь, Разыменователь, ЗначениеПоУмолчанию)
	
	РезультатXPath = ДокументDOM.ВычислитьВыражениеXPath (Путь, ДокументDOM, Разыменователь);
	ПолученныйЭлемент = РезультатXPath.ПолучитьСледующий();
	
	Возврат ?(ПолученныйЭлемент = Неопределено, ЗначениеПоУмолчанию, ПолученныйЭлемент.ТекстовоеСодержимое);
	
КонецФункции

