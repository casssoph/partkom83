//Выгрузка//
Процедура ВыгрузитьКонтрагента(Контрагент, ЭтоНовый = Ложь) Экспорт
	
	Если РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Elma", "Выгружать контрагентов", Ложь) Тогда
		КаталогФайлов = РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Elma", "Папка выгрузки контрагентов", "\\1csql01-g9\1c_exchange\Elma_watching\NewElma\");
		ИмяФайла = КаталогФайлов + "спрКА_" + Контрагент.Код + ".xml";
		ДанныеКонтрагента = ПолучитьДанныеКонтрагента(Контрагент);
		ЛогинELMAТекущегоПользователя = ПараметрыСеанса.ТекущийПользователь.ЛогинЭЛМА;
		ЛогинELMAЗакрепленногоМенеджера = ДанныеКонтрагента.ЛогинЭлмаЗакрепленногоМенеджера;
		Если ВозможноВыгрузитьВЭлма(ДанныеКонтрагента, ЛогинELMAТекущегоПользователя, ЛогинELMAЗакрепленногоМенеджера) Тогда
			ВыгрузитьДанныеКонтрагентаВФайл(ИмяФайла, ДанныеКонтрагента, ЛогинELMAТекущегоПользователя, ЛогинELMAЗакрепленногоМенеджера, ЭтоНовый);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
Функция ПолучитьДанныеКонтрагента(Контрагент)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Контрагенты.Ссылка
	                      |ПОМЕСТИТЬ Контрагент
	                      |ИЗ
	                      |	Справочник.Контрагенты КАК Контрагенты
	                      |ГДЕ
	                      |	Контрагенты.Ссылка = &Контрагент
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailДляОбменаДокументамиСКонтрагентами)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК EMail,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ЮрАдрес,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ФактАдрес,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК Телефон,
	                      |	МАКСИМУМ(КонтактнаяИнформация.Страна) КАК Страна,
	                      |	МАКСИМУМ(КонтактнаяИнформация.Регион) КАК Регион,
	                      |	МАКСИМУМ(КонтактнаяИнформация.Город) КАК Город
	                      |ПОМЕСТИТЬ КонтактнаяИнформацияКонтрагента
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Объект = &Контрагент
	                      |	И КонтактнаяИнформация.Вид В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailДляОбменаДокументамиСКонтрагентами))
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	УчетныеЗаписиСайта.Код,
	                      |	УчетныеЗаписиСайта.Пароль
	                      |ПОМЕСТИТЬ УчетнаяЗапись
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиСайта КАК УчетныеЗаписиСайта
	                      |ГДЕ
	                      |	УчетныеЗаписиСайта.Владелец.Владелец = &Контрагент
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	КонтактныеЛицаКонтрагентов.Ссылка,
	                      |	КонтактныеЛицаКонтрагентов.Код,
	                      |	КонтактныеЛицаКонтрагентов.Наименование КАК ФИО,
	                      |	КонтактныеЛицаКонтрагентов.КонтактноеЛицо.Фамилия КАК Фамилия,
	                      |	КонтактныеЛицаКонтрагентов.КонтактноеЛицо.Имя КАК Имя,
	                      |	КонтактныеЛицаКонтрагентов.КонтактноеЛицо.Отчество КАК Отчество,
	                      |	КонтактныеЛицаКонтрагентов.Должность,
	                      |	КонтактныеЛицаКонтрагентов.ДатаРождения
	                      |ПОМЕСТИТЬ КонтактноеЛицоКонтрагента
	                      |ИЗ
	                      |	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	                      |ГДЕ
	                      |	КонтактныеЛицаКонтрагентов.Владелец = &Контрагент
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailПочтыКонтактногоЛицаКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК EMail,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛицаКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК Телефон
	                      |ПОМЕСТИТЬ КонтактнаяИнформацияКонтактногоЛица
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Объект В
	                      |			(ВЫБРАТЬ
	                      |				КонтактноеЛицоКонтрагента.Ссылка
	                      |			ИЗ
	                      |				КонтактноеЛицоКонтрагента КАК КонтактноеЛицоКонтрагента)
	                      |	И КонтактнаяИнформация.Вид В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailПочтыКонтактногоЛицаКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛицаКонтрагента))
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МАКСИМУМ(МенеджерыТорговыхТочекСрезПоследних.Менеджер) КАК Менеджер
	                      |ПОМЕСТИТЬ МенеджерыТорговыхТочек
	                      |ИЗ
	                      |	РегистрСведений.МенеджерыТорговыхТочек.СрезПоследних КАК МенеджерыТорговыхТочекСрезПоследних
	                      |ГДЕ
	                      |	МенеджерыТорговыхТочекСрезПоследних.ТорговаяТочка.Владелец = &Контрагент
	                      |	И МенеджерыТорговыхТочекСрезПоследних.Менеджер.Пользователь.ЛогинЭЛМА <> """"
	                      |	И (НЕ &Покупатель
	                      |				И МенеджерыТорговыхТочекСрезПоследних.ВидМенеджера В (ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.Снабжения), ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.ЗамМенеджераСнабжения))
	                      |			ИЛИ &Покупатель
	                      |				И МенеджерыТорговыхТочекСрезПоследних.ВидМенеджера В (ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.Продажи), ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.ЗамМенеджераПродажи)))
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Контрагент.Ссылка,
	                      |	Контрагент.Ссылка.Код КАК CRM_Company_OID,
	                      |	Контрагент.Ссылка.Наименование КАК CRM_Company_Name,
	                      |	Контрагент.Ссылка.НаименованиеПолное КАК CRM_Company_Industry_Name,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.СайтГруппаКонтрагента.Код, 0) КАК CRM_Company_Industry_ID,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнаяТорговаяТочка.Регион.Наименование, """") КАК CRM_Company_Region_Name,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойДоговорКонтрагента.Организация.Код, """") КАК CRM_Company_Type_ID,
	                      |	ВЫБОР
	                      |		КОГДА Контрагент.Ссылка.Покупатель
	                      |			ТОГДА ""Покупатель""
	                      |		ИНАЧЕ ""Поставщик""
	                      |	КОНЕЦ КАК CRM_Company_Type_Name,
	                      |	ЕСТЬNULL(УчетнаяЗапись.Код, """") КАК CRM_Company_Site,
	                      |	ЕСТЬNULL(УчетнаяЗапись.Пароль, """") КАК CRM_Company_Site_,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.Телефон, """") КАК CRM_Company_OtherPhone,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.EMail, """") КАК CRM_Company_Email,
	                      |	Контрагент.Ссылка.ДатаСоздания КАК CRM_Company_CompanyDay,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.ЮрАдрес, """") КАК CRM_Company_BAdress_PostalAdress,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.Страна, """") КАК CRM_Company_BAdress_Country,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.Регион, """") КАК CRM_Company_BAdress_State,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.Город, """") КАК CRM_Company_BAdress_City,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.ФактАдрес, """") КАК CRM_Company_PAdress_PostalAdress,
	                      |	Контрагент.Ссылка.ИНН КАК CRM_Company_INN,
	                      |	Контрагент.Ссылка.КПП КАК CRM_Company_KPP,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойДоговорКонтрагента.ДопустимаяСуммаЗадолженности, 0) КАК CRM_Company_Limit,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности, 0) КАК CRM_Company_DelayOfPayment,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.Код, """") КАК CRM_Contact_OID,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.ФИО, """") КАК CRM_Contact_FirstName,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.Фамилия, """") КАК CRM_Contact_LastName,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.Отчество, """") КАК CRM_Contact_PatronymicName,
	                      |	Контрагент.Ссылка.Код КАК CRM_Contact_Company_OID,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.Должность, """") КАК CRM_Contact_Post,
	                      |	ВЫБОР
	                      |		КОГДА КонтактноеЛицоКонтрагента.ДатаРождения = ДАТАВРЕМЯ(1, 1, 1)
	                      |				ИЛИ КонтактноеЛицоКонтрагента.ДатаРождения ЕСТЬ NULL
	                      |			ТОГДА 0
	                      |		ИНАЧЕ ГОД(КонтактноеЛицоКонтрагента.ДатаРождения)
	                      |	КОНЕЦ КАК CRM_Contact_BirthdateYear,
	                      |	ВЫБОР
	                      |		КОГДА КонтактноеЛицоКонтрагента.ДатаРождения = ДАТАВРЕМЯ(1, 1, 1)
	                      |				ИЛИ КонтактноеЛицоКонтрагента.ДатаРождения ЕСТЬ NULL
	                      |			ТОГДА 0
	                      |		ИНАЧЕ МЕСЯЦ(КонтактноеЛицоКонтрагента.ДатаРождения)
	                      |	КОНЕЦ КАК CRM_Contact_BirthdateMonth,
	                      |	ВЫБОР
	                      |		КОГДА КонтактноеЛицоКонтрагента.ДатаРождения = ДАТАВРЕМЯ(1, 1, 1)
	                      |				ИЛИ КонтактноеЛицоКонтрагента.ДатаРождения ЕСТЬ NULL
	                      |			ТОГДА 0
	                      |		ИНАЧЕ ДЕНЬ(КонтактноеЛицоКонтрагента.ДатаРождения)
	                      |	КОНЕЦ КАК CRM_Contact_BirthdateDay,
	                      |	ЕСТЬNULL(КонтактноеЛицоКонтрагента.ФИО, """") КАК CRM_Contact_AssignedTo_Login,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтактногоЛица.EMail, """") КАК CRM_Contact_Email,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтактногоЛица.Телефон, """") КАК CRM_Contact_MobilePhone,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойБанковскийСчет.Банк.Наименование, """") КАК CRM_Company_Bank,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойБанковскийСчет.Банк.Код, """") КАК CRM_Company_BIK,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойБанковскийСчет.НомерСчета, """") КАК CRM_Company_RS,
	                      |	ЕСТЬNULL(Контрагент.Ссылка.ОсновнойБанковскийСчет.Банк.КоррСчет, """") КАК CRM_Company_KS,
	                      |	ВЫБОР
	                      |		КОГДА Контрагент.Ссылка.КоэффициентДоставки = 0
	                      |			ТОГДА 1
	                      |		ИНАЧЕ Контрагент.Ссылка.КоэффициентДоставки
	                      |	КОНЕЦ КАК CRM_Company_КоэффициентДоставки,
	                      |	ВЫБОР
	                      |		КОГДА Контрагент.Ссылка.ОсновнойДоговорКонтрагента.КоэффициентСуммыКредита ЕСТЬ NULL
	                      |			ТОГДА 1
	                      |		КОГДА Контрагент.Ссылка.ОсновнойДоговорКонтрагента.КоэффициентСуммыКредита = 0
	                      |			ТОГДА 1
	                      |		ИНАЧЕ Контрагент.Ссылка.ОсновнойДоговорКонтрагента.КоэффициентСуммыКредита
	                      |	КОНЕЦ КАК CRM_Company_КоэффициентСуммыКредита,
	                      |	Контрагент.Ссылка.Комментарий КАК CRM_Company_Description,
	                      |	ВЫБОР
	                      |		КОГДА КонтактноеЛицоКонтрагента.Ссылка ЕСТЬ NULL
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК ЕстьКонтактноеЛицо,
	                      |	МенеджерыТорговыхТочек.Менеджер.Пользователь.ЛогинЭЛМА КАК ЛогинЭлмаЗакрепленногоМенеджера
	                      |ИЗ
	                      |	Контрагент КАК Контрагент
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КонтактноеЛицоКонтрагента КАК КонтактноеЛицоКонтрагента
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КонтактнаяИнформацияКонтактногоЛица КАК КонтактнаяИнформацияКонтактногоЛица
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяЗапись КАК УчетнаяЗапись
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ МенеджерыТорговыхТочек КАК МенеджерыТорговыхТочек
	                      |		ПО (ИСТИНА)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КонтактнаяИнформацияКонтрагента КАК КонтактнаяИнформацияКонтрагента
	                      |		ПО (ИСТИНА)");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Покупатель", Контрагент.Покупатель);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка;
	
КонецФункции
Процедура ВыгрузитьДанныеКонтрагентаВФайл(ИмяФайла, ДанныеКонтрагента, ЛогинELMAТекущегоПользователя, ЛогинELMAЗакрепленногоМенеджера, ЭтоНовый)
	
	
	ТипELMA = ФабрикаXDTO.Тип("1c2Elma", "ELMA");
	ОбъектXTDO = ФабрикаXDTO.Создать(ТипELMA);
	
	УстановитьЗначениеXDTO(ОбъектXTDO, ТипELMA, "login", Новый Структура("login,Required", "admin", Истина));
	УстановитьЗначениеXDTO(ОбъектXTDO, ТипELMA, "password", Новый Структура("password,Required", "bpmn17", Истина));
	
	СвойствоCRM = ТипELMA.Свойства.Получить("CRM");
	ОбъектCRM = ФабрикаXDTO.Создать(СвойствоCRM.Тип);
	
   		СвойствоCompany = СвойствоCRM.Тип.Свойства.Получить("Company");
		СтруктураCompany = Новый Структура();
		СтруктураCompany.Вставить("Action", ?(ЭтоНовый, "NEW", "UPDATE"));
		СтруктураCompany.Вставить("CountEmployees", ДанныеКонтрагента.CRM_Company_DelayOfPayment);
		СтруктураCompany.Вставить("AnnualRevenue", Окр(ДанныеКонтрагента.CRM_Company_Limit, 0));
		СтруктураCompany.Вставить("Site", ДанныеКонтрагента.CRM_Company_Site);
		СтруктураCompany.Вставить("PhoneOffice", ДанныеКонтрагента.CRM_Company_OtherPhone);
		СтруктураCompany.Вставить("Fax", ДанныеКонтрагента.CRM_Company_OID);
		СтруктураCompany.Вставить("OtherPhone", "" + ДанныеКонтрагента.CRM_Company_КоэффициентДоставки + "/" + ДанныеКонтрагента.CRM_Company_КоэффициентСуммыКредита);
		СтруктураCompany.Вставить("Email", ДанныеКонтрагента.CRM_Company_Email);
		СтруктураCompany.Вставить("CompanyDay", Формат(НачалоГода(ДанныеКонтрагента.CRM_Company_CompanyDay), "ДФ=dd.MM.yy"));
		СтруктураCompany.Вставить("INN", ДанныеКонтрагента.CRM_Company_INN);
		СтруктураCompany.Вставить("KPP", ДанныеКонтрагента.CRM_Company_KPP);
		СтруктураCompany.Вставить("Bank", ДанныеКонтрагента.CRM_Company_Bank);
		СтруктураCompany.Вставить("BIK", ДанныеКонтрагента.CRM_Company_BIK);
		СтруктураCompany.Вставить("RS", ДанныеКонтрагента.CRM_Company_RS);
		СтруктураCompany.Вставить("KS", ДанныеКонтрагента.CRM_Company_KS);
		СтруктураCompany.Вставить("Limit", ДанныеКонтрагента.CRM_Company_Limit);
		СтруктураCompany.Вставить("DelayOfPayment", ДанныеКонтрагента.CRM_Company_DelayOfPayment);
		СтруктураCompany.Вставить("Registered", Истина);
		СтруктураCompany.Вставить("Description", ДанныеКонтрагента.CRM_Company_Description);
		ОбъектCompany = УстановитьЗначениеXDTO(ОбъектCRM, СвойствоCRM.Тип, "Company", СтруктураCompany);
		
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "OID", Новый Структура("OID,Required", ДанныеКонтрагента.CRM_Company_OID, Истина));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "Name", Новый Структура("Name,Required", ДанныеКонтрагента.CRM_Company_Name, Истина));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "Industry", Новый Структура("Name,ID", ДанныеКонтрагента.CRM_Company_Industry_Name, ДанныеКонтрагента.CRM_Company_Industry_ID));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "Region", Новый Структура("Name,ID", ДанныеКонтрагента.CRM_Company_Region_Name, 0));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "Type", Новый Структура("Name,ID", ДанныеКонтрагента.CRM_Company_Type_Name, ""));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "AssignedTo", Новый Структура("Required,ID,Login", Истина, "", ЛогинELMAЗакрепленногоМенеджера));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "Author", Новый Структура("Required,ID,Login", Истина, "", "admin"));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "BAdress", Новый Структура("PostalAdress,Country,State,City,PostalCode", ДанныеКонтрагента.CRM_Company_BAdress_PostalAdress, "Россия", ДанныеКонтрагента.CRM_Company_BAdress_State, ДанныеКонтрагента.CRM_Company_BAdress_City, ""));
		УстановитьЗначениеXDTO(ОбъектCompany, СвойствоCompany.Тип, "PAdress", Новый Структура("PostalAdress,Country,State,City,PostalCode", ДанныеКонтрагента.CRM_Company_PAdress_PostalAdress, "Россия", "", "", ""));
		
	ОбъектCRM.Установить(СвойствоCompany, ОбъектCompany);
	
		Если ДанныеКонтрагента.ЕстьКонтактноеЛицо Тогда
			СвойствоContact = СвойствоCRM.Тип.Свойства.Получить("Contact");
			СтруктураContact = Новый Структура();
			СтруктураContact.Вставить("Action", ?(ЭтоНовый, "NEW", "UPDATE"));
			СтруктураContact.Вставить("OID", ДанныеКонтрагента.CRM_Company_OID + "_1");
			СтруктураContact.Вставить("FirstName", ДанныеКонтрагента.CRM_Contact_FirstName);
			СтруктураContact.Вставить("PatronymicName", ДанныеКонтрагента.CRM_Contact_PatronymicName);
			СтруктураContact.Вставить("Department", "");
			СтруктураContact.Вставить("Post", ДанныеКонтрагента.CRM_Contact_Post);
			СтруктураContact.Вставить("BirthdateYear", ДанныеКонтрагента.CRM_Contact_BirthdateYear);
			СтруктураContact.Вставить("BirthdateMonth", ДанныеКонтрагента.CRM_Contact_BirthdateMonth);
			СтруктураContact.Вставить("BirthdateDay", ДанныеКонтрагента.CRM_Contact_BirthdateDay);
			СтруктураContact.Вставить("Priority", 1);
			СтруктураContact.Вставить("Site", "");
			СтруктураContact.Вставить("PhoneOffice", "");
			СтруктураContact.Вставить("Fax", "");
			СтруктураContact.Вставить("Email", ДанныеКонтрагента.CRM_Contact_Email);
			СтруктураContact.Вставить("ICQ", "");
			СтруктураContact.Вставить("Skype", "");
			СтруктураContact.Вставить("HomePhone", "");
			СтруктураContact.Вставить("MobilePhone", ДанныеКонтрагента.CRM_Contact_MobilePhone);
			СтруктураContact.Вставить("CreateDate", "");
			СтруктураContact.Вставить("Description", "");
			ОбъектContact = УстановитьЗначениеXDTO(ОбъектCRM, СвойствоCRM.Тип, "Contact", СтруктураContact);
			
			УстановитьЗначениеXDTO(ОбъектContact, СвойствоContact.Тип, "LastName", Новый Структура("LastName,Required", ДанныеКонтрагента.CRM_Contact_LastName, Истина));
			УстановитьЗначениеXDTO(ОбъектContact, СвойствоContact.Тип, "Company", Новый Структура("OID,ID", ДанныеКонтрагента.CRM_Contact_Company_OID, ""));
			УстановитьЗначениеXDTO(ОбъектContact, СвойствоContact.Тип, "Type", Новый Структура("Name,ID", "Другое", ""));
			УстановитьЗначениеXDTO(ОбъектContact, СвойствоContact.Тип, "AssignedTo", Новый Структура("Required,ID,Login", Истина, "", ЛогинELMAЗакрепленногоМенеджера));
			УстановитьЗначениеXDTO(ОбъектContact, СвойствоContact.Тип, "Author", Новый Структура("Required,ID,Login", Истина, "", "admin"));//Возможно ЛогинELMAТекущегоПользователя, вместо "admin"
			
			ОбъектCRM.Установить(СвойствоContact, ОбъектContact);
		КонецЕсли;
			
	ОбъектXTDO.Установить(СвойствоCRM, ОбъектCRM);
			
	ЗаписьXML = Новый ЗаписьXML;
	ПараметрыЗаписиXML = Новый ПараметрыЗаписиXML("Windows-1251", "1.0", Ложь);
	ЗаписьXML.ОткрытьФайл(ИмяФайла, ПараметрыЗаписиXML);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXTDO);
	ЗаписьXML.Закрыть();
	
КонецПроцедуры
Функция УстановитьЗначениеXDTO(ОбъектXTDO, Тип, ИмяСвойства, ЗначениеСтруктура)
	
	Свойство = Тип.Свойства.Получить(ИмяСвойства);
	Значение = ФабрикаXDTO.Создать(Свойство.Тип);
	ЗаполнитьЗначенияСвойств(Значение, ЗначениеСтруктура);
	
	ОбъектXTDO.Установить(Свойство, Значение);
	
	Возврат Значение;
	
КонецФункции
Функция ВозможноВыгрузитьВЭлма(ДанныеКонтрагента, ЛогинELMAТекущегоПользователя, ЛогинELMAЗакрепленногоМенеджера)
	
	ВозможноВыгрузитьВЭлма = Истина;
	ТекстОшибки = "";
	
	Если НЕ ЗначениеЗаполнено(ЛогинELMAТекущегоПользователя) И НЕ ЗначениеЗаполнено(ЛогинELMAЗакрепленногоМенеджера) Тогда
		ВозможноВыгрузитьВЭлма = Ложь;
		ТекстОшибки = "Не определен логин ELMA для текущего пользователя/закрепленного менеджера";
	Иначе
		Если НЕ ЗначениеЗаполнено(ЛогинELMAТекущегоПользователя) Тогда
			ЛогинELMAТекущегоПользователя = ЛогинELMAЗакрепленногоМенеджера;
		ИначеЕсли  НЕ ЗначениеЗаполнено(ЛогинELMAЗакрепленногоМенеджера) Тогда
			ЛогинELMAЗакрепленногоМенеджера = ЛогинELMAТекущегоПользователя;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеКонтрагента.CRM_Company_Region_Name) Тогда
		ВозможноВыгрузитьВЭлма = Ложь;
		ТекстОшибки = ТекстОшибки + Символы.ПС + "У торговой точки не указан регион(поле обязательно к заполнению в Элма)";
	КонецЕсли;
	
	#Если Клиент Тогда
	Если НЕ ВозможноВыгрузитьВЭлма Тогда
		Сообщить(ТекстОшибки);
	КонецЕсли;
	#КонецЕсли
	
	Возврат ВозможноВыгрузитьВЭлма;
	
КонецФункции