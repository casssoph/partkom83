Процедура ВыполнитьРегламентноеЗадание() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторУзлаОбмена) тогда
		ВызватьИсключение "Не заполнен реквизит ""Узел обмена"" Отправителя.";		
	КонецЕсли;
	
	URI = "http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema";
	Настройка = Справочники.НастройкиРеквизитовДляОбменов.Обмен_1С_Сайт_Заявки_Express;
	АдресВебСервиса = ?(ОбщегоНазначения.ЭтоРабочаяИнформационнаяБаза(), Настройка.СтрокаДляРабочейБазы, Настройка.СтрокаДляТестовойБазы);
	МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок_exp;
	МенеджерПланаОбмена = ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок_exp;
	
	Получатель = МенеджерПланаОбмена.ЭтотУзел();
	Отправитель = ОбменДаннымиКлиентСервер.ПолучитьВходящийУзелОбмена(МетаданныеПланаОбмена, ИдентификаторУзлаОбмена);
	Если НЕ ЗначениеЗаполнено(Отправитель) тогда
		ВызватьИсключение "[ЗагрузитьСообщениеОбмена]: не найден отправитель сообщения в списке узлов.";	
	КонецЕсли;                                                                      		
	
	КодПолучателя = Получатель.ИдентификаторУзла;
	КодОтправителя = ИдентификаторУзлаОбмена;
	НомерПринятого = Отправитель.НомерОтправленного;
	
	Определения = Новый WSОпределения(АдресВебСервиса);
	Прокси = Новый WSПрокси(Определения, Определения.Сервисы[0].URIПространстваИмен, Определения.Сервисы[0].Имя, Определения.Сервисы[0].ТочкиПодключения[0].Имя);
	
	ОтветСервера = Прокси.GetExchangeMessage("orders_express_nn", КодПолучателя, НомерПринятого);
	РаспакованноеСообщение = ОбменДаннымиСервер.РаспаковатьСообщениеОбмена(ОтветСервера);
	
	ТипСообщениеОбмена = ФабрикаXDTO.Тип(URI, "СообщениеОбмена");
	ЧтениеХМЛ = Новый ЧтениеXML;
	ЧтениеХМЛ.УстановитьСтроку(РаспакованноеСообщение);
	ДанныеХДТО = ФабрикаXDTO.ПрочитатьXML(ЧтениеХМЛ, ТипСообщениеОбмена);
	ОбъектыXDTO = ДанныеХДТО.Объекты.ПолучитьСписок("Объект");
	НомерСообщения = ДанныеХДТО.НомерСообщения;
	Если НомерСообщения <= НомерПринятого Тогда
		ВызватьИсключение "[ЗагрузитьСообщениеОбмена]: неправильное значение элемента ""НомерСообщения"": сообщение с таким номером уже загружалось.";
	КонецЕсли;
	
	Если ОбъектыXDTO.Количество() > 0 Тогда
		РегистрыСведений.ИсторияОбменаССайтом_ЗаявкиПокупателей.ЗарегистрироватьСообщение(КодОтправителя, КодПолучателя, ОтветСервера, НомерПринятого);
		ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок.ОбработатьПакетЗаявокССайта(ОбъектыXDTO, URI, НомерСообщения, Истина);
	КонецЕсли;
	
	ОбъектУзла = Отправитель.ПолучитьОбъект();
	ОбъектУзла.НомерОтправленного = НомерСообщения;
	ОбъектУзла.Записать();
	
КонецПроцедуры

