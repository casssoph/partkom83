//Валиахметов 09.04.2019 http://jira.part-kom.ru/browse/XX-2115
			   
Процедура ВыполнитьРегламентноеЗадание() Экспорт
	
	лКлючАлгоритма = "Обработка_СозданиеКорректировокПоПодчиненнымКонтрагентам_ВыполнитьРегламентноеЗадание";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		Выполнить(лЗамена);
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент.ОсновнойДоговорКонтрагента КАК ОсновнойДоговорГоловнойКонтрагента,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец КАК Контрагент,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент КАК ГоловнойКонтрагент,
	               |	ВзаиморасчетыОстатки.СуммаУпрОстаток КАК Сумма,
	               |	ВзаиморасчетыОстатки.СуммаРеглОстаток КАК СуммаРегл
	               |ИЗ
	               |	РегистрНакопления.Взаиморасчеты.Остатки(
	               |			,
	               |			ДоговорКонтрагента.Владелец.Покупатель
	               |				И ДоговорКонтрагента.ВидОплаты = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)
	               |				И ДоговорКонтрагента.Владелец.ГоловнойКонтрагент <> ДоговорКонтрагента.Владелец
	               |				И ДоговорКонтрагента.ВидОплаты = ДоговорКонтрагента.Владелец.ГоловнойКонтрагент.ОсновнойДоговорКонтрагента.ВидОплаты) КАК ВзаиморасчетыОстатки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент.ОсновнойДоговорКонтрагента,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец,
	               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент,
	               |	ВзаиморасчетыОстатки.СуммаУпрОстаток,
	               |	ВзаиморасчетыОстатки.СуммаРеглОстаток
	               |ИЗ
	               |	РегистрНакопления.Взаиморасчеты.Остатки(
	               |			,
	               |			ДоговорКонтрагента.Владелец.Покупатель
	               |				И ДоговорКонтрагента.ВидОплаты = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Безналичные)
	               |				И ДоговорКонтрагента.Владелец.ГоловнойКонтрагент <> ДоговорКонтрагента.Владелец
	               |				И ДоговорКонтрагента.Организация = ДоговорКонтрагента.Владелец.ГоловнойКонтрагент.ОсновнойДоговорКонтрагента.Организация
	               |				И ДоговорКонтрагента.ВидОплаты = ДоговорКонтрагента.Владелец.ГоловнойКонтрагент.ОсновнойДоговорКонтрагента.ВидОплаты) КАК ВзаиморасчетыОстатки
	               |ИТОГИ
	               |	МАКСИМУМ(ОсновнойДоговорГоловнойКонтрагента)
	               |ПО
	               |	ГоловнойКонтрагент";
	ВыборкаПоКонтрагентам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ГоловнойКонтрагент");
	Пока ВыборкаПоКонтрагентам.Следующий() Цикл 
		ВыборкаСоздатьКорр = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСоздатьКорр.Следующий() Цикл 
				Док = СоздатьКорректировкуДолга(ВыборкаСоздатьКорр.Контрагент, 
												ВыборкаСоздатьКорр.ДоговорКонтрагента, 
												ВыборкаСоздатьКорр.ГоловнойКонтрагент,
												ВыборкаСоздатьКорр.ОсновнойДоговорГоловнойКонтрагента,
												ВыборкаСоздатьКорр.Сумма, 
												ВыборкаСоздатьКорр.СуммаРегл);
				Попытка								
					Док.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					
				КонецПопытки;
			КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьКорректировкуДолга(Контрагент, ДоговорКонтрагента, ГоловнойКонтрагент, ОсновнойДоговорГоловнойКонтрагента, Сумма, СуммаРегл)
	лКлючАлгоритма = "Обработка_СозданиеКорректировокПоПодчиненнымКонтрагентам_СоздатьКорректировкуДолга";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	

	Док = Документы.КорректировкаДолга.СоздатьДокумент();
	Док.Ответственный = ПолныеПрава.ТекущийПользователь();
	Док.ВалютаДокумента = глЗначениеПеременной("ВалютаРегламентированногоУчета");
	Док.КурсДокумента = 1;
	Док.КратностьДокумента = 1;
	
	Док.КонтрагентДебитор = Контрагент;
	Док.КонтрагентКредитор = ГоловнойКонтрагент;
	Док.ДоговорКонтрагента = ОсновнойДоговорГоловнойКонтрагента;
	
	Док.ВидКорректировки = Справочники.ВидыКорректировок.Другое;
	Док.ВидОперации = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности;
	Док.Организация = ДоговорКонтрагента.Организация;
	Док.Комментарий = "Перенос долгов подчиненных контрагентов на головного контрагента";
	НоваяСтрока = Док.СуммыДолга.Добавить();
	Если Сумма > 0  Тогда 
		НоваяСтрока.ВидЗадолженности = Перечисления.ВидыЗадолженности.Дебиторская;
		НоваяСтрока.Сумма = Сумма;
		НоваяСтрока.СуммаРегл = СуммаРегл;
	Иначе
		НоваяСтрока.ВидЗадолженности = Перечисления.ВидыЗадолженности.Кредиторская;
		НоваяСтрока.Сумма = - Сумма;
		НоваяСтрока.СуммаРегл = - СуммаРегл;
	КонецЕсли;
	
	НоваяСтрока.ДоговорКонтрагента = ДоговорКонтрагента;
	НоваяСтрока.КурсВзаиморасчетов = 1;
	НоваяСтрока.КратностьВзаиморасчетов = 1;
	НоваяСтрока.Контрагент = Контрагент;
	
	Док.Дата = ТекущаяДата();
	Возврат Док;
КонецФункции

//Конец Валиахметов 09.04.2019 http://jira.part-kom.ru/browse/XX-2115
