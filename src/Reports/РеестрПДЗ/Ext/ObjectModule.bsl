
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтборУстановлен = ОтборУстановлен();
	
	Если НЕ ОтборУстановлен Тогда
		Сообщить("Необходимо установить хотя бы один отбор!", СтатусСообщения.Важное);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	
КонецПроцедуры

Функция ОтборУстановлен()
	
	ОтборУстановлен = Ложь;	
	
	Для каждого ЭлОтбор Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		Если ЗначениеЗаполнено(ЭлОтбор.ИдентификаторПользовательскойНастройки) Тогда
			ЭлементДляПроверки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлОтбор.ИдентификаторПользовательскойНастройки);
			Если ЭлементДляПроверки = Неопределено Тогда
				ЭлементДляПроверки = ЭлОтбор;
			КонецЕсли;
		Иначе
			ЭлементДляПроверки = ЭлОтбор;
		КонецЕсли;
		
		Если ЭлементДляПроверки.Использование Тогда
			ОтборУстановлен = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	//Проверим доп пользовательские отборы
	Если НЕ ОтборУстановлен Тогда
		Для каждого ЭлПольз Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлПольз) = Тип("ОтборКомпоновкиДанных") Тогда
				Для каждого ЭлОтбора Из ЭлПольз.Элементы Цикл
					Если ЭлОтбора.Использование Тогда
						ОтборУстановлен = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ОтборУстановлен;	
	
КонецФункции