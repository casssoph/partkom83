Функция СформироватьОтчет(Контрагент, Склад, НачалоПериода, КонецПериода) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	УстановитьПараметрСКД(Настройки, "НачалоПериода", НачалоПериода);
	УстановитьПараметрСКД(Настройки, "КонецПериода", КонецПериода);
	УстановитьПараметрСКД(Настройки, "Контрагент", Контрагент);
	УстановитьПараметрСКД(Настройки, "ДатаЗапроса", ТекущаяДата());
	
	//<< 10.07.2019 Горохов П.Е. ХХ-2831
	Если ЗначениеЗаполнено(Склад) Тогда
		ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Склад;
		ЭлементОтбора.Использование = Истина;
	КонецЕсли;
	//>> 10.07.2019 Горохов П.Е. ХХ-2831
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки); 
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура УстановитьПараметрСКД(Настройки, ИмяПараметра, Значение)
	
	Параметр = Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = Значение;
		Параметр.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры