
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = "";
	
	Если ОшибкаНастройки(ТекстОшибки) Тогда
		Предупреждение(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	УстановитьПараметрСКД(Настройки, "Организация", Организация);
	УстановитьПараметрСКД(Настройки, "Контрагент", Контрагент);
	УстановитьПараметрСКД(Настройки, "ОтборПоКонтрагенту", НЕ Контрагент.Пустая());
	УстановитьПараметрСКД(Настройки, "НачалоПериода", НачалоПериода);
	УстановитьПараметрСКД(Настройки, "КонецПериода", КонецДня(КонецПериода));
	УстановитьПараметрСКД(Настройки, "ДоговорКонтрагента", ДоговорКонтрагента);
	УстановитьПараметрСКД(Настройки, "ОтборПоДоговоруКонтрагента", НЕ ДоговорКонтрагента.Пустая());
	УстановитьПараметрСКД(Настройки, "Склад", Склад);
	УстановитьПараметрСКД(Настройки, "ОтборПоСкладу", НЕ Склад.Пустая());
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

Процедура УстановитьПараметрСКД(Настройки, ИмяПараметра, Значение)
	
	Параметр = Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = Значение;
		Параметр.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ОшибкаНастройки(ТекстОшибки)
	
	Отказ = Ложь;
	Если Организация.Пустая() Тогда
		ТекстОшибки = "Не заполнена организация";
		Отказ = Истина;
	КонецЕсли;
	Если Контрагент.Пустая() Тогда
		ТекстОшибки = ТекстОшибки + ?(ТекстОшибки = "", "", Символы.ПС) + "Не заполнен контрагент";
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции
