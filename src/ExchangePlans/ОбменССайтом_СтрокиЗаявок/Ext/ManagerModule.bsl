#Область Выгрузка
Функция ВыгрузитьСообщениеОбмена(ИдентификаторУзлаОбмена, НомерПринятого, НеСжиматьСообщение = Ложь) Экспорт
	лКлючАлгоритма = "ПланОбмена_ОбменССайтом_СтрокиЗаявок_МодульМенеджера_ВыгрузитьСообщениеОбмена";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	////////////////////////////////////////////////
	
	УзелСайт = ОбменДаннымиКлиентСервер.ПолучитьВходящийУзелОбмена(ПолучитьМетаданные(), ИдентификаторУзлаОбмена);
	Если НЕ ЗначениеЗаполнено(УзелСайт) тогда
		ВызватьИсключение "[ВыгрузитьСообщениеОбмена_ОбменССайтом_СтрокиЗаявок]: неправильный параметр номер 1(Узел 1С)";
	КонецЕсли;
	
	НомерОтправленного = УзелСайт.НомерОтправленного;
	Если НомерПринятого > НомерОтправленного Тогда
		//ВызватьИсключение "[ВыгрузитьСообщениеОбмена_ОбменССайтом_СтрокиЗаявок]: Сообщение №"+ НомерПринятого + " ещё не отправлялось(последнее №" + НомерОтправленного + ")";
		//Иногда разбегаются счетчики, номер принятого на 1 превышает номер отправленного, обычно ночью такое//
		ТекстОшибки = "Расхождение счетчиков, ReceivedNo:" + НомерПринятого + ", НомерОтправленного:" + НомерОтправленного;
		ЗаписьЖурналаРегистрации("Обмен данными.Выгрузка.ОбменССайтом_СтрокиЗаявок", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
		НомерСообщения = НомерПринятого + 1;
		НомерПринятого = НомерОтправленного;
	Иначе
		НомерСообщения = НомерОтправленного + 1;
	КонецЕсли;
	
	СтруктураХранения = ПолучитьСтруктуруХраненияБазыДанных(,Истина);
	ФиксацияПринятогоСообщения(УзелСайт, НомерПринятого, СтруктураХранения);
	
	//#XX-2336 Kalinin V.A. ( 2019-04-24 )  /*
	ОбменДаннымиКлиентСервер.НачатьЗаписьВИсториюОбменовПоОбъектам();	
	// */
	
	ТипОбъекты = ФабрикаXDTO.Тип(URIПространстваИмен(), "Объекты");
	ТипСообщениеОбмена = ФабрикаXDTO.Тип(URIПространстваИмен(), "СообщениеОбмена");
	
	Узел = УзелСайт.ПолучитьОбъект();
	Узел.НомерОтправленного = НомерСообщения;
	Узел.Записать();
	
	СообщениеОбмена = ФабрикаXDTO.Создать(ТипСообщениеОбмена);
	СообщениеОбмена.ПланОбмена = "motion";
	СообщениеОбмена.Отправитель = ЭтотУзел().ИдентификаторУзла;
	СообщениеОбмена.Получатель = ИдентификаторУзлаОбмена;
	СообщениеОбмена.НомерСообщения = НомерСообщения;
	СообщениеОбмена.НомерПринятого = 0; //Исходящих данных нет//
	
	Объекты = ФабрикаXDTO.Создать(ТипОбъекты);
	СписокОбъектов = Объекты.ПолучитьСписок("Объект");
	РегистрыСведений.СостояниеЗаявокПокупателя.ДобавитьДанныеСостояниеСтрокЗаявок(СписокОбъектов, НомерСообщения, СтруктураХранения);
	РегистрыСведений.ИсторияЗаявокПокупателя.ДобавитьДанныеИсторияЗаявокПокупателя(СписокОбъектов, НомерСообщения, СтруктураХранения);
	РегистрыСведений.УдалениеСостоянийЗаявок.ДобавитьДанныеУдаленияСостоянийСтрокЗаявок(СписокОбъектов,НомерСообщения,СтруктураХранения);
	СообщениеОбмена.Объекты = Объекты;
	
	ЗаписьХМЛ = Новый ЗаписьXML;
	ЗаписьХМЛ.УстановитьСтроку("utf-8");
	ЗаписьХМЛ.ЗаписатьОбъявлениеXML();
	ФабрикаXDTO.ЗаписатьXML(ЗаписьХМЛ, СообщениеОбмена);

	НесжатоеСообщение = ЗаписьХМЛ.Закрыть();
	
	//#XX-2336 Kalinin V.A. ( 2019-04-24 )  /*
 	ОбменДаннымиКлиентСервер.ЗафиксироватьСообщениеВИсторииОбмена(НомерСообщения, ЭтотУзел() , НесжатоеСообщение);
  	// */
	
	Если НеСжиматьСообщение Тогда
		Возврат НесжатоеСообщение;
	Иначе
		Возврат ОбщегоНазначенияВызовСервера.ЗапаковатьСообщение(НесжатоеСообщение);
	КонецЕсли;
КонецФункции
#КонецОбласти

#Область ОбщиеФункции
Функция ПолучитьМетаданные()
	
	Возврат Метаданные.ПланыОбмена.ОбменССайтом_СтрокиЗаявок;
	
КонецФункции
Функция URIПространстваИмен() 
	
	Возврат "http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema";
	
КонецФункции
Функция ФиксацияПринятогоСообщения(УзелСайт, НомерПринятого, СтруктураХранения)
	
	РегистрыСведений.СостояниеЗаявокПокупателя.ФиксацияПринятогоСообщения(НомерПринятого, СтруктураХранения);
	РегистрыСведений.ИсторияЗаявокПокупателя.ФиксацияПринятогоСообщения(НомерПринятого, СтруктураХранения);
	
КонецФункции
#КонецОбласти
