Перем мЗарегистрироватьИзмененияДляСайта;

Процедура ПередУдалением(Отказ)

	Если НЕ ЭтоГруппа И РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Сайт", "Справочник: Склады", Ложь) Тогда
		ОбменДаннымиКлиентСервер.ЗарегистрироватьОбъект(ЭтотОбъект, ПланыОбмена.ОбменПартКом83_Сайт.ПолучитьМетаданные(), "Справочник.Склады.МодульОбъекта.ПередУдалением"); //Семенов И.П. 31.01.2019 XX-1768
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если мЗарегистрироватьИзмененияДляСайта И НЕ ЭтоГруппа Тогда
		ОбменДаннымиКлиентСервер.ЗарегистрироватьОбъект(ЭтотОбъект, ПланыОбмена.ОбменПартКом83_Сайт.ПолучитьМетаданные(), "Справочник.Склады.МодульОбъекта.ПриЗаписи"); //Семенов И.П. 31.01.2019 XX-1768
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если РегистрыСведений.НастройкиПодсистем.ЗначениеПараметра("Обмен 1С-Сайт", "Справочник: Склады", Ложь) И НЕ  ОбменДанными.Загрузка Тогда
		мЗарегистрироватьИзмененияДляСайта = ОбменДаннымиКлиентСервер.НеобходимаРегистрацияИзменений(ПланыОбмена.ОбменПартКом83_Сайт.ПолучитьМетаданные(), ЭтотОбъект);
	КонецЕсли;
	
	ВыполнитьПроверкиПередЗаписью(Отказ);
	
	Если Не ЭтоГруппа Тогда 
		ПриРазмещенииИгнорироватьСтатусПартии = ЭтоСкладПриемкаТоплог Или ЭтоСкладПриемкаТоплогМосква;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//#PK83-742 Kalinin V.A. ( 2018-06-19 )
	МассивНепроверяемыхРеквизитов = новый Массив;
	Если не ВыгружатьНаСайт тогда 
	    МассивНепроверяемыхРеквизитов.Добавить("МаршрутДоставкиСамовывоза");
	КонецЕсли;	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
КонецПроцедуры

Процедура ВыполнитьПроверкиПередЗаписью(Отказ)
	
	Если ЗначениеЗаполнено(СкладХаб) И Не СкладХаб.Хаб Тогда
		ВызватьИсключение "В качестве склада хаба должен быть указан хаб!";
	КонецЕсли;
	
	Если НЕ Возвраты И ЗначениеЗаполнено(СкладХаб) И СкладХаб.Возвраты Тогда
		ВызватьИсключение "В качестве склада хаба нельзя указывать склад возвратов!";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СкладВозвратов) И НЕ СкладВозвратов.Возвраты Тогда
		ВызватьИсключение "В качестве склада возвратов может быть указан только склад возвратов!";
	КонецЕсли;
	
	Если Возвраты И Не ЗначениеЗаполнено(ФизическийСклад) Тогда
		ВызватьИсключение "Для склада возвратов должен быть указан физический склад!";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СкладВозвратов) И НЕ СкладВозвратов.Заблокирован Тогда
		ВызватьИсключение "Нельзя указывать в качестве склада возвратов незаблокированный склад!";
	КонецЕсли;
	
	Если ОбменСTopLog И НЕ СкладVMI И Не ЗначениеЗаполнено(СкладВозвратов) И НЕ Возвраты Тогда
		ВызватьИсключение "Для не-vmi складов, которые включены в обмен с Топлог, должен быть указан склад возврата!";
	КонецЕсли;
	
	Если Возвраты И Не ЗначениеЗаполнено(СкладНедостач) Тогда
		ВызватьИсключение "Для склада возвратов должен быть указан склад недостач!";
	КонецЕсли;
	
КонецПроцедуры



мЗарегистрироватьИзмененияДляСайта = Ложь;
