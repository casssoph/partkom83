Функция ПолучитьРеквизитыКонтроля(МетаданныеОтбора) Экспорт
	
	Структура = Новый Структура;
	
	Если МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт тогда
		Структура.Вставить("Шапка", "АвтоСозданиеЗаказов,ДниНедели,ДниНедели1,ДниНедели2,ДниНедели3,ДниНедели4," +
									"ДниНедели5,ДниНедели6,ДниНедели7,ВремяЗапуска,ВывозДень1,ВывозДень2,ВывозДень3,ВывозДень4," +
									"ВывозДень5,ВывозДень6,ВывозДень7,ОтдельныйЗаказ,МинСуммаАвтоЗаказа");
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

Функция ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора) Экспорт
	
	Возврат	РаботаСПоследовательностямиКлиентСервер.ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора);
	
КонецФункции

Функция ВыгрузитьЭлементы(ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена, ВыгружаемыеОбъекты = Неопределено) Экспорт
	
	URI = ПланыОбмена.ОбменПартКом83_Сайт.URIПространстваИмен();
	ТипОбъектаXDTO = ФабрикаXDTO.Тип(URI, "Справочник.ПараметрыФормированияЗаказовПоставщикам");
	ТипУдалениеОбъекта = ФабрикаXDTO.Тип(URI, "УдалениеОбъекта");
	ТипПрайсыПоставщика = ФабрикаXDTO.Тип(URI, "ПараметрыФормированияЗаказов.ПрайсПоставщика");
	ТипПрайсыПоставщикаСписок = ФабрикаXDTO.Тип(URI, "ПараметрыФормированияЗаказов.ПрайсыПоставщика");
	
	МассивОбъектов = Новый Массив;
	
	Если МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт Тогда
		
		ОбъектыОбмена = ДанныеОбъектовПланаОбменаОбменПартКом83_Сайт(ТаблицаСсылокНаОбъекты);
		
		Выборка = ОбъектыОбмена[2].Выбрать();
		Пока Выборка.Следующий() цикл
			ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
			ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
			ОбъектXDTO.Поставщик = Выборка.Поставщик.УникальныйИдентификатор();
			ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка,,"Ссылка,Поставщик");
			
			//Заполняем используемые прайсы
			Список = ФабрикаXDTO.Создать(ТипПрайсыПоставщикаСписок);
			СписокПрайсовПоставщика = Список.ПолучитьСписок("ПрайсПоставщикаСсылка");

			Для Каждого Строка Из Выборка.Ссылка.Прайсы Цикл
				ПрайсПоставщикаXDTO = ФабрикаXDTO.Создать(ТипПрайсыПоставщика);
				ПрайсПоставщикаXDTO.Ссылка = Строка.ПрайсПоставщика.УникальныйИдентификатор();
				СписокПрайсовПоставщика.Добавить(ПрайсПоставщикаXDTO);
			КонецЦикла;
			ОбъектXDTO.ПрайсыПоставщиков = Список;
			//Семенов И.П. 07.02.2019 XX-1768(
			ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
			//)Семенов И.П.
			МассивОбъектов.Добавить(ОбъектXDTO);
		КонецЦикла;			
		
		Выборка = ОбъектыОбмена[3].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектXDTO = ФабрикаXDTO.Создать(ТипУдалениеОбъекта);
			ОбъектXDTO.ТипОбъекта = "Справочник.ПараметрыФормированияЗаказовПоставщикам";
			ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
			//Семенов И.П. 07.02.2019 XX-1768(
			ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
			//)Семенов И.П.
			МассивОбъектов.Добавить(ОбъектXDTO);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивОбъектов;
	
КонецФункции

Функция ДанныеОбъектовПланаОбменаОбменПартКом83_Сайт(ТаблицаСсылокНаОбъекты)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВнешняяТаблица.Ссылка
	                      |ПОМЕСТИТЬ ЗарегистрированныеОбъекты
	                      |ИЗ
	                      |	&ТаблицаСсылокНаОбъекты КАК ВнешняяТаблица
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ПараметрыФормированияЗаказовПоставщикам) КАК Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ПараметрыФормированияЗаказовПоставщикам).ВерсияДанных ЕСТЬ NULL
	                      |				И (ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ПараметрыФормированияЗаказовПоставщикам)) <> ЗНАЧЕНИЕ(Справочник.ПараметрыФормированияЗаказовПоставщикам.ПустаяСсылка)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК ЭтоУдаление
	                      |ПОМЕСТИТЬ Объекты
	                      |ИЗ
	                      |	ЗарегистрированныеОбъекты КАК ЗарегистрированныеОбъекты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка,
	                      |	Объекты.Ссылка.Владелец.Владелец КАК Поставщик,
	                      |	Объекты.Ссылка.ПометкаУдаления КАК ПометкаУдаления,
	                      |	Объекты.Ссылка.АвтоСозданиеЗаказов КАК АвтоСозданиеЗаказов,
	                      |	Объекты.Ссылка.ДниНедели1 КАК ДниНедели1,
	                      |	Объекты.Ссылка.ДниНедели2 КАК ДниНедели2,
	                      |	Объекты.Ссылка.ДниНедели3 КАК ДниНедели3,
	                      |	Объекты.Ссылка.ДниНедели4 КАК ДниНедели4,
	                      |	Объекты.Ссылка.ДниНедели5 КАК ДниНедели5,
	                      |	Объекты.Ссылка.ДниНедели6 КАК ДниНедели6,
	                      |	Объекты.Ссылка.ДниНедели7 КАК ДниНедели7,
	                      |	Объекты.Ссылка.ВремяЗапуска КАК ВремяЗапуска,
	                      |	Объекты.Ссылка.МинСуммаАвтоЗаказа КАК МинСуммаАвтоЗаказа
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	НЕ Объекты.ЭтоУдаление
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	Объекты.ЭтоУдаление");	
						  
		Запрос.УстановитьПараметр("ТаблицаСсылокНаОбъекты", ТаблицаСсылокНаОбъекты);
		
		Возврат Запрос.ВыполнитьПакет();

КонецФункции