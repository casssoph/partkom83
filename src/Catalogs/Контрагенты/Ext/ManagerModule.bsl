Функция ПолучитьРеквизитыКонтроля(МетаданныеОтбора) Экспорт
	
	СтруктураПроверяемыхРеквизитов = Новый Структура;
	
	Если МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт Тогда
		СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "ГоловнойКонтрагент,Наименование,НаименованиеПолное,Код,ДатаСоздания,ИНН,КПП,РаботатьСОкномПоставщика,ОсновнойДоговорКонтрагентаЗакупка,
												|ОсновнаяТорговаяТочка,ОсновнойБанковскийСчет,ПроцентОтклоненияЦенПрихода,СегментКонтрагента,КоэффициентДоставки,ПоставщикVMI,
												|Блокировка,Блокировка_Отгрузок_Дата,Блокировка_Заказов_Дата,КлючевойКлиент,СайтГруппаКонтрагента,СайтГруппаКонтрагентаЗаблокировано,СайтГруппаКонтрагентаЗаблокированоДо,
// + Пушкин 20181113 XX-1307
												|ДоступнаУслугаЭкспрессДоставки,СтоимостьУслугиЭДрубли,СтоимостьУслугиЭДбонусы,СуммаБесплатнойЭД,
// - Пушкин 20181113 XX-1307

// 16.04.19 Строганов Роман > 
												|Неактивный");
// 16.04.19 Строганов Роман <
		
	ИначеЕсли МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_77 Тогда
		СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "ГоловнойКонтрагент,Наименование,НаименованиеПолное,Код,ДатаСоздания,ИНН,РаботатьСОкномПоставщика,
												|ОсновнаяТорговаяТочка,ОсновнойБанковскийСчет,ПроцентОтклоненияЦенПрихода,СегментКонтрагента,КоэффициентДоставки,
												|Блокировка,Блокировка_Отгрузок_Дата,Блокировка_Заказов_Дата,КлючевойКлиент,СайтГруппаКонтрагента,СайтГруппаКонтрагентаЗаблокировано,СайтГруппаКонтрагентаЗаблокированоДо");
		
	ИначеЕсли МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_БитФинанс Тогда
		СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "Код,Наименование,ИНН,КПП,Комментарий,НаименованиеПолное,Покупатель,Поставщик,ЮрФизЛицо");
	Иначе
// + Пушкин 20181212 XX-1455		
		Если ТипЗнч(МетаданныеОтбора) = Тип("Строка") Тогда
			Если ВРЕГ(МетаданныеОтбора) = ВРЕГ("ЛогированиеЗначенийЭД") тогда
				СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "ДоступнаУслугаЭкспрессДоставки,СтоимостьУслугиЭДрубли,СтоимостьУслугиЭДбонусы,СуммаБесплатнойЭД");
			КонецЕсли;
		КОнецЕсли;
// - Пушкин 20181212 XX-1455				
	КонецЕсли;
	
	Возврат СтруктураПроверяемыхРеквизитов;
	
КонецФункции
Функция ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора) Экспорт
	
	Возврат	РаботаСПоследовательностямиКлиентСервер.ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора);
	
КонецФункции

Функция ДанныеБлокировкиКонтрагента(Контрагент) Экспорт
	
	Если ЗначениеЗаполнено(Контрагент.ГоловнойКонтрагент) Тогда
		Структура = Новый Структура("Заблокирован,ПоГоловномуКонтрагенту", Контрагент.ГоловнойКонтрагент.Блокировка, Истина);
	Иначе
		Структура = Новый Структура("Заблокирован,ПоГоловномуКонтрагенту", Контрагент.Блокировка, Ложь);
	КонецЕсли;

	Возврат Структура;
	
КонецФункции
Функция ЗадолженностьКонтрагента(Контрагент, Период = Неопределено, ПоГоловномуКонтрагенту = Неопределено) Экспорт
	
	Если ПоГоловномуКонтрагенту = Неопределено Тогда
		ПоГоловномуКонтрагенту = ЗначениеЗаполнено(Контрагент.ГоловнойКонтрагент);
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Если ПоГоловномуКонтрагенту Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(ВзаиморасчетыОстатки.СуммаРеглОстаток) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.Взаиморасчеты.Остатки(&Период, ДоговорКонтрагента.Владелец.ГоловнойКонтрагент = &ГоловнойКонтрагент) КАК ВзаиморасчетыОстатки";
		Запрос.УстановитьПараметр("ГоловнойКонтрагент", Контрагент.ГоловнойКонтрагент);
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		               |	СУММА(ВзаиморасчетыОстатки.СуммаРеглОстаток) КАК Сумма
		               |ИЗ
		               |	РегистрНакопления.Взаиморасчеты.Остатки(&Период, ДоговорКонтрагента.Владелец = &Контрагент) КАК ВзаиморасчетыОстатки";
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
	КонецЕсли;
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Период), Период, ТекущаяДата()));
	Результат = Запрос.Выполнить().Выбрать();

	Возврат ?(Результат.Следующий(), Результат.Сумма, 0);
	
КонецФункции

Функция ПросроченнаяЗадолженностьКонтрагента(Контрагент, ДатаМоментВремени = Неопределено, ПоГоловномуКонтрагенту = Неопределено) Экспорт
	
	лКлючАлгоритма = "Справочник_Контрагенты_МодульМенеджера_ПросроченнаяЗадолженностьКонтрагента";
	лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	Если Не лЗамена = Неопределено Тогда
		АлгоритмыЗначениеВозврата = Неопределено;		
		Выполнить(лЗамена);		
		Возврат АлгоритмыЗначениеВозврата;		
	КонецЕсли;	
	///////////////////////////////////////////////////////////////////////////
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ГоловнойКонтрагент");
	
	Если ПоГоловномуКонтрагенту = Неопределено Тогда
		ПоГоловномуКонтрагенту = Контрагент <> РеквизитыКонтрагента.ГоловнойКонтрагент;
	КонецЕсли;
	
	Если ПоГоловномуКонтрагенту Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент КАК Контрагент,
		               |	СУММА(ВзаиморасчетыОстатки.СуммаРеглОстаток) КАК Задолженность
		               |ПОМЕСТИТЬ ОбщаяЗадолженность
		               |ИЗ
		               |	РегистрНакопления.Взаиморасчеты.Остатки(
		               |			&ДатаМоментВремени,
		               |			ДоговорКонтрагента.Владелец.ГоловнойКонтрагент = &ГоловнойКонтрагент
		               |				И ДоговорКонтрагента.ВидДоговора = &ВидСПокупателем
		               |				И НЕ ДоговорКонтрагента.ДоговорИнвестКонтракт) КАК ВзаиморасчетыОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА НЕ РеализацияТоваровУслуг.КонтрагентВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		               |			ТОГДА РеализацияТоваровУслуг.КонтрагентВзаиморасчетов.ГоловнойКонтрагент
		               |		ИНАЧЕ РеализацияТоваровУслуг.Контрагент.ГоловнойКонтрагент
		               |	КОНЕЦ КАК Контрагент,
		               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		               |	РеализацияТоваровУслуг.ДатаОплаты КАК ДатаОплаты,
		               |	РеализацияТоваровУслуг.ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженности,
		               |	РеализацияТоваровУслуг.СуммаДокумента
		               |ПОМЕСТИТЬ РеализацияТоваровУслуг
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Контрагент.ГоловнойКонтрагент = &ГоловнойКонтрагент
		               |	И РеализацияТоваровУслуг.Проведен
		               |	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора = &ВидСПокупателем
		               |	И НЕ РеализацияТоваровУслуг.ДоговорКонтрагента.ДоговорИнвестКонтракт
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
		               |	СУММА(РеализацияТоваровУслуг.СуммаДокумента) КАК Сумма
		               |ПОМЕСТИТЬ НеПросроченнаяЗадолженность
		               |ИЗ
		               |	РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	ДОБАВИТЬКДАТЕ(РеализацияТоваровУслуг.ДатаОплаты, ДЕНЬ, &ЛимитДнейЗадолженности) >= &Дата
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	РеализацияТоваровУслуг.Контрагент
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОбщаяЗадолженность.Контрагент,
		               |	ОбщаяЗадолженность.Задолженность - ЕСТЬNULL(НеПросроченнаяЗадолженность.Сумма, 0) КАК Сумма
		               |ИЗ
		               |	ОбщаяЗадолженность КАК ОбщаяЗадолженность
		               |		ЛЕВОЕ СОЕДИНЕНИЕ НеПросроченнаяЗадолженность КАК НеПросроченнаяЗадолженность
		               |		ПО ОбщаяЗадолженность.Контрагент = НеПросроченнаяЗадолженность.Контрагент
		               |ГДЕ
		               |	ОбщаяЗадолженность.Задолженность - ЕСТЬNULL(НеПросроченнаяЗадолженность.Сумма, 0) > 0";	
		
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент КАК Контрагент,
		               |	СУММА(ВзаиморасчетыОстатки.СуммаРеглОстаток) КАК Задолженность
		               |ПОМЕСТИТЬ ОбщаяЗадолженность
		               |ИЗ
		               |	РегистрНакопления.Взаиморасчеты.Остатки(
		               |			&ДатаМоментВремени,
		               |			ДоговорКонтрагента.Владелец.ГоловнойКонтрагент = &Контрагент
		               |				И ДоговорКонтрагента.ВидДоговора = &ВидСПокупателем
		               |				И НЕ ДоговорКонтрагента.ДоговорИнвестКонтракт) КАК ВзаиморасчетыОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец,
		               |	ВзаиморасчетыОстатки.ДоговорКонтрагента.Владелец.ГоловнойКонтрагент
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВЫБОР
		               |		КОГДА НЕ РеализацияТоваровУслуг.КонтрагентВзаиморасчетов = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		               |			ТОГДА РеализацияТоваровУслуг.КонтрагентВзаиморасчетов.ГоловнойКонтрагент
		               |		ИНАЧЕ РеализацияТоваровУслуг.Контрагент.ГоловнойКонтрагент
		               |	КОНЕЦ КАК Контрагент,
		               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		               |	РеализацияТоваровУслуг.ДатаОплаты КАК ДатаОплаты,
		               |	РеализацияТоваровУслуг.ДоговорКонтрагента.ДопустимоеЧислоДнейЗадолженности КАК ДопустимоеЧислоДнейЗадолженности,
		               |	РеализацияТоваровУслуг.СуммаДокумента
		               |ПОМЕСТИТЬ РеализацияТоваровУслуг
		               |ИЗ
		               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	РеализацияТоваровУслуг.Контрагент.ГоловнойКонтрагент = &Контрагент
		               |	И РеализацияТоваровУслуг.Проведен
		               |	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора = &ВидСПокупателем
		               |	И НЕ РеализацияТоваровУслуг.ДоговорКонтрагента.ДоговорИнвестКонтракт
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
		               |	СУММА(РеализацияТоваровУслуг.СуммаДокумента) КАК Сумма
		               |ПОМЕСТИТЬ НеПросроченнаяЗадолженность
		               |ИЗ
		               |	РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		               |ГДЕ
		               |	ДОБАВИТЬКДАТЕ(РеализацияТоваровУслуг.ДатаОплаты, ДЕНЬ, &ЛимитДнейЗадолженности) >= &Дата
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	РеализацияТоваровУслуг.Контрагент
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ОбщаяЗадолженность.Контрагент,
		               |	ОбщаяЗадолженность.Задолженность - ЕСТЬNULL(НеПросроченнаяЗадолженность.Сумма, 0) КАК Сумма
		               |ИЗ
		               |	ОбщаяЗадолженность КАК ОбщаяЗадолженность
		               |		ЛЕВОЕ СОЕДИНЕНИЕ НеПросроченнаяЗадолженность КАК НеПросроченнаяЗадолженность
		               |		ПО ОбщаяЗадолженность.Контрагент = НеПросроченнаяЗадолженность.Контрагент
		               |ГДЕ
		               |	ОбщаяЗадолженность.Задолженность - ЕСТЬNULL(НеПросроченнаяЗадолженность.Сумма, 0) > 0";
		
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", РеквизитыКонтрагента.ГоловнойКонтрагент);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Если ДатаМоментВремени = Неопределено ИЛИ ТипЗнч(ДатаМоментВремени) = Тип("Дата") Тогда
		Запрос.УстановитьПараметр("ДатаМоментВремени", ?(ЗначениеЗаполнено(ДатаМоментВремени), ДатаМоментВремени, ТекущаяДата()));
		Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(ДатаМоментВремени), ДатаМоментВремени, ТекущаяДата()));
	Иначе //МоментВремени
		Запрос.УстановитьПараметр("ДатаМоментВремени", Новый Граница(ДатаМоментВремени, ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("Дата", ДатаМоментВремени.Дата);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидСПокупателем", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	Запрос.УстановитьПараметр("ЛимитДнейЗадолженности", Константы.ЛимитДнейЗадолженности.Получить());
	
	APDEX_ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("Контрагенты_ПросроченнаяЗадолженностьКонтрагента");
	
	Результат = Запрос.Выполнить().Выбрать();
	
	APDEX_ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени("Контрагенты_ПросроченнаяЗадолженностьКонтрагента", , ТекстЗапроса , Контрагент);


	Возврат ?(Результат.Следующий(), Результат.Сумма, 0);
	
КонецФункции


Функция ВыгрузитьЭлементы(ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена, ВыгружаемыеОбъекты = Неопределено) Экспорт
	
	лКлючАлгоритма = "Справочник_Контрагенты_МодульМенеджера_ВыгрузитьЭлементы";
	 лЗамена =  АлгоритмыПолучитьЗамену(лКлючАлгоритма);
	 Если Не лЗамена = Неопределено Тогда
	 	АлгоритмыЗначениеВозврата = Неопределено;		
	 	Выполнить(лЗамена);		
	 	Возврат АлгоритмыЗначениеВозврата;		
	 КонецЕсли;	
	 ////////////////////////////////////////////////
	
	
	
	URI = ПланыОбмена.ОбменПартКом83_Сайт.URIПространстваИмен();
	ПокупателиТипОбъектаXDTO			= ФабрикаXDTO.Тип(URI, "Справочник.Покупатели");
	ПоставщикиТипОбъектаXDTO			= ФабрикаXDTO.Тип(URI, "Справочник.Поставщики");
	СписокКонтактныхЛицТипОбъектаXDTO	= ФабрикаXDTO.Тип(URI, "Справочник.Покупатели.КонтактныеЛица");
	ДанныеКонтактныхЛицТипОбъектаXDTO	= ФабрикаXDTO.Тип(URI, "Справочник.Покупатели.КонтактноеЛицо");
	ГруппыТипОбъектаXDTO				= ФабрикаXDTO.Тип(URI, "Справочники.ГруппыПокупателей");
	ТипУдалениеОбъекта = ФабрикаXDTO.Тип(URI, "УдалениеОбъекта");
	
	МассивОбъектов = Новый Массив;
	
	Если МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт Тогда
		
		ОбъектыОбмена = ДанныеОбъектовПланаОбменаОбменПартКом83_Сайт(ТаблицаСсылокНаОбъекты);
		КонтактныеЛица = ОбъектыОбмена[11].Выгрузить();
		
		Выборка = ОбъектыОбмена[8].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ЯвляетсяГруппой Тогда
				ОбъектXDTO = ФабрикаXDTO.Создать(ГруппыТипОбъектаXDTO);
				ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
				ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка,"Код,Наименование");
				
				МассивОбъектов.Добавить(ОбъектXDTO);
				//Семенов И.П. 07.02.2019 XX-1768(
				ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
				//)Семенов И.П.
			Иначе
				//Добавляем контрагентов, как покупателей
				Если Выборка.ЯвляетсяПокупателем Тогда
					ОбъектXDTO = ФабрикаXDTO.Создать(ПокупателиТипОбъектаXDTO);
					ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
					ОбъектXDTO.Менеджер = Выборка.МенеджерПродаж.УникальныйИдентификатор();
					ОбъектXDTO.Логин = СокрЛП(Выборка.Логин);
					ОбъектXDTO.Пароль = СокрЛП(Выборка.Пароль);
					ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка,,"Ссылка,Логин,Пароль,ГоловнойКонтрагент");
					ОбъектXDTO.ГоловнойКонтрагент = Выборка.ГоловнойКонтрагент.УникальныйИдентификатор();
					//Заполняем контактные лица
					СписокКонтактовГруппа = ФабрикаXDTO.Создать(СписокКонтактныхЛицТипОбъектаXDTO);
					СписокКонтактов = СписокКонтактовГруппа.ПолучитьСписок("КонтактноеЛицо");
					Для Каждого ДанныеКонтактногоЛица Из КонтактныеЛица.НайтиСтроки(Новый Структура("Контрагент", Выборка.Ссылка)) Цикл
						СтрокаКонтактовКонтрагента = ФабрикаXDTO.Создать(ДанныеКонтактныхЛицТипОбъектаXDTO);
						ЗаполнитьЗначенияСвойств(СтрокаКонтактовКонтрагента, ДанныеКонтактногоЛица);
						СписокКонтактов.Добавить(СтрокаКонтактовКонтрагента);
					КонецЦикла;
					ОбъектXDTO.КонтактныеЛица = СписокКонтактовГруппа;
					
					МассивОбъектов.Добавить(ОбъектXDTO);
					//Семенов И.П. 07.02.2019 XX-1768(
					ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
					//)Семенов И.П.
				КонецЕсли;
				
				//Добавляем контрагентов, как поставщиков
				Если Выборка.ЯвляетсяПоставщиком Тогда
					ОбъектXDTO = ФабрикаXDTO.Создать(ПоставщикиТипОбъектаXDTO);
					ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
					ОбъектXDTO.МенеджерСнабжения = Выборка.МенеджерСнабжения.УникальныйИдентификатор();
					ОбъектXDTO.ЗамМенеджераСнабжения = Выборка.ЗамМенеджераСнабжения.УникальныйИдентификатор();
					ОбъектXDTO.СкладVMI = Выборка.СкладVMI.УникальныйИдентификатор();
					ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка,,"Ссылка,МенеджерСнабжения,ЗамМенеджераСнабжения,СкладVMI");

					МассивОбъектов.Добавить(ОбъектXDTO);
					//Семенов И.П. 07.02.2019 XX-1768(
					ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
					//)Семенов И.П.
				КонецЕсли;
			КонецЕсли;
			
			ВыгружаемыеОбъекты.Добавить(Выборка.Ссылка);
			
		КонецЦикла;			
		
		Выборка = ОбъектыОбмена[9].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектXDTO = ФабрикаXDTO.Создать(ТипУдалениеОбъекта);
			ОбъектXDTO.ТипОбъекта = "Справочники.Контрагенты";
			ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
			//Семенов И.П. 07.02.2019 XX-1768(
			ОбменДаннымиКлиентСервер.ДобавитьСтрокуИсторииПоОбъекту(Выборка.Ссылка, ОбъектXDTO);
			//)Семенов И.П.
			МассивОбъектов.Добавить(ОбъектXDTO);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивОбъектов;
	
КонецФункции

Функция ДанныеОбъектовПланаОбменаОбменПартКом83_Сайт(ТаблицаСсылокНаОбъекты)
	//Семенов И.П. 13.03.2019 XX-1249 добавил в запрос поле УдаленноеРасположение
	// 19.04.19 Строганов Роман > XX-250 Выгрузка Контрагентов на сайт: добавил реквизит «Неактивный»
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВнешняяТаблица.Ссылка
	                      |ПОМЕСТИТЬ ЗарегистрированныеОбъекты
	                      |ИЗ
	                      |	&ТаблицаСсылокНаОбъекты КАК ВнешняяТаблица
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.Контрагенты) КАК Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.Контрагенты).ВерсияДанных ЕСТЬ NULL
	                      |				И (ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.Контрагенты)) <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК ЭтоУдаление
	                      |ПОМЕСТИТЬ Объекты
	                      |ИЗ
	                      |	ЗарегистрированныеОбъекты КАК ЗарегистрированныеОбъекты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка,
	                      |	Объекты.Ссылка.ОсновнаяТорговаяТочка КАК ОсновнаяТорговаяТочка
	                      |ПОМЕСТИТЬ ИзмененныеОбъекты
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	НЕ Объекты.ЭтоУдаление
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	МенеджерыТорговыхТочекСрезПоследних.ТорговаяТочка.Владелец КАК Контрагент,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА МенеджерыТорговыхТочекСрезПоследних.ВидМенеджера = ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.Снабжения)
	                      |				ТОГДА МенеджерыТорговыхТочекСрезПоследних.Менеджер
	                      |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)
	                      |		КОНЕЦ) КАК МенеджерСнабжения,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА МенеджерыТорговыхТочекСрезПоследних.ВидМенеджера = ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.ЗамМенеджераСнабжения)
	                      |				ТОГДА МенеджерыТорговыхТочекСрезПоследних.Менеджер
	                      |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)
	                      |		КОНЕЦ) КАК ЗамМенеджераСнабжения,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА МенеджерыТорговыхТочекСрезПоследних.ВидМенеджера = ЗНАЧЕНИЕ(Перечисление.ВидыМенеджеров.Продажи)
	                      |				ТОГДА МенеджерыТорговыхТочекСрезПоследних.Менеджер
	                      |			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)
	                      |		КОНЕЦ) КАК МенеджерПродаж
	                      |ПОМЕСТИТЬ МенеджерыТорговыхТочек
	                      |ИЗ
	                      |	РегистрСведений.МенеджерыТорговыхТочек.СрезПоследних(
	                      |			,
	                      |			ТорговаяТочка В
	                      |				(ВЫБРАТЬ
	                      |					ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка
	                      |				ИЗ
	                      |					ИзмененныеОбъекты КАК ИзмененныеОбъекты)) КАК МенеджерыТорговыхТочекСрезПоследних
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	МенеджерыТорговыхТочекСрезПоследних.ТорговаяТочка.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ПрайсыПоставщиков.Владелец.Владелец КАК Контрагент
	                      |ПОМЕСТИТЬ Поставщики
	                      |ИЗ
	                      |	Справочник.ПрайсыПоставщиков КАК ПрайсыПоставщиков
	                      |ГДЕ
	                      |	ПрайсыПоставщиков.Владелец.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				ИзмененныеОбъекты.Ссылка
	                      |			ИЗ
	                      |				Объекты КАК ИзмененныеОбъекты)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ПрайсыПоставщиков.Владелец.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КонтактнаяИнформация.Объект,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailДляОбменаДокументамиСКонтрагентами)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ЭлектроннаяПочта,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ФактическийАдрес,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ЮридическийАдрес,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресДоставкиКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК АдресДоставки
	                      |ПОМЕСТИТЬ КонтактнаяИнформацияКонтрагента
	                      |ИЗ
	                      |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |ГДЕ
	                      |	КонтактнаяИнформация.Объект В
	                      |			(ВЫБРАТЬ
	                      |				ИзмененныеОбъекты.Ссылка
	                      |			ИЗ
	                      |				ИзмененныеОбъекты КАК ИзмененныеОбъекты)
	                      |	И КонтактнаяИнформация.Вид В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресДоставкиКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailДляОбменаДокументамиСКонтрагентами), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента))
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КонтактнаяИнформация.Объект
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ПрайсыПоставщиков.Владелец.Владелец КАК Контрагент,
	                      |	МАКСИМУМ(ПрайсыПоставщиков.Ссылка) КАК ПрайсVMI
	                      |ПОМЕСТИТЬ ДанныеПрайсовVMI
	                      |ИЗ
	                      |	Справочник.ПрайсыПоставщиков КАК ПрайсыПоставщиков
	                      |ГДЕ
	                      |	ПрайсыПоставщиков.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				ИзмененныеОбъекты.ОсновнаяТорговаяТочка
	                      |			ИЗ
	                      |				ИзмененныеОбъекты КАК ИзмененныеОбъекты)
	                      |	И ПрайсыПоставщиков.Склад.СкладVMI
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ПрайсыПоставщиков.Владелец.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	УчетныеЗаписиСайта.Владелец.Владелец КАК Контрагент,
	                      |	МАКСИМУМ(УчетныеЗаписиСайта.Ссылка) КАК УчетнаяЗапись
	                      |ПОМЕСТИТЬ УчетныеЗаписиТорговыхТочек
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиСайта КАК УчетныеЗаписиСайта
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	УчетныеЗаписиСайта.Владелец.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ИзмененныеОбъекты.Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА Поставщики.Контрагент ЕСТЬ NULL
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК ЯвляетсяПоставщиком,
	                      |	ВЫБОР
	                      |		КОГДА УчетныеЗаписиТорговыхТочек.УчетнаяЗапись ЕСТЬ NULL
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК ЯвляетсяПокупателем,
	                      |	ИзмененныеОбъекты.Ссылка.ЭтоГруппа КАК ЯвляетсяГруппой,
	                      |	ЕСТЬNULL(УчетныеЗаписиТорговыхТочек.УчетнаяЗапись.Код, """") КАК Логин,
	                      |	ЕСТЬNULL(УчетныеЗаписиТорговыхТочек.УчетнаяЗапись.Пароль, """") КАК Пароль,
	                      |	ИзмененныеОбъекты.Ссылка.Код КАК Код,
	                      |	ИзмененныеОбъекты.Ссылка.Наименование КАК Наименование,
	                      |	ИзмененныеОбъекты.Ссылка.НаименованиеПолное КАК НаименованиеПолное,
	                      |	ИзмененныеОбъекты.Ссылка.КоэффициентДоставки КАК КоэффициентДоставки,
	                      |	ВЫБОР
	                      |		КОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	                      |			ТОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент.Блокировка
	                      |		ИНАЧЕ ИзмененныеОбъекты.Ссылка.Блокировка
	                      |	КОНЕЦ КАК Заблокирован,
	                      |	ВЫБОР
	                      |		КОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	                      |			ТОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент.Блокировка_Отгрузок_Дата
	                      |		ИНАЧЕ ИзмененныеОбъекты.Ссылка.Блокировка_Отгрузок_Дата
	                      |	КОНЕЦ КАК ДатаБлокировкиОтгрузок,
	                      |	ВЫБОР
	                      |		КОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	                      |			ТОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент.Блокировка_Заказов_Дата
	                      |		ИНАЧЕ ИзмененныеОбъекты.Ссылка.Блокировка_Заказов_Дата
	                      |	КОНЕЦ КАК ДатаБлокировкиЗаказов,
	                      |	ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка КАК ОсновнаяТорговаяТочка,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.Регион.Код, 0) КАК РегионКод,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.Город.Код, 0) КАК ГородКод,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.МаршрутДоставки.Наименование, """") КАК МаршрутДоставкиНаименование,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.МаршрутДоставки.Код, """") КАК МаршрутДоставкиКод,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.ГородПоставки.Код, 0) КАК КодГородаРазгрузки1,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка.ГородПоставки2.Код, 0) КАК КодГородаРазгрузки2,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.Родитель.Код, """") КАК ХолдингКод,
	                      |	ВЫБОР
	                      |		КОГДА ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент = ИзмененныеОбъекты.Ссылка
	                      |			ТОГДА ЗНАЧЕНИЕ(Справочник.КОнтрагенты.ПустаяСсылка)
	                      |		ИНАЧЕ ИзмененныеОбъекты.Ссылка.ГоловнойКонтрагент
	                      |	КОНЕЦ КАК ГоловнойКонтрагент,
	                      |	ИзмененныеОбъекты.Ссылка.ИНН КАК ИНН,
	                      |	ИзмененныеОбъекты.Ссылка.КПП КАК КПП,
	                      |	ИзмененныеОбъекты.Ссылка.ДоступнаУслугаЭкспрессДоставки КАК ДоступнаУслугаЭкспрессДоставки,
	                      |	ИзмененныеОбъекты.Ссылка.СтоимостьУслугиЭДрубли КАК СтоимостьУслугиЭДрубли,
	                      |	ИзмененныеОбъекты.Ссылка.СтоимостьУслугиЭДбонусы КАК СтоимостьУслугиЭДбонусы,
	                      |	ИзмененныеОбъекты.Ссылка.СуммаБесплатнойЭД КАК СуммаБесплатнойЭД,
	                      |	ИзмененныеОбъекты.Ссылка.ДатаСоздания КАК ДатаСоздания,
	                      |	ИзмененныеОбъекты.Ссылка.СегментКонтрагента КАК СегментКонтрагента,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.СегментКонтрагента.Код, 0) КАК СегментКонтрагентаКод,
	                      |	ИзмененныеОбъекты.Ссылка.КлючевойКлиент КАК КлючевойКлиент,
	                      |	ИзмененныеОбъекты.Ссылка.СайтГруппаКонтрагента КАК ГруппаКонтрагента,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.СайтГруппаКонтрагента.Код, 0) КАК ГруппаКонтрагентаКод,
	                      |	ИзмененныеОбъекты.Ссылка.СайтГруппаКонтрагентаЗаблокировано КАК ГруппаКонтрагентаЗаблокировано,
	                      |	ИзмененныеОбъекты.Ссылка.СайтГруппаКонтрагентаЗаблокированоДо КАК ГруппаКонтрагентаЗаблокированоДо,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.АдресДоставки, """") КАК АдресДоставки,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.ЭлектроннаяПочта, """") КАК ЭлектроннаяПочта,
	                      |	ЕСТЬNULL(МенеджерыТорговыхТочек.МенеджерСнабжения, ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)) КАК МенеджерСнабжения,
	                      |	ЕСТЬNULL(МенеджерыТорговыхТочек.ЗамМенеджераСнабжения, ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)) КАК ЗамМенеджераСнабжения,
	                      |	ЕСТЬNULL(МенеджерыТорговыхТочек.МенеджерПродаж, ЗНАЧЕНИЕ(Справочник.Менеджеры.ПустаяСсылка)) КАК МенеджерПродаж,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.ФактическийАдрес, """") КАК ФактическийАдрес,
	                      |	ЕСТЬNULL(КонтактнаяИнформацияКонтрагента.ЮридическийАдрес, """") КАК ЮридическийАдрес,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.НашРасчетныйСчет.Банк.Наименование, """") КАК БанкНаименование,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.НашРасчетныйСчет.Банк.Код, """") КАК БанкБИК,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.НашРасчетныйСчет.Банк.КоррСчет, """") КАК КоррСчет,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.НашРасчетныйСчет.НомерСчета, """") КАК БанковскийСчет,
	                      |	ИзмененныеОбъекты.Ссылка.РаботатьСОкномПоставщика КАК РаботаетСОкномПоставщика,
	                      |	ИзмененныеОбъекты.Ссылка.ПроцентОтклоненияЦенПрихода КАК ПроцентОтклоненияЦенПрихода,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнойДоговорКонтрагентаЗакупка.ГлубинаКредита, 0) КАК ОтсрочкаПлатежа,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнойДоговорКонтрагентаЗакупка.ОплатаВЛюбойДень, ЛОЖЬ) КАК ОплатаВЛюбойДень,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнойДоговорКонтрагентаЗакупка.ДоговорНаОферту, ЛОЖЬ) КАК ПризнакОферты,
	                      |	ИСТИНА КАК CROSS,
	                      |	ИСТИНА КАК STOCK,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнойДоговорКонтрагентаЗакупка.Организация.Код, 0) КАК Организация,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.ОсновнойДоговорКонтрагентаЗакупка.ГарантийноеПисьмо, ЛОЖЬ) КАК ГарантийноеПисьмо,
	                      |	ИзмененныеОбъекты.Ссылка.ПоставщикVMI КАК VMI,
	                      |	ЕСТЬNULL(ДанныеПрайсовVMI.ПрайсVMI.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладVMI,
	                      |	ИзмененныеОбъекты.Ссылка.УдаленноеРасположение КАК УдаленноеРасположение,
	                      |	ЕСТЬNULL(ИзмененныеОбъекты.Ссылка.СервисНа5, ЛОЖЬ) КАК СервисНа5,
	                      |	ЕСТЬNULL(ВЫРАЗИТЬ(ИзмененныеОбъекты.Ссылка КАК Справочник.Контрагенты).ОбратнаяРеализацияПриВозвратеОтКлиента, ЛОЖЬ) КАК ОбратнаяРеализацияПриВозвратеОтКлиента,
	                      |	ИзмененныеОбъекты.Ссылка.Неактивный КАК Неактивный
	                      |ИЗ
	                      |	ИзмененныеОбъекты КАК ИзмененныеОбъекты
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Поставщики КАК Поставщики
	                      |		ПО ИзмененныеОбъекты.Ссылка = Поставщики.Контрагент
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ МенеджерыТорговыхТочек КАК МенеджерыТорговыхТочек
	                      |		ПО ИзмененныеОбъекты.Ссылка = МенеджерыТорговыхТочек.Контрагент
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ КонтактнаяИнформацияКонтрагента КАК КонтактнаяИнформацияКонтрагента
	                      |		ПО ИзмененныеОбъекты.Ссылка = КонтактнаяИнформацияКонтрагента.Объект
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПрайсовVMI КАК ДанныеПрайсовVMI
	                      |		ПО ИзмененныеОбъекты.Ссылка = ДанныеПрайсовVMI.Контрагент
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ УчетныеЗаписиТорговыхТочек КАК УчетныеЗаписиТорговыхТочек
	                      |		ПО ИзмененныеОбъекты.Ссылка = УчетныеЗаписиТорговыхТочек.Контрагент
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	Объекты.ЭтоУдаление
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КонтактныеЛицаКонтрагентов.Владелец КАК ТорговаяТочка,
	                      |	КонтактныеЛицаКонтрагентов.Ссылка КАК КонтактноеЛицоКонтрагента,
	                      |	КонтактныеЛицаКонтрагентов.КонтактноеЛицо,
	                      |	КонтактныеЛицаКонтрагентов.Должность
	                      |ПОМЕСТИТЬ КонтактныеЛицаТорговыхТочек
	                      |ИЗ
	                      |	Справочник.КонтактныеЛицаКонтрагентов КАК КонтактныеЛицаКонтрагентов
	                      |ГДЕ
	                      |	КонтактныеЛицаКонтрагентов.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				ИзмененныеОбъекты.Ссылка.ОсновнаяТорговаяТочка
	                      |			ИЗ
	                      |				ИзмененныеОбъекты КАК ИзмененныеОбъекты)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КонтактныеЛицаТорговыхТочек.ТорговаяТочка.Владелец КАК Контрагент,
	                      |	КонтактныеЛицаТорговыхТочек.КонтактноеЛицо.Наименование КАК ФИО,
	                      |	КонтактныеЛицаТорговыхТочек.Должность,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.РабочийТелефонКонтактногоЛицаКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ТелефонСотовый,
	                      |	МАКСИМУМ(ВЫБОР
	                      |			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛицаКонтрагента)
	                      |				ТОГДА КонтактнаяИнформация.Представление
	                      |			ИНАЧЕ """"
	                      |		КОНЕЦ) КАК ТелефонЛичногоКабинета
	                      |ИЗ
	                      |	КонтактныеЛицаТорговыхТочек КАК КонтактныеЛицаТорговыхТочек
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	                      |		ПО КонтактныеЛицаТорговыхТочек.КонтактноеЛицоКонтрагента = КонтактнаяИнформация.Объект
	                      |			И (КонтактнаяИнформация.Вид В (ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛицаКонтрагента), ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.РабочийТелефонКонтактногоЛицаКонтрагента)))
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	КонтактныеЛицаТорговыхТочек.ТорговаяТочка.Владелец,
	                      |	КонтактныеЛицаТорговыхТочек.Должность,
	                      |	КонтактныеЛицаТорговыхТочек.КонтактноеЛицо.Наименование");	
	
	Запрос.УстановитьПараметр("ТаблицаСсылокНаОбъекты", ТаблицаСсылокНаОбъекты);
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция ИспользуетКоррСчетФактуры(Контрагент, Дата = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "флРаботает_сКоррСФ, ДатаНачалаРаботыСКоррСчФактурами");
	
	Если  Реквизиты.флРаботает_сКоррСФ И Реквизиты.ДатаНачалаРаботыСКоррСчФактурами <= Дата Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции

Функция КонтрагентыПоМенеджеруФилиалу(СтруктураПараметров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МенеджерыТорговыхТочекСрезПоследних.ТорговаяТочка.Владелец КАК Контрагент
		|ПОМЕСТИТЬ ПоМенеджерам
		|ИЗ
		|	РегистрСведений.МенеджерыТорговыхТочек.СрезПоследних(
		|			,
		|			ВидМенеджера = &ВидМенеджера
		|				И &ИспользоватьОтборМенеджер) КАК МенеджерыТорговыхТочекСрезПоследних
		|ГДЕ
		|	МенеджерыТорговыхТочекСрезПоследних.Менеджер %ВидСравненияМенеджер% (&Менеджеры)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		%ОтборМенеджер%
		|
		|  %ОтборФилиал% ";
	
	ИспользоватьОтборМенеджер = СтруктураПараметров.ОтборМенеджер.Менеджеры.Количество() > 0;	
	Запрос.УстановитьПараметр("ИспользоватьОтборМенеджер", 	ИспользоватьОтборМенеджер);
	Запрос.УстановитьПараметр("ВидМенеджера", 	СтруктураПараметров.ОтборМенеджер.ВидМенеджера);
	Запрос.УстановитьПараметр("Менеджеры", 		СтруктураПараметров.ОтборМенеджер.Менеджеры);
	
	ИспользоватьОтборФилиал = СтруктураПараметров.ОтборФилиал.Филиалы.Количество() > 0;	
	Запрос.УстановитьПараметр("ИспользоватьОтборФилиал", 	ИспользоватьОтборФилиал);
	Запрос.УстановитьПараметр("Филиалы", 					СтруктураПараметров.ОтборФилиал.Филиалы);
	
	ОтборМенеджерТекст = "ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоМенеджерам КАК ПоМенеджерам ПО Контрагенты.Ссылка = ПоМенеджерам.Контрагент";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборМенеджер%", 
		?(ИспользоватьОтборМенеджер, ОтборМенеджерТекст,""));
		
		
	ОтборФилиалТекст = "ГДЕ Контрагенты.ОсновнаяТорговаяТочка.Регион.Филиал %ВидСравненияФилиал% (&Филиалы)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборФилиал%", 
		?(ИспользоватьОтборФилиал, ОтборФилиалТекст,""));
		
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ВидСравненияМенеджер%", 
		?(СтруктураПараметров.ОтборМенеджер.ВидСравнения = ВидСравнения.ВСписке, "В","НЕ В"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ВидСравненияФилиал%", 
		?(СтруктураПараметров.ОтборФилиал.ВидСравнения = ВидСравнения.ВСписке, "В", "НЕ В"));
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ФилиалКонтрагента(Контрагент) Экспорт
	
	ФилиалКонтрагента = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Контрагенты.ОсновнаяТорговаяТочка.Регион.Филиал, НЕОПРЕДЕЛЕНО) КАК Филиал
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ФилиалКонтрагента = Выборка.Филиал;		
	КонецЕсли;
	
	Возврат ФилиалКонтрагента;
	
КонецФункции

// + Пушкин 20181212 XX-1455
Процедура ЗалогироватьЭД(пар_Ссылка,пар_Лог) экспорт
	
	лПериод = ТекущаяДата();
	лПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Для Индекс = 1 По СтрЧислоСтрок(пар_Лог) Цикл
		лстр = СтрПолучитьСтроку(пар_Лог, Индекс);
		
		лстр = СтрЗаменить(лстр, """", "");
		лстр = СтрЗаменить(лстр, " ", "");
		лстр = СтрЗаменить(лстр, ":", ";");
		лстр = СтрЗаменить(лстр, "<>", ";");
		лстр = СтрЗаменить(лстр, "<пусто!>", "0");
		
        мСтроки = СтрРазделить(лстр,";");
		Если мСтроки.Количество() < 3 тогда
			Продолжить;
		КонецЕсли;
		
		мРекв = СокрлП(мСтроки[0]); 
		мРеквСтарое = мСтроки[1];
		мРеквНовое = мСтроки[2];
		
	    Если СтрНайти(мРеквНовое,"Да") > 0 Тогда
			мРеквНовое = 1;
	    ИначеЕсли СтрНайти(мРеквНовое,"Нет") > 0 Тогда
			мРеквНовое = 0;
		Иначе
			мРеквНовое = Число(мРеквНовое);
		КонецЕсли;
		
	    Если СтрНайти(мРеквСтарое,"Да") > 0 Тогда
			мРеквСтарое = 1;
	    ИначеЕсли СтрНайти(мРеквСтарое,"Нет") > 0 Тогда
			мРеквСтарое = 0;
		Иначе
			мРеквСтарое = Число(мРеквСтарое);
		КонецЕсли;
		
		//Сообщить(мРекв + " : " + мРеквНовое + " => " + мРеквСтарое + " / ");
		Попытка
			МенеджерЗаписи = РегистрыСведений.ИсторияИзмененийСтоимостиЭД.СоздатьМенеджерЗаписи();
			
			МенеджерЗаписи.Период = лПериод;
			МенеджерЗаписи.Пользователь = лПользователь;
			МенеджерЗаписи.КА = пар_Ссылка;
			
			МенеджерЗаписи.Реквизит_Имя = мРекв;
			МенеджерЗаписи.Реквизит_СтароеЗначение = мРеквСтарое;
			МенеджерЗаписи.Реквизит_НовоеЗначение = мРеквНовое;

			МенеджерЗаписи.Записать();
			
		Исключение
			//стр = ОписаниеОшибки();
			//Сообщить(стр);
		КонецПопытки
		
	КонецЦикла;	
	
КонецПроцедуры
// - Пушкин 20181212 XX-1455

//Валиахметов http://jira.part-kom.ru/browse/XX-2724 27.06.2019
Процедура ЗаполнитьДопСвойствоВидПечатнойФормы_ВозвратПоставщику() Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Контрагенты.Ссылка
	               |ИЗ
	               |	Справочник.Контрагенты КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Поставщик
	               |	И Контрагенты.флРаботает_сКоррСФ
	               |	И НЕ Контрагенты.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();
	Инд = 1;
	Пока Выборка.Следующий() Цикл 
		Менеджер = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
		Менеджер.Объект = Выборка.Ссылка;
		Менеджер.Свойство = УправлениеСвойствамиПовтИсп.Свойство_ВидПечатнойФормыДляВозвратаПоставщику();
		Менеджер.Значение = УправлениеСвойствамиПовтИсп.ЗначениеСвойствОбъектов("УПД-2", Менеджер.Свойство);
		Менеджер.Записать();
		#Если Клиент Тогда 
			Состояние("Выполнено " + Инд +  " из " + Выборка.Количество());
		#КонецЕсли
		Инд = Инд + 1;
	КонецЦикла;
КонецПроцедуры
//Конец Валиахметов http://jira.part-kom.ru/browse/XX-2724 27.06.2019
