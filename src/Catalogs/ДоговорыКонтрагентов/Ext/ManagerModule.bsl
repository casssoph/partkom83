Функция ПолучитьРеквизитыКонтроля(МетаданныеОтбора) Экспорт
	
	СтруктураПроверяемыхРеквизитов = Новый Структура;
	
	Если МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт Тогда
		СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "Наименование,Код,Владелец,Дата,Номер,Организация,СрокДействия,ДопустимаяСуммаЗадолженности,КоэффициентСуммыКредита");
	КонецЕсли;
	
	Если МетаданныеОтбора = Метаданные.ПланыОбмена.ОбменПартКом83_77 Тогда
		СтруктураПроверяемыхРеквизитов.Вставить("Шапка", "Наименование,Код,Владелец,Дата,Номер,Организация,СрокДействия");
	КонецЕсли;
	
	Возврат СтруктураПроверяемыхРеквизитов;
	
КонецФункции

Функция ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора) Экспорт
	
	Возврат	РаботаСПоследовательностямиКлиентСервер.ПолучитьЗначенияРеквизитовКонтроля(СсылкаНаОбъект, МетаданныеОтбора);
	
КонецФункции

Функция ВыгрузитьЭлементы(ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена) Экспорт
	
	МассивОбъектов = Новый Массив;
	URI = ПланыОбмена.ОбменПартКом83_Сайт.URIПространстваИмен();
	ТипОбъектаXDTO = ФабрикаXDTO.Тип(URI, "Справочники.ДоговорыКонтрагентов");
	ТипУдалениеОбъекта = ФабрикаXDTO.Тип(URI, "УдалениеОбъекта");	

	ОбъектыОбмена = ДанныеОбъектовПланаОбмена(ТаблицаСсылокНаОбъекты);

	Если МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт тогда
		
		Выборка = ОбъектыОбмена[6].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
			ЗаполнитьЗначенияСвойств(ОбъектXDTO, Выборка,,"Ссылка,КонтрагентЛогин");
			ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
			ОбъектXDTO.КонтрагентUUID = Выборка.Контрагент.УникальныйИдентификатор();
			ОбъектXDTO.ОрганизацияUUID = Выборка.Организация.УникальныйИдентификатор();
			ОбъектXDTO.КонтрагентЛогин = СокрЛП(Выборка.КонтрагентЛогин);
			МассивОбъектов.Добавить(ОбъектXDTO);
		КонецЦикла;		
		
		Выборка = ОбъектыОбмена[7].Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектXDTO = ФабрикаXDTO.Создать(ТипУдалениеОбъекта);
			ОбъектXDTO.ТипОбъекта = "Справочники.ДоговорыКонтрагентов";
			ОбъектXDTO.Ссылка = Выборка.Ссылка.УникальныйИдентификатор();
			
			МассивОбъектов.Добавить(ОбъектXDTO);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивОбъектов;
	
КонецФункции

Функция ДанныеОбъектовПланаОбмена(ТаблицаСсылокНаОбъекты)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВнешняяТаблица.Ссылка
	                      |ПОМЕСТИТЬ ЗарегистрированныеОбъекты
	                      |ИЗ
	                      |	&ТаблицаСсылокНаОбъекты КАК ВнешняяТаблица
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ДоговорыКонтрагентов) КАК Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ДоговорыКонтрагентов).ВерсияДанных ЕСТЬ NULL
	                      |				И (ВЫРАЗИТЬ(ЗарегистрированныеОбъекты.Ссылка КАК Справочник.ДоговорыКонтрагентов)) <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК ЭтоУдаление
	                      |ПОМЕСТИТЬ Объекты
	                      |ИЗ
	                      |	ЗарегистрированныеОбъекты КАК ЗарегистрированныеОбъекты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ДоговорыКонтрагентов.Ссылка
	                      |ПОМЕСТИТЬ СПодчиненнымиДоговорами
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	                      |		ПО (НЕ Объекты.ЭтоУдаление)
	                      |			И Объекты.Ссылка.Владелец = ДоговорыКонтрагентов.Владелец.ГоловнойКонтрагент
	                      |			И Объекты.Ссылка.Владелец <> ДоговорыКонтрагентов.Владелец
	                      |			И (ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем))
	                      |			И Объекты.Ссылка.Организация = ДоговорыКонтрагентов.Организация
	                      |			И Объекты.Ссылка.ДоговорНаОферту = ДоговорыКонтрагентов.ДоговорНаОферту
	                      |			И Объекты.Ссылка.ВидОплаты = ДоговорыКонтрагентов.ВидОплаты
	                      |			И (НЕ ДоговорыКонтрагентов.СлужебныйДоговор)
	                      |			И (НЕ ДоговорыКонтрагентов.Владелец.ПометкаУдаления)
	                      |			И (НЕ ДоговорыКонтрагентов.ДоговорПриостановлен)
	                      |
	                      |ОБЪЕДИНИТЬ
	                      |
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	НЕ Объекты.ЭтоУдаление
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	УчетныеЗаписиСайта.Владелец.Владелец КАК Контрагент,
	                      |	МАКСИМУМ(УчетныеЗаписиСайта.Код) КАК Логин
	                      |ПОМЕСТИТЬ ЛогиныКонтрагентов
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиСайта КАК УчетныеЗаписиСайта
	                      |ГДЕ
	                      |	УчетныеЗаписиСайта.Владелец.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				Объекты.Ссылка.Владелец
	                      |			ИЗ
	                      |				СПодчиненнымиДоговорами КАК Объекты)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	УчетныеЗаписиСайта.Владелец.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СПодчиненнымиДоговорами.Ссылка
	                      |ПОМЕСТИТЬ ДоговорыПодчиненныхКонтрагентов
	                      |ИЗ
	                      |	СПодчиненнымиДоговорами КАК СПодчиненнымиДоговорами
	                      |ГДЕ
	                      |	СПодчиненнымиДоговорами.Ссылка.Владелец <> СПодчиненнымиДоговорами.Ссылка.Владелец.ГоловнойКонтрагент
	                      |	И СПодчиненнымиДоговорами.Ссылка.Владелец.ГоловнойКонтрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ДоговорыПодчиненныхКонтрагентов.Ссылка КАК ПодчиненныйДоговор,
	                      |	ДоговорыКонтрагентов.Ссылка КАК ДоговорХолдинга,
	                      |	ДоговорыКонтрагентов.ДатаДоговораОферты,
	                      |	ДоговорыКонтрагентов.НомерЗаявкиОферты,
	                      |	ДоговорыКонтрагентов.ДатаДоговораОферты <> ДАТАВРЕМЯ(1, 1, 1)
	                      |		ИЛИ ДоговорыКонтрагентов.НомерЗаявкиОферты <> """"
	                      |		ИЛИ ДоговорыКонтрагентов.ДоговорПодписан КАК АксептОферты
	                      |ПОМЕСТИТЬ ДоговораХолдингов
	                      |ИЗ
	                      |	ДоговорыПодчиненныхКонтрагентов КАК ДоговорыПодчиненныхКонтрагентов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	                      |		ПО ДоговорыПодчиненныхКонтрагентов.Ссылка.Организация = ДоговорыКонтрагентов.Организация
	                      |			И ДоговорыПодчиненныхКонтрагентов.Ссылка.ВидОплаты = ДоговорыКонтрагентов.ВидОплаты
	                      |			И ДоговорыПодчиненныхКонтрагентов.Ссылка.ДоговорНаОферту = ДоговорыКонтрагентов.ДоговорНаОферту
	                      |			И (НЕ ДоговорыКонтрагентов.Ссылка.ПометкаУдаления)
	                      |			И (НЕ ДоговорыКонтрагентов.Ссылка.ДоговорПриостановлен)
	                      |			И (НЕ ДоговорыКонтрагентов.Ссылка.СлужебныйДоговор)
	                      |			И ДоговорыПодчиненныхКонтрагентов.Ссылка.Владелец.ГоловнойКонтрагент = ДоговорыКонтрагентов.Владелец
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СПодчиненнымиДоговорами.Ссылка,
	                      |	СПодчиненнымиДоговорами.Ссылка.Код КАК Код,
	                      |	СПодчиненнымиДоговорами.Ссылка.Организация КАК Организация,
	                      |	ЕСТЬNULL(СПодчиненнымиДоговорами.Ссылка.Организация.Код, """") КАК ОрганизацияКод,
	                      |	ЕСТЬNULL(ЛогиныКонтрагентов.Логин, """") КАК КонтрагентЛогин,
	                      |	СПодчиненнымиДоговорами.Ссылка.Владелец КАК Контрагент,
	                      |	СПодчиненнымиДоговорами.Ссылка.Дата КАК ДатаДоговора,
	                      |	СПодчиненнымиДоговорами.Ссылка.Номер КАК НомерДоговора,
	                      |	СПодчиненнымиДоговорами.Ссылка.ДоговорПриостановлен КАК Заблокирован,
	                      |	ЕСТЬNULL(ДоговораХолдингов.АксептОферты, СПодчиненнымиДоговорами.Ссылка.ДоговорПодписан
	                      |			ИЛИ СПодчиненнымиДоговорами.Ссылка.ДатаДоговораОферты <> ДАТАВРЕМЯ(1, 1, 1)
	                      |			ИЛИ СПодчиненнымиДоговорами.Ссылка.НомерЗаявкиОферты <> """") КАК Подписан,
	                      |	СПодчиненнымиДоговорами.Ссылка.СлужебныйДоговор КАК СлужебныйДоговор,
	                      |	ВЫБОР
	                      |		КОГДА СПодчиненнымиДоговорами.Ссылка.ВидОплаты = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК НаличныйРасчет,
	                      |	СПодчиненнымиДоговорами.Ссылка.ДоговорНаОферту КАК Оферта,
	                      |	ВЫБОР
	                      |		КОГДА СПодчиненнымиДоговорами.Ссылка.ВидРасчетаДней = ЗНАЧЕНИЕ(Перечисление.ВидыРасчетаДней.ПоКалендарнымДням)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК РасчетПоКалендарнымДням,
	                      |	ЕСТЬNULL(ДоговораХолдингов.ДоговорХолдинга.ДопустимаяСуммаЗадолженности, СПодчиненнымиДоговорами.Ссылка.ДопустимаяСуммаЗадолженности) КАК СуммаКредита,
	                      |	ЕСТЬNULL(ДоговораХолдингов.ДоговорХолдинга.ДопустимоеЧислоДнейЗадолженности, СПодчиненнымиДоговорами.Ссылка.ДопустимоеЧислоДнейЗадолженности) КАК ДнейКредита,
	                      |	ЕСТЬNULL(ДоговораХолдингов.ДоговорХолдинга.КоэффициентСуммыКредита, СПодчиненнымиДоговорами.Ссылка.КоэффициентСуммыКредита) КАК КредитныйКоэффициент,
	                      |	ЕСТЬNULL(_ДляПереносаДанных.Строка77, ""00000000-0000-0000-0000-000000000000"") КАК UUID77
	                      |ИЗ
	                      |	СПодчиненнымиДоговорами КАК СПодчиненнымиДоговорами
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений._ДляПереносаДанных КАК _ДляПереносаДанных
	                      |		ПО СПодчиненнымиДоговорами.Ссылка = _ДляПереносаДанных.Объект
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ЛогиныКонтрагентов КАК ЛогиныКонтрагентов
	                      |		ПО СПодчиненнымиДоговорами.Ссылка.Владелец = ЛогиныКонтрагентов.Контрагент
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ДоговораХолдингов КАК ДоговораХолдингов
	                      |		ПО СПодчиненнымиДоговорами.Ссылка = ДоговораХолдингов.ПодчиненныйДоговор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Объекты.Ссылка
	                      |ИЗ
	                      |	Объекты КАК Объекты
	                      |ГДЕ
	                      |	Объекты.ЭтоУдаление");
	Запрос.УстановитьПараметр("ТаблицаСсылокНаОбъекты", ТаблицаСсылокНаОбъекты);
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция БанковскийСчетПоДоговору(ДоговорКонтрагента) Экспорт
	
	СчетОрганизации = Неопределено;
	
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Возврат СчетОрганизации
	КонецЕсли;
	
	Реквизиты =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "БанковскийСчет, Организация");
	Если ЗначениеЗаполнено(Реквизиты.БанковскийСчет) Тогда
		СчетОрганизации = Реквизиты.БанковскийСчет;
	ИначеЕсли ЗначениеЗаполнено(Реквизиты.Организация) Тогда
		СчетОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Организация, "ОсновнойБанковскийСчет");
	КонецЕсли;
	
	Возврат СчетОрганизации;
	
КонецФункции

Функция ИспользоватьРегистрОсновныеДоговорыКонтрагентов() Экспорт
	
	Возврат ОбщегоНазначенияПовтИсп.ЗначениеКонстанты("ИспользоватьРегистрОсновныеДоговорыКонтрагентов");	
	
КонецФункции