Функция ВыгрузитьЭлементы(ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена, НомерСообщения) Экспорт
	
	Результат = Новый Массив;
	Если МетаданныеПланаОбмена = Метаданные.ПланыОбмена.ОбменПартКом83_Сайт_состояние_заявок тогда
		ВыгрузитьДанныеПланаОбмена_СостояниеЗаявок(Результат, ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена, НомерСообщения);
	Иначе
		ВызватьИсключение "[ВыгрузитьЭлементы]: неправильный параметр номер 2.";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//План обмена "Состояние строк заявок"
Процедура ВыгрузитьДанныеПланаОбмена_СостояниеЗаявок(Результат, ТаблицаСсылокНаОбъекты, МетаданныеПланаОбмена, НомерСообщения)
	
	МенеджерПланаОбмена = ПланыОбмена[МетаданныеПланаОбмена.Имя];
	ТипОбъектаXDTO = ФабрикаXDTO.Тип("http://ws-02.part-kom.ru/partkom83/hs/SiteExchange/XMLSchema", "ИсторияСтрокЗаявок");
	ТипУдалениеОбъекта = ФабрикаXDTO.Тип(ПланыОбмена.ОбменПартКом83_Сайт.URIПространстваИмен(), "УдалениеОбъекта");
	ТекущаяДата = ТекущаяДата();
	
	ВыборкаОбъектов = ДанныеПоОбъектамСтрокЗаявок(ТаблицаСсылокНаОбъекты);
	ВыборкаОбъектовИстории = ВыборкаОбъектов[3].Выбрать();
	ВыборкаУдаляемыхОбъектов = ВыборкаОбъектов[2].Выбрать();
	
	НаборЗаписей  = РегистрыСведений.ИсторияЗаявокДляПланаОбменаССайтом.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ReceivedNo.Установить(НомерСообщения);
	Пока ВыборкаОбъектовИстории.Следующий() Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.ReceivedNo = НомерСообщения;
		Запись.СсылкаНаИсторию = ВыборкаОбъектовИстории.Ссылка;
		Запись.Количество = ВыборкаОбъектовИстории.Количество;
		Запись.ДтВрм_Расчета = ТекущаяДата;
		Запись.ДтВрм_Посылки = ТекущаяДата;
		
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъектаXDTO);
		ЗаполнитьЗначенияСвойств(ОбъектXDTO, ВыборкаОбъектовИстории, "IDSite,ВерсияДанных,Количество,НомерЗаявки,Артикул,Цена,ЗакупочнаяЦена");
		ОбъектXDTO.Состояние = ВыборкаОбъектовИстории.Состояние.УникальныйИдентификатор();
		ОбъектXDTO.Склад = Число(ВыборкаОбъектовИстории.Склад);
		Результат.Добавить(ОбъектXDTO);
		
	КонецЦикла;
	НаборЗаписей.Записать();
	
	Пока ВыборкаУдаляемыхОбъектов.Следующий() Цикл
		ОбъектXDTO = ФабрикаXDTO.Создать(ТипУдалениеОбъекта);
		ОбъектXDTO.ТипОбъекта = "Справочники.ИсторияСтрокЗаявок";
		ОбъектXDTO.Ссылка = ВыборкаУдаляемыхОбъектов.Ссылка.УникальныйИдентификатор();
		Результат.Добавить(ОбъектXDTO);
	КонецЦикла;
	
КонецПроцедуры
Функция ДанныеПоОбъектамСтрокЗаявок(ТаблицаСсылок)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВнешняяТаблица.Ссылка
	                      |ПОМЕСТИТЬ ВТ_СсылкиНаОбъекты
	                      |ИЗ
	                      |	&ТаблицаСсылок КАК ВнешняяТаблица
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫРАЗИТЬ(ВТ_СсылкиНаОбъекты.Ссылка КАК Справочник.ИсторияСтрокЗаявок) КАК Ссылка,
	                      |	ВЫБОР
	                      |		КОГДА ВЫРАЗИТЬ(ВТ_СсылкиНаОбъекты.Ссылка КАК Справочник.ИсторияСтрокЗаявок).ВерсияДанных ЕСТЬ NULL
	                      |				И (ВЫРАЗИТЬ(ВТ_СсылкиНаОбъекты.Ссылка КАК Справочник.ИсторияСтрокЗаявок)) <> ЗНАЧЕНИЕ(Справочник.ИсторияСтрокЗаявок.ПустаяСсылка)
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК УдалениеОбъекта
	                      |ПОМЕСТИТЬ ВТ_РазделениеУдаленныхОбъектов
	                      |ИЗ
	                      |	ВТ_СсылкиНаОбъекты КАК ВТ_СсылкиНаОбъекты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_РазделениеУдаленныхОбъектов.Ссылка
	                      |ИЗ
	                      |	ВТ_РазделениеУдаленныхОбъектов КАК ВТ_РазделениеУдаленныхОбъектов
	                      |ГДЕ
	                      |	ВТ_РазделениеУдаленныхОбъектов.УдалениеОбъекта
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_РазделениеУдаленныхОбъектов.Ссылка,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.IDSite, """") КАК IDSite,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.ДатаВремя, ДАТАВРЕМЯ(1, 1, 1)) КАК ВерсияДанных,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.Состояние, ЗНАЧЕНИЕ(Справочник.СостоянияСтрокДокументов.ПустаяСсылка)) КАК Состояние,
	                      |	ВТ_РазделениеУдаленныхОбъектов.Ссылка.Количество КАК Количество,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.Заявка.Номер, """") КАК НомерЗаявки,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.ЦенаЗакупки, 0) КАК ЗакупочнаяЦена,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.Цена, 0) КАК Цена,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.Заявка.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладСсылка,
	                      |	ЕСТЬNULL(ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.Заявка.Склад.Код, 0) КАК Склад,
	                      |	ЕСТЬNULL(ЗаявкаПокупателяТовары.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	                      |	ЕСТЬNULL(ЗаявкаПокупателяТовары.Номенклатура.Артикул, """") КАК Артикул
	                      |ИЗ
	                      |	ВТ_РазделениеУдаленныхОбъектов КАК ВТ_РазделениеУдаленныхОбъектов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаПокупателя.Товары КАК ЗаявкаПокупателяТовары
	                      |		ПО ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки = ЗаявкаПокупателяТовары.СтрокаЗаявки
	                      |			И ВТ_РазделениеУдаленныхОбъектов.Ссылка.СтрокаЗаявки.Заявка = ЗаявкаПокупателяТовары.Ссылка
	                      |ГДЕ
	                      |	НЕ ВТ_РазделениеУдаленныхОбъектов.УдалениеОбъекта");
	Запрос.УстановитьПараметр("ТаблицаСсылок", ТаблицаСсылок);
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции
