
Функция ДоступныеСтатусыТекущегоПользователя() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Статус
		|ИЗ
		|	Справочник.ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателя.Статусы КАК ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы
		|		Внутреннее СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка = Пользователи.ГруппаДоступаКСтатусамПроцессаВозвратаОтПокупателя
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка
		|	И НЕ ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыСеанса.ТекущийПользователь);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Статус");
		
КонецФункции

Функция СтатусДоступенТекущемуПользователю(Статус) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Статус
		|ИЗ
		|	Справочник.ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателя.Статусы КАК ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка = Пользователи.ГруппаДоступаКСтатусамПроцессаВозвратаОтПокупателя
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка
		|	И ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Статус = &Статус
		|	И НЕ ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Статус", Статус);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
		
КонецФункции

//ХудинВВ 07052019 XX-2214
Функция ЗапрещеноИзменениеРеквизитовГруппойДоступности(Статус) Экспорт
	
	ЗапрещеноИзменениеРеквизитов = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.ЗапрещеноИзменениеРеквизитов, ИСТИНА) КАК ЗапрещеноИзменениеРеквизитов
		|ИЗ
		|	Справочник.ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателя.Статусы КАК ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка = Пользователи.ГруппаДоступаКСтатусамПроцессаВозвратаОтПокупателя
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка
		|	И ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Статус = &Статус
		|	И НЕ ГруппыДоступаКСтатусамПроцессаВозвратаОтПокупателяСтатусы.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Статус", Статус);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗапрещеноИзменениеРеквизитов = Выборка.ЗапрещеноИзменениеРеквизитов;
	КонецЕсли;
	
	Возврат ЗапрещеноИзменениеРеквизитов;
		
КонецФункции

//ХудинВВ 23052019 XX-2346
Функция ЗаданыОграниченияВзаимосвязейТекущегоПользователя(Статус) Экспорт
	
	РежимОграниченияВзаимосвязей = Справочники.ВзаимосвязиСтатусовПроцессаВозвратаОтПокупателя.РежимОграниченияВзаимосвязей(
	ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ГруппаДоступаКСтатусамПроцессаВозвратаОтПокупателя"), Статус);
	
	Возврат ЗначениеЗаполнено(РежимОграниченияВзаимосвязей);	
		
КонецФункции