
Функция ПолучитьРегламентноеЗадание() Экспорт
	
	Результат = Неопределено;
	Если ЗначениеЗаполнено(ИдентификаторРегламентногоЗадания) тогда
		Результат = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторРегламентногоЗадания);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	лРегламентноеЗадание = ПолучитьРегламентноеЗадание();
	
	Если НЕ ЭтоНовый() И лРегламентноеЗадание <> Неопределено тогда
		// установили пометку на удаление, нужно снять Использование	
		Если (ПометкаУдаления <> Ссылка.ПометкаУдаления) И ПометкаУдаления Тогда
			лРегламентноеЗадание.Использование = Ложь;
			лРегламентноеЗадание.Записать();
			// 31.05.19 Строганов Роман > 
			РегистрыСведений.ИсторияРегламентныхЗаданий.ЗафиксироватьОтключениеРегламентногоЗадания(Ссылка);
			// 31.05.19 Строганов Роман <		
		ИначеЕсли Использование <> лРегламентноеЗадание.Использование Тогда 
			// 31.05.19 Строганов Роман > 
			Если Использование Тогда
				РегистрыСведений.ИсторияРегламентныхЗаданий.ЗафиксироватьВключениеРегламентногоЗадания(Ссылка);
			Иначе
				РегистрыСведений.ИсторияРегламентныхЗаданий.ЗафиксироватьОтключениеРегламентногоЗадания(Ссылка);
			КонецЕсли;
			// 31.05.19 Строганов Роман <
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	лРегламентноеЗадание = ПолучитьРегламентноеЗадание();
	Если (лРегламентноеЗадание <> Неопределено) тогда
		лРегламентноеЗадание.Удалить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ИдентификаторРегламентногоЗадания = Неопределено;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	#Если Клиент Тогда
		Оповестить("ЗаписьРегламентногоЗадания", Ссылка);
	#КонецЕсли
КонецПроцедуры



	
